//------------------Sonic CD 3D Ramp Script-------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value1		:	Player.XOriginPos
#alias Object[+3].Value0	:	StarBush1.XOriginPos
#alias Object[+3].XPos		:	StarBush1.XPos
#alias Object[+4].Value0	:	StarBush2.XOriginPos
#alias Object[+4].XPos		:	StarBush2.XPos
#alias Object[+5].Value0	:	BoostSpinner.XOriginPos
#alias Object[+5].XPos		:	BoostSpinner.XPos

// States
#alias 0	:	RAMP3D_RUN
#alias 1	:	RAMP3D_EXIT_UNUSED	// a copy of Exit Boost
#alias 2	:	RAMP3D_EXIT
#alias 3	:	RAMP3D_SET_YBOUND	// this doesn't really have anything to do with this object
#alias 4	:	RAMP3D_ENTER
#alias 5	:	RAMP3D_DROP
#alias 6	:	RAMP3D_SET_YBOUND2	// this doesn't really have anything to do with this object
#alias 7	:	RAMP3D_RUN_FROM_TOP
#alias 8	:	RAMP3D_EXIT_BOOST

// Gravity
#alias 0	:	GRAVITY_GROUND
#alias 1	:	GRAVITY_AIR

// Priority
#alias 0	:	PRIORITY_BOUNDS
#alias 1	:	PRIORITY_ACTIVE

// Control Mode
#alias -1	:	CONTROLMODE_NONE
#alias  0	:	CONTROLMODE_NORMAL

// Function declarations
#function Ramp3D_SetPlayerLaunch

function Ramp3D_SetPlayerLaunch
#platform: Use_Origins
	PlayerObjectCollision(C_TOUCH, -16, -16, 16, 16)
	if CheckResult == 1
		Player.Timer = 0
		Player.State = Player_State_Air_NoDropDash

		Player.TileCollisions = true
		
		Player.Speed	 =  0x80000
		Player.XVelocity =  0x80000
		Player.YVelocity = -0xE0000

		Player.Gravity  = GRAVITY_AIR
		Player.Rotation = 384

		Stage.NewYBoundary2 = 1280
		Stage.NewXBoundary1 = 2080

		if Player.Animation == ANI_ROLL3D
			Player.Animation = ANI_JUMPING
		else
			if Player.Animation != ANI_JUMPING
				Player.Animation = ANI_WALKING
			end if
		end if

		Object[15].Type = TypeName[BackgroundFX]
		Object[15].Priority  = PRIORITY_ACTIVE
		Object[15].DrawOrder = 0

		ArrayPos0 = 0
		while ArrayPos0 < 2
			HParallax[ArrayPos0].ScrollPos = 0
			ArrayPos0++
		loop
	end if
	if Player.YVelocity < 0
		Player.ControlMode  = CONTROLMODE_NONE
		Player.Left			= false
		Player.Right		= false
		Object.Priority 	= PRIORITY_ACTIVE
	end if
#endplatform
end function


sub ObjectPlayerInteraction
	switch Object.PropertyValue
	case RAMP3D_RUN
		if Player.Gravity == GRAVITY_GROUND

			PlayerObjectCollision(C_TOUCH, -16, -16, 16, 16)
			if CheckResult == true

				if Player.State != Player_State_Static
					Player.State = Player_State_Static

					Player.TrackScroll	  = true
					Player.TileCollisions = false

					Player.Speed	 =  0
					Player.XVelocity =  0
					Player.YVelocity = -0xC0000

					Player.AnimationSpeed = 0
				end if
			end if
		end if
		break

	case RAMP3D_EXIT_UNUSED
		PlayerObjectCollision(C_TOUCH, -16, -16, 16, 16)
		if CheckResult == true
			
#platform: Use_Origins
			Player.State = Player_State_Air_NoDropDash
#endplatform

#platform: Use_Standalone
			Player.State = Player_State_Air
#endplatform
			
			Player.TileCollisions = true

			Player.Speed	 =  0x80000
			Player.XVelocity =  0x80000
			Player.YVelocity = -0xE0000

			Player.Gravity  = GRAVITY_AIR
			Player.Rotation = 384

			Stage.NewYBoundary2 = 1280
			Stage.NewXBoundary1 = 2080

			if Player.Animation == ANI_ROLL3D
				Player.Animation = ANI_JUMPING
			else
				if Player.Animation != ANI_JUMPING
					Player.Animation = ANI_WALKING
				end if
			end if

			Object[15].Type		 = TypeName[Background FX]
			Object[15].Priority	 = PRIORITY_ACTIVE
			Object[15].DrawOrder = 0

			ArrayPos0 = 0
			while ArrayPos0 < 2
				HParallax[ArrayPos0].ScrollPos = 0
				ArrayPos0++
			loop
		end if
		break

	case RAMP3D_EXIT
		if Player.State == Player_State_Static
			PlayerObjectCollision(C_TOUCH, -16, -16, 16, 16)
			if CheckResult == true
				
#platform: Use_Origins
				Player.State = Player_State_Air_NoDropDash
#endplatform

#platform: Use_Standalone
				Player.State = Player_State_Air
#endplatform

				Player.TileCollisions = true

				Player.Speed	 =  0x40000
				Player.XVelocity =  0x40000
				Player.YVelocity = -0xC0000

				Player.Gravity  = GRAVITY_AIR
				Player.Rotation = 384

				Stage.NewYBoundary2 = 1280

				if Player.Animation == ANI_ROLL3D
					Player.Animation = ANI_JUMPING
				else
					if Player.Animation != ANI_JUMPING
						Player.Animation = ANI_WALKING
					end if
				end if
			end if
		end if
		break

	case RAMP3D_SET_YBOUND
		if Player.XPos > Object.XPos
			Stage.YBoundary2 = 1024
		else
			Stage.YBoundary2 = 1280
		end if
		break

	case RAMP3D_ENTER
		if Player.XPos > Object.XPos
			if Player.Gravity == GRAVITY_GROUND
				if Player.State != Player_State_Static
					if Player.State == Player_State_Roll

						if Player.Angle > 216
							Player.Animation = ANI_JUMPING
						else
							if Player.Angle == 0
								Player.Animation = ANI_JUMPING
							else
								Player.Animation = ANI_ROLL3D
							end if
						end if
					else

						if Player.Speed != 0
							Player.State = Player_State_Ramp3D
						end if
					end if
				end if
			end if

			StarBush1.XOriginPos  = Player.XPos
			StarBush1.XOriginPos -= Object.XPos

			BoostSpinner.XOriginPos  = StarBush1.XOriginPos
			BoostSpinner.XOriginPos /= 3

			StarBush1.XOriginPos >>= 1
			StarBush2.XOriginPos   = StarBush1.XOriginPos
			StarBush1.XOriginPos  += StarBush1.XPos
			StarBush2.XOriginPos  += StarBush2.XPos

			BoostSpinner.XOriginPos += BoostSpinner.XPos

			TempValue0  = Object.YPos
			TempValue0 -= Player.YPos
			if TempValue0 < 0xE00000
			
				TempValue0  = Player.XPos
				TempValue0 -= Player.XOriginPos
				HParallax[0].ScrollPos += TempValue0

				ArrayPos0  = 13
				TempValue2 = 128
				while ArrayPos0 < 141
					TempValue1   = TempValue0
					TempValue1  *= TempValue2
					TempValue1 >>= 7
					HParallax[ArrayPos0].ScrollPos += TempValue1

					ArrayPos0++

					TempValue2--
				loop

				TempValue0 <<= 1
				HParallax[1].ScrollPos += TempValue0
			end if
		else
			StarBush1.XOriginPos = StarBush1.XPos

			StarBush2.XOriginPos = StarBush2.XPos
			
			BoostSpinner.XOriginPos = BoostSpinner.XPos

			if Player.Gravity == GRAVITY_GROUND
				if Player.State == Player_State_Ramp3D
					Player.State = Player_State_Ground
				end if
			end if

		end if
		Player.XOriginPos = Player.XPos
		break
		
	case RAMP3D_DROP
	#platform: Use_Origins
		CheckEqual(Player.State, Player_State_Air_NoDropDash)
		TempValue0  = CheckResult
		CheckEqual(Player.State, Player_State_Air)
		TempValue0 |= CheckResult
	#endplatform

	#platform: Use_Standalone
		CheckEqual(Player.State, Player_State_Air)	// used to be a straight check, changed to be have a cleaner check with origins
		TempValue0 = CheckResult
	#endplatform

		if TempValue0 != false
			PlayerObjectCollision(C_TOUCH, -16, -128, 16, 16)
			if CheckResult == true
				Player.Animation = ANI_DROPPING
				Player.XOriginPos = Player.XPos
			end if

			if Player.Animation == ANI_DROPPING
				Player.Speed	 = 0
				Player.XVelocity = 0
				Player.XPos		 = Player.XOriginPos
			end if
		end if
		break

	case RAMP3D_SET_YBOUND2
		if Player.YPos < Object.YPos
			if Player.XPos > Object.XPos
				Stage.YBoundary2 = 792
			else
				Stage.YBoundary2 = 1024
			end if
		end if
		break

	case RAMP3D_RUN_FROM_TOP // Technically speaking another YBound-like object
		if Player.XPos > Object.XPos
			Stage.YBoundary2 = 1024
			if Player.Gravity == GRAVITY_GROUND
				if Player.State == Player_State_Ramp3D
					Player.State = Player_State_Ground
				end if
			end if
		else
			Stage.YBoundary2 = 792
		end if
		break

	case RAMP3D_EXIT_BOOST
#platform: Use_Standalone
		PlayerObjectCollision(C_TOUCH, -16, -16, 16, 16)
		if CheckResult == true
			Player.Timer = 0
			Player.State = Player_State_Air

			Player.TileCollisions = true

			Player.Speed	 =  0x80000
			Player.XVelocity =  0x80000
			Player.YVelocity = -0xE0000

			Player.Gravity  = GRAVITY_AIR
			Player.Rotation = 384

			Stage.NewYBoundary2 = 1280
			Stage.NewXBoundary1 = 2080

			if Player.Animation == ANI_ROLL3D
				Player.Animation = ANI_JUMPING
			else
				if Player.Animation != ANI_JUMPING
					Player.Animation = ANI_WALKING
				end if
			end if

			Object[15].Type = TypeName[Background FX]
			Object[15].Priority  = PRIORITY_ACTIVE
			Object[15].DrawOrder = 0

			ArrayPos0 = 0
			while ArrayPos0 < 2
				HParallax[ArrayPos0].ScrollPos = 0
				ArrayPos0++
			loop
		end if

		if Player.YVelocity < 0
			Player.ControlMode  = CONTROLMODE_NONE
			Player.Left			= false
			Player.Right		= false
			Object.Priority 	= PRIORITY_ACTIVE
		else
			Player.ControlMode  = CONTROLMODE_NORMAL
			Object.Priority		= PRIORITY_BOUNDS
		end if
#endplatform

#platform: Use_Origins
		if Stage.PlayerListPos == PLAYER_KNUCKLES
			PlayerObjectCollision(C_TOUCH, -16, -16, 16, 16)
			if CheckResult == true
				TempValue0 = true
				if Player.Angle == 0
					TempValue0 = false
				end if
				if TempValue0 == true
					CallFunction(Ramp3D_SetPlayerLaunch)
				end if
			end if
		else
			CallFunction(Ramp3D_SetPlayerLaunch)
		end if
		if Player.YVelocity >= 0
			Player.ControlMode  = CONTROLMODE_NORMAL
			Object.Priority		= PRIORITY_BOUNDS
		end if
#endplatform
		
		break
	end switch
end sub


sub ObjectStartup
	if Player.XPos > 0x7D00000
		Stage.YBoundary2 = 1280
		Object[15].Type		 = TypeName[Background FX]
		Object[15].Priority	 = PRIORITY_ACTIVE
		Object[15].DrawOrder = 0
	end if

	if Player.XPos > 0xFA00000
		Stage.YBoundary2 = 1024
	end if
end sub


// ========================
// Editor Subs
// ========================

sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
		case 0 // type
			CheckResult  = Object.PropertyValue
			CheckResult %= 9
			break
		end switch
	else
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
		case 0 // type
			Object.PropertyValue = Editor.VariableValue
			CheckResult %= 9
			break
		end switch
	end if
end sub


sub RSDKDraw
	LoadSpriteSheet("Global/Display.gif")
	TempValue5 = Object.SpriteSheet
	LoadSpriteSheet("Players/Sonic2.gif")
	TempValue6 = Object.SpriteSheet
	LoadSpriteSheet("Players/Sonic3.gif")
	TempValue7 = Object.SpriteSheet

	switch object.PropertyValue
	case 0
		Object.SpriteSheet = TempValue6
		Object.InkEffect = 1
		DrawSpriteFX(3,FX_INK, Object.XPos, Object.YPos)
		Object.InkEffect = 0
		if Editor.ShowGizmos == true
			Editor.DrawingOverlay = true
			TempValue1  = Object.YPos
			TempValue1 -= 0xC00000
			DrawArrow(Object.XPos, Object.YPos, Object.XPos, TempValue1, 255, 255, 0)
			Editor.DrawingOverlay = false
		end if
		break
	case 1
	case 8
		Object.SpriteSheet = TempValue6
		Object.InkEffect = 1
		DrawSpriteFX(3,FX_INK, Object.XPos, Object.YPos)
		Object.InkEffect = 0
		if Editor.ShowGizmos == true
			Editor.DrawingOverlay = true
			TempValue0  = Object.XPos
			TempValue0 += 0x800000
			TempValue1  = Object.YPos
			TempValue1 -= 0xE00000
			DrawArrow(Object.XPos, Object.YPos, TempValue0, TempValue1, 255, 255, 0)
			Editor.DrawingOverlay = false
		end if
		break
	case 2
		Object.SpriteSheet = TempValue6
		Object.InkEffect = 1
		DrawSpriteFX(3,FX_INK, Object.XPos, Object.YPos)
		Object.InkEffect = 0
		if Editor.ShowGizmos == true
			Editor.DrawingOverlay = true
			TempValue0  = Object.XPos
			TempValue0 += 0x400000
			TempValue1  = Object.YPos
			TempValue1 -= 0xC00000
			DrawArrow(Object.XPos, Object.YPos, TempValue0, TempValue1, 255, 255, 0)
			Editor.DrawingOverlay = false
		end if
		break
	case 3
	case 6
		Object.SpriteSheet = TempValue5
		TempValue0  = Object.XPos
		TempValue0 -= 0x100000
		TempValue1  = Object.YPos
		TempValue1 -= 0x100000
		DrawRectWorld(TempValue0,TempValue1,32,32,96,96,192,255)

		TempValue0  = Object.XPos
		TempValue0 -= 0x80000
		TempValue1  = Object.YPos
		TempValue1 -= 0x80000
		DrawSpriteXY(1, TempValue0, TempValue1)

		TempValue0 += 0x100000
		DrawSpriteXY(1, TempValue0, TempValue1)

		TempValue1 += 0x100000
		DrawSpriteXY(1, TempValue0, TempValue1)

		TempValue0 -= 0x100000
		DrawSpriteXY(1, TempValue0, TempValue1)
		break
	case 7
		Object.SpriteSheet = TempValue5
		TempValue0  = Object.XPos
		TempValue0 -= 0x100000
		TempValue1  = Object.YPos
		TempValue1 -= 0xC0000
		DrawSpriteXY(1, TempValue0, TempValue1)

		Object.SpriteSheet = TempValue6
		Object.InkEffect = 1
		DrawSpriteFX(2, FX_INK, Object.XPos, Object.YPos)
		Object.InkEffect = 0

		if Editor.ShowGizmos == true
			Editor.DrawingOverlay = true
			TempValue0  = Object.XPos
			TempValue0 -= 0x200000
			DrawArrow(Object.XPos, Object.YPos, TempValue0, Object.YPos, 255, 255, 0)
			Editor.DrawingOverlay = false
		end if
		break
	case 4
		Object.SpriteSheet = TempValue6
		Object.InkEffect = 1
		DrawSpriteFX(2, FX_INK, Object.XPos, Object.YPos)
		Object.InkEffect = 0

		if Editor.ShowGizmos == true
			Editor.DrawingOverlay = true
			TempValue0  = Object.XPos
			TempValue0 += 0x200000
			DrawArrow(Object.XPos, Object.YPos, TempValue0, Object.YPos, 255, 255, 0)


			Editor.DrawingOverlay = false
		end if
		break
	case 5
		Object.SpriteSheet = TempValue7
		Object.InkEffect = 1
		DrawSpriteFX(4, FX_INK, Object.XPos, Object.YPos)
		Object.InkEffect = 0
		if Editor.ShowGizmos == true
			Editor.DrawingOverlay = true
			TempValue0  = Object.YPos
			TempValue0 += 0x200000
			DrawArrow(Object.XPos, Object.YPos, Object.XPos, TempValue0, 255, 255, 0)
			Editor.DrawingOverlay = false
		end if
		break
	default
		Object.SpriteSheet = TempValue5
		DrawSprite(0)
	end switch
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)	// #0 - "Script" Icon
	SpriteFrame(-4, -4, 8, 8, 10, 98)		// #1 - "Y"

	LoadSpriteSheet("Players/Sonic2.gif")
	SpriteFrame(-20, -20, 40, 40, 133, 171)	// #2 - Sonic 3D Ramp Enter
	SpriteFrame(-16, -20, 32, 40, 1, 169)	// #3 - Sonic 3D Ramp Exit

	LoadSpriteSheet("Players/Sonic3.gif")
	SpriteFrame(-20, -24, 40, 48, 1, 83)	// #4 - Sonic 3D Ramp Drop


	AddEditorVariable("type")
	SetActiveVariable("type")
	AddEnumVariable("3D Ramp Run", RAMP3D_RUN)
	AddEnumVariable("3D Ramp Exit Boost (Unused)", RAMP3D_EXIT_UNUSED)
	AddEnumVariable("3D Ramp Exit ", RAMP3D_EXIT)
	AddEnumVariable("3D Ramp (Set YBound)", RAMP3D_SET_YBOUND)
	AddEnumVariable("3D Ramp Start", RAMP3D_ENTER)
	AddEnumVariable("3D Ramp Drop", RAMP3D_DROP)
	AddEnumVariable("3D Ramp (Set YBound 2)", RAMP3D_SET_YBOUND2)
	AddEnumVariable("3D Ramp Top Run", RAMP3D_RUN_FROM_TOP)
	AddEnumVariable("3D Ramp Exit Boost", RAMP3D_EXIT_BOOST)
end sub
