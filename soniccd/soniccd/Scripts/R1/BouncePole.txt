//-----------------Sonic CD Bounce Pole Script----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.FrameTimer

// Gravity
#alias 1	:	GRAVITY_AIR

// Not directly used, but added for documentation purposes
#alias 0	:	DIRECTION_RIGHT
#alias 1	:	DIRECTION_LEFT


sub ObjectPlayerInteraction
	// Check:
	// Collision with object
	// Player is in the air
	// The vertical momentum goes downwards
	PlayerObjectCollision(C_TOUCH, -16, -8, 16, 8)
	if CheckResult == true
		if Player.Gravity == GRAVITY_AIR
			if Player.YVelocity > 0
			
#platform: Use_Origins
				Player.State = Player_State_Air_NoDropDash
#endplatform

#platform: Use_Standalone
				Player.State = Player_State_Air
#endplatform
				Player.Timer = 0
				Player.Animation = ANI_JUMPING
				FlipSign(Player.YVelocity)
				Player.YVelocity -= 0x20000

				// Vertical velocity cap
				if Player.YVelocity < -0xA0000
					Player.YVelocity = -0xA0000
				end if

				Object.Frame = 1	// Start bounce pole animation
			end if
		end if
	end if
end sub


sub ObjectDraw
	Object.Direction = Object.PropertyValue
	DrawSpriteFX(Object.Frame, FX_FLIP, Object.XPos, Object.YPos)
	if Object.Frame > 0
	
		Object.FrameTimer++
		if Object.FrameTimer == 3
			Object.FrameTimer = 0

			Object.Frame++
			if Object.Frame > 20
				Object.Frame = 0
			end if
		end if
	end if
end sub


sub ObjectStartup
	LoadSpriteSheet("R1/Objects.gif")

	SpriteFrame(-16, -8, 32, 16, 190, 34)		// #0  - Bounce Pole Still

	SpriteFrame(-16, -8, 32, 16, 190, 34)		// #1  - Bounce Pole Bounce Frame 0
	SpriteFrame(-16, -8, 32, 20, 190, 80)		// #2  - Bounce Pole Bounce Frame 1
	SpriteFrame(-16, -8, 32, 16, 190, 34)		// #3  - Bounce Pole Bounce Frame 2
	SpriteFrame(-16, -20, 32, 28, 190, 51)		// #4  - Bounce Pole Bounce Frame 3
	SpriteFrame(-16, -8, 32, 16, 190, 34)		// #5  - Bounce Pole Bounce Frame 4
	SpriteFrame(-16, -8, 32, 20, 190, 80)		// #6  - Bounce Pole Bounce Frame 5
	SpriteFrame(-16, -8, 32, 16, 190, 34)		// #7  - Bounce Pole Bounce Frame 6
	SpriteFrame(-16, -8, 32, 16, 190, 34)		// #8  - Bounce Pole Bounce Frame 7
	SpriteFrame(-16, -8, 32, 20, 190, 80)		// #9  - Bounce Pole Bounce Frame 8
	SpriteFrame(-16, -8, 32, 16, 190, 34)		// #10 - Bounce Pole Bounce Frame 9
	SpriteFrame(-16, -20, 32, 28, 190, 51)		// #11 - Bounce Pole Bounce Frame 10
	SpriteFrame(-16, -8, 32, 16, 190, 34)		// #12 - Bounce Pole Bounce Frame 11
	SpriteFrame(-16, -8, 32, 20, 190, 80)		// #13 - Bounce Pole Bounce Frame 12
	SpriteFrame(-16, -8, 32, 16, 190, 34)		// #14 - Bounce Pole Bounce Frame 13
	SpriteFrame(-16, -8, 32, 16, 190, 34)		// #15 - Bounce Pole Bounce Frame 14
	SpriteFrame(-16, -8, 32, 20, 190, 80)		// #16 - Bounce Pole Bounce Frame 15
	SpriteFrame(-16, -8, 32, 16, 190, 34)		// #17 - Bounce Pole Bounce Frame 16
	SpriteFrame(-16, -20, 32, 28, 190, 51)		// #18 - Bounce Pole Bounce Frame 17
	SpriteFrame(-16, -8, 32, 16, 190, 34)		// #19 - Bounce Pole Bounce Frame 18
	SpriteFrame(-16, -8, 32, 20, 190, 80)		// #20 - Bounce Pole Bounce Frame 19
end sub


// ========================
// Editor Subs
// ========================

sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			CheckResult = Object.PropertyValue
			CheckResult &= 1
			break
		case 0 // direction
			CheckResult = Object.PropertyValue
			CheckResult &= 1
			break
		end switch
	else
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			Object.PropertyValue = Editor.VariableValue
			Object.PropertyValue &= 1
			break
		case 0 // direction
			Object.PropertyValue = Editor.VariableValue
			Object.PropertyValue &= 1
			break
		end switch
	end if
end sub


sub RSDKDraw
	Object.Direction = Object.PropertyValue
	DrawSpriteFX(0, FX_FLIP, Object.XPos, Object.YPos)
end sub


sub RSDKLoad
	LoadSpriteSheet("R1/Objects.gif")

	SpriteFrame(-16, -8, 32, 16, 190, 34)		// #0  - Bounce Pole Still


	AddEditorVariable("direction")
	SetActiveVariable("direction")
	AddEnumVariable("Right", 0)
	AddEnumVariable("Left", 1)
end sub
