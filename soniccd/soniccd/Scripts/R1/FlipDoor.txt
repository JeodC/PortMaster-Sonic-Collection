//-----------------Sonic CD Flip Door Script------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.Timer

// Stage SFX
#alias 4	:	SFX_S_DOOR

// Property Values
#alias 0	:	HORIZONTAL_DOOR
#alias 1	:	VERTICAL_DOOR


sub ObjectPlayerInteraction
	if Object.PropertyValue == HORIZONTAL_DOOR
		PlayerObjectCollision(C_PLATFORM, -24, -4, 24, 32)
		PlayerObjectCollision(C_TOUCH, -24, -8, 24, 8)
		if CheckResult == true
			if Player.YPos > Object.YPos
				if Object.Timer == 0
					PlayStageSfx(SFX_S_DOOR, 0)
				end if

				Object.Timer = 10
			end if
		end if
	else
		if Player.XPos < Object.XPos
			PlayerObjectCollision(C_TOUCH, -16, -24, 0, 24)
			if CheckResult == true
				if Object.Timer == 0
					PlayStageSfx(SFX_S_DOOR, 0)
				end if

				Object.Timer = 10
			end if
		else
			if Object.Timer == 0
				PlayerObjectCollision(C_BOX, -24, -24, 4, 24)
			end if
		end if
	end if
end sub


sub ObjectDraw
	if Object.PropertyValue == HORIZONTAL_DOOR
		if Object.Timer > 0
			DrawSprite(1)
			Object.Timer--
		else
			DrawSprite(0)
		end if
	else
		if Object.Timer > 0
			DrawSprite(3)
			Object.Timer--
		else
			DrawSprite(2)
		end if
	end if
end sub


sub ObjectStartup
	LoadSpriteSheet("R1/Objects.gif")

	SpriteFrame(-24, -4, 48, 8, 117, 34)	// #0 - Flip Door Horizontal
	SpriteFrame(-24, -44, 8, 48, 167, 43)	// #1 - Flip Door Horizontal Open
	SpriteFrame(-4, -24, 8, 48, 176, 43)	// #2 - Flip Door Vertical
	SpriteFrame(-4, -24, 48, 8, 117, 34)	// #3 - Flip Door Vertical Open

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Flip Door]
			Object[ArrayPos0].DrawOrder = 5
		end if
		ArrayPos0++
	loop
end sub


// ========================
// Editor Subs
// ========================

sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			CheckResult = Object.PropertyValue
			CheckResult &= 1
			break
		case 0 // orientation
			CheckResult = Object.PropertyValue
			CheckResult &= 1
			break
		end switch
	else
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			Object.PropertyValue = Editor.VariableValue
			Object.PropertyValue &= 1
			break
		case 0 // orientation
			Object.PropertyValue = Editor.VariableValue
			Object.PropertyValue &= 1
			break
		end switch
	end if
end sub


sub RSDKDraw
	DrawSprite(Object.PropertyValue)
end sub


sub RSDKLoad
	LoadSpriteSheet("R1/Objects.gif")

	SpriteFrame(-24, -4, 48, 8, 117, 34)	// #0 - Flip Door Horizontal
	SpriteFrame(-4, -24, 8, 48, 176, 43)	// #1 - Flip Door Vertical


	AddEditorVariable("orientation")
	SetActiveVariable("orientation")
	AddEnumVariable("Horizontal", 0)
	AddEnumVariable("Vertical", 1)
end sub
