//---------------Sonic CD Boost Spinner Script----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0 : Object.XOriginPos // Set by Ramp3D
#alias Object.Value1 : Object.Timer
#alias Object.Value2 : Object.SpinTimer

// States
#alias 0 : BOOSTSPINNER_IDLE
#alias 1 : BOOSTSPINNER_SPIN


sub ObjectPlayerInteraction
	if Object.State == BOOSTSPINNER_IDLE
		if Object.Frame == 3
			PlayerObjectCollision(C_TOUCH, 0, -24, 128, 8)
			if CheckResult == true
				Object.State = BOOSTSPINNER_SPIN
				Object.SpinTimer = 4
				
#platform: Use_Haptics
				HapticEffect(30, 0, 0, 0)
#endplatform
			end if
		end if
	end if
end sub


sub ObjectDraw
	TempValue0   = Object.XOriginPos
	TempValue0  -= Object.XPos
	TempValue0 >>= 20

	Object.Frame = TempValue0
	if Object.Frame > 3
		Object.Frame = 3
	end if

	if Object.State == BOOSTSPINNER_SPIN
		DrawSpriteXY(Object.SpinTimer, Object.XOriginPos, Object.YPos)
		Object.Timer++
		if Object.Timer == 3
			Object.Timer = 0

			Object.SpinTimer++
			if Object.SpinTimer == 16
				Object.State = BOOSTSPINNER_IDLE
			end if
		end if
	else
		DrawSpriteXY(Object.Frame, Object.XOriginPos, Object.YPos)
	end if
end sub


sub ObjectStartup
	LoadSpriteSheet("R1/Objects2.gif")

	SpriteFrame(-24, -24, 48, 48, 1, 82)	// #0  - Boost spinner front
	SpriteFrame(-34, -24, 56, 48, 50, 82)	// #1  - Boost spinner angle 1
	SpriteFrame(-38, -24, 57, 48, 107, 82)	// #2  - Boost spinner angle 2
	SpriteFrame(-40, -24, 56, 48, 1, 131)	// #3  - Boost spinner angle 3
	
	// Spinning
	SpriteFrame(-40, -24, 56, 48, 58, 131)	// #4  - Boost spinner rotating 0
	SpriteFrame(-40, -24, 64, 48, 58, 180)	// #5  - Boost spinner rotating 1
	SpriteFrame(-40, -24, 56, 48, 1, 180)	// #6  - Boost spinner rotating 2
	SpriteFrame(-40, -24, 64, 48, 123, 180)	// #7  - Boost spinner rotating 3
	SpriteFrame(-40, -24, 56, 48, 58, 131)	// #8  - Boost spinner rotating 4
	SpriteFrame(-40, -24, 64, 48, 58, 180)	// #9  - Boost spinner rotating 5
	SpriteFrame(-40, -24, 56, 48, 1, 180)	// #10 - Boost spinner rotating 6
	SpriteFrame(-40, -24, 64, 48, 123, 180)	// #11 - Boost spinner rotating 7
	SpriteFrame(-40, -24, 56, 48, 58, 131)	// #12 - Boost spinner rotating 8
	SpriteFrame(-40, -24, 64, 48, 58, 180)	// #13 - Boost spinner rotating 9
	SpriteFrame(-40, -24, 56, 48, 1, 180)	// #14 - Boost spinner rotating 10
	SpriteFrame(-40, -24, 64, 48, 123, 180)	// #15 - Boost spinner rotating 11

	// Used to be below LoadSpriteSheet, moved here for consistency
	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Boost Spinner]
			Object[ArrayPos0].DrawOrder = 4
		end if
		
		ArrayPos0++
	loop
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("R1/Objects2.gif")
	SpriteFrame(-24, -24, 48, 48, 1, 82)	// #0  - Boost spinner front

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
