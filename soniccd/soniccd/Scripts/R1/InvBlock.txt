//---------------Sonic CD Invisible Block Script--------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0			:	Object.OnObject
#alias Object.Value2			:	Object.YOriginPos

// HUD Alias
#alias Object[24].PropertyValue	:	HUD.CurrentTimePeriod

// State
#alias 0	:	INVBLOCK_IDLE
#alias 1	:	INVBLOCK_FALL
#alias 2	:	INVBLOCK_RESTORE

// Priority
#alias 0	:	PRIORITY_BOUNDS
#alias 1	:	PRIORITY_ACTIVE

// Time Periods
#alias 0	:	TIME_PRESENT
#alias 1	:	TIME_PAST
#alias 2	:	TIME_GOOD_FUTURE
#alias 3	:	TIME_BAD_FUTURE


sub ObjectPlayerInteraction
	if Object.State < INVBLOCK_RESTORE
		PlayerObjectCollision(C_PLATFORM, -12, -12, 12, 12)
		Object.OnObject = CheckResult
	end if

	if HUD.CurrentTimePeriod > 1	// Inv Blocks in the future fall upon contact
		switch Object.State
		case INVBLOCK_IDLE
			if Object.OnObject == true
				Object.State = INVBLOCK_FALL
				Object.Priority = PRIORITY_ACTIVE
				Player.YPos += 0x20000
			end if
			break

		case INVBLOCK_FALL
			if Object.OnObject == true
				Player.YPos += 0x20000
			end if
			break
		end switch
	end if
end sub


sub ObjectDraw
	switch Object.State
	case INVBLOCK_FALL
		Object.YPos += 0x20000
		if Object.OutOfBounds == true
			Object.State = INVBLOCK_RESTORE
			Object.YPos = Object.YOriginPos
		end if
		break

	case INVBLOCK_RESTORE
		if Object.OutOfBounds == true
			Object.State = INVBLOCK_IDLE
			Object.Priority = PRIORITY_BOUNDS
		end if
		break

	end switch

	switch HUD.CurrentTimePeriod	// they are invisible only in the present
	case TIME_PRESENT
		if Object.OnObject == true
			if Object.Frame < 24
				Object.Frame++
			end if
		else
			if Object.Frame > 0
				Object.Frame--
			end if
		end if
		DrawSprite(Object.Frame)
		break

	case TIME_PAST
		DrawSprite(5)
		break

	case TIME_GOOD_FUTURE
	case TIME_BAD_FUTURE
		if Object.State < INVBLOCK_RESTORE
			DrawSprite(5)
		end if
		break

	end switch
end sub


sub ObjectStartup
	LoadSpriteSheet("R1/Objects.gif")
	
	SpriteFrame(0, 0, 4, 4, 68, 162)		// #0  - Cheat Sprite
	SpriteFrame(-12, -12, 24, 24, 93, 142)	// #1  - Inv Block Appearing Frame 0
	SpriteFrame(-12, -12, 24, 24, 93, 142)	// #2  - Inv Block Appearing Frame 1
	SpriteFrame(-12, -12, 24, 24, 93, 142)	// #3  - Inv Block Appearing Frame 2
	SpriteFrame(-12, -12, 24, 24, 93, 142)	// #4  - Inv Block Appearing Frame 3
	SpriteFrame(-12, -12, 24, 24, 68, 142)	// #5  - Inv Block Appearing Frame 4 / Main Frame in other time zones
	SpriteFrame(-12, -12, 24, 24, 68, 142)	// #6  - Inv Block Appearing Frame 5
	SpriteFrame(-12, -12, 24, 24, 68, 142)	// #7  - Inv Block Appearing Frame 6
	SpriteFrame(-12, -12, 24, 24, 68, 142)	// #8  - Inv Block Appearing Frame 7
	SpriteFrame(-12, -12, 24, 24, 118, 142)	// #9  - Inv Block Appearing Frame 8
	SpriteFrame(-12, -12, 24, 24, 118, 142)	// #10 - Inv Block Appearing Frame 9
	SpriteFrame(-12, -12, 24, 24, 118, 142)	// #11 - Inv Block Appearing Frame 10
	SpriteFrame(-12, -12, 24, 24, 68, 142)	// #12 - Inv Block Appearing Frame 11
	SpriteFrame(-12, -12, 24, 24, 68, 142)	// #13 - Inv Block Appearing Frame 12
	SpriteFrame(-12, -12, 24, 24, 68, 142)	// #14 - Inv Block Appearing Frame 13
	SpriteFrame(-12, -12, 24, 24, 143, 142)	// #15 - Inv Block Appearing Frame 14
	SpriteFrame(-12, -12, 24, 24, 143, 142)	// #16 - Inv Block Appearing Frame 15
	SpriteFrame(-12, -12, 24, 24, 143, 142)	// #17 - Inv Block Appearing Frame 16
	SpriteFrame(-12, -12, 24, 24, 68, 142)	// #18 - Inv Block Appearing Frame 17
	SpriteFrame(-12, -12, 24, 24, 68, 142)	// #19 - Inv Block Appearing Frame 18
	SpriteFrame(-12, -12, 24, 24, 68, 142)	// #20 - Inv Block Appearing Frame 19
	SpriteFrame(-12, -12, 24, 24, 168, 142)	// #21 - Inv Block Appearing Frame 20
	SpriteFrame(-12, -12, 24, 24, 168, 142)	// #22 - Inv Block Appearing Frame 21
	SpriteFrame(-12, -12, 24, 24, 168, 142)	// #23 - Inv Block Appearing Frame 22
	SpriteFrame(-12, -12, 24, 24, 68, 142)	// #24 - Inv Block Appearing Frame 23 / Main Frame in the Present

	// Used to be below LoadSpriteSheet, moved here for consistency
	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Invisible Block]
			Object[ArrayPos0].YOriginPos = Object[ArrayPos0].YPos
		end if
		ArrayPos0++
	loop

end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("R1/Objects.gif")
	SpriteFrame(-12, -12, 24, 24, 68, 142)	// #0 - Inv Block Appearing Frame 23 / Main Frame in the Present

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
