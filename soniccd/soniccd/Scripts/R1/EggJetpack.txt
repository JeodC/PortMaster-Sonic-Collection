//---------------Sonic CD Eggman Jetpack Script---------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0		:	Object.Timer
#alias Object.Value3		:	Object.Oscillation
#alias Object.Value4		:	Object.YOriginPos

// BossFace Aliases
#alias Object[-1].State		:	BossFace.State
#alias Object[-1].Rotation	:	BossFace.Timer

// States
#alias 0	:	R1_EGGMAN_FLY_UP
#alias 2	:	R1_EGGMAN_ESCAPE

// Boss Face States
#alias 5	:	R1_EGGMAN_RESTORE_BOUNDS


sub ObjectMain
	switch Object.State
	case R1_EGGMAN_FLY_UP
		if Object.Timer < 64
			Object.Timer++
			if Object.Timer > 27
				Object.XPos += 0x8000

				Object.YPos -= 0x20000
			end if
		else
			Object.Timer = 0
			Object.State = R1_EGGMAN_ESCAPE
			Object.YOriginPos = Object.YPos
		end if
		break

	case R1_EGGMAN_ESCAPE
		Object.Oscillation += 3
		Object.Oscillation &= 255
		Sin256(TempValue0, Object.Oscillation)
		TempValue0 <<= 11

		Object.XPos += 0x28000

		Object.YPos  = Object.YOriginPos
		Object.YPos -= TempValue0

		if Object.OutOfBounds == true
			Object.Type = TypeName[Blank Object]
			BossFace.State = R1_EGGMAN_RESTORE_BOUNDS
			BossFace.Timer = 0
		end if
		break
	end switch
end sub


sub ObjectDraw
	TempValue0   = Object.Frame
	TempValue0 >>= 2
	TempValue0  += Object.State

	DrawSprite(TempValue0)

	Object.Frame++
	Object.Frame &= 7
end sub


sub ObjectStartup
	LoadSpriteSheet("R1/Objects3.gif")

	SpriteFrame(-28, -24, 56, 56, 1, 107)	// #0 - Eggman Jetpack Frame 0
	SpriteFrame(-28, -24, 56, 56, 1, 164)	// #1 - Eggman Jetpack Frame 1
	SpriteFrame(-28, -24, 56, 56, 58, 107)	// #2 - Eggman Jetpack Frame 2
	SpriteFrame(-28, -24, 56, 56, 58, 164)	// #3 - Eggman Jetpack Frame 3
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("R1/Objects3.gif")
	SpriteFrame(-28, -24, 56, 56, 1, 107)	// #0 - Eggman Jetpack Frame 0

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
