//-------------------Sonic CD Anton Script--------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// ========================
// Aliases
// ========================

#alias Object.PropertyValue : Object.Quality

#alias Object.Value0 : Object.Timer

// States
#alias 0 : ANTON_MOVING_LEFT
#alias 1 : ANTON_MOVING_RIGHT

// Direction Aliases
#alias 0 : FLIP_NONE
#alias 1 : FLIP_X

// Collision Side Aliases
#alias 0 : CSIDE_FLOOR

// Badnik Quality (Property Value) Aliases
#alias 0 : QUALITY_GOOD
#alias 1 : QUALITY_BAD


// ========================
// Events
// ========================

sub ObjectMain
	if Object.Quality == QUALITY_GOOD
		Object.Frame += 2
		
		switch Object.State
		case ANTON_MOVING_LEFT
			Object.XPos -= 0x10000

			Object.Timer--
			if Object.Timer == -128
				Object.State = ANTON_MOVING_RIGHT
				Object.Direction = FLIP_X
			end if

			ObjectTileGrip(CSIDE_FLOOR, 0, 20, 0)
			if CheckResult == false
				Object.State = ANTON_MOVING_RIGHT
				Object.Timer = -128
				Object.Direction = FLIP_X
			end if
			break

		case ANTON_MOVING_RIGHT
			Object.XPos += 0x10000

			Object.Timer++
			if Object.Timer == 128
				Object.State = ANTON_MOVING_LEFT
				Object.Direction = FLIP_NONE
			end if

			ObjectTileGrip(CSIDE_FLOOR, 0, 20, 0)
			if CheckResult == false
				Object.State = ANTON_MOVING_LEFT
				Object.Timer = 128
				Object.Direction = FLIP_NONE
			end if
			break
			
		end switch
	else
		Object.Frame++
		
		switch Object.State
		case ANTON_MOVING_LEFT
			Object.XPos -= 0x8000

			Object.Timer--
			if Object.Timer == -256
				Object.State = ANTON_MOVING_RIGHT
				Object.Direction = FLIP_X
			end if

			ObjectTileGrip(CSIDE_FLOOR, 0, 20, 0)
			if CheckResult == false
				Object.State = ANTON_MOVING_RIGHT
				Object.Timer = -256
				Object.Direction = FLIP_X
			end if
			break

		case ANTON_MOVING_RIGHT
			Object.XPos += 0x8000
			
			Object.Timer++
			if Object.Timer == 256
				Object.State = ANTON_MOVING_LEFT
				Object.Direction = FLIP_NONE
			end if

			ObjectTileGrip(CSIDE_FLOOR, 0, 20, 0)
			if CheckResult == false
				Object.State = ANTON_MOVING_LEFT
				Object.Timer = 256
				Object.Direction = FLIP_NONE
			end if
			break

		end switch

	end if

	Object.Frame &= 15
	CallFunction(StageSetup_CheckGoodFuture)	// Check if it should be a flower
end sub


sub ObjectPlayerInteraction
#platform: Use_Origins
	PlayerObjectCollision(C_ENEMY, -12, -18, 12, 20)
#endplatform
#platform: Use_Standalone
	PlayerObjectCollision(C_TOUCH, -12, -18, 12, 20)
#endplatform
	if CheckResult == true
		CallFunction(Player_BadnikBreak)
	end if
end sub


sub ObjectDraw
	TempValue0   = Object.Frame
	TempValue0 >>= 3
	if Object.Quality == QUALITY_GOOD
		TempValue0 += 4
		DrawSpriteFX(TempValue0, FX_FLIP, Object.XPos, Object.YPos)	// Wheel
		TempValue0 -= 4
		DrawSpriteFX(TempValue0, FX_FLIP, Object.XPos, Object.YPos)	// Anton
		TempValue0 += 6
		DrawSpriteFX(TempValue0, FX_FLIP, Object.XPos, Object.YPos)	// Smoke Puff
	else
		TempValue0 += 4
		DrawSpriteFX(TempValue0, FX_FLIP, Object.XPos, Object.YPos)	// Wheel
		TempValue0 -= 2
		DrawSpriteFX(TempValue0, FX_FLIP, Object.XPos, Object.YPos)	// Anton
		TempValue0 += 4
		DrawSpriteFX(TempValue0, FX_FLIP, Object.XPos, Object.YPos)	// Smoke Puff
	end if
end sub


sub ObjectStartup
	LoadSpriteSheet("R1/Objects.gif")

	SpriteFrame(-12, -19, 24, 32, 1, 1)		// #0 - Anton Frame 0
	SpriteFrame(-12, -20, 24, 32, 1, 1)		// #1 - Anton Frame 1
	SpriteFrame(-12, -19, 24, 32, 26, 1)	// #2 - Old Anton Frame 0
	SpriteFrame(-12, -20, 24, 32, 26, 1)	// #3 - Old Anton Frame 1

	SpriteFrame(-8, 4, 16, 16, 1, 34)		// #4 - Anton Wheel Frame 0
	SpriteFrame(-8, 4, 16, 16, 18, 34)		// #5 - Anton Wheel Frame 1

	SpriteFrame(14, -4, 8, 8, 35, 34)		// #6 - Anton Smoke Puff Frame 0
	SpriteFrame(18, -5, 8, 8, 35, 34)		// #7 - Anton Smoke Puff Frame 1
end sub


// ========================
// Editor Subs
// ========================

sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
		case 0 // quality
			GetBit(CheckResult, Object.Quality, 0)
			break
		end switch
	else
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
		case 0 // quality
			Object.Quality  = Editor.VariableValue
			Object.Quality &= QUALITY_BAD
			break
		end switch
	end if
end sub


sub RSDKDraw
	// Wheel
	Object.Frame  = Object.Quality
	Object.Frame += 2
	DrawSprite(Object.Frame)

	// Anton
	DrawSprite(Object.Quality)
end sub


sub RSDKLoad
	LoadSpriteSheet("R1/Objects.gif")

	SpriteFrame(-12, -19, 24, 32, 1, 1)		// #0 - Anton
	SpriteFrame(-12, -19, 24, 32, 26, 1)	// #1 - Old Anton

	SpriteFrame(-8, 4, 16, 16, 1, 34)		// #2 - Anton Wheel
	SpriteFrame(-8, 4, 16, 16, 18, 34)		// #3 - Old Anton Wheel

	AddEditorVariable("quality")
	SetActiveVariable("quality")
	AddEnumVariable("Good", QUALITY_GOOD)
	AddEnumVariable("Bad",  QUALITY_BAD)
end sub
