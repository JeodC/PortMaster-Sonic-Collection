//------------------Sonic CD Pod Seed Script------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0 		:	Object.Timer
#alias Object.Value1 		:	Object.ParentPod
#alias Object.Value2 		:	Object.XVelocity
#alias Object.Value3 		:	Object.YVelocity

// Flower States
#alias 2	:	FLOWER_SEED_PLANTED_1

// Collision Sides
#alias 0	:	CSIDE_FLOOR

// Player
#alias 0	:	PLAYER_SONIC_A
#alias 1	:	PLAYER_TAILS_A

// Control Mode
#alias -1	:	CONTROLMODE_NONE

// Property Values
#alias 1	:	TRIGGER_RESULTS


sub ObjectMain

	// Animate the seed
	Object.Frame  = Object.Timer
	Object.Frame /= 3
	Object.Timer++
	Object.Timer %= 18
	
	// Update movements
	Object.XPos += Object.XVelocity
	Object.YPos += Object.YVelocity
	Object.YVelocity += 0x4000

	// Before landing, the object has to be going at a rate of at least 6px per frame
	if Object.YVelocity > 0x60000
		ObjectTileCollision(CSIDE_FLOOR, 0, 8, 0)
		if CheckResult == true
			if Object.PropertyValue == TRIGGER_RESULTS
				ArrayPos0 = Object.ParentPod
				Object[ArrayPos0].State++
				Player.ControlMode = CONTROLMODE_NONE
			end if

			// Grow into a Flower
			ResetObjectEntity(Object.EntityNo, Flower_TypeNo, 0, Object.XPos, Object.YPos)
			Object.YPos += 0x80000
			Object.State = FLOWER_SEED_PLANTED_1
			Object.Frame = 2
			Object.DrawOrder = 4
		end if
	end if
	
end sub


sub ObjectDraw
	DrawSprite(Object.Frame)
end sub


sub ObjectStartup
	if Stage.PlayerListPos == PLAYER_SONIC_A
		LoadSpriteSheet("Global/Items2.gif")
	end if
	if Stage.PlayerListPos == PLAYER_TAILS_A
		LoadSpriteSheet("Global/Items2_t.gif")
	end if
#platform: Use_Origins
	if Stage.PlayerListPos == PLAYER_KNUCKLES
		LoadSpriteSheet("Global/Items2_k.gif")
	end if
	if Stage.PlayerListPos == PLAYER_AMY
		LoadSpriteSheet("Global/Items2_a.gif")
	end if
#endplatform
	
	// Seed Frames
	SpriteFrame(-8, -8, 16, 16, 83, 66)
	SpriteFrame(-8, -8, 16, 16, 83, 83)
	SpriteFrame(-8, -8, 16, 16, 83, 100)
	SpriteFrame(-8, -8, 16, 16, 83, 117)
	SpriteFrame(-8, -8, 16, 16, 83, 83)
	SpriteFrame(-8, -8, 16, 16, 83, 134)
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Items2.gif")
	SpriteFrame(-8, -8, 16, 16, 83, 66)
	
	// Although used by the object, it's not to be set from the editor
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
