//-------------------Sonic CD UFO Script----------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.Timer
#alias Object.Value1	:	Object.YOffSet
#alias Object.Value2	:	Object.YVelocity
#alias Object.Value3	:	Object.Wobble
#alias Object.Value4	:	Object.XVelocity

// States
#alias 0	:	UFO_WOBBLE1
#alias 1	:	UFO_DESCEND
#alias 2	:	UFO_ASCEND
#alias 3	:	UFO_WOBBLE2

// Property Values
#alias 0	:	YELLOW_UFO
#alias 3	:	GREEN_UFO


sub ObjectMain
	if Object.Direction == FACING_RIGHT
		Object.XPos += Object.XVelocity
	else
		Object.XPos -= Object.XVelocity
	end if

	switch Object.State
	case UFO_WOBBLE1
	case UFO_WOBBLE2
		Object.Frame   = 0
		Object.Wobble += 8
		Object.Wobble &= 511
		Sin(Object.YPos, Object.Wobble)
		Object.YPos  <<= 9
		Object.YPos   += Object.YOffSet
		break

	case UFO_DESCEND
		Object.YPos   += Object.YVelocity
		Object.Frame   = 1
		Object.YOffSet = Object.YPos
		if Object.YPos > 0xD00000
			Object.State = UFO_ASCEND
		end if
		break

	case UFO_ASCEND
		Object.YPos   -= Object.YVelocity
		Object.Frame   = 2
		Object.YOffSet = Object.YPos
		if Object.YPos < 0x200000
			Object.State = UFO_DESCEND
		end if
		break

	end switch

	if Object.Timer > 0
		Object.Timer--
	else
		Object.Value1 = Object.YPos
		Rand(Object.State, 3)
		Rand(Object.Timer, 90)
		Rand(Object.XVelocity, 6)
		Object.XVelocity++
		Object.XVelocity <<= 14
		Rand(Object.YVelocity, 6)
		Object.YVelocity++
		Object.YVelocity <<= 15
	end if

	if Object.OutOfBounds == true
		Object.Type = TypeName[Blank Object]
	end if
end sub


sub ObjectDraw
	TempValue0  = Object.Frame
	TempValue0 += Object.PropertyValue
	DrawSprite(TempValue0)
end sub


sub ObjectStartup
	LoadSpriteSheet("DAGarden/Objects.gif")

	SpriteFrame(-12, -12, 24, 24, 354, 804)		// #0 - Yellow UFO Frame 0
	SpriteFrame(-12, -12, 24, 24, 379, 804)		// #1 - Yellow UFO Frame 1
	SpriteFrame(-12, -12, 24, 24, 404, 804)		// #2 - Yellow UFO Frame 2
	SpriteFrame(-12, -12, 24, 24, 354, 829)		// #3 - Green UFO Frame 0
	SpriteFrame(-12, -12, 24, 24, 379, 829)		// #4 - Green UFO Frame 1
	SpriteFrame(-12, -12, 24, 24, 404, 829)		// #5 - Green UFO Frame 2
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("DAGarden/Objects.gif")
	SpriteFrame(-12, -12, 24, 24, 354, 804)		// #0 - Yellow UFO Frame 0

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
