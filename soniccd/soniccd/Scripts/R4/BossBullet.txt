//----------------Sonic CD Boss Bullet Script-----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.Timer
#alias Object.Value1	:	Object.XVelocity
#alias Object.Value2	:	Object.YVelocity
#alias Object.Value5	:	Object.BossEntity
#alias Object.Value7	:	Object.Animation

// R4 Eggman aliases
#alias Object.Value7	:	Boss.Animation
#alias Object.XPos		:	Boss.XPos
#alias Object.YPos		:	Boss.YPos
#alias Object.State		:	Boss.State

// States
#alias 0	:	BOSS_BULLET_SETUP
#alias 1	:	BOSS_BULLET_CHARGE
#alias 2	:	BOSS_BULLET_SHOT

// R4 Eggman states
#alias 13	:	R4_EGG_BUBBLE_MOVE_TO_L

// R4 Eggman animations
#alias 1	:	EGGANI_LAUGH


sub ObjectMain
	switch Object.State
	case BOSS_BULLET_SETUP
		if Object.Timer < 56
			Object.Timer++
		else
			switch Object.PropertyValue
			case 0
				Object.XVelocity = -0x14000
				Object.YVelocity =  0x14000
				break

			case 1
				Object.XVelocity =  0
				Object.YVelocity =  0x1C000
				break

			case 2
				Object.XVelocity =  0x10000
				Object.YVelocity =  0x18000
				break

			case 3
				Object.XVelocity =  0x14000
				Object.YVelocity =  0x14000
				break

			case 4
				Object.XVelocity =  0x14000
				Object.YVelocity =  0x14000
				break

			case 5
				Object.XVelocity =  0
				Object.YVelocity =  0x1C000
				break

			case 6
				Object.XVelocity = -0x10000
				Object.YVelocity =  0x18000
				break

			case 7
				Object.XVelocity = -0x14000
				Object.YVelocity =  0x14000
				break

			end switch

			Object.Timer = 0
			Object.State++
		end if

		ArrayPos0 = Object.BossEntity

		Object.XPos = Boss[ArrayPos0].XPos
		Object.YPos = Boss[ArrayPos0].YPos

		switch Object.PropertyValue
		case 0
			Object.XPos -= 0x1D0000
			Object.YPos += 0x170000
			break

		case 1
			Object.XPos -= 0x60000
			Object.YPos += 0x200000
			break

		case 2
			Object.XPos += 0x110000
			Object.YPos += 0x1D0000
			break

		case 3
			Object.XPos += 0x1D0000
			Object.YPos += 0x110000
			break

		case 4
			Object.XPos += 0x1D0000
			Object.YPos += 0x170000
			break

		case 5
			Object.XPos += 0x60000
			Object.YPos += 0x200000
			break

		case 6
			Object.XPos -= 0x110000
			Object.YPos += 0x1D0000
			break

		case 7
			Object.XPos -= 0x1D0000
			Object.YPos += 0x110000
			break

		end switch
		break

	case BOSS_BULLET_CHARGE
		Object.XPos += Object.XVelocity
		Object.YPos += Object.YVelocity
		if Object.Timer < 60
			Object.Timer++
		else
			Object.Timer = 0
			Object.DrawOrder = 3
			Object.State++
		end if
		break

	case BOSS_BULLET_SHOT
		Object.XPos += Object.XVelocity
		Object.YPos += Object.YVelocity
		break

	end switch

	if Object.OutOfBounds == true
		Object.Type = TypeName[Blank Object]
	end if
	
	ArrayPos0 = Object.BossEntity
	if Boss[ArrayPos0].State > R4_EGG_BUBBLE_MOVE_TO_L
		Object.Type = TypeName[Blank Object]
	end if
end sub


sub ObjectPlayerInteraction
	if Object.State > BOSS_BULLET_SETUP
		PlayerObjectCollision(C_TOUCH, -4, -4, 4, 4)
		if CheckResult == true
			CallFunction(Player_Hit)
			if Player.State == Player_State_GotHit
				ArrayPos0 = Object.BossEntity
				Boss[ArrayPos0].Animation = EGGANI_LAUGH
			end if
		end if
	end if
end sub


sub ObjectDraw
	TempValue0   = Object.Frame
	TempValue0 >>= 1
	DrawSprite(TempValue0)

	Object.Frame++
	Object.Frame &= 3
end sub


sub ObjectStartup
	LoadSpriteSheet("R4/Objects2.gif")

	SpriteFrame(-4, -4, 8, 8, 52, 35)	// #0 - Boss Bullet frame 0
	SpriteFrame(-4, -4, 8, 8, 52, 44)	// #1 - Boss Bullet frame 1
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("R4/Objects2.gif")

	SpriteFrame(-4, -4, 8, 8, 52, 35)	// #0 - Boss Bullet

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
