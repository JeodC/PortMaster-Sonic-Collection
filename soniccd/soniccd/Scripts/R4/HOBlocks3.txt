//------------Sonic CD Horizontal Blocks 3 Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value1			:	Object.XDrawPos
#alias Object.Value2			:	Object.XOffSet
#alias Object.Value3			:	Object.Angle
#alias Object.Value4			:	Object.AgainstBlock

// HUD alias
#alias Object[24].PropertyValue	:	HUD.CurrentTimePeriod

// Property Values
#alias 0	:	ONE_BLOCK
#alias 1	:	TWO_BLOCKS
#alias 2	:	THREE_BLOCKS
#alias 3	:	FOUR_BLOCKS

// Collision
#alias 1	:	PLAYER_COL_FLOOR
#alias 2	:	PLAYER_COL_LWALL
#alias 3	:	PLAYER_COL_RWALL

// Time Periods
#alias 0	:	TIME_PRESENT
#alias 1	:	TIME_PAST
#alias 2	:	TIME_GOOD_FUTURE
#alias 3	:	TIME_BAD_FUTURE


sub ObjectMain
	Object.Angle   = Oscillation
	Object.Angle  += Object.PropertyValue
	Object.Angle <<= 1
	Cos(Object.XOffSet, Object.Angle)

	switch Object.PropertyValue
	case ONE_BLOCK
		Object.XOffSet <<= 11
		break
	case TWO_BLOCKS
		Object.XOffSet <<= 12
		break
	case THREE_BLOCKS
		Object.XOffSet  *= 0x1800
		break
	case FOUR_BLOCKS
		Object.XOffSet <<= 13
		break
	end switch

	Object.XOffSet += Object.XPos
	Object.XOffSet &= 0xFFFF0000 // Truncate the value
	Object.XOffSet -= Object.XDrawPos
	Object.AgainstBlock = 0
end sub


sub ObjectPlayerInteraction
	TempValue0  = Object.XPos
	Object.XPos = Object.XDrawPos
	
	switch Object.PropertyValue
	case ONE_BLOCK
		PlayerObjectCollision(C_BOX, -16, -16, 16, 16)
		break
	case TWO_BLOCKS
		PlayerObjectCollision(C_BOX, -32, -16, 32, 16)
		break
	case THREE_BLOCKS
		PlayerObjectCollision(C_BOX, -48, -16, 48, 16)
		break
	case FOUR_BLOCKS
		PlayerObjectCollision(C_BOX, -64, -16, 64, 16)
		break
	end switch

	Object.XPos = TempValue0

	switch CheckResult
	case PLAYER_COL_FLOOR
		Player.XPos += Object.XOffSet
		break

	case PLAYER_COL_LWALL
		PlayerObjectCollision(C_BOX, -160, -16, -128, 16)
		if CheckResult == PLAYER_COL_RWALL
			CallFunction(Player_Kill)
		end if
		break
	end switch
end sub


sub ObjectDraw
	switch Object.PropertyValue
	case ONE_BLOCK
		Cos(Object.XDrawPos, Object.Angle)
		Object.XDrawPos <<= 11
		Object.XDrawPos  += Object.XPos
		Object.XDrawPos  &= 0xFFFF0000 // Truncate the value
		DrawSpriteXY(1, Object.XDrawPos, Object.YPos)
		break

	case TWO_BLOCKS
		Cos(Object.XDrawPos, Object.Angle)
		Object.XDrawPos <<= 12
		Object.XDrawPos  += Object.XPos
		Object.XDrawPos  &= 0xFFFF0000 // Truncate the value
		DrawSpriteXY(4, Object.XDrawPos, Object.YPos)
		DrawSpriteXY(5, Object.XDrawPos, Object.YPos)
		break

	case THREE_BLOCKS
		Cos(Object.XDrawPos, Object.Angle)
		Object.XDrawPos *= 0x1800
		Object.XDrawPos += Object.XPos
		Object.XDrawPos &= 0xFFFF0000 // Truncate the value
		DrawSpriteXY(0, Object.XDrawPos, Object.YPos)
		DrawSpriteXY(1, Object.XDrawPos, Object.YPos)
		DrawSpriteXY(2, Object.XDrawPos, Object.YPos)
		break

	case FOUR_BLOCKS
		Cos(Object.XDrawPos, Object.Angle)
		Object.XDrawPos <<= 13
		Object.XDrawPos  += Object.XPos
		Object.XDrawPos  &= 0xFFFF0000 // Truncate the value
		DrawSpriteXY(3, Object.XDrawPos, Object.YPos)
		DrawSpriteXY(4, Object.XDrawPos, Object.YPos)
		DrawSpriteXY(5, Object.XDrawPos, Object.YPos)
		DrawSpriteXY(6, Object.XDrawPos, Object.YPos)
		break
	end switch
end sub


sub ObjectStartup
	switch HUD.CurrentTimePeriod
	case TIME_PRESENT
		LoadSpriteSheet("R4/Objects.gif")
		TempValue0 = 163
		TempValue1 = 1
		TempValue2 = 196
		break

	case TIME_PAST
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 157
		TempValue2 = 34
		break

	case TIME_GOOD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 190
		TempValue2 = 34
		break

	case TIME_BAD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 223
		TempValue2 = 34
		break
	end switch

	// R4 Blocks
	SpriteFrame(-48, -16, 32, 32, TempValue0, TempValue1)
	SpriteFrame(-16, -16, 32, 32, TempValue0, TempValue1)
	SpriteFrame(16, -16, 32, 32, TempValue0, TempValue1)
	SpriteFrame(-64, -16, 32, 32, TempValue0, TempValue1)
	SpriteFrame(-32, -16, 32, 32, TempValue0, TempValue1)
	SpriteFrame(0, -16, 32, 32, TempValue0, TempValue1)
	SpriteFrame(32, -16, 32, 32, TempValue0, TempValue1)
end sub


// ========================
// Editor Subs
// ========================

sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			CheckResult = Object.PropertyValue
			break
		case 0 // blocks
			CheckResult = Object.PropertyValue
			break
		end switch
	else
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			Object.PropertyValue = Editor.VariableValue
			break
		case 0 // blocks
			Object.PropertyValue = Editor.VariableValue
			break
		end switch
	end if
end sub


sub RSDKDraw
	TempValue2   = Object.PropertyValue
	TempValue2 <<= 1
		
	TempValue1  = Object.PropertyValue
	TempValue1++
	TempValue1 *= 0x100000
	FlipSign(TempValue1)
	TempValue1 += Object.XPos
	
	switch Object.PropertyValue
	case ONE_BLOCK
		Cos(TempValue0, TempValue2)
		TempValue0 <<= 11
		TempValue0  += Object.XPos
		TempValue0  &= 0xFFFF0000 // Truncate the value
		DrawSpriteXY(1, TempValue0, Object.YPos)
		DrawArrow(TempValue0, Object.YPos, TempValue1, Object.YPos, 255, 255, 0)

		if Editor.ShowGizmos == true
			Editor.DrawingOverlay = true
			TempValue4  = Object.XPos
			TempValue4 -= 0x200000
			TempValue3  = Object.YPos
			TempValue3 -= 0x100000
			DrawRectOutline(TempValue4, TempValue3, 0x40, 0x20, 255, 255, 255, 255)
			DrawRectOutline(TempValue1, TempValue3, 0x20, 0x20, 255, 255, 0, 255)
			DrawArrow(TempValue1, Object.YPos, TempValue0, Object.YPos, 255, 255, 255)
			Editor.DrawingOverlay = false
		end if
		break

	case TWO_BLOCKS
		Cos(TempValue0, TempValue2)
		TempValue0 <<= 12
		TempValue0  += Object.XPos
		TempValue0  &= 0xFFFF0000 // Truncate the value
		DrawSpriteXY(4, TempValue0, Object.YPos)
		DrawSpriteXY(5, TempValue0, Object.YPos)
		
		DrawArrow(TempValue0, Object.YPos, TempValue1, Object.YPos, 255, 255, 0)

		if Editor.ShowGizmos == true
			Editor.DrawingOverlay = true
			TempValue4  = Object.XPos
			TempValue4 -= 0x400000
			TempValue3  = Object.YPos
			TempValue3 -= 0x100000
			DrawRectOutline(TempValue4, TempValue3, 0x80, 0x20, 255, 255, 255, 255)
			DrawRectOutline(TempValue1, TempValue3, 0x40, 0x20, 255, 255, 0, 255)
			DrawArrow(TempValue1, Object.YPos, TempValue0, Object.YPos, 255, 255, 255)
			Editor.DrawingOverlay = false
		end if
		break

	case THREE_BLOCKS
		Cos(TempValue0, TempValue2)
		TempValue0 *= 0x1800
		TempValue0 += Object.XPos
		TempValue0 &= 0xFFFF0000 // Truncate the value
		DrawSpriteXY(0, TempValue0, Object.YPos)
		DrawSpriteXY(1, TempValue0, Object.YPos)
		DrawSpriteXY(2, TempValue0, Object.YPos)
		
		DrawArrow(TempValue0, Object.YPos, TempValue1, Object.YPos, 255, 255, 0)

		if Editor.ShowGizmos == true
			Editor.DrawingOverlay = true
			TempValue4  = Object.XPos
			TempValue4 -= 0x600000
			TempValue3  = Object.YPos
			TempValue3 -= 0x100000
			DrawRectOutline(TempValue4, TempValue3, 0x80, 0x20, 255, 255, 255, 255)
			DrawRectOutline(TempValue1, TempValue3, 0x60, 0x20, 255, 255, 0, 255)
			DrawArrow(TempValue1, Object.YPos, TempValue0, Object.YPos, 255, 255, 255)
			Editor.DrawingOverlay = false
		end if
		break

	case FOUR_BLOCKS
		Cos(TempValue0, TempValue2)
		TempValue0 <<= 13
		TempValue0  += Object.XPos
		TempValue0  &= 0xFFFF0000 // Truncate the value
		DrawSpriteXY(3, TempValue0, Object.YPos)
		DrawSpriteXY(4, TempValue0, Object.YPos)
		DrawSpriteXY(5, TempValue0, Object.YPos)
		DrawSpriteXY(6, TempValue0, Object.YPos)
		
		DrawArrow(TempValue0, Object.YPos, TempValue1, Object.YPos, 255, 255, 0)

		if Editor.ShowGizmos == true
			Editor.DrawingOverlay = true
			TempValue4  = Object.XPos
			TempValue4 -= 0x800000
			TempValue3  = Object.YPos
			TempValue3 -= 0x100000
			DrawRectOutline(TempValue4, TempValue3, 0xC0, 0x20, 255, 255, 255, 255)
			DrawRectOutline(TempValue1, TempValue3, 0x80, 0x20, 255, 255, 0, 255)
			DrawArrow(TempValue1, Object.YPos, TempValue0, Object.YPos, 255, 255, 255)
			Editor.DrawingOverlay = false
		end if
		break
	end switch
end sub


sub RSDKLoad
	CallFunction(EditorHelpers_FindTimePeriod)
	switch CheckResult
	case TIME_PRESENT
		LoadSpriteSheet("R4/Objects.gif")
		TempValue0 = 163
		TempValue1 = 1
		TempValue2 = 196
		break

	case TIME_PAST
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 157
		TempValue2 = 34
		break

	case TIME_GOOD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 190
		TempValue2 = 34
		break

	case TIME_BAD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 223
		TempValue2 = 34
		break
	end switch

	// R4 Blocks
	SpriteFrame(-48, -16, 32, 32, TempValue0, TempValue1)
	SpriteFrame(-16, -16, 32, 32, TempValue0, TempValue1)
	SpriteFrame(16, -16, 32, 32, TempValue0, TempValue1)
	SpriteFrame(-64, -16, 32, 32, TempValue0, TempValue1)
	SpriteFrame(-32, -16, 32, 32, TempValue0, TempValue1)
	SpriteFrame(0, -16, 32, 32, TempValue0, TempValue1)
	SpriteFrame(32, -16, 32, 32, TempValue0, TempValue1)

	AddEditorVariable("blocks")
	SetActiveVariable("blocks")
	AddEnumVariable("One block", ONE_BLOCK)
	AddEnumVariable("Two blocks", TWO_BLOCKS)
	AddEnumVariable("Three blocks", THREE_BLOCKS)
	AddEnumVariable("Four blocks", FOUR_BLOCKS)
end sub
