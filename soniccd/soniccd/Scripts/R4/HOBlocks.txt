//--------------Sonic CD Horizontal Blocks Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value1			:	Object.XDrawPos
#alias Object.Value2			:	Object.XOffSet
#alias Object.Value3			:	Object.Angle
#alias Object.Value4			:	Object.AgainstBlock

// HUD alias
#alias Object[24].PropertyValue	:	HUD.CurrentTimePeriod

// Time Periods
#alias 0	:	TIME_PRESENT
#alias 1	:	TIME_PAST
#alias 2	:	TIME_GOOD_FUTURE
#alias 3	:	TIME_BAD_FUTURE

// Collision
#alias 1	:	PLAYER_COL_FLOOR
#alias 2	:	PLAYER_COL_LWALL
#alias 3	:	PLAYER_COL_RWALL


sub ObjectMain
	Object.Angle   = Oscillation
	Object.Angle  += Object.PropertyValue
	Object.Angle <<= 1

	Cos(Object.XOffSet, Object.Angle)
	Object.XOffSet <<= 12
	Object.XOffSet  += Object.XPos
	Object.XOffSet  &= 0xFFFF0000 // Truncate the value
	Object.XOffSet  -= Object.XDrawPos

	Object.AgainstBlock = false
end sub


sub ObjectPlayerInteraction
	TempValue0  = Object.XPos
	Object.XPos = Object.XDrawPos

	PlayerObjectCollision(C_BOX, -64, -16, 64, 16)
	switch CheckResult
	case PLAYER_COL_FLOOR
		Player.XPos += Object.XOffSet
		break

	case PLAYER_COL_LWALL
		// Check the previous block's collision
		if Object[-1].AgainstBlock == true
			CallFunction(Player_Kill)
		end if
		break

	case PLAYER_COL_RWALL
		Object.AgainstBlock = true
		break
	end switch
	
	Object.XPos = TempValue0
end sub


sub ObjectDraw
	Cos(Object.XDrawPos, Object.Angle)
	Object.XDrawPos <<= 12
	Object.XDrawPos  += Object.XPos
	Object.XDrawPos  &= 0xFFFF0000 // Truncate the value

	DrawSpriteXY(0, Object.XDrawPos, Object.YPos)
	DrawSpriteXY(1, Object.XDrawPos, Object.YPos)
	DrawSpriteXY(2, Object.XDrawPos, Object.YPos)
	DrawSpriteXY(3, Object.XDrawPos, Object.YPos)
end sub


sub ObjectStartup
	switch HUD.CurrentTimePeriod
	case TIME_PRESENT
		LoadSpriteSheet("R4/Objects.gif")
		TempValue0 = 163
		TempValue1 = 1
		TempValue2 = 196
		break

	case TIME_PAST
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 157
		TempValue2 = 34
		break

	case TIME_GOOD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 190
		TempValue2 = 34
		break

	case TIME_BAD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 223
		TempValue2 = 34
		break
	end switch

	// R4 Blocks
	SpriteFrame(-64, -16, 32, 32, TempValue0, TempValue1)
	SpriteFrame(-32, -16, 32, 32, TempValue0, TempValue1)
	SpriteFrame(0, -16, 32, 32, TempValue0, TempValue1)
	SpriteFrame(32, -16, 32, 32, TempValue0, TempValue1)
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSpriteXY(0, Object.XPos, Object.YPos)
	DrawSpriteXY(1, Object.XPos, Object.YPos)
	DrawSpriteXY(2, Object.XPos, Object.YPos)
	DrawSpriteXY(3, Object.XPos, Object.YPos)
end sub


sub RSDKLoad
	CallFunction(EditorHelpers_FindTimePeriod)
	switch CheckResult
	case TIME_PRESENT
		LoadSpriteSheet("R4/Objects.gif")
		TempValue0 = 163
		TempValue1 = 1
		TempValue2 = 196
		break

	case TIME_PAST
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 157
		TempValue2 = 34
		break

	case TIME_GOOD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 190
		TempValue2 = 34
		break

	case TIME_BAD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 223
		TempValue2 = 34
		break
	end switch

	// R4 Blocks
	SpriteFrame(-64, -16, 32, 32, TempValue0, TempValue1)
	SpriteFrame(-32, -16, 32, 32, TempValue0, TempValue1)
	SpriteFrame(0, -16, 32, 32, TempValue0, TempValue1)
	SpriteFrame(32, -16, 32, 32, TempValue0, TempValue1)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
