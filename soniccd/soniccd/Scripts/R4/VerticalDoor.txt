//----------------Sonic CD Vertical Door Script---------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0			:	Object.Timer

// Push Button alias
#alias Object.PropertyValue		:	PushButton.Pressed

// States
#alias 0	:	VERTICALDOOR_INACTIVE
#alias 1	:	VERTICALDOOR_ACTIVE

// Property Values
#alias 0	:	VDOOR_BUTTON_HOLD
#alias 1	:	VDOOR_CLOSE_AFTER_PLAYER_ROOF
#alias 2	:	VDOOR_CLOSE_AFTER_PLAYER_FLOOR

// Collision
#alias 4	:	PLAYER_COL_ROOF


sub ObjectMain
	switch Object.PropertyValue
	case VDOOR_BUTTON_HOLD
		if PushButton[-1].Pressed == false
			if Object.Timer > 0
				Object.Timer -= 0x80000
				Object.YPos  += 0x80000
			end if
		else
			if Object.Timer < 0x800000
				Object.Timer += 0x80000
				Object.YPos  -= 0x80000
			end if
		end if
		break

	case VDOOR_CLOSE_AFTER_PLAYER_ROOF
		if PushButton[-1].Pressed == true
			Object.State = VERTICALDOOR_ACTIVE
		end if

		TempValue0  = Object.XPos
		TempValue0 += 0x180000
		if Player.XPos > TempValue0
			Object.State = VERTICALDOOR_INACTIVE
		end if

		if Object.State == VERTICALDOOR_INACTIVE
			if Object.Timer > 0
				Object.Timer -= 0x80000
				Object.YPos  += 0x80000
			end if
		else
			if Object.Timer < 0x800000
				Object.Timer += 0x80000
				Object.YPos  -= 0x80000
			end if
		end if
		break

	case VDOOR_CLOSE_AFTER_PLAYER_FLOOR
		if PushButton[-1].Pressed == true
			Object.State = VERTICALDOOR_ACTIVE
		end if

		TempValue0  = Object.XPos
		TempValue0 += 0x180000
		if Player.XPos > TempValue0
			Object.State = VERTICALDOOR_INACTIVE
		end if

		if Object.State == VERTICALDOOR_INACTIVE
			if Object.Timer > 0
				Object.Timer -= 0x80000
				Object.YPos  -= 0x80000
			end if
		else
			if Object.Timer < 0x800000
				Object.Timer += 0x80000
				Object.YPos  += 0x80000
			end if
		end if
		break
	end switch
end sub


sub ObjectPlayerInteraction
	TempValue0 = Player.YPos
	PlayerObjectCollision(C_BOX, -8, -64, 8, 64)
	if CheckResult == PLAYER_COL_ROOF
		Player.YPos = TempValue0
	end if

	PlayerObjectCollision(C_TOUCH, -8, -64, 8, 64)
	if CheckResult == true
		if Player.XPos < Object.XPos
			Player.XPos   = Player.CollisionLeft
			Player.XPos <<= 16
			Player.XPos  += Object.XPos
			Player.XPos  -= 0x80000
		else
			Player.XPos   = Player.CollisionRight
			Player.XPos <<= 16
			Player.XPos  += Object.XPos
			Player.XPos  += 0x80000
		end if
	end if
end sub


sub ObjectDraw
	DrawSprite(0)
end sub


sub ObjectStartup
	LoadSpriteSheet("R4/Objects.gif")

	SpriteFrame(-8, -64, 16, 128, 1, 69)	// #0 - Vertical Door
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("R4/Objects.gif")

	SpriteFrame(-8, -64, 16, 128, 1, 69)	// #0 - Vertical Door

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
