//--------------Sonic CD Vertical Blocks Script---------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value1			:	Object.YDrawPos
#alias Object.Value2			:	Object.YOffset
#alias Object.Value3			:	Object.Angle

// HUD alias
#alias Object[24].PropertyValue	:	HUD.CurrentTimePeriod

// Time Periods
#alias 0	:	TIME_PRESENT
#alias 1	:	TIME_PAST
#alias 2	:	TIME_GOOD_FUTURE
#alias 3	:	TIME_BAD_FUTURE


sub ObjectMain
	Object.Angle   = Oscillation
	Object.Angle  += Object.PropertyValue
	Object.Angle <<= 1
	
	Sin(Object.YOffset, Object.Angle)
	Object.YOffset *= 0x1800
	Object.YOffset += Object.YPos
	Object.YOffset &= 0xFFFF0000 // Truncate the value
	Object.YOffset -= Object.YDrawPos
end sub


sub ObjectPlayerInteraction
	TempValue0  = Object.YPos
	Object.YPos = Object.YDrawPos
	PlayerObjectCollision(C_BOX, -16, -16, 16, 16)
	if CheckResult == true
		Player.YPos += Object.YOffset
	end if
	Object.YPos = TempValue0
end sub


sub ObjectDraw
	Sin(Object.YDrawPos, Object.Angle)
	Object.YDrawPos *= 0x1800
	Object.YDrawPos += Object.YPos
	Object.YDrawPos &= 0xFFFF0000 // Truncate the value
	
	DrawSpriteXY(0, Object.XPos, Object.YDrawPos)
end sub


sub ObjectStartup
	switch HUD.CurrentTimePeriod
	case TIME_PRESENT
		LoadSpriteSheet("R4/Objects.gif")
		TempValue0 = 163
		TempValue1 = 1
		TempValue2 = 196
		break
	case TIME_PAST
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 157
		TempValue2 = 34
		break
	case TIME_GOOD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 190
		TempValue2 = 34
		break
	case TIME_BAD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 223
		TempValue2 = 34
		break
	end switch

	SpriteFrame(-16, -16, 32, 32, TempValue2, TempValue1)	// R4 Block
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	TempValue1   = Oscillation
	TempValue1  += Object.PropertyValue
	TempValue1 <<= 1
	Sin(TempValue0, TempValue1)
	TempValue0 *= 0x1800
	TempValue0 += Object.YPos
	TempValue0 &= 0xFFFF0000 // Truncate the value
	
	DrawSpriteXY(0, Object.XPos, TempValue0)
end sub


sub RSDKLoad
	CallFunction(EditorHelpers_FindTimePeriod)
	switch CheckResult
	case TIME_PRESENT
		LoadSpriteSheet("R4/Objects.gif")
		TempValue0 = 163
		TempValue1 = 1
		TempValue2 = 196
		break
	case TIME_PAST
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 157
		TempValue2 = 34
		break
	case TIME_GOOD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 190
		TempValue2 = 34
		break
	case TIME_BAD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 223
		TempValue2 = 34
		break
	end switch

	SpriteFrame(-16, -16, 32, 32, TempValue2, TempValue1)	// R4 Block

	SetVariableAlias(ALIAS_VAR_PROPVAL, "startingAngle")
end sub
