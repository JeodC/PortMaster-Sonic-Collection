//-------------Sonic CD Vertical Blocks 4 Script--------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value1			:	Object.YDrawPos
#alias Object.Value2			:	Object.YOffset
#alias Object.Value3			:	Object.Angle
#alias Object.Value4			:	Object.R4BlockSheet
#alias Object.Value5			:	Object.SpikeSheet

// HUD alias
#alias Object[24].PropertyValue	:	HUD.CurrentTimePeriod

// Time Periods
#alias 0	:	TIME_PRESENT
#alias 1	:	TIME_PAST
#alias 2	:	TIME_GOOD_FUTURE
#alias 3	:	TIME_BAD_FUTURE

// Collision
#alias 4	:	PLAYER_COL_ROOF

// Gravity
#alias 1	:	GRAVITY_AIR


sub ObjectMain
	Object.Angle   = Oscillation
	Object.Angle  += Object.PropertyValue
	Object.Angle <<= 1

	Sin(Object.YOffset, Object.Angle)
	Object.YOffset <<= 13
	Object.YOffset  += Object.YPos
	Object.YOffset  &= 0xFFFF0000 // Truncate the value
	Object.YOffset  -= Object.YDrawPos
end sub


sub ObjectPlayerInteraction
	TempValue0  = Object.YPos
	Object.YPos = Object.YDrawPos
	PlayerObjectCollision(C_BOX, -16, -64, 16, 62)
	if CheckResult == PLAYER_COL_ROOF
		if Player.Gravity == GRAVITY_AIR
			CallFunction(Player_Hit)
		else
#platform: Use_Origins
			if Object.YOffset > 0
				CallFunction(Player_Kill)
			end if
#endplatform
#platform: Use_Standalone
			CallFunction(Player_Kill)
#endplatform
		end if
	end if
	Object.YPos = TempValue0
end sub


sub ObjectDraw
	Sin(Object.YDrawPos, Object.Angle)
	Object.YDrawPos <<= 13
	Object.YDrawPos  += Object.YPos
	Object.YDrawPos  &= 0xFFFF0000 // Truncate the value

	Object.SpriteSheet = Object.R4BlockSheet
	DrawSpriteXY(0, Object.XPos, Object.YDrawPos)
	DrawSpriteXY(1, Object.XPos, Object.YDrawPos)
	DrawSpriteXY(2, Object.XPos, Object.YDrawPos)

	Object.SpriteSheet = Object.SpikeSheet
	DrawSpriteXY(3, Object.XPos, Object.YDrawPos)
end sub


sub ObjectStartup
	switch HUD.CurrentTimePeriod
	case TIME_PRESENT
		LoadSpriteSheet("R4/Objects.gif")
		TempValue0 = 163
		TempValue1 = 1
		TempValue2 = 196
		break

	case TIME_PAST
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 157
		TempValue2 = 34
		break

	case TIME_GOOD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 190
		TempValue2 = 34
		break

	case TIME_BAD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 223
		TempValue2 = 34
		break

	end switch

	// R4 Blocks
	SpriteFrame(-16, -64, 32, 32, TempValue0, TempValue1)
	SpriteFrame(-16, -32, 32, 32, TempValue0, TempValue1)
	SpriteFrame(-16, 0, 32, 32, TempValue2, TempValue1)
	TempValue0 = Object.SpriteSheet

	LoadSpriteSheet("Global/Items3.gif")
	TempValue1 = Object.SpriteSheet

	SpriteFrame(-16, 32, 32, 32, 50, 100)	// #3 - Spikes

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[VOBlocks4]
			Object[ArrayPos0].R4BlockSheet = TempValue0
			Object[ArrayPos0].SpikeSheet   = TempValue1
		end if
		ArrayPos0++
	loop
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	CallFunction(EditorHelpers_FindTimePeriod)
	switch CheckResult
	case TIME_PRESENT
		LoadSpriteSheet("R4/Objects.gif")
		break
	case TIME_PAST
	case TIME_GOOD_FUTURE
	case TIME_BAD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		break
	end switch
	TempValue6 = Object.SpriteSheet

	LoadSpriteSheet("Global/Items3.gif")
	TempValue7 = Object.SpriteSheet

	TempValue1   = Oscillation
	TempValue1  += Object.PropertyValue
	TempValue1 <<= 1
	Sin(TempValue0, TempValue1)
	TempValue0 <<= 13
	TempValue0  += Object.YPos
	TempValue0  &= 0xFFFF0000 // Truncate the value

	Object.SpriteSheet = TempValue6
	DrawSpriteXY(0, Object.XPos, TempValue0)
	DrawSpriteXY(1, Object.XPos, TempValue0)
	DrawSpriteXY(2, Object.XPos, TempValue0)

	Object.SpriteSheet = TempValue7
	DrawSpriteXY(3, Object.XPos, TempValue0)
end sub


sub RSDKLoad
	CallFunction(EditorHelpers_FindTimePeriod)
	switch CheckResult
	case TIME_PRESENT
		LoadSpriteSheet("R4/Objects.gif")
		TempValue0 = 163
		TempValue1 = 1
		TempValue2 = 196
		break
	case TIME_PAST
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 157
		TempValue2 = 34
		break
	case TIME_GOOD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 190
		TempValue2 = 34
		break
	case TIME_BAD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 223
		TempValue2 = 34
		break
	end switch

	// R4 Blocks
	SpriteFrame(-16, -64, 32, 32, TempValue0, TempValue1)
	SpriteFrame(-16, -32, 32, 32, TempValue0, TempValue1)
	SpriteFrame(-16, 0, 32, 32, TempValue2, TempValue1)

	LoadSpriteSheet("Global/Items3.gif")
	SpriteFrame(-16, 32, 32, 32, 50, 100)	// #3 - Spikes

	SetVariableAlias(ALIAS_VAR_PROPVAL, "startingAngle")
end sub