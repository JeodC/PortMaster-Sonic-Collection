//-----------------Sonic CD Taga Taga Script------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0		:	Object.Timer
#alias Object.Value1		:	Object.XOriginPos

#alias Object.PropertyValue	:	Object.Quality

// Taga Spikes Aliases
#alias Object.Value1		:	Object.XVelocity
#alias Object.Value2		:	Object.YVelocity

// States
#alias 0	:	TAGATAGA_MOVE_LEFT
#alias 1	:	TAGATAGA_MOVE_RIGHT
#alias 2	:	TAGATAGA_SHOOT_LEFT
#alias 3	:	TAGATAGA_SHOOT_RIGHT
#alias 4	:	TAGATAGA_RESTORE

// Stage SFX
#alias 5	:	SFX_S_SHOT2

// Priority
#alias 0	:	PRIORITY_BOUNDS
#alias 1	:	PRIORITY_ACTIVE

// Badnik Quality / Property Values
#alias 0	:	GOOD_QUALITY
#alias 1	:	BAD_QUALITY


sub ObjectMain
	if Object.Quality == GOOD_QUALITY
		switch Object.State
		case TAGATAGA_MOVE_LEFT
			if Object.Timer == 0
				Object.State	 = TAGATAGA_MOVE_RIGHT
				Object.Direction = FACING_LEFT
				Object.Timer	 = 192
			else
				Object.Timer--
				Object.XPos -= 0x10000
			end if
			break

		case TAGATAGA_MOVE_RIGHT
			if Object.Timer == 0
				Object.State	 = TAGATAGA_MOVE_LEFT
				Object.Direction = FACING_RIGHT
				Object.Timer	 = 192
			else
				Object.Timer--
				Object.XPos += 0x10000
			end if
			break

		case TAGATAGA_SHOOT_LEFT
			if Object.Timer < 120
				Object.Timer++
				if Object.Timer == 60
					CreateTempObject(TypeName[Taga Spike], 0, Object.XPos, Object.YPos)
					Object[TempObjectPos].XPos -= 0x10000
					Object[TempObjectPos].YPos -= 0xA0000
					Object[TempObjectPos].XVelocity = -0x20000
					Object[TempObjectPos].YVelocity = -0x30000

					CreateTempObject(TypeName[Taga Spike], 0, Object.XPos, Object.YPos)
					Object[TempObjectPos].XPos += 0x30000
					Object[TempObjectPos].YPos -= 0xA0000
					Object[TempObjectPos].XVelocity =  0x10000
					Object[TempObjectPos].YVelocity = -0x30000

					CreateTempObject(TypeName[Taga Spike], 0, Object.XPos, Object.YPos)
					Object[TempObjectPos].XPos += 0x70000
					Object[TempObjectPos].YPos -= 0x90000
					Object[TempObjectPos].XVelocity +=  0x20000
					Object[TempObjectPos].YVelocity  = -0x30000

					PlayStageSfx(SFX_S_SHOT2, false)
				end if
			else
				Object.XPos -= 0x40000
				if Object.OutOfBounds == true
					Object.State = TAGATAGA_RESTORE
					Object.XPos = Object.XOriginPos
				end if
			end if
			break

		case TAGATAGA_SHOOT_RIGHT
			if Object.Timer < 120
				Object.Timer++
				if Object.Timer == 60
					CreateTempObject(TypeName[Taga Spike], 0, Object.XPos, Object.YPos)
					Object[TempObjectPos].XPos += 0x10000
					Object[TempObjectPos].YPos -= 0xA0000
					Object[TempObjectPos].XVelocity =  0x20000
					Object[TempObjectPos].YVelocity = -0x30000

					CreateTempObject(TypeName[Taga Spike], 0, Object.XPos, Object.YPos)
					Object[TempObjectPos].XPos -= 0x30000
					Object[TempObjectPos].YPos -= 0xA0000
					Object[TempObjectPos].XVelocity = -0x10000
					Object[TempObjectPos].YVelocity = -0x30000
					
					CreateTempObject(TypeName[Taga Spike], 0, Object.XPos, Object.YPos)
					Object[TempObjectPos].XPos -= 0x70000
					Object[TempObjectPos].YPos -= 0x90000
					Object[TempObjectPos].XVelocity = -0x20000
					Object[TempObjectPos].YVelocity = -0x30000

					PlayStageSfx(SFX_S_SHOT2, false)
				end if
			else
				Object.XPos += 0x40000
				if Object.OutOfBounds == true
					Object.State = TAGATAGA_RESTORE
					Object.XPos  = Object.XOriginPos
				end if
			end if
			break

		case TAGATAGA_RESTORE
			if Object.OutOfBounds == true
				Object.State	 = TAGATAGA_MOVE_LEFT
				Object.Timer	 = 0
				Object.Direction = FACING_RIGHT
				Object.XPos		 = Object.XOriginPos
			end if
			break

		end switch
	else
		switch Object.State
		case TAGATAGA_MOVE_LEFT
			if Object.Timer == 0
				Object.State	 = TAGATAGA_MOVE_RIGHT
				Object.Direction = FACING_LEFT
				Object.Timer	 = 384
			else
				Object.Timer--
				Object.XPos -= 0x8000
			end if
			break

		case TAGATAGA_MOVE_RIGHT
			if Object.Timer == 0
				Object.State	 = TAGATAGA_MOVE_LEFT
				Object.Direction = FACING_RIGHT
				Object.Timer	 = 384
			else
				Object.Timer--
				Object.XPos += 0x8000
			end if
			break

		case TAGATAGA_SHOOT_LEFT
			if Object.Timer < 120
				Object.Timer++
			else
				Object.XPos -= 0x18000
				if Object.OutOfBounds == true
					Object.State = TAGATAGA_RESTORE
					Object.XPos = Object.XOriginPos
				end if
			end if
			break

		case TAGATAGA_SHOOT_RIGHT
			if Object.Timer < 120
				Object.Timer++
			else
				Object.XPos += 0x18000

				if Object.OutOfBounds == true
					Object.State = TAGATAGA_RESTORE
					Object.XPos = Object.XOriginPos
				end if
			end if
			break

		case TAGATAGA_RESTORE
			if Object.OutOfBounds == true
				Object.State	 = TAGATAGA_MOVE_LEFT
				Object.Timer	 = 0
				Object.Direction = FACING_RIGHT
				Object.XPos		 = Object.XOriginPos
			end if
			break

		end switch
	end if

	CallFunction(StageSetup_CheckGoodFuture)	// Check if this should be a flower
end sub


sub ObjectPlayerInteraction
	if Object.Quality == GOOD_QUALITY
		switch Object.State
		case TAGATAGA_MOVE_LEFT
			PlayerObjectCollision(C_TOUCH, -128, -32, 0, 8)
			if CheckResult == true
				Object.State	= TAGATAGA_SHOOT_LEFT
				Object.Timer	= 0
				Object.Priority = PRIORITY_ACTIVE
			end if
			break

		case TAGATAGA_MOVE_RIGHT
			PlayerObjectCollision(C_TOUCH, 0, -32, 128, 8)
			if CheckResult == true
				Object.State	= TAGATAGA_SHOOT_RIGHT
				Object.Timer	= 0
				Object.Priority = PRIORITY_ACTIVE
			end if
			break

		end switch
	end if

#platform: Use_Origins
	TempValue0 = 0
	if game.playMode == BOOT_PLAYMODE_MISSION
		CheckEqual(Player.Animation, ANI_FANROTATE)
		TempValue0 |= CheckResult
		CheckEqual(Player.State, Player_State_WaterCurrent)	// Check that you're on a water stream
		TempValue0 |= CheckResult
	end if
	CheckEqual(TempValue0, 0)
	TempValue0  = CheckResult
	CheckLower(Object.State, TAGATAGA_RESTORE)
	TempValue0 &= CheckResult
	if TempValue0 == true
		PlayerObjectCollision(C_ENEMY, -12, -8, 12, 8)
		if CheckResult == true
			CallFunction(Player_BadnikBreak)
		end if
	end if
#endplatform

#platform: Use_Standalone
	if Object.State < TAGATAGA_RESTORE
		PlayerObjectCollision(C_TOUCH, -12, -8, 12, 8)
		if CheckResult == true
			CallFunction(Player_BadnikBreak)
		end if
	end if
#endplatform
end sub


sub ObjectDraw
	if Object.Quality == GOOD_QUALITY
		if Object.State < TAGATAGA_RESTORE
			TempValue0   = Object.Frame
			TempValue0 >>= 5
			DrawSpriteFX(TempValue0, FX_FLIP, Object.XPos, Object.YPos)
		end if

		if Object.State < TAGATAGA_SHOOT_LEFT
			DrawSpriteFX(2, FX_FLIP, Object.XPos, Object.YPos)
			DrawSpriteFX(3, FX_FLIP, Object.XPos, Object.YPos)
			DrawSpriteFX(4, FX_FLIP, Object.XPos, Object.YPos)
			Object.Frame++
			Object.Frame &= 63
		else
			if Object.Timer < 60
				DrawSpriteFX(2, FX_FLIP, Object.XPos, Object.YPos)
				DrawSpriteFX(3, FX_FLIP, Object.XPos, Object.YPos)
				DrawSpriteFX(4, FX_FLIP, Object.XPos, Object.YPos)
			end if
		end if
	else
		if Object.State < TAGATAGA_RESTORE
			TempValue0   = Object.Frame
			TempValue0 >>= 5
			DrawSpriteFX(TempValue0, FX_FLIP, Object.XPos, Object.YPos)
		end if

		if Object.State < TAGATAGA_SHOOT_LEFT
			Object.Frame++
			Object.Frame &= 63
		end if
	end if
end sub


sub ObjectStartup
	LoadSpriteSheet("R4/Objects.gif")
	
	SpriteFrame(-20, -12, 40, 24, 215, 86)	// #0 - Taga Taga Frame 0
	SpriteFrame(-20, -12, 40, 24, 215, 111)	// #1 - Taga Taga Frame 1

	SpriteFrame(-1, -10, 8, 8, 206, 52)		// #2 - Taga Taga Top Spike 1
	SpriteFrame(3, -10, 8, 8, 206, 52)		// #3 - Taga Taga Top Spike 2
	SpriteFrame(7, -9, 8, 8, 206, 52)		// #4 - Taga Taga Top Spike 3

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Taga Taga]
			Object[ArrayPos0].XOriginPos = Object[ArrayPos0].XPos
		end if
		ArrayPos0++
	loop
end sub


// ========================
// Editor Subs
// ========================

sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			CheckResult = Object.PropertyValue
			CheckResult &= 1
			break
		case 0 // condition
			CheckResult = Object.PropertyValue
			CheckResult &= 1
			break
		end switch
	else
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			Object.PropertyValue = Editor.VariableValue
			Object.PropertyValue &= 1
			break
		case 0 // condition
			Object.PropertyValue = Editor.VariableValue
			Object.PropertyValue &= 1
			break
		end switch
	end if
end sub


sub RSDKDraw
	if Object.PropertyValue == GOOD_QUALITY
		DrawSpriteFX(0, FX_FLIP, Object.XPos, Object.YPos)
		DrawSpriteFX(2, FX_FLIP, Object.XPos, Object.YPos)
		DrawSpriteFX(3, FX_FLIP, Object.XPos, Object.YPos)
		DrawSpriteFX(4, FX_FLIP, Object.XPos, Object.YPos)
	else
		DrawSpriteFX(0, FX_FLIP, Object.XPos, Object.YPos)
	end if
end sub


sub RSDKLoad
	LoadSpriteSheet("R4/Objects.gif")
	
	SpriteFrame(-20, -12, 40, 24, 215, 86)	// #0 - Taga Taga Frame 0
	SpriteFrame(-20, -12, 40, 24, 215, 111)	// #1 - Taga Taga Frame 1

	SpriteFrame(-1, -10, 8, 8, 206, 52)		// #2 - Taga Taga Top Spike 1
	SpriteFrame(3, -10, 8, 8, 206, 52)		// #3 - Taga Taga Top Spike 2
	SpriteFrame(7, -9, 8, 8, 206, 52)		// #4 - Taga Taga Top Spike 3

	AddEditorVariable("condition")
	SetActiveVariable("condition")
	AddEnumVariable("Good Quality", GOOD_QUALITY)
	AddEnumVariable("Bad Quality", BAD_QUALITY)
end sub
