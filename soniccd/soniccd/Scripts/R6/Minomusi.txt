//-----------------Sonic CD Minomusi Script-------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0		:	Object.Timer
#alias Object.Value1 		:	Object.YOriginPos
#alias Object.Value2 		:	Object.SpikeTimer

#alias Object.PropertyValue :	Object.Quality

// States
#alias 0	:	MINOMUSI_DETECT_PLAYER
#alias 1	:	MINOMUSI_DROP

// Good Quality States
#alias 2	:	MINOMUSI_GOOD_SPIKE_ANIM_1
#alias 3	:	MINOMUSI_GOOD_SPIKE_ANIM_2
#alias 4	:	MINOMUSI_GOOD_SPIKE_ANIM_3
#alias 5	:	MINOMUSI_GOOD_SPIKE_ANIM_4
#alias 6	:	MINOMUSI_GOOD_SPIKE_ANIM_5
#alias 7	:	MINOMUSI_GOOD_SPIKE_ANIM_6
#alias 8	:	MINOMUSI_GOOD_SPIKE_ANIM_7
#alias 9	:	MINOMUSI_GOOD_SPIKE_ANIM_8
#alias 10	:	MINOMUSI_GOOD_SPIKE_ANIM_9
#alias 11	:	MINOMUSI_GOOD_RETIRE

// Bad Quality States
#alias 2	:	MINOMUSI_BAD_FLIP
#alias 3	:	MINOMUSI_BAD_FLIP_2
#alias 4	:	MINOMUSI_BAD_RETIRE

// Stage SFX
#alias 8	:	SFX_S_SPIKES

// Badnik Quality / Property Values
#alias 0	:	GOOD_QUALITY
#alias 1	:	BAD_QUALITY

// Priority
#alias 0	:	PRIORITY_BOUNDS
#alias 1	:	PRIORITY_ACTIVE


sub ObjectMain
	if Object.OutOfBounds == true
		Object.State = MINOMUSI_DETECT_PLAYER
		Object.Timer = 0
		Object.SpikeTimer = 0
		Object.YPos = Object.YOriginPos
		Object.Priority = PRIORITY_BOUNDS
	end if

	if Object.Quality == GOOD_QUALITY
		switch Object.State
		case MINOMUSI_DETECT_PLAYER
			Object.Frame = 0
			if Object.Timer > 0
				Object.Timer--
			end if
			break

		case MINOMUSI_DROP
			if Object.Timer < 12
				Object.YPos += 0x80000
				Object.Timer++
			else
				Object.Timer = 0
				Object.State++
			end if
			break
			
		case MINOMUSI_GOOD_SPIKE_ANIM_1
			if Object.SpikeTimer < 30
				Object.SpikeTimer++
			else
				Object.SpikeTimer = 0
				Object.State++
				Object.Frame = 3
			end if
			break

		case MINOMUSI_GOOD_SPIKE_ANIM_2
			Object.State++
			Object.Frame = 4
			PlayStageSfx(SFX_S_SPIKES, false)
			break

		case MINOMUSI_GOOD_SPIKE_ANIM_3
			if Object.SpikeTimer < 3
				Object.SpikeTimer++
			else
				Object.SpikeTimer = 0
				Object.State++
				Object.Frame = 3
			end if
			break

		case MINOMUSI_GOOD_SPIKE_ANIM_4
			Object.State++
			Object.Frame = 0
			break

		case MINOMUSI_GOOD_SPIKE_ANIM_5
			Object.State++
			Object.Frame = 3
			break

		case MINOMUSI_GOOD_SPIKE_ANIM_6
			Object.State++
			Object.Frame = 4
			break

		case MINOMUSI_GOOD_SPIKE_ANIM_7
			if Object.SpikeTimer < 30
				Object.SpikeTimer++
			else
				Object.SpikeTimer = 0
				Object.State++
				Object.Frame = 3
			end if
			break

		case MINOMUSI_GOOD_SPIKE_ANIM_8
			Object.Frame = 0
			if Object.Timer < 2
				Object.State = MINOMUSI_GOOD_SPIKE_ANIM_1
				Object.Timer++
			else
				Object.State++
				Object.Timer = 0
			end if
			break

		case MINOMUSI_GOOD_SPIKE_ANIM_9
			if Object.Timer < 20
				Object.Timer++
			else
				Object.Timer = 0
				Object.State++
			end if
			break

		case MINOMUSI_GOOD_RETIRE
			if Object.Timer < 12
				Object.YPos -= 0x80000
				Object.Timer++
			else
				Object.Timer = 120
				Object.State = MINOMUSI_DETECT_PLAYER
			end if
			break

		end switch
	else
		switch Object.State
		case MINOMUSI_DETECT_PLAYER
			Object.Frame = 1
			if Object.Timer > 0
				Object.Timer--
			end if
			break

		case MINOMUSI_DROP
			Object.Frame = 1
			if Object.Timer < 12
				Object.YPos += 0x80000
				Object.Timer++
			else
				Object.Timer = 0
				Object.State++
			end if
			break

		case MINOMUSI_BAD_FLIP
			if Object.SpikeTimer < 10
				Object.SpikeTimer++
			else
				Object.SpikeTimer = 0
				Object.State++
				Object.Frame = 2
			end if
			break

		case MINOMUSI_BAD_FLIP_2
			if Object.SpikeTimer < 10
				Object.SpikeTimer++
			else
				Object.SpikeTimer = 0
				Object.Frame = 1
				Object.Timer++
				if Object.Timer == 3
					Object.Timer = 0
					Object.State++
				else
					Object.State = MINOMUSI_BAD_FLIP
				end if
			end if
			break

		case MINOMUSI_BAD_RETIRE
			if Object.Timer < 48
				Object.YPos -= 0x20000
				Object.Timer++
			else
				Object.Timer = 120
				Object.State = MINOMUSI_DETECT_PLAYER
			end if
			break

		end switch
	end if

	// Check if this badnik should be a flower
	CallFunction(StageSetup_CheckGoodFuture)
end sub


sub ObjectPlayerInteraction
#platform: Use_Standalone
	PlayerObjectCollision(C_TOUCH, -8, -6, 8, 22)
#endplatform

#platform: Use_Origins
	PlayerObjectCollision(C_ENEMY, -8, -6, 8, 22)
#endplatform

	if CheckResult == true
		CallFunction(Player_BadnikBreak)
	end if

	if Object.Quality == GOOD_QUALITY
		if Object.Type == TypeName[Minomusi]
			if Object.Frame > 2
				PlayerObjectCollision(C_TOUCH, -17, -4, 17, 20)
				if CheckResult == true
					CallFunction(Player_Hit)
				end if
			end if
		end if
	end if

	if Object.State == MINOMUSI_DETECT_PLAYER
		if Object.Timer == 0
			PlayerObjectCollision(C_TOUCH, -64, 0, 64, 128)
			if CheckResult == true
				Object.State = MINOMUSI_DROP
				Object.Priority = PRIORITY_ACTIVE
			end if
		end if
	end if
end sub


sub ObjectDraw
	TempValue0   = Object.YPos
	TempValue0  -= Object.YOriginPos
	TempValue0 >>= 19
	TempValue0  += 5
	DrawSpriteXY(TempValue0, Object.XPos, Object.YOriginPos)

	DrawSprite(Object.Frame)
end sub


sub ObjectStartup
	LoadSpriteSheet("R6/Objects.gif")
	// Good
	SpriteFrame(-8, -8, 16, 32, 82, 134)	// #0  - Minomusi
	// Bad
	SpriteFrame(-8, -8, 16, 32, 17, 184)	// #1  - Minomusi frame 0
	SpriteFrame(-8, -8, 16, 32, 1, 184)		// #2  - Minomusi frame 1
	// Spike Attack
	SpriteFrame(-12, -8, 24, 32, 133, 166)	// #3  - Minomusi Spiked frame 0
	SpriteFrame(-16, -8, 32, 32, 1, 151)	// #4  - Minomusi Spiked frame 1
	// String
	SpriteFrame(-4, 16, 8, 0, 246, 91)		// #5  - Minomusi String frame 0
	SpriteFrame(-4, 16, 8, 8, 246, 91)		// #6  - Minomusi String frame 1
	SpriteFrame(-4, 16, 8, 16, 246, 91)		// #7  - Minomusi String frame 2
	SpriteFrame(-4, 16, 8, 24, 246, 91)		// #8  - Minomusi String frame 3
	SpriteFrame(-4, 16, 8, 32, 246, 91)		// #9  - Minomusi String frame 4
	SpriteFrame(-4, 16, 8, 40, 246, 91)		// #10 - Minomusi String frame 5
	SpriteFrame(-4, 16, 8, 48, 246, 91)		// #11 - Minomusi String frame 6
	SpriteFrame(-4, 16, 8, 56, 246, 91)		// #12 - Minomusi String frame 7
	SpriteFrame(-4, 16, 8, 64, 246, 91)		// #13 - Minomusi String frame 8
	SpriteFrame(-4, 16, 8, 72, 246, 91)		// #14 - Minomusi String frame 9
	SpriteFrame(-4, 16, 8, 72, 246, 91)		// #15 - Minomusi String frame 10
	SpriteFrame(-4, 16, 8, 72, 246, 91)		// #16 - Minomusi String frame 11
	SpriteFrame(-4, 16, 8, 72, 246, 91)		// #17 - Minomusi String frame 12

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Minomusi]
			Object[ArrayPos0].YOriginPos = Object[ArrayPos0].YPos
		end if
		ArrayPos0++
	loop
end sub


// ========================
// Editor Subs
// ========================

sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			CheckResult = Object.PropertyValue
			CheckResult &= 1
			break
		case 0 // condition
			CheckResult = Object.PropertyValue
			CheckResult &= 1
			break
		end switch
	else
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			Object.PropertyValue = Editor.VariableValue
			Object.PropertyValue &= 1
			break
		case 0 // condition
			Object.PropertyValue = Editor.VariableValue
			Object.PropertyValue &= 1
			break
		end switch
	end if
end sub

sub RSDKDraw
	DrawSprite(Object.PropertyValue)
end sub


sub RSDKLoad
	LoadSpriteSheet("R6/Objects.gif")
	// Good
	SpriteFrame(-8, -8, 16, 32, 82, 134)	// #0  - Minomusi
	// Bad
	SpriteFrame(-8, -8, 16, 32, 17, 184)	// #1  - Minomusi frame 0


	AddEditorVariable("condition")
	SetActiveVariable("condition")
	AddEnumVariable("Good Quality", GOOD_QUALITY)
	AddEnumVariable("Bad Quality", BAD_QUALITY)
end sub
