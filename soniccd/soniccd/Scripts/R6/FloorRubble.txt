//---------------Sonic CD Floor Rubble Script-----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.RubbleType
#alias Object.Value1	:	Object.XVelocity
#alias Object.Value2	:	Object.YVelocity
#alias Object.Value3	:	Object.FallSpeed

// Property Values
#alias 0	:	BIG_RUBBLE
#alias 1	:	BIG_RUBBLE_ALT
#alias 2	:	RUBBLE_CHUNK_1
#alias 3	:	RUBBLE_CHUNK_2
#alias 4	:	RUBBLE_CHUNK_3
#alias 5	:	RUBBLE_CHUNK_4

// Collision Sides
#alias 0	:	CSIDE_FLOOR


sub ObjectMain
	Object.XPos		 += Object.XVelocity
	Object.YVelocity += Object.FallSpeed
	Object.YPos		 += Object.YVelocity

	if Object.PropertyValue == BIG_RUBBLE
		ObjectTileCollision(CSIDE_FLOOR, 0, 8, 0)
		if CheckResult == true
			Object.Type = TypeName[Blank Object]
			CreateTempObject(TypeName[Floor Rubble], RUBBLE_CHUNK_1, Object.XPos, Object.YPos)
			Object[TempObjectPos].XPos		-=  0x40000
			Object[TempObjectPos].YPos		-=  0x40000
			Object[TempObjectPos].XVelocity	 = -0x10000
			Object[TempObjectPos].YVelocity  = -0x28000
			Object[TempObjectPos].FallSpeed  =  0x3000

			CreateTempObject(TypeName[Floor Rubble], RUBBLE_CHUNK_2, Object.XPos, Object.YPos)
			Object[TempObjectPos].XPos		+=  0x40000
			Object[TempObjectPos].YPos		-=  0x40000
			Object[TempObjectPos].XVelocity  =  0x10000
			Object[TempObjectPos].YVelocity  = -0x28000
			Object[TempObjectPos].FallSpeed  =  0x3000

			CreateTempObject(TypeName[Floor Rubble], RUBBLE_CHUNK_3, Object.XPos, Object.YPos)
			Object[TempObjectPos].XPos		-=  0x40000
			Object[TempObjectPos].YPos		+=  0x40000
			Object[TempObjectPos].XVelocity  = -0x20000
			Object[TempObjectPos].YVelocity  = -0x1C000
			Object[TempObjectPos].FallSpeed  =  0x3000

			CreateTempObject(TypeName[Floor Rubble], RUBBLE_CHUNK_4, Object.XPos, Object.YPos)
			Object[TempObjectPos].XPos		+=  0x40000
			Object[TempObjectPos].YPos		+=  0x40000
			Object[TempObjectPos].XVelocity  =  0x20000
			Object[TempObjectPos].YVelocity  = -0x1C000
			Object[TempObjectPos].FallSpeed  =  0x3000
		end if
	else
		if Object.OutOfBounds == true
			Object.Type = TypeName[Blank Object]
		end if
	end if
end sub


sub ObjectDraw
	if Object.PropertyValue == BIG_RUBBLE
		TempValue0  = Object.RubbleType
		TempValue0 /= 10
		DrawSprite(TempValue0)

		Object.RubbleType++
		Object.RubbleType %= 20
	else
		DrawSprite(Object.PropertyValue)
	end if
end sub


sub ObjectStartup
	LoadSpriteSheet("R6/Objects2.gif")

	SpriteFrame(-8, -8, 16, 16, 165, 191)	// #0 - Big rubble
	SpriteFrame(-8, -8, 16, 16, 181, 191)	// #1 - Big rubble alt
	
	SpriteFrame(-4, -4, 8, 8, 165, 191)		// #2 - Rubble chunk 1
	SpriteFrame(-4, -4, 8, 8, 173, 191)		// #3 - Rubble chunk 2
	SpriteFrame(-4, -4, 8, 8, 165, 199)		// #4 - Rubble chunk alt 1
	SpriteFrame(-4, -4, 8, 8, 173, 199)		// #5 - Rubble chunk alt 2
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(object.PropertyValue)
end sub


sub RSDKLoad
	LoadSpriteSheet("R6/Objects2.gif")
	SpriteFrame(-8, -8, 16, 16, 165, 191)	// #0 - Big rubble
	SpriteFrame(-8, -8, 16, 16, 181, 191)	// #1 - Big rubble alt
	
	SpriteFrame(-4, -4, 8, 8, 165, 191)		// #2 - Rubble chunk 1
	SpriteFrame(-4, -4, 8, 8, 173, 191)		// #3 - Rubble chunk 2
	SpriteFrame(-4, -4, 8, 8, 165, 199)		// #4 - Rubble chunk alt 1
	SpriteFrame(-4, -4, 8, 8, 173, 199)		// #5 - Rubble chunk alt 2
	SetVariableAlias(ALIAS_VAR_PROPVAL, "type")
end sub
