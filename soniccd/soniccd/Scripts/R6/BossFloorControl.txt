//---------------Sonic CD Floor Control Script----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0			:	Object.Timer
#alias Object.Value1			:	Object.XVelocity
#alias Object.Value3			:	Object.RubbleTimer
#alias Object.Value4			:	Object.RubbleYPos
#alias Object.Value5			:	Object.TargetFloor
#alias Object.Value6			:	Object.CurrentFloor
#alias Object.Value7			:	Object.PlatformTimer
#alias Object.Scale				:	Object.SpikeAmount

#alias Object[-7].YPos			:	BossCamera.YPos
#alias Object[-6].YPos			:	FirstElectrode.YPos
#alias Object[-1].XPos			:	Boss.XPos
#alias Object[-1].YPos			:	Boss.YPos


#alias Object.YPos				:	BossPlat.YPos
#alias Object.Value2			:	BossPlat.YVelocity
#alias Object.Value3			:	BossPlat.FallSpeed
#alias Object.Value5			:	BossPlat.FCEntityNo

#alias Object.XPos				:	FloorRubble.XPos
#alias Object.Value1			:	FloorRubble.XVelocity
#alias Object.Value2			:	FloorRubble.YVelocity
#alias Object.Value3			:	FloorRubble.FallSpeed

// HUD Alias
#alias Object[24].PropertyValue	:	HUD.CurrentTimePeriod

// States
#alias 0	:	FLOORCONTROL_INACTIVE
#alias 1	:	FLOORCONTROL_SETUP_CRUMBLE
#alias 2	:	FLOORCONTROL_SPAWN_FALL_OBJS
#alias 3	:	FLOORCONTROL_ELEC_ARC
#alias 4	:	FLOORCONTROL_TILE_SWAP
#alias 5	:	FLOORCONTROL_COOLDOWN

// Stage SFX
#alias 1	:	SFX_S_ELECTRICITY
#alias 12	:	SFX_S_CRUMBLE

// Time Periods
#alias 2	:	TIME_GOOD_FUTURE


// Function declarations
#function FloorControl_SpawnRubble
#function FloorControl_SpawnPlatFarLeft
#function FloorControl_SpawnPlatLeft
#function FloorControl_SpawnPlatRight
#function FloorControl_SpawnPlatFarRight


function FloorControl_SpawnRubble
	CreateTempObject(TypeName[Floor Rubble], 0, Boss.XPos, Object.RubbleYPos)
	Rand(FloorRubble[TempObjectPos].XPos, 160)
	FloorRubble[TempObjectPos].XPos  -= 80
	FloorRubble[TempObjectPos].XPos <<= 16
	FloorRubble[TempObjectPos].XPos  += Boss.XPos

	Rand(FloorRubble[TempObjectPos].XVelocity, 0x500)
	TempValue0 = FloorRubble[TempObjectPos].XVelocity
	FloorRubble[TempObjectPos].XVelocity  -= 0x280
	FloorRubble[TempObjectPos].XVelocity <<= 8

	FloorRubble[TempObjectPos].YVelocity   = 0x10000
	
	FloorRubble[TempObjectPos].FallSpeed   = 2048
	TempValue0 &= 4095
	FloorRubble[TempObjectPos].FallSpeed  += TempValue0
end function


function FloorControl_SpawnPlatFarLeft
	switch Object.TargetFloor
	case 0
		ArrayPos0 = 2
		break

	case 1
		ArrayPos0 = 5
		break

	case 2
		ArrayPos0 = 11
		break
	end switch
	
	Object[+ArrayPos0].Type = TypeName[Boss Platform]
	BossPlat[+ArrayPos0].YPos 		= Object.RubbleYPos
	BossPlat[+ArrayPos0].YVelocity  = 0
	BossPlat[+ArrayPos0].FallSpeed  = 2816
	BossPlat[+ArrayPos0].FCEntityNo = Object.EntityNo
end function


function FloorControl_SpawnPlatLeft
	switch Object.TargetFloor
	case 0
		ArrayPos0 = 3
		break

	case 1
		ArrayPos0 = 6
		break

	case 2
		ArrayPos0 = 12
		break
	end switch

	Object[+ArrayPos0].Type = TypeName[Boss Platform]
	BossPlat[+ArrayPos0].YPos 		= Object.RubbleYPos
	BossPlat[+ArrayPos0].YVelocity  = 0
	BossPlat[+ArrayPos0].FallSpeed  = 2816
	BossPlat[+ArrayPos0].FCEntityNo = Object.EntityNo
end function


function FloorControl_SpawnPlatRight
	switch Object.TargetFloor
	case 0
		ArrayPos0 = 1
		break

	case 1
		ArrayPos0 = 7
		break

	case 2
		ArrayPos0 = 10
		break
	end switch

	Object[+ArrayPos0].Type = TypeName[Boss Platform]
	BossPlat[+ArrayPos0].YPos		= Object.RubbleYPos
	BossPlat[+ArrayPos0].YVelocity	= 0
	BossPlat[+ArrayPos0].FallSpeed	= 2816
	BossPlat[+ArrayPos0].FCEntityNo	= Object.EntityNo
end function


function FloorControl_SpawnPlatFarRight
	switch Object.TargetFloor
	case 0
		ArrayPos0 = 4
		break

	case 1
		ArrayPos0 = 8
		break
		
	case 2
		ArrayPos0 = 9
		break
	end switch

	Object[+ArrayPos0].Type = TypeName[Boss Platform]
	BossPlat[+ArrayPos0].YPos		= Object.RubbleYPos
	BossPlat[+ArrayPos0].YVelocity	= 0
	BossPlat[+ArrayPos0].FallSpeed	= 2816
	BossPlat[+ArrayPos0].FCEntityNo = Object.EntityNo
end function


sub ObjectMain
	if Player.State == Player_State_Fly
		if Player.Timer < 360
			Player.Timer = 360
		end if
	end if
	TempValue0   = BossCamera.YPos
	TempValue0  += 0x800000
	TempValue0  -= Player.YPos
	TempValue0 >>= 25

	if TempValue0 < Object.CurrentFloor
		//Restore the chunks to their turned off versions
		switch Object.TargetFloor
		case 0
			SetTileLayerEntry(500, 0, 20, 9)
			SetTileLayerEntry(501, 0, 21, 9)
			SetTileLayerEntry(506, 0, 22, 9)
		case 1
			SetTileLayerEntry(500, 0, 20, 5)
			SetTileLayerEntry(501, 0, 21, 5)
			SetTileLayerEntry(502, 0, 22, 5)
			break
		end switch
		Object.CurrentFloor = Object.TargetFloor
	end if

	switch Object.State
	case FLOORCONTROL_SETUP_CRUMBLE
		if Object.Timer < 90
			Object.Timer++
		else
			Object.TargetFloor   = BossCamera.YPos
			Object.TargetFloor  -= Boss.YPos
			Object.TargetFloor >>= 25

			switch Object.TargetFloor
			case 0
				Object.RubbleYPos = 0x58C0000
				Object.SpikeAmount = 0
				break
			case 1
				Object.RubbleYPos = 0x38C0000
				Object.SpikeAmount = 0
				break
			case 2
				Object.RubbleYPos = 0x18C0000
				Object.SpikeAmount = 6
				if Player.XPos > Boss.XPos
					Object.XVelocity = 0xA00000
				else
					Object.XVelocity = -0xA00000
				end if
				break

			end switch

			PlayStageSfx(SFX_S_CRUMBLE, false)
			
#platform: Use_Haptics
			HapticEffect(77, 0, 0, 0)
#endplatform

			Object.Timer		 = 0
			Object.RubbleTimer	 = 24
			Object.PlatformTimer = 0
			
			Object.State++
		end if
		break

	case FLOORCONTROL_SPAWN_FALL_OBJS
		Object.PlatformTimer++
		if Object.PlatformTimer == 128
			CallFunction(FloorControl_SpawnPlatFarLeft)
		end if

		if Object.PlatformTimer == 148
			CallFunction(FloorControl_SpawnPlatLeft)
		end if

		if Object.PlatformTimer == 168
			CallFunction(FloorControl_SpawnPlatRight)
		end if

		if Object.PlatformTimer == 188
			CallFunction(FloorControl_SpawnPlatFarRight)
		end if

		TempValue0 = Object.PlatformTimer
		TempValue0 &= 3
		if TempValue0 == 0
			Screen.ShakeY = 2
		end if

		if Object.SpikeAmount > 0
			TempValue0  = Object.PlatformTimer
			TempValue0 %= 35
			if TempValue0 == 0
				Object.SpikeAmount--
				CreateTempObject(TypeName[Boss Spike], 0, 0xAC00000, Object.RubbleYPos)
				Object[TempObjectPos].XPos += Object.XVelocity
				if Object.XVelocity > 0
					Object.XVelocity -= 0x200000
				else
					Object.XVelocity += 0x200000
				end if
			end if
		end if

		Object.Timer++
		if Object.Timer == 16
			Object.Timer = 0
			CallFunction(FloorControl_SpawnRubble)
			if Object.RubbleTimer > 0
				Object.RubbleTimer--
			else
				Object.State++
			end if
		end if
		break

	case FLOORCONTROL_ELEC_ARC
		if Object.Timer == 30
			PlayStageSfx(SFX_S_ELECTRICITY, false)
			Object.TargetFloor   = BossCamera.YPos
			Object.TargetFloor  -= Player.YPos
			Object.TargetFloor >>= 25
			if Object.CurrentFloor < Object.TargetFloor
				Object.CurrentFloor = Object.TargetFloor
				CreateTempObject(TypeName[Electric Arc], 0, Boss.XPos, FirstElectrode.YPos)
				TempValue0	 = Object.TargetFloor
				TempValue0 <<= 25
				TempValue0  += 0x20000
				Object[TempObjectPos].YPos		-= TempValue0
				Object[TempObjectPos].Direction	 = FACING_LEFT
				Object[TempObjectPos].DrawOrder	 = 4
			end if
		end if

		Object.Timer++
		if Object.Timer == 60
			Object.Timer = 0
			Object.State++
		end if
		break

	case FLOORCONTROL_TILE_SWAP
		Object.Timer++
		if Object.Timer == 60
			Object.Timer = 0
			Object.State++
			if HUD.CurrentTimePeriod == TIME_GOOD_FUTURE
				Copy16x16Tile(739, 827)
			else
				Copy16x16Tile(810, 863)
			end if
		else
			TempValue0   = Object.Timer
			TempValue0 >>= 1
			TempValue0  %= 5
			if HUD.CurrentTimePeriod == TIME_GOOD_FUTURE
				TempValue0 += 826
				Copy16x16Tile(739, TempValue0)
			else
				TempValue0 += 862
				Copy16x16Tile(810, TempValue0)
			end if
		end if
		break

	case FLOORCONTROL_COOLDOWN
		Object.Timer++
		if Object.Timer == 420
			Object.Timer = 0
			Object.State = FLOORCONTROL_ELEC_ARC
		end if
		break

	end switch
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
