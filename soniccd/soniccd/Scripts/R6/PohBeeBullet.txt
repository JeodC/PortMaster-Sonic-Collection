//---------------Sonic CD PohBee Bullet Script----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.Timer
#alias Object.Value1	:	Object.XVelocity
#alias Object.Value2	:	Object.YVelocity

// States
#alias 0	:	POHBEEBULLET_BURST
#alias 1	:	POHBEEBULLET_FIRE
#alias 2	:	POHBEEBULLET_PROJECTILE


sub ObjectMain
	switch Object.State
	case POHBEEBULLET_BURST
		if Object.Timer < 4
			Object.Timer++
		else
			Object.Timer = 0
			Object.State++
			Object.Frame = 1
		end if
		break

	case POHBEEBULLET_FIRE
		Object.XPos += Object.XVelocity
		Object.YPos += Object.YVelocity
		if Object.Timer < 12
			Object.Timer++
		else
			Object.Timer = 0
			Object.State++
			Object.Frame = 2
		end if
		break

	case POHBEEBULLET_PROJECTILE
		Object.XPos += Object.XVelocity
		Object.YPos += Object.YVelocity
		if Object.Timer > 1
			Object.Frame = 3
		else
			Object.Frame = 2
		end if
		Object.Timer++
		Object.Timer &= 3
		break
	end switch

	if Object.OutOfBounds == true
		Object.Type = TypeName[Blank Object]
	end if
end sub


sub ObjectPlayerInteraction
	if Object.State > POHBEEBULLET_BURST
		PlayerObjectCollision(C_TOUCH, -6, -6, 6, 6)
		if CheckResult == true
			CallFunction(Player_Hit)
		end if
	end if
end sub


sub ObjectDraw
	DrawSpriteFX(Object.Frame, FX_FLIP, Object.XPos, Object.YPos)
end sub


sub ObjectStartup
	LoadSpriteSheet("R6/Objects.gif")

	SpriteFrame(-8, -8, 16, 16, 197, 68)	// #0 - Firing Animation frame 0
	SpriteFrame(-8, -8, 16, 16, 69, 117)	// #1 - Firing Animation frame 1

	SpriteFrame(-8, -8, 16, 16, 1, 134)		// #2 - Bullet Animation frame 0
	SpriteFrame(-8, -8, 16, 16, 17, 134)	// #3 - Bullet Animation frame 1
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("R6/Objects.gif")
	SpriteFrame(-8, -8, 16, 16, 1, 134)		// #0 - Bullet Animation

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
