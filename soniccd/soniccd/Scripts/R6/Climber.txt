//-----------------Sonic CD Climber Script--------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value1	:	Object.YOriginPos // Unused
#alias Object.Value2	:	Object.BottomStep
#alias Object.Value3	:	Object.FlameFrame
#alias Object.Value4	:	Object.FlameAnimation
#alias Object.Value5	:	Object.Timer

// Step Sides
#alias 0	:	LEFT_STEP
#alias 1	:	RIGHT_STEP

// States
#alias 0	:	CLIMBER_ACTIVE
#alias 1	:	CLIMBER_DESCENDING

// Collision Sides
#alias 0	:	CSIDE_FLOOR


sub ObjectMain
	if Object.State == CLIMBER_ACTIVE
		if Object.BottomStep == RIGHT_STEP
			if Object.Frame > 0
				Object.YPos -= 0xC0000
				Object.Frame--
			else
				if Object.Timer < 120
					Object.Timer++
					if Object.Timer == 60
						Object.FlameAnimation = 1
					end if
				else
					Object.Timer = 0
					Object.State = CLIMBER_DESCENDING
					Object.FlameAnimation = 2
				end if
			end if
		else
			if Object.Frame < 4
				Object.Frame++
				Object.YPos -= 0xC0000
			else
				if Object.Timer < 120
					Object.Timer++
					if Object.Timer == 60
						Object.FlameAnimation = 1
					end if
				else
					Object.Timer = 0
					Object.State = CLIMBER_DESCENDING
					Object.FlameAnimation = 2
				end if
			end if
		end if
	else
		Object.YPos += 0x10000
		if Object.BottomStep == LEFT_STEP
			ObjectTileCollision(CSIDE_FLOOR, -25, 32, 0)
		else
			ObjectTileCollision(CSIDE_FLOOR, 25, 32, 0)
		end if
	end if
end sub


sub ObjectPlayerInteraction
	switch Object.Frame
	case 0
		PlayerObjectCollision(C_PLATFORM, -56, -33, -24, -16)
		if CheckResult == true
			Player.YPos += 0x10000
			if Object.BottomStep == RIGHT_STEP
				Object.BottomStep	  = LEFT_STEP
				Object.State		  = CLIMBER_ACTIVE
				Object.FlameAnimation = 0
				Object.Timer = 0
			end if
		end if

		PlayerObjectCollision(C_PLATFORM, 24, 15, 56, 32)
		if CheckResult == true
			Player.YPos += 0x10000
			if Object.BottomStep == LEFT_STEP
				Object.BottomStep	  = RIGHT_STEP
				Object.State		  = CLIMBER_ACTIVE
				Object.FlameAnimation = 0
				Object.Timer = 0
			end if
		end if
		break

	case 1
		PlayerObjectCollision(C_PLATFORM, -56, -21, -24, -4)
		if CheckResult == true
			Player.YPos += 0x10000
			if Object.BottomStep == RIGHT_STEP
				Object.BottomStep = LEFT_STEP
				Object.State = CLIMBER_ACTIVE
				Object.FlameAnimation = 0
				Object.Timer = 0
			end if
		end if
		
		PlayerObjectCollision(C_PLATFORM, 24, 3, 56, 20)
		if CheckResult == true
			Player.YPos += 0x10000
			if Object.BottomStep == LEFT_STEP
				Object.BottomStep	  = RIGHT_STEP
				Object.State		  = CLIMBER_ACTIVE
				Object.FlameAnimation = 0
				Object.Timer = 0
			end if
		end if
		break

	case 2
		PlayerObjectCollision(C_PLATFORM, -56, -9, -24, 8)
		if CheckResult == true
			Player.YPos += 0x10000
			if Object.BottomStep == RIGHT_STEP
				Object.BottomStep	  = LEFT_STEP
				Object.State		  = CLIMBER_ACTIVE
				Object.FlameAnimation = 0
				Object.Timer = 0
			end if
		end if

		PlayerObjectCollision(C_PLATFORM, 24, -9, 56, 8)
		if CheckResult == true
			Player.YPos += 0x10000
			if Object.BottomStep == LEFT_STEP
				Object.BottomStep	  = RIGHT_STEP
				Object.State		  = CLIMBER_ACTIVE
				Object.FlameAnimation = 0
				Object.Timer = 0
			end if
		end if
		break

	case 3
		PlayerObjectCollision(C_PLATFORM, -56, 3, -24, 20)
		if CheckResult == true
			Player.YPos += 0x10000
			if Object.BottomStep == RIGHT_STEP
				Object.BottomStep	  = LEFT_STEP
				Object.State		  = CLIMBER_ACTIVE
				Object.FlameAnimation = 0
				Object.Timer = 0
			end if
		end if

		PlayerObjectCollision(C_PLATFORM, 24, -21, 56, -4)
		if CheckResult == true
			Player.YPos += 0x10000
			if Object.BottomStep == LEFT_STEP
				Object.BottomStep	  = RIGHT_STEP
				Object.State		  = CLIMBER_ACTIVE
				Object.FlameAnimation = 0
				Object.Timer = 0
			end if
		end if
		break

	case 4
		PlayerObjectCollision(C_PLATFORM, -56, 15, -24, 32)
		if CheckResult == true
			Player.YPos += 0x10000
			if Object.BottomStep == RIGHT_STEP
				Object.BottomStep	  = LEFT_STEP
				Object.State		  = CLIMBER_ACTIVE
				Object.FlameAnimation = 0
				Object.Timer = 0
			end if
		end if

		PlayerObjectCollision(C_PLATFORM, 24, -33, 56, -16)
		if CheckResult == true
			Player.YPos += 0x10000
			if Object.BottomStep == LEFT_STEP
				Object.BottomStep 	  = RIGHT_STEP
				Object.State		  = CLIMBER_ACTIVE
				Object.FlameAnimation = 0
				Object.Timer = 0
			end if
		end if
		break

	end switch
end sub


sub ObjectDraw
	switch Object.Frame
	case 0
		DrawSprite(0)
		DrawSprite(3)
		DrawSpriteFX(7, FX_FLIP, Object.XPos, Object.YPos)
		break

	case 1
		DrawSprite(1)
		DrawSprite(4)
		DrawSpriteFX(6, FX_FLIP, Object.XPos, Object.YPos)
		break

	case 2
		DrawSprite(2)
		DrawSprite(5)
		DrawSpriteFX(5, FX_FLIP, Object.XPos, Object.YPos)
		break

	case 3
		DrawSpriteFX(1, FX_FLIP, Object.XPos, Object.YPos)
		DrawSprite(6)
		DrawSpriteFX(4, FX_FLIP, Object.XPos, Object.YPos)
		break

	case 4
		DrawSpriteFX(0, FX_FLIP, Object.XPos, Object.YPos)
		DrawSprite(7)
		DrawSpriteFX(3, FX_FLIP, Object.XPos, Object.YPos)
		break

	end switch
	
	switch Object.FlameAnimation
	case 0
		TempValue0  = Object.FlameFrame
		TempValue0 /= 3
		TempValue0 += 8

		Object.FlameFrame++
		Object.FlameFrame %= 6
		break
		
	case 1 // leaving this animation sometimes creates a bug where the frame gets out of index, resulting in garbage sprites
		TempValue0  = Object.FlameFrame
		TempValue0 /= 3
		TempValue0 += 8

		Object.FlameFrame++
		Object.FlameFrame %= 12
		break

	case 2
		TempValue0  = Object.FlameFrame
		TempValue0 /= 3
		TempValue0 += 10

		Object.FlameFrame++
		Object.FlameFrame %= 6
		break

	end switch
	DrawSprite(TempValue0)
end sub


sub ObjectStartup
	LoadSpriteSheet("R6/Objects3.gif")

	SpriteFrame(-24, -24, 48, 48, 1, 52)	// #0  - Climber Body
	SpriteFrame(-24, -16, 48, 32, 174, 220)	// #1  - Climber Body half-step
	SpriteFrame(-24, -8, 48, 23, 122, 181)	// #2  - Climber Body balanced

	SpriteFrame(-56, -32, 32, 16, 223, 233)	// #3  - Climber Step Bottom
	SpriteFrame(-56, -20, 32, 16, 223, 233)	// #4  - Climber Half-Step Bottom
	SpriteFrame(-56, -8, 32, 16, 223, 233)	// #5  - Climber Middle-Step
	SpriteFrame(-56, 4, 32, 16, 223, 233)	// #6  - Climber Half-Step Top
	SpriteFrame(-56, 16, 32, 16, 223, 233)	// #7  - Climber Step Top

	SpriteFrame(-6, 15, 12, 9, 197, 200)	// #8  - Powered Flame frame 0
	SpriteFrame(-6, 15, 12, 9, 210, 200)	// #9  - Powered Flame frame 1
	SpriteFrame(-6, 15, 12, 9, 197, 210)	// #10 - Flame frame 0
	SpriteFrame(-6, 15, 12, 9, 210, 210)	// #11 - Flame frame 1

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Climber]
			Object[ArrayPos0].YOriginPos = Object[ArrayPos0].YPos
			Object[ArrayPos0].Direction  = FACING_LEFT
		end if
		ArrayPos0++
	loop
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
	DrawSprite(3)
	Object.Direction = FACING_LEFT
	DrawSpriteFX(7, FX_FLIP, Object.XPos, Object.YPos)
end sub


sub RSDKLoad
	LoadSpriteSheet("R6/Objects3.gif")

	SpriteFrame(-24, -24, 48, 48, 1, 52)	// #0  - Climber Body
	SpriteFrame(-24, -16, 48, 32, 174, 220)	// #1  - Climber Body half-step
	SpriteFrame(-24, -8, 48, 23, 122, 181)	// #2  - Climber Body balanced

	SpriteFrame(-56, -32, 32, 16, 223, 233)	// #3  - Climber Step Bottom
	SpriteFrame(-56, -20, 32, 16, 223, 233)	// #4  - Climber Half-Step Bottom
	SpriteFrame(-56, -8, 32, 16, 223, 233)	// #5  - Climber Middle-Step
	SpriteFrame(-56, 4, 32, 16, 223, 233)	// #6  - Climber Half-Step Top
	SpriteFrame(-56, 16, 32, 16, 223, 233)	// #7  - Climber Step Top

	SpriteFrame(-6, 15, 12, 9, 197, 200)	// #8  - Powered Flame frame 0
	SpriteFrame(-6, 15, 12, 9, 210, 200)	// #9  - Powered Flame frame 1
	SpriteFrame(-6, 15, 12, 9, 197, 210)	// #10 - Flame frame 0
	SpriteFrame(-6, 15, 12, 9, 210, 210)	// #11 - Flame frame 1
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
