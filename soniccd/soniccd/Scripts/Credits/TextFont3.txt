//---------------Sonic CD Text Font 3 Script------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value1	:	Object.TargetLine

// Text Info type
#alias 0	:	TEXTINFO_TEXTDATA
#alias 1	:	TEXTINFO_TEXTSIZE


sub ObjectDraw

	// Starting from the third letter because the first 3 aren't actually credits text
	ArrayPos0 = 3

	// Get how many letters are in the line
	GetTextInfo(TempValue1, MENU_1, TEXTINFO_TEXTSIZE, Object.TargetLine, 0)

	// And subtract 3 from that for the resasons stated above
	TempValue1 -= 3

	// And since the text should be centered, get its position offset now
	TempValue2   = TempValue1
	TempValue2 <<= 18
	FlipSign(TempValue2)
	TempValue2  += Object.XPos

	// Loop through all the characters in the text line
	while TempValue1 > 0

		// Get what character's at the target position
		GetTextInfo(TempValue0, MENU_1, TEXTINFO_TEXTDATA, Object.TargetLine, ArrayPos0)

		// If needed, jump to the expecial exception case
		switch TempValue0
		default
			Object.Frame = 0

			// 65-90 are lowercase letter IDs, turn them into Sprite Frames
			if TempValue0 > 64
				if TempValue0 < 91
					Object.Frame  = TempValue0
					Object.Frame -= 64
				end if
			end if

			// 97-122 are uppercase letter IDs, turn them into Sprite Frames
			if TempValue0 > 96
				if TempValue0 < 123
					Object.Frame  = TempValue0
					Object.Frame -= 96
				end if
			end if

			if Object.Frame > 0
				// If it's a valid Frame, then draw the Sprite
				DrawSpriteXY(Object.Frame, TempValue2, Object.YPos)
			end if
			break

		case 38
			// & (Ampersand)
			DrawSpriteXY(32, TempValue2, Object.YPos)
			break

		case 39
			// ' (Tick)
			DrawSpriteXY(30, TempValue2, Object.YPos)
			break

		case 44
			// , (Comma)
			DrawSpriteXY(28, TempValue2, Object.YPos)
			break

		case 46
			// . (Period)
			DrawSpriteXY(27, TempValue2, Object.YPos)
			break

		case 47
			// / (Forward Slash)
			DrawSpriteXY(31, TempValue2, Object.YPos)
			break

		case 58
			//	:	(Colon)
			DrawSpriteXY(29, TempValue2, Object.YPos)
			break

		case 225
			// á (Accent mark A)
			DrawSpriteXY(33, TempValue2, Object.YPos)
			break

		case 233
			// é (Accent mark E)
			DrawSpriteXY(34, TempValue2, Object.YPos)
			break

		case 243
			// ó (Accent mark O)
			DrawSpriteXY(35, TempValue2, Object.YPos)
			break

		case 250
			// ú (Accent mark U)
			DrawSpriteXY(36, TempValue2, Object.YPos)
			break

		end switch

		// Next character
		ArrayPos0++
		TempValue1--

		// Letters are to be 8 pixels apart
		TempValue2 += 0x80000
	loop

end sub


sub ObjectStartup
	LoadSpriteSheet("Menu/CreditsText.gif")

	// Letter Sprite Frames, primarily using loops

	SpriteFrame(-3, -9, 8, 16, 37, 35)
	
	TempValue1 = 88
	while TempValue1 < 110
		TempValue0 = 1
		while TempValue0 < 127
			SpriteFrame(-3, -5, 8, 10, TempValue0, TempValue1)
			TempValue0 += 9
		loop
		TempValue1 += 11
	loop
	
	TempValue0 = 1
	while TempValue0 < 37
		SpriteFrame(-3, -5, 8, 10, TempValue0, TempValue1)
		TempValue0 += 9
	loop
	
	SpriteFrame(-3, -8, 8, 13, 37, 110)
	SpriteFrame(-3, -8, 8, 13, 46, 110)
	SpriteFrame(-3, -8, 8, 13, 55, 110)
	SpriteFrame(-3, -8, 8, 13, 64, 110)
	
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	TempValue0 = 0
	while TempValue0 < 4
		DrawSprite(TempValue0)
		TempValue0++
	loop
end sub


sub RSDKLoad
	LoadSpriteSheet("Menu/CreditsText.gif")
	SpriteFrame(0, -5, 8, 10, 46, 99)
	SpriteFrame(9, -5, 8, 10, 37, 88)
	SpriteFrame(18, -5, 8, 10, 82, 99)
	SpriteFrame(27, -5, 8, 10, 46, 99)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
