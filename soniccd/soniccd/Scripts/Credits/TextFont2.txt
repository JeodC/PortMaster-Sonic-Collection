//---------------Sonic CD Text Font 2 Script------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value1	:	Object.TargetLine

// Text Info type
#alias 0	:	TEXTINFO_TEXTDATA
#alias 1	:	TEXTINFO_TEXTSIZE


sub ObjectDraw

	// Only show the 4th letter and onwards, the first 3 letters are for identifying the text type
	ArrayPos0 = 3

	// Get how long the line is
	GetTextInfo(TempValue1, MENU_1, TEXTINFO_TEXTSIZE, Object.TargetLine, 0)

	// Taking 3 away from the letter account for the reason mentioned above...
	TempValue1 -= 3

	// Getting the base position to draw the text at
	TempValue2   = TempValue1
	TempValue2 <<= 18
	FlipSign(TempValue2)
	TempValue2  += Object.XPos

	// Main loop for drawing all the letters in the text
	while TempValue1 > 0

		// Get the current target letter
		GetTextInfo(TempValue0, MENU_1, TEXTINFO_TEXTDATA, Object.TargetLine, ArrayPos0)

		// And if it's a special exception, jump to its corresponding Ansi ID
		switch TempValue0
		default

			// Translating lowercase letters to Sprite Frames
			Object.Frame = 0
			if TempValue0 > 64
				if TempValue0 < 91
					Object.Frame  = TempValue0
					Object.Frame -= 64
				end if
			end if
			
			// Translating uppercase letters to Sprite Frames
			if TempValue0 > 96
				if TempValue0 < 123
					Object.Frame  = TempValue0
					Object.Frame -= 96
				end if
			end if

			if Object.Frame > 0
				// Draw the letter, if valid
				DrawSpriteXY(Object.Frame, TempValue2, Object.YPos)
			end if
			break

		case 38
			// Ampersand - &
			DrawSpriteXY(32, TempValue2, Object.YPos)
			break

		case 39
			// Tick - '
			DrawSpriteXY(30, TempValue2, Object.YPos)
			break

		case 44
			// Comma - ,
			DrawSpriteXY(28, TempValue2, Object.YPos)
			break

		case 46
			// Period - .
			DrawSpriteXY(27, TempValue2, Object.YPos)
			break

		case 47
			// Forward slash - /
			DrawSpriteXY(31, TempValue2, Object.YPos)
			break

		case 58
			// Colon - ;
			DrawSpriteXY(29, TempValue2, Object.YPos)
			break
			
		case 225
			// Accent mark A - á
			DrawSpriteXY(33, TempValue2, Object.YPos)
			break

		case 233
			// Accent mark E - é
			DrawSpriteXY(34, TempValue2, Object.YPos)
			break

		case 243
			// Accent mark O - ó
			DrawSpriteXY(35, TempValue2, Object.YPos)
			break

		case 250
			// Accent mark U - ú
			DrawSpriteXY(36, TempValue2, Object.YPos)
			break

		end switch

		// Move to the next letter & decrease the letter count by onw
		ArrayPos0++
		TempValue1--

		// Each letter has 8px spacing
		TempValue2 += 0x80000
	loop

end sub


sub ObjectStartup
	LoadSpriteSheet("Menu/CreditsText.gif")
	
	// Setup all the Sprite Frames
	SpriteFrame(-3, -9, 8, 16, 37, 35)

	TempValue1 = 52
	while TempValue1 < 74
		TempValue0 = 1
		while TempValue0 < 127
			SpriteFrame(-3, -5, 8, 10, TempValue0, TempValue1)
			TempValue0 += 9
		loop
		TempValue1 += 11
	loop

	TempValue0 = 1
	while TempValue0 < 37
		SpriteFrame(-3, -5, 8, 10, TempValue0, TempValue1)
		TempValue0 += 9
	loop
	
	SpriteFrame(-3, -8, 8, 13, 37, 74)
	SpriteFrame(-3, -8, 8, 13, 46, 74)
	SpriteFrame(-3, -8, 8, 13, 55, 74)
	SpriteFrame(-3, -8, 8, 13, 64, 74)

end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	TempValue0 = 0
	while TempValue0 < 4
		DrawSprite(TempValue0)
		TempValue0++
	loop
end sub


sub RSDKLoad
	LoadSpriteSheet("Menu/CreditsText.gif")
	SpriteFrame(0, -5, 8, 10, 46, 63)
	SpriteFrame(9, -5, 8, 10, 37, 52)
	SpriteFrame(18, -5, 8, 10, 82, 63)
	SpriteFrame(27, -5, 8, 10, 46, 63)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
