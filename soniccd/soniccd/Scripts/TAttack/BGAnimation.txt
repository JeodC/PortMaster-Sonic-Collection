//----------------Sonic CD BG Animation Script----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value1	:	Object.VLLYOffset 			// Vertical Line Left
#alias Object.Value2	:	Object.VLRYOffset 			// Vertical Line Right
#alias Object.Value3	:	Object.ArrowXOffset

// Menu Control Aliases
#alias Object[0].State	:	MenuControl.State
#alias Object[0].Value3	:	MenuControl.SavedRound		// This is used AFTER you play in time attack
#alias Object[0].Value7	:	MenuControl.SSUnlocked

// Large Icon / Special Large Icon Aliases
#alias Object[40].XPos	:	Object.LIconXPos

// Menu Control States
// *SM = Sub Menu
#alias 0	:	MENUCONTROL_SETUP
#alias 1	:	MENUCONTROL_FADEIN
#alias 2	:	MENUCONTROL_CONTROLS
#alias 3	:	MENUCONTROL_BLANK
#alias 4	:	MENUCONTROL_RETURN_TO_MENU
#alias 5	:	MENUCONTROL_ENTER_SM
#alias 6	:	MENUCONTROL_CALL_RECORD_MENU
#alias 7	:	MENUCONTROL_SM_CONTROLS
#alias 8	:	MENUCONTROL_SM_SCROLL_DOWN
#alias 9	:	MENUCONTROL_SM_SCROLL_UP
#alias 10	:	MENUCONTROL_SM_CONFIRMED
#alias 11	:	MENUCONTROL_SM_LOAD_LEVEL
#alias 12	:	MENUCONTROL_SM_LEVEL_RETURN
#alias 13	:	MENUCONTROL_SM_COMFIRMED_LEAVE
#alias 14	:	MENUCONTROL_SM_EXIT
#alias 15	:	MENUCONTROL_SM_RECORD_BEATED
#alias 16	:	MENUCONTROL_SCROLL_DOWN
#alias 17	:	MENUCONTROL_SCROLLING_1
#alias 18	:	MENUCONTROL_SCROLL_UP
#alias 19	:	MENUCONTROL_SCROLLING_2


sub ObjectMain
	Object.VLLYOffset--
	if Object.VLLYOffset < -31
		Object.VLLYOffset += 32
	end if

	Object.VLRYOffset++
	if Object.VLRYOffset > -1
		Object.VLRYOffset -= 32
	end if

	Object.ArrowXOffset++
	if Object.ArrowXOffset > -1
		Object.ArrowXOffset -= 96
	end if
end sub


sub ObjectDraw
	if Object.DrawOrder == 3
		DrawRect(0, 0, 2, Screen.YSize, 0, 0, 0, 255)
		TempValue0  = Screen.CenterX
		TempValue0 -= 32
		DrawRect(0, 160, TempValue0, 2, 0, 0, 0, 255)
		TempValue0 += 48

		switch MenuControl.State
		case MENUCONTROL_SETUP
		case MENUCONTROL_FADEIN
		case MENUCONTROL_CONTROLS
		case MENUCONTROL_BLANK
		case MENUCONTROL_RETURN_TO_MENU
		case MENUCONTROL_ENTER_SM
		case MENUCONTROL_SM_EXIT
		case MENUCONTROL_SCROLL_DOWN
		case MENUCONTROL_SCROLLING_1
		case MENUCONTROL_SCROLL_UP
		case MENUCONTROL_SCROLLING_2
			DrawRect(TempValue0, 0, Screen.CenterX, Screen.YSize, 224, 0, 0, 255)
			DrawRect(TempValue0, 175, Screen.CenterX, 38, 0, 0, 96, 255)
			DrawRect(TempValue0, 31, Screen.CenterX, 9, 0, 0, 0, 255)
			break

		case MENUCONTROL_CALL_RECORD_MENU
		case MENUCONTROL_SM_CONTROLS
		case MENUCONTROL_SM_SCROLL_DOWN
		case MENUCONTROL_SM_SCROLL_UP
		case MENUCONTROL_SM_CONFIRMED
		case MENUCONTROL_SM_LOAD_LEVEL
		case MENUCONTROL_SM_LEVEL_RETURN
		case MENUCONTROL_SM_COMFIRMED_LEAVE
		case MENUCONTROL_SM_RECORD_BEATED
			DrawRect(TempValue0, 0, Screen.CenterX, 32, 224, 0, 0, 255)
			DrawRect(TempValue0, 226, Screen.CenterX, 14, 224, 0, 0, 255)
			DrawRect(TempValue0, 31, Screen.CenterX, 2, 0, 0, 0, 255)
			break

		end switch
		TempValue0 -= 50
		DrawRect(2, 0, TempValue0, 160, 160, 160, 64, 255)
		DrawRect(2, 162, TempValue0, 78, 0, 128, 0, 255)
		
		TempValue1   = Object.ArrowXOffset
		TempValue1 >>= 1
		DrawSpriteScreenXY(3, TempValue1, 226)
		DrawSpriteScreenXY(4, TempValue1, 226)
		
		TempValue0  += 2
		TempValue1   = Object.VLLYOffset
		TempValue1 >>= 1
		DrawSpriteScreenXY(1, TempValue0, TempValue1)

		TempValue1   = Object.VLRYOffset
		TempValue1 >>= 1
		DrawSpriteScreenXY(2, TempValue0, TempValue1)

		SetDrawListEntityRef(Object.EntityNo, 4, Screen[4].DrawListSize)
		Screen[4].DrawListSize++
		Object.DrawOrder = 4
	else
		DrawRect(0, 213, Screen.XSize, 13, 0, 0, 0, 255)
		DrawSpriteScreenXY(0, 24, 195)

		TempValue0  = Screen.CenterX
		TempValue0 += 19
		DrawSpriteScreenXY(5, TempValue0, 213)

		if MenuControl.SSUnlocked == true
			TempValue0   = Screen.XSize
			TempValue0  -= 64
			TempValue1   = Object.LIconXPos
			TempValue1  -= -0x480000
			TempValue1 >>= 17
			TempValue0  += TempValue1
			if MenuControl.SavedRound == 8
				DrawSpriteScreenXY(7, TempValue0, 197)
			else
				DrawSpriteScreenXY(6, TempValue0, 197)
			end if
		end if
		Object.DrawOrder = 3
	end if
end sub


sub ObjectStartup
	LoadSpriteSheet("TAttack/TimeAttack.gif")
	LoadPalette("TimeAttack.act", 0, 0, 0, 256)

	MatrixTranslateXYZ(MAT_WORLD, 0, 0, 0)
	MatrixRotateXYZ(MAT_VIEW, 0, 0, 0)
	
	SpriteFrame(0, 0, 152, 30, 70, 666)		// #0 - Time Attack Header
	SpriteFrame(0, 0, 24, 256, 463, 577)	// #1 - BG Vertical Line First Half
	SpriteFrame(24, 0, 24, 256, 487, 577)	// #2 - BG Vertical Line Second Half
	SpriteFrame(0, 0, 448, 14, 1, 701)		// #3 - BG Right Arrow Line
	SpriteFrame(448, 0, 448, 14, 1, 701)	// #4 - BG Left Arrow Line
	SpriteFrame(0, 0, 104, 13, 358, 687)	// #5 - Sonic The Hedgehog CD

#platform: Standard
	SpriteFrame(-16, 0, 80, 16, 326, 798)	// #6 - Next
	SpriteFrame(-16, 0, 80, 16, 326, 815)	// #7 - Next Highlight
#endplatform

#platform: Mobile
	SpriteFrame(0, 0, 64, 16, 326, 764)	// #6 - Next
	SpriteFrame(0, 0, 64, 16, 326, 781)	// #7 - Next Highlight
#endplatform
	Object[31].Type = TypeName[BGAnimation]
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
