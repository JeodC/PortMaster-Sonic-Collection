//-----------------Sonic CD Round Icon Script-----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0		:	Object.Timer
#alias Object.Value1		:	Object.Minutes
#alias Object.Value2		:	Object.Seconds
#alias Object.Value3		:	Object.Milliseconds
#alias Object.Value4		:	Object.LockedZone
#alias Object.PropertyValue	:	Object.Portrait

// Menu Control Aliases
#alias Object[0].Value2		:	MenuControl.SelectedButton			// Only used for Mobile
#alias Object[0].Value3		:	MenuControl.SavedRound				// This is used AFTER you play in time attack
#alias Object[0].State		:	MenuControl.State					// This is used AFTER you play in time attack

// States
#alias 0	:	ROUNDICON_BLANK
#alias 1	:	ROUNDICON_SELECTED_ZONE

// Menu Control States
#alias 2	:	MENUCONTROL_CONTROLS
#alias 3	:	MENUCONTROL_BLANK
#alias 4	:	MENUCONTROL_RETURN_TO_MENU
#alias 5	:	MENUCONTROL_ENTER_SM

// Global SFX
#alias 27	:	SFX_G_SELECT

// Property Values
#alias 0	:	PALMTREE_PANIC_ICON
#alias 1	:	COLLISION_CHAOS_ICON
#alias 2	:	TIDAL_TEMPEST_ICON
#alias 3	:	QUARTZ_QUADRANT_ICON
#alias 4	:	WACKY_WORKBENCH_ICON
#alias 5	:	STARDUST_SPEEDWAY_ICON
#alias 6	:	METALLIC_MADNESS_ICON
#alias 7	:	EXIT_ICON

// Face Buffer flags
#alias 1	:	FACE_FLAG_TEXTURED_2D


sub ObjectMain
#platform: Mobile
	if Options.PhysicalControls == false
		if MenuControl.State == MENUCONTROL_CONTROLS
			if Object.LockedZone == false
				TempValue0  = Object.iXPos
				TempValue0 -= 34
				TempValue1  = Object.iXPos
				TempValue1 += 34
				TempValue2  = Object.iYPos
				TempValue2 -= 45
				TempValue3  = Object.iYPos
				TempValue3 += 45
				CheckTouchRect(TempValue0, TempValue2, TempValue1, TempValue3)
				if CheckResult > -1
					MenuControl.SelectedButton = Object.Portrait
				else
					CheckTouchRect(0, 0, Screen.XSize, Screen.YSize)
					if CheckResult < 0
						if MenuControl.SelectedButton == Object.Portrait
							PlaySfx(SFX_G_SELECT, false)
							MenuControl.State = MENUCONTROL_BLANK
							Object.State	  = ROUNDICON_SELECTED_ZONE
							if Object.Portrait == EXIT_ICON	// Exit button pressed?
								StopMusic()
							end if
						end if
					else
						if MenuControl.SelectedButton == Object.Portrait
							MenuControl.SelectedButton = -1
						end if
					end if
				end if
			end if
		end if
	end if
#endplatform
end sub


sub ObjectDraw
	DrawSprite(10)
	// Icon Movement
	Object.Rotation++
	Object.Rotation &= 511
	Cos(TempValue0, Object.Rotation)
	TempValue0 >>= 4
	Sin(TempValue1, Object.Rotation)
	TempValue1 >>= 4

	switch Object.Portrait
	case PALMTREE_PANIC_ICON
		TempValue0 += 64
		TempValue1 += 76
		break

	case COLLISION_CHAOS_ICON
		TempValue0 += 320
		TempValue1 += 76
		break

	case TIDAL_TEMPEST_ICON
		TempValue0 += 448
		TempValue1 += 76
		break

	case QUARTZ_QUADRANT_ICON
		TempValue0 += 64
		TempValue1 += 220
		break

	case WACKY_WORKBENCH_ICON
		TempValue0 += 192
		TempValue1 += 220
		break

	case STARDUST_SPEEDWAY_ICON
		TempValue0 += 320
		TempValue1 += 220
		break

	case METALLIC_MADNESS_ICON
		TempValue0 += 448
		TempValue1 += 220
		break

	end switch

	if Object.Portrait < EXIT_ICON	// Draw for all main stages
		VertexBuffer[0].x   = Object.XPos
		VertexBuffer[0].x >>= 16
		VertexBuffer[0].x  -= 32
		VertexBuffer[0].u   = TempValue0
		VertexBuffer[0].u  -= 32
		VertexBuffer[0].y   = Object.YPos
		VertexBuffer[0].y >>= 16
		VertexBuffer[0].y  -= 43
		VertexBuffer[0].v   = TempValue1
		VertexBuffer[0].v  -= 43
		VertexBuffer[1].x   = Object.XPos
		VertexBuffer[1].x >>= 16
		VertexBuffer[1].x  += 31
		VertexBuffer[1].u   = TempValue0
		VertexBuffer[1].u  += 31
		VertexBuffer[1].y   = VertexBuffer[0].y
		VertexBuffer[1].v   = VertexBuffer[0].v
		VertexBuffer[2].x   = VertexBuffer[0].x
		VertexBuffer[2].u   = VertexBuffer[0].u
		VertexBuffer[2].y   = Object.YPos
		VertexBuffer[2].y >>= 16
		VertexBuffer[2].y  += 24
		VertexBuffer[2].v   = TempValue1
		VertexBuffer[2].v  += 24
		VertexBuffer[3].x   = VertexBuffer[1].x
		VertexBuffer[3].u   = VertexBuffer[1].u
		VertexBuffer[3].y   = VertexBuffer[2].y
		VertexBuffer[3].v   = VertexBuffer[2].v
		Draw3DScene()
		DrawSprite(19)

		TempValue0   = Object.XPos
		TempValue0 >>= 16
		TempValue0  -= 24

		TempValue1   = Object.YPos
		TempValue1 >>= 16
		TempValue1  += 34
		// Zone specific time
		DrawNumbers(0, TempValue0, TempValue1, Object.Minutes, 2, 6, 1)
		TempValue0 += 16
		DrawNumbers(0, TempValue0, TempValue1, Object.Seconds, 2, 6, 1)
		TempValue0 += 19
		DrawNumbers(0, TempValue0, TempValue1, Object.Milliseconds, 2, 6, 1)
	else	// Draw the Exit
		DrawSprite(20)
		TempValue0   = Object.XPos
		TempValue0 >>= 16
		TempValue0  -= 18
		TempValue1   = Object.YPos
		TempValue1 >>= 16
		TempValue1  += 34
		// Total Time
		DrawNumbers(0, TempValue0, TempValue1, Object.Minutes, 3, 6, 1)
		TempValue0 += 16
		DrawNumbers(0, TempValue0, TempValue1, Object.Seconds, 2, 6, 1)
		TempValue0 += 19
		DrawNumbers(0, TempValue0, TempValue1, Object.Milliseconds, 2, 6, 1)
		DrawSprite(22)
	end if

	DrawSprite(Object.Frame)
	if Object.State == ROUNDICON_SELECTED_ZONE
		TempValue0   = Object.Timer
		TempValue0  &= 3
		TempValue0 >>= 1
		if TempValue0 == 0
			if MenuControl.SavedRound == Object.Portrait
				DrawSprite(21)
			end if
		end if

		Object.Timer++
		if Object.Timer == 30
			Object.Timer = 0
			Object.State = ROUNDICON_BLANK
			if Object.Portrait == EXIT_ICON
				MenuControl.State = MENUCONTROL_RETURN_TO_MENU
			else
				MenuControl.State = MENUCONTROL_ENTER_SM
				MenuControl.SelectedButton = -1
			end if
		end if
	else
		if MenuControl.State == MENUCONTROL_CONTROLS
			if MenuControl.SavedRound == Object.Portrait
				DrawSprite(21)
			end if
		end if
	end if

	if Object.LockedZone == true
		DrawSprite(23)
	end if
end sub


sub ObjectStartup
	LoadSpriteSheet("TAttack/TimeAttack.gif")

	SpriteFrame(0, 0, 6, 8, 330, 577)		// #0  - 0
	SpriteFrame(0, 0, 6, 8, 337, 577)		// #1  - 1
	SpriteFrame(0, 0, 6, 8, 344, 577)		// #2  - 2
	SpriteFrame(0, 0, 6, 8, 351, 577)		// #3  - 3
	SpriteFrame(0, 0, 6, 8, 330, 586)		// #4  - 4
	SpriteFrame(0, 0, 6, 8, 337, 586)		// #5  - 5
	SpriteFrame(0, 0, 6, 8, 344, 586)		// #6  - 6
	SpriteFrame(0, 0, 6, 8, 351, 586)		// #7  - 7
	SpriteFrame(0, 0, 6, 8, 330, 595)		// #8  - 8
	SpriteFrame(0, 0, 6, 8, 337, 595)		// #9  - 9
	SpriteFrame(-34, -45, 68, 90, 1, 609)	// #10 - Border
	SpriteFrame(-32, 15, 64, 16, 70, 577)	// #11 - Palmtree Panic
	SpriteFrame(-32, 15, 64, 16, 135, 577)	// #12 - Collision Chaos
	SpriteFrame(-32, 15, 64, 16, 200, 577)	// #13 - Tidal Tempest
	SpriteFrame(-32, 15, 64, 16, 265, 577)	// #14 - Quartz Quadrant
	SpriteFrame(-32, 15, 64, 16, 70, 593)	// #15 - Wacky Workbench
	SpriteFrame(-32, 15, 64, 16, 135, 593)	// #16 - Stardust Speedway
	SpriteFrame(-32, 15, 64, 16, 200, 593)	// #17 - Metallic Madness
	SpriteFrame(-32, 15, 64, 16, 265, 593)	// #18 - Total Time
	SpriteFrame(-32, 31, 64, 13, 70, 638)	// #19 - ' "
	SpriteFrame(-32, 31, 64, 13, 70, 652)	// #20 -  ' "
	SpriteFrame(-35, -46, 70, 92, 441, 860)	// #21 - Selected Border
	SpriteFrame(-29, -22, 56, 21, 223, 679)	// #22 - Exit
	SpriteFrame(20, 16, 8, 13, 330, 604)	// #23 - Lock

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Round Icon]
			Object[ArrayPos0].Frame = Object[ArrayPos0].Portrait
			Object[ArrayPos0].Frame += 11
			Object[ArrayPos0].Rotation = Object[ArrayPos0].Portrait
			Object[ArrayPos0].Rotation <<= 8
			if Object[ArrayPos0].Portrait > 3
				Object[ArrayPos0].Rotation += 256
			end if
		end if
		ArrayPos0++
	loop

	3DScene.NoVertices = 4
	3DScene.NoFaces    = 1
	FaceBuffer[0].a    = 0
	FaceBuffer[0].b    = 1
	FaceBuffer[0].c    = 2
	FaceBuffer[0].d    = 3
	FaceBuffer[0].Flag = FACE_FLAG_TEXTURED_2D
end sub


// ========================
// Editor Subs
// ========================

sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			CheckResult = Object.Portrait
			CheckResult &= 7
			break
		case 0 // roundID
			CheckResult = Object.Portrait
			CheckResult &= 7
			break
		end switch
	else
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			Object.Portrait = Editor.VariableValue
			Object.Portrait &= 7
			break
		case 0 // roundID
			Object.Portrait = Editor.VariableValue
			Object.Portrait &= 7
			break
		end switch
	end if
end sub


sub RSDKDraw
	Object.Frame = Object.Portrait
	Object.Frame += 11

	DrawSprite(10)

	if Object.Portrait < 7	// Draw for all main stages
		DrawSprite(19)
	else	// Draw the Exit
		DrawSprite(20)
		DrawSprite(22)
	end if

	DrawSprite(Object.Frame)
end sub


sub RSDKLoad
	LoadSpriteSheet("TAttack/TimeAttack.gif")
	SpriteFrame(0, 0, 6, 8, 330, 577)		// #0  - 0
	SpriteFrame(0, 0, 6, 8, 337, 577)		// #1  - 1
	SpriteFrame(0, 0, 6, 8, 344, 577)		// #2  - 2
	SpriteFrame(0, 0, 6, 8, 351, 577)		// #3  - 3
	SpriteFrame(0, 0, 6, 8, 330, 586)		// #4  - 4
	SpriteFrame(0, 0, 6, 8, 337, 586)		// #5  - 5
	SpriteFrame(0, 0, 6, 8, 344, 586)		// #6  - 6
	SpriteFrame(0, 0, 6, 8, 351, 586)		// #7  - 7
	SpriteFrame(0, 0, 6, 8, 330, 595)		// #8  - 8
	SpriteFrame(0, 0, 6, 8, 337, 595)		// #9  - 9
	SpriteFrame(-34, -45, 68, 90, 1, 609)	// #10 - Border
	SpriteFrame(-32, 15, 64, 16, 70, 577)	// #11 - Palmtree Panic
	SpriteFrame(-32, 15, 64, 16, 135, 577)	// #12 - Collision Chaos
	SpriteFrame(-32, 15, 64, 16, 200, 577)	// #13 - Tidal Tempest
	SpriteFrame(-32, 15, 64, 16, 265, 577)	// #14 - Quartz Quadrant
	SpriteFrame(-32, 15, 64, 16, 70, 593)	// #15 - Wacky Workbench
	SpriteFrame(-32, 15, 64, 16, 135, 593)	// #16 - Stardust Speedway
	SpriteFrame(-32, 15, 64, 16, 200, 593)	// #17 - Metallic Madness
	SpriteFrame(-32, 15, 64, 16, 265, 593)	// #18 - Total Time
	SpriteFrame(-32, 31, 64, 13, 70, 638)	// #19 - ' "
	SpriteFrame(-32, 31, 64, 13, 70, 652)	// #20 -  ' "
	SpriteFrame(-35, -46, 70, 92, 441, 860)	// #21 - Selected Border
	SpriteFrame(-29, -22, 56, 21, 223, 679)	// #22 - Exit
	SpriteFrame(20, 16, 8, 13, 330, 604)	// #23 - Lock

	AddEditorVariable("roundID")
	SetActiveVariable("roundID")
	AddEnumVariable("Palmtree Panic", 0)
	AddEnumVariable("Collision Chaos", 1)
	AddEnumVariable("Tidal Tempest", 2)
	AddEnumVariable("Quartz Quadrant", 3)
	AddEnumVariable("Wacky Workbench", 4)
	AddEnumVariable("Stardust Speedway", 5)
	AddEnumVariable("Metallic Madness", 6)
	AddEnumVariable("Total Time", 7)
end sub
