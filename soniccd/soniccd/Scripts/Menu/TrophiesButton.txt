//-----------------Sonic CD Exit Button Script----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//
//-----This Object is normally only used by WP7 Data files----//

// Aliases
#alias Object.Value1	:	Object.InputCheck

//Object[0] is Trophies Menu
#alias Object[0].State	:	ThropiesMenu.State

// Thropies Menu States
#alias 2	:	TROPHIES_CONTROLS
#alias 5	:	TROPHIES_EXIT

// Global SFX
#alias 26	:	SFX_G_OUTTAHERE

//Engine & CallBacks
#alias 2	:	MESSAGE_LOSTFOCUS


sub ObjectMain
	if ThropiesMenu.State == TROPHIES_CONTROLS
		if Engine.Message == MESSAGE_LOSTFOCUS
			Object.InputCheck = 0
		end if
		TempValue0   = Object.XPos
		TempValue0 >>= 16
		TempValue1   = TempValue0
		TempValue0  -= 18
		TempValue0  -= Screen.XOffset
		TempValue1  += 18
		TempValue1  -= Screen.XOffset
		CheckTouchRect(TempValue0, 0, TempValue1, 48)
		if CheckResult > -1
			 Object.InputCheck = true
		else
			CheckTouchRect(0, 0, Screen.XSize, Screen.YSize)
			if CheckResult < 0
				if Object.InputCheck == true
					Object.InputCheck = false
					PlaySfx(SFX_G_OUTTAHERE, false)
					StopMusic()
					ThropiesMenu.State = TROPHIES_EXIT
				end if
			else
				 Object.InputCheck = false
			end if
		end if
	end if
end sub


sub ObjectDraw
	Object.Scale = 128
	if Object.InputCheck == true
		DrawSpriteFX(1, FX_SCALE, Object.XPos, Object.YPos)
	else
		DrawSpriteFX(0, FX_SCALE, Object.XPos, Object.YPos)
	end if
end sub


sub ObjectStartup
	LoadSpriteSheet("Menu/Trophies.gif")

	SpriteFrame(-63, -39, 126, 78, 129, 385)	//#0 - Back Button
	SpriteFrame(-60, -37, 120, 74, 260, 387)	//#1 - Pressed Back Button

	TempValue0   = Screen.CenterX
	TempValue0  -= 160
	TempValue0 <<= 16

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Exit Button]
			Object[ArrayPos0].XPos += TempValue0
		end if
		ArrayPos0++
	loop
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	Object.Scale = 128
	DrawSpriteFX(0, FX_SCALE, Object.XPos, Object.YPos)
end sub


sub RSDKLoad
	LoadSpriteSheet("Menu/Trophies.gif")

	SpriteFrame(-63, -39, 126, 78, 129, 385)	//#0 - Back Button

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
