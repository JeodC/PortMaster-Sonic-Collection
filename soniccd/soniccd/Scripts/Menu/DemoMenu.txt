//------------------Sonic CD Demo Menu Script-----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//
//---This Object is normally only used by Lite Data files-----//

// Aliases
#alias Object.Value0			:	Object.Fade
#alias Object.Value1			:	Object.CurrentButton
#alias Object.Value2			:	Object.CurrentRecord
#alias Object.Value3			:	Object.BossUnlockRecord

// MenuHeading Aliases
#alias Object[33].State			:	MenuHeading.State
#alias Object[33].PropertyValue	:	MenuHeading.Header

// MenuWindow Alias
#alias Object[-1].State			:	MenuWindow.State

// States
#alias 0	:	DEMOMENU_FADEIN
#alias 1	:	DEMOMENU_CONTROLS
#alias 2	:	DEMOMENU_LEVEL_SELECTED
#alias 3	:	DEMOMENU_BACK
#alias 4	:	DEMOMENU_LEVEL_LOAD

// Menu Heading States
#alias 1	:	MENUHEADING_LEAVE_SCREEN
#alias 2	:	MENUHEADING_ENTER_SCREEN

// MenuButton States
#alias 1	:	MENUBUTTON_BUTTONS_IN

// MenuWindow States
#alias 3	:	MENUWINDOW_SHRINK

// MenuHeading PropertyValues
#alias 5	:	MAINMENU_HEADER

// Presentation Stages
#alias 0	:	STAGE_R_PPZ1A
#alias 34	:	STAGE_R_QQZ2A
#alias 38	:	STAGE_R_QQZ3C

// Global SFX
#alias 23	:	SFX_G_MENUBUTTON
#alias 27	:	SFX_G_SELECT

// Stage SFX
#alias 1	:	SFX_S_BACKBUTTON

// Game Mode Aliases
#alias 3	:	MODE_TRIAL

//SaveRAM Values
// 48	:	PPZ1DemoRecord
// 49	:	QQZ2DemoRecord

// Function declarations
#function DemoMenu_StageTimeRecord
#function DemoMenu_StageTotalTimeRecord
#function DemoMenu_StagePortraitMovement


function DemoMenu_StageTimeRecord
	TempValue4  = TempValue0
	TempValue4 += 4
	TempValue2  = TempValue1
	TempValue2 /= 6000
	TempValue3  = TempValue2
	TempValue3 /= 10
	TempValue3 += 11
	DrawSpriteScreenFX(TempValue3, FX_INK, TempValue4, 136)

	TempValue4 += 6
	TempValue3  = TempValue2
	TempValue3 %= 10
	TempValue3 += 11
	DrawSpriteScreenFX(TempValue3, FX_INK, TempValue4, 136)

	TempValue4 += 11
	TempValue2  = TempValue1
	TempValue2 /= 100
	TempValue2 %= 60
	TempValue3  = TempValue2
	TempValue3 /= 10
	TempValue3 += 11
	DrawSpriteScreenFX(TempValue3, FX_INK, TempValue4, 136)

	TempValue4 += 6
	TempValue3  = TempValue2
	TempValue3 %= 10
	TempValue3 += 11
	DrawSpriteScreenFX(TempValue3, FX_INK, TempValue4, 136)

	TempValue4 += 13
	TempValue2  = TempValue1
	TempValue2 %= 100
	TempValue3  = TempValue2
	TempValue3 /= 10
	TempValue3 += 11
	DrawSpriteScreenFX(TempValue3, FX_INK, TempValue4, 136)

	TempValue4 += 6
	TempValue3  = TempValue2
	TempValue3 %= 10
	TempValue3 += 11
	DrawSpriteScreenFX(TempValue3, FX_INK, TempValue4, 136)
end function


function DemoMenu_StageTotalTimeRecord
	TempValue4  = TempValue0
	TempValue2  = TempValue1
	TempValue2 /= 6000
	TempValue3  = TempValue2
	TempValue3 /= 10
	TempValue3 += Object.Frame
	DrawSpriteScreenFX(TempValue3, FX_INK, TempValue4, 155)

	TempValue4 += 8
	TempValue3  = TempValue2
	TempValue3 %= 10
	TempValue3 += Object.Frame
	DrawSpriteScreenFX(TempValue3, FX_INK, TempValue4, 155)

	TempValue4 += 15
	TempValue2  = TempValue1
	TempValue2 /= 100
	TempValue2 %= 60
	TempValue3  = TempValue2
	TempValue3 /= 10
	TempValue3 += Object.Frame
	DrawSpriteScreenFX(TempValue3, FX_INK, TempValue4, 155)

	TempValue4 += 8
	TempValue3  = TempValue2
	TempValue3 %= 10
	TempValue3 += Object.Frame
	DrawSpriteScreenFX(TempValue3, FX_INK, TempValue4, 155)

	TempValue4 += 17
	TempValue2  = TempValue1
	TempValue2 %= 100
	TempValue3  = TempValue2
	TempValue3 /= 10
	TempValue3 += Object.Frame
	DrawSpriteScreenFX(TempValue3, FX_INK, TempValue4, 155)
	
	TempValue4 += 8
	TempValue3  = TempValue2
	TempValue3 %= 10
	TempValue3 += Object.Frame
	DrawSpriteScreenFX(TempValue3, FX_INK, TempValue4, 155)
end function


function DemoMenu_StagePortraitMovement
	Cos(TempValue4, Object.Rotation)
	TempValue4 >>= 4
	TempValue4  += TempValue2
	Sin(TempValue5, Object.Rotation)
	TempValue5  *= 3
	TempValue5 >>= 6
	TempValue5  += TempValue3
	Object.Rotation += 128
	Object.Rotation &= 511
	EditFrame(10, 2, 2, 64, 58, TempValue4, TempValue5)
end function


sub ObjectMain
	switch Object.State
	case DEMOMENU_FADEIN
		if Options.PhysicalControls == false
			Object.CurrentButton = -1
		else
			Object.CurrentButton = 0
		end if

		if Object.Fade == 0
			Object.BossUnlockRecord = 9000
			Object.CurrentRecord = SaveRAM[48]	// PPZ1 Demo Record
			Object.CurrentRecord += SaveRAM[49]	// QQZ2 Demo Record
			if Object.CurrentRecord == 0
				SaveRAM[48] = 60000
				SaveRAM[49] = 60000
				Object.CurrentRecord = 120000
			end if
		end if

		if Object.Fade < 256
			Object.Alpha = Object.Fade
			Object.Fade += 6
		else
			Object.Fade  = 0
			Object.Alpha = 255
			Object.State++
		end if
		break

	case DEMOMENU_CONTROLS
		if Options.PhysicalControls == true

			if KeyPress[0].Left == true
				Object.CurrentButton--
				if Object.CurrentButton < 0
					Object.CurrentButton = 2
				end if
				PlaySfx(SFX_G_MENUBUTTON, false)
			end if

			if KeyPress[0].Right == true
				Object.CurrentButton++
				if Object.CurrentButton > 2
					Object.CurrentButton = 0
				end if
				PlaySfx(SFX_G_MENUBUTTON, false)
			end if

			CheckTouchRect(0, 0, Screen.XSize, Screen.YSize)
			if CheckResult > -1
				Options.PhysicalControls = false
				Object.CurrentButton = -1
			end if
		else

			CheckTouchRect(0, 0, Screen.XSize, Screen.YSize)
			TempValue3  = CheckResult
			TempValue0  = Screen.CenterX
			TempValue0 -= 113
			TempValue1  = Screen.CenterX
			TempValue1 -= 45
			CheckTouchRect(TempValue0, 67, TempValue1, 157)
			if CheckResult > -1
				Object.CurrentButton = 0
			else
				if TempValue3 < 0
					if Object.CurrentButton == 0
						KeyPress[0].Start = true
					end if
				else
					if Object.CurrentButton == 0
						Object.CurrentButton = -1
					end if
				end if
			end if

			TempValue0  = Screen.CenterX
			TempValue0 -= 35
			TempValue1  = Screen.CenterX
			TempValue1 += 35
			CheckTouchRect(TempValue0, 67, TempValue1, 157)
			if CheckResult > -1
				Object.CurrentButton = 1
			else
				if TempValue3 < 0
					if Object.CurrentButton == 1
						KeyPress[0].Start = true
					end if
				else
					if Object.CurrentButton == 1
						Object.CurrentButton = -1
					end if
				end if
			end if

			if Object.CurrentRecord <= Object.BossUnlockRecord
				TempValue0  = Screen.CenterX
				TempValue0 += 45
				TempValue1  = Screen.CenterX
				TempValue1 += 113
				CheckTouchRect(TempValue0, 67, TempValue1, 157)
				if CheckResult > -1
					Object.CurrentButton = 2
				else
					if TempValue3 < 0
						if Object.CurrentButton == 2
							KeyPress[0].Start = true
						end if
					else
						if Object.CurrentButton == 2
							Object.CurrentButton = -1
						end if
					end if
				end if
			end if
			CallFunction(MenuHeading_CheckTouchBack)

			if KeyPress[0].Left == true
				Object.CurrentButton = 1
				Options.PhysicalControls = true
			end if

			if KeyPress[0].Right == true
				Object.CurrentButton = 0
				Options.PhysicalControls = true
			end if

		end if

		CheckResult = false
		if Object.CurrentButton > -1
			if KeyPress[0].Start == true
				if Object.CurrentButton == 2
					if Object.CurrentRecord <= Object.BossUnlockRecord
						CheckResult = true
					end if
				else
					CheckResult = true
				end if
			end if

			if KeyPress[0].ButtonA == true
				if Object.CurrentButton == 2
					if Object.CurrentRecord <= Object.BossUnlockRecord
						CheckResult = true
					end if
				else
					CheckResult = true
				end if
			end if
		end if

		if CheckResult == true
			Object.State++
			StopMusic()
			PlaySfx(SFX_G_SELECT, false)
		end if

		if KeyPress[0].ButtonB == true
			Object.Fade  = 248
			Object.State = DEMOMENU_BACK
			PlayStageSfx(SFX_S_BACKBUTTON, false)
		end if
		break

	case DEMOMENU_LEVEL_SELECTED
		if Object.AnimationTimer < 30
			Object.AnimationTimer++
		else
			Object.AnimationTimer = 0
			Object.State = DEMOMENU_LEVEL_LOAD
		end if
		break

	case DEMOMENU_BACK
		if Object.Fade > 0
			Object.Alpha = Object.Fade
			Object.Fade -= 8
		else
			Object.Type = TypeName[Blank Object]
			MenuWindow.State = MENUWINDOW_SHRINK
			//All of this is MenuButton Objects
			Object[34].State = MENUBUTTON_BUTTONS_IN
			Object[35].State = MENUBUTTON_BUTTONS_IN
			Object[36].State = MENUBUTTON_BUTTONS_IN
			Object[37].State = MENUBUTTON_BUTTONS_IN
			Object[38].State = MENUBUTTON_BUTTONS_IN
			Object[39].State = MENUBUTTON_BUTTONS_IN
			Object[40].State = MENUBUTTON_BUTTONS_IN
			Object[41].State = MENUBUTTON_BUTTONS_IN
			Object[42].State = MENUBUTTON_BUTTONS_IN
			//End
			MenuHeading.State  = MENUHEADING_LEAVE_SCREEN
			MenuHeading.Header = MAINMENU_HEADER
		end if
		break

	case DEMOMENU_LEVEL_LOAD
		if Object.Fade < 320
			Object.Fade += 8
		else
			Options.GameMode	  = MODE_TRIAL
			Player.Score		  = 0
			Player.Lives		  = 3
			Player.ScoreBonus	  = 50000
			Transporter_Destroyed = false
			MetalSonic_Destroyed  = false
			
			TimeAttack.Round = Object.CurrentButton
			Stage.ActiveList = REGULAR_STAGE
			switch Object.CurrentButton
			case 0
				Stage.ListPos = STAGE_R_PPZ1A
				Good_Future = false
				break
			case 1
				Stage.ListPos = STAGE_R_QQZ2A
				Good_Future = false
				break
			case 2
				Stage.ListPos = STAGE_R_QQZ3C
				Good_Future = true
				break
			end switch
			LoadStage()
		end if
		SetScreenFade(0, 0, 0, Object.Fade)
		break

	end switch
end sub


sub ObjectDraw
	TempValue0  = Screen.CenterX
	TempValue0 -= 113
	DrawSpriteScreenFX(0, FX_INK, TempValue0, 57)

	TempValue2 = 33
	TempValue3 = 267
	CallFunction(DemoMenu_StagePortraitMovement)

	DrawSpriteScreenFX(10, FX_INK, TempValue0, 57)
	DrawSpriteScreenFX(6, FX_INK, TempValue0, 134)

	TempValue1 = SaveRAM[48]	// PPZ1DemoRecord
	CallFunction(DemoMenu_StageTimeRecord)

	TempValue0 += 78
	DrawSpriteScreenFX(1, FX_INK, TempValue0, 57)

	TempValue2 = 33
	TempValue3 = 396
	CallFunction(DemoMenu_StagePortraitMovement)

	DrawSpriteScreenFX(10, FX_INK, TempValue0, 57)
	DrawSpriteScreenFX(6, FX_INK, TempValue0, 134)

	TempValue1 = SaveRAM[49]	// QQZ2DemoRecord
	CallFunction(DemoMenu_StageTimeRecord)

	TempValue0 += 78
	DrawSpriteScreenFX(2, FX_INK, TempValue0, 57)

	TempValue2 = 321
	TempValue3 = 399
	CallFunction(DemoMenu_StagePortraitMovement)

	DrawSpriteScreenFX(10, FX_INK, TempValue0, 57)

	Object.Rotation += 129
	Object.Rotation &= 511

	TempValue0 = Screen.CenterX

	TempValue1   = Object.AnimationTimer
	TempValue1  &= 3
	TempValue1 >>= 1
	TempValue1  += 7

	switch Object.CurrentButton
	case 0
		TempValue0 -= 113
		DrawSpriteScreenFX(TempValue1, FX_INK, TempValue0, 57)
		break

	case 1
		TempValue0 -= 35
		DrawSpriteScreenFX(TempValue1, FX_INK, TempValue0, 57)
		break

	case 2
		TempValue0 += 43
		DrawSpriteScreenFX(TempValue1, FX_INK, TempValue0, 57)
		break

	end switch

	if Object.CurrentRecord > Object.BossUnlockRecord
		TempValue0  = Screen.CenterX
		TempValue0 += 97
		DrawSpriteScreenFX(9, FX_INK, TempValue0, 128)

		TempValue0  = Screen.CenterX
		TempValue0 -= 113
		DrawSpriteScreenFX(3, FX_INK, TempValue0, 155)
		DrawSpriteScreenFX(4, FX_INK, TempValue0, 155)

		TempValue0  += 44
		Object.Frame = 21
		TempValue1	 = Object.CurrentRecord
		CallFunction(DemoMenu_StageTotalTimeRecord)

		TempValue0  += 114
		DrawSpriteScreenFX(5, FX_INK, TempValue0, 154)

		TempValue0  += 11
		Object.Frame = 31
		TempValue1 	 = Object.BossUnlockRecord
		CallFunction(DemoMenu_StageTotalTimeRecord)
	else
		TempValue0  = Screen.CenterX
		TempValue0 -= 113
		DrawSpriteScreenFX(3, FX_INK, TempValue0, 155)
		DrawSpriteScreenFX(4, FX_INK, TempValue0, 155)

		TempValue0 += 44

		Object.Frame = 21
		TempValue1   = Object.CurrentRecord
		CallFunction(DemoMenu_StageTotalTimeRecord)
	end if
end sub


sub ObjectStartup
	LoadSpriteSheet("Menu/MenuGfx1_EN.gif")

	SpriteFrame(0, 0, 68, 90, 321, 1)		//#0  - Palmtree Panic Border
	SpriteFrame(0, 0, 68, 90, 321, 92)		//#1  - Quartz Quadrant Border
	SpriteFrame(0, 0, 68, 90, 321, 183)		//#2  - Boss Border
	SpriteFrame(0, 0, 36, 13, 219, 440)		//#3  - Total
	SpriteFrame(61, 0, 30, 5, 219, 434)		//#4  - ´	´´
	SpriteFrame(0, 0, 66, 14, 211, 349)		//#5  - Locked
	SpriteFrame(17, 0, 22, 4, 169, 435)		//#6  - Large	´	´´
	SpriteFrame(-1, -1, 70, 92, 321, 274)	//#7  - Border
	SpriteFrame(-1, -1, 0, 0, 325, 279)		//#8  - Empty
	SpriteFrame(0, -10, 8, 13, 258, 418)	//#9  - Lock
	SpriteFrame(2, 2, 64, 58, 1, 1)			//#10 - Unknown
	// Small Numbers
	SpriteFrame(0, 0, 6, 8, 244, 400)		//#11 - 0
	SpriteFrame(0, 0, 6, 8, 251, 400)		//#12 - 1
	SpriteFrame(0, 0, 6, 8, 258, 400)		//#13 - 2
	SpriteFrame(0, 0, 6, 8, 265, 400)		//#14 - 3
	SpriteFrame(0, 0, 6, 8, 244, 409)		//#15 - 4
	SpriteFrame(0, 0, 6, 8, 251, 409)		//#16 - 5
	SpriteFrame(0, 0, 6, 8, 258, 409)		//#17 - 6
	SpriteFrame(0, 0, 6, 8, 265, 409)		//#18 - 7
	SpriteFrame(0, 0, 6, 8, 244, 418)		//#19 - 8
	SpriteFrame(0, 0, 6, 8, 251, 418)		//#20 - 9
	// White Numbers
	SpriteFrame(0, 0, 8, 16, 154, 400)		//#21 - 0
	SpriteFrame(0, 0, 8, 16, 163, 400)		//#22 - 1
	SpriteFrame(0, 0, 8, 16, 172, 400)		//#23 - 2
	SpriteFrame(0, 0, 8, 16, 181, 400)		//#24 - 3
	SpriteFrame(0, 0, 8, 16, 190, 400)		//#25 - 4
	SpriteFrame(0, 0, 8, 16, 199, 400)		//#26 - 5
	SpriteFrame(0, 0, 8, 16, 208, 400)		//#27 - 6
	SpriteFrame(0, 0, 8, 16, 217, 400)		//#28 - 7
	SpriteFrame(0, 0, 8, 16, 226, 400)		//#29 - 8
	SpriteFrame(0, 0, 8, 16, 235, 400)		//#30 - 9
	// Red Numbers
	SpriteFrame(0, 0, 8, 16, 154, 417)		//#31 - 0
	SpriteFrame(0, 0, 8, 16, 163, 417)		//#32 - 1
	SpriteFrame(0, 0, 8, 16, 172, 417)		//#33 - 2
	SpriteFrame(0, 0, 8, 16, 181, 417)		//#34 - 3
	SpriteFrame(0, 0, 8, 16, 190, 417)		//#35 - 4
	SpriteFrame(0, 0, 8, 16, 199, 417)		//#36 - 5
	SpriteFrame(0, 0, 8, 16, 208, 417)		//#37 - 6
	SpriteFrame(0, 0, 8, 16, 217, 417)		//#38 - 7
	SpriteFrame(0, 0, 8, 16, 226, 417)		//#39 - 8
	SpriteFrame(0, 0, 8, 16, 235, 417)		//#40 - 9
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-17, -5, 34, 11, 1, 176) // "Demo"

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
