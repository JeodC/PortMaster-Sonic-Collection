//------------------Sonic CD Rabbit Script--------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0			:	Object.YVelocity
#alias Object.Value1			:	Object.Bounces

// HUD alias
#alias Object[24].PropertyValue	:	HUD.CurrentTimePeriod

// States
#alias 0	:	RABBIT_HOPRIGHT
#alias 1	:	RABBIT_HOPLEFT

// Collision Side Aliases
#alias 0	:	CSIDE_FLOOR

// Time Period Aliases
#alias 2	:	TIME_GOOD_FUTURE


sub ObjectMain

	// Note: As with the other animals, this Rabbit is always running, regardless of the stage's or generator's state
	// However, it's just set not to draw if it's in a scenario it shouldn't, although it is always being updated all the same

	// Update the Y movement of the Rabbit
	Object.YPos += Object.YVelocity
	
	// Apply a gravity of ~0.17 pixels per frame
	Object.YVelocity += 0x2C00
	
	switch Object.State
	case RABBIT_HOPRIGHT
	
		// Move the Rabbit right at a speed of 1 pixel per frame
		Object.XPos += 0x10000
		
		// The Rabbit can only interact with the floor if it's falling at a rate of 4 pixels per frame or more
		if Object.YVelocity > 0x40000
		
			// Check to see if the Rabbit's landed
			ObjectTileCollision(CSIDE_FLOOR, 0, 8, 0)
			
			if CheckResult == true
				// If the Rabbit's landed, then bounce it back up again at a rate of 5 px per frame
				Object.YVelocity = -0x50000
				
				// Incerase the Bounce count so that the Rabbit will be able to turn around again when needed
				Object.Bounces++
				
				if Object.Bounces == 2
					// If this is the Rabbit's third bounce in this direction, then turn it around
					
					Object.Bounces = 0
					
					Object.State	 = RABBIT_HOPLEFT
					Object.Direction = FACING_LEFT
				end if
			end if
		end if
		break
		
	case RABBIT_HOPLEFT
	
		// Move the Rabbit left at a 1 pixel per frame rate
		Object.XPos -= 0x10000
		
		// The Rabbit's only allowed to bounce when going downards at a rate of 4 pixels per frame or more
		if Object.YVelocity > 0x40000
		
			// Check to see if the Rabbit's landed
			ObjectTileCollision(CSIDE_FLOOR, 0, 8, 0)
			
			if CheckResult == true
				// The Rabbit's on the floor, so shoot it up again with a velocity of 5 pixels upwards per frame
				Object.YVelocity = -0x50000
				
				// Add to the Bounce count, in order to know when to turn around
				Object.Bounces++
				
				if Object.Bounces == 2
					// The Rabbit should bounce twice, then turn around
					
					Object.Bounces = 0

					Object.State	 = RABBIT_HOPRIGHT
					Object.Direction = FACING_RIGHT
				end if
			end if
		end if
		break
		
	end switch
	
end sub


sub ObjectDraw

	// Only draw if the stage's generator has been destroyed or we're in the Good Future right now

	if MetalSonic_Destroyed == true
		if Object.YVelocity < 0
			DrawSpriteFX(0, FX_FLIP, Object.XPos, Object.YPos)
		else
			DrawSpriteFX(1, FX_FLIP, Object.XPos, Object.YPos)
		end if
	else
		if HUD.CurrentTimePeriod == TIME_GOOD_FUTURE
			if Object.YVelocity < 0
				DrawSpriteFX(0, FX_FLIP, Object.XPos, Object.YPos)
			else
				DrawSpriteFX(1, FX_FLIP, Object.XPos, Object.YPos)
			end if
		end if
	end if

end sub


sub ObjectStartup
	LoadSpriteSheet("R7/Objects.gif")

	// Rabbit Frames
	SpriteFrame(-8, -12, 16, 24, 1, 232)
	SpriteFrame(-8, -12, 16, 24, 18, 232)
	
	// Used to be below LoadSpriteSheet, moved here for consistency
	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Rabbit]
			Object[ArrayPos0].YVelocity = -0x50000
			Object[ArrayPos0].Direction = FACING_RIGHT
		end if
		ArrayPos0++
	loop
	
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("R7/Objects.gif")
	SpriteFrame(-8, -12, 16, 24, 1, 232)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
