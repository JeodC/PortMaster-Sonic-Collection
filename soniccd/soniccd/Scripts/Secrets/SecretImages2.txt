//---------------Sonic CD Secret Images 2 Script--------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.Timer // Also used for fading

// States
#alias 0	:	SECRETIMAGES2_SETUP
#alias 1	:	SECRETIMAGES2_FADE_IN
#alias 2	:	SECRETIMAGES2_CONTROL_DELAY
#alias 3	:	SECRETIMAGES2_CONTROL_CHECK
#alias 4	:	SECRETIMAGES2_EXIT

// Presentation Stage
#alias 3	:	STAGE_P_LEVELSELECT

// Game Mode Aliases
#alias 0	:	MODE_NOSAVE


sub ObjectMain
	switch Object.State
	case SECRETIMAGES2_SETUP
		if Object.Timer < 8
			Object.Timer++
		else
			Object.Timer = 288
			SetActivePalette(1, 0, Screen.YSize)
			Object.State++
		end if
		SetScreenFade(0, 0, 0, 255)
		break

	case SECRETIMAGES2_FADE_IN
		if Object.Timer > 0
			Object.Timer -= 16
		else
			PlayMusic(Options.Soundtrack)
			Object.State++
		end if
		SetScreenFade(0, 0, 0, Object.Timer)
		break

	case SECRETIMAGES2_CONTROL_DELAY
		if Object.Timer < 60
			Object.Timer++
		else
			Object.Timer = 0
			Object.State++
		end if
		break

	case SECRETIMAGES2_CONTROL_CHECK
		if KeyPress[0].ButtonA == true
			Object.State++
		end if

		if KeyPress[0].ButtonB == true
			Object.State++
		end if
		
		if KeyPress[0].ButtonC == true
			Object.State++
		end if

		if KeyPress[0].Start == true
			Object.State++
		end if

		CheckTouchRect(0, 0, Screen.XSize, Screen.YSize)
		if CheckResult > -1
			Object.State++
		end if
		break

	case SECRETIMAGES2_EXIT
		if Object.Timer < 288
			Object.Timer += 8
			Music.Volume -= 5
		else
			SetActivePalette(0, 0, Screen.YSize)
			StopMusic()
			Options.GameMode = MODE_NOSAVE
			Stage.ActiveList = PRESENTATION_STAGE
			Stage.ListPos	 = STAGE_P_LEVELSELECT
			LoadStage()
		end if

		TempValue0 = Object.Timer
		if TempValue0 > 255
			TempValue0 = 255
		end if
		SetScreenFade(0, 0, 0, TempValue0)
		break

	end switch
end sub


sub ObjectDraw
	DrawRect(0, 0, Screen.XSize, Screen.YSize, 224, 224, 224, 255)
	DrawSpriteScreenXY(0, Screen.CenterX, Screen.CenterY)
end sub


sub ObjectStartup
	LoadSpriteSheet("Secrets/DD_R2.gif")
	SpriteFrame(-160, -120, 320, 240, 0, 0)		// #0 - Desert Dazzle
	LoadPalette("DDZ_Pal.act", 1, 0, 0, 256)
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Secrets/DD_R2.gif")
	SpriteFrame(-160, -120, 320, 240, 0, 0)		// #0 - Desert Dazzle

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
