//----------------Sonic CD Glow Effect Script-----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0 : Object.Timer

// Layer Aliases
#alias 1 : LAYER_INSIDE

// Priority
#alias 1 : PRIORITY_ACTIVE


sub ObjectDraw
	
	// Only animate when the currently active BG is the Cave
	if Stage.ActiveLayer[0] == LAYER_INSIDE
		
		// Get the Palette Animation Frame to use
		TempValue0 = Object.Timer
		TempValue0 /= 6
		
		// From this Frame, jump as needed to the Palette ID needed
		switch TempValue0
		case 0
			// Cases 0/3 are all the same and could probably be combined,
			// akin to 4/8 or 5/7, but I suppose they just didn't feel like doing that...
			SetActivePalette(0, 0, Screen.YSize)
			break
		case 1
			SetActivePalette(0, 0, Screen.YSize)
			break
		case 2
			SetActivePalette(0, 0, Screen.YSize)
			break
		case 3
			SetActivePalette(0, 0, Screen.YSize)
			break
		case 4
		case 8
			SetActivePalette(1, 0, Screen.YSize)
			break
		case 5
		case 7
			SetActivePalette(2, 0, Screen.YSize)
			break
		case 6
			SetActivePalette(3, 0, Screen.YSize)
			break
		end switch
		
		// Animate as needed
		Object.Timer++
		Object.Timer %= 54
		
	end if
	
end sub


sub ObjectStartup
	
	// This object is only for when Software rendering is active
	// That's because both this and the Conveyors use palette effects, and due to how the HW render works only one can be active at a time
	// The Conveyors take precedence since they're part of the actual gameplay, these BG rocks are just a visual effect so they're stuffed in the BG and left unanimated in that case
#platform: SW_Rendering
	
	// Place a Glow Effect Object into the level
	Object[21].Type = TypeName[Glow Effect]
	
	// Make it active, since BG rocks should always glow throughout the stage
	Object[21].Priority = PRIORITY_ACTIVE
	
	// Give it the lowest Draw Order possible, so that it only effects the BG Rocks
	// This Draw Order is intentionally set to be lower than the Conveyor Belt Object,
	// which is Draw Order 1 and does Palette trickery too
	Object[21].DrawOrder = 0
	
	// Setup all the Palettes
	
	// There's no need to copy palette bank 0 into all the others,
	// because that's already been done by the Conveyor Belt Object, which comes before this one
	
	TempValue0 = 0
	TempValue1 = 0
	TempValue2 = 16
	while TempValue0 < 5
		LoadPalette("R5A_GlowEffect.act", TempValue0, 176, TempValue1, TempValue2)
		TempValue1 += 16
		TempValue2 += 16
		
		TempValue0++
	loop
	
#endplatform
	
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
