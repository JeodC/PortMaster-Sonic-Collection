//---------------Sonic CD Kumo Kumo Web Script----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias 0	:	KUMOKUMOWEB_THROWN
#alias 1	:	KUMOKUMOWEB_ATTACHED

#alias Object.Value0	:	Object.Timer
#alias Object.Value1	:	Object.XVelocity


sub ObjectMain

	if Object.State == KUMOKUMOWEB_THROWN
	
		// Fly through the air
		Object.XPos += Object.XVelocity
		
		if Object.OutOfBounds == true
			// Unload if out of bounds
			Object.Type = TypeName[Blank Object]
		else
		
			// If far vertically too, then unload too
			TempValue0   = Screen.YOffset
			TempValue0  -= 8
			TempValue0 <<= 16
			if Object.YPos < TempValue0
				Object.Type = TypeName[Blank Object]
			end if
			
		end if
	else
	
		// Attached to a Player
		
		if Object.Timer < 120
			Object.Timer++
			
			// Slow the Player down to a maximum speed of 0.25 pixels a second

			// Right-wards limit
			if Player.Speed > 0x4000
				Player.Speed = 0x4000
			end if

			// Left-wards limit
			if Player.Speed < -0x4000
				Player.Speed = -0x4000
			end if
			
		else
		
			// The Web's sticky time is over, unload now
			Object.Type = TypeName[Blank Object]
			
		end if
	end if
	
end sub


sub ObjectPlayerInteraction

	// Check to see if the Web should attach to Sonic
	// (Although it doesn't matter too much anyway, this check continues to run even if the Web is already in its ATTACHED state)
	PlayerObjectCollision(C_TOUCH, -6, -6, 6, 6)
	if CheckResult == true
		Object.State = KUMOKUMOWEB_ATTACHED
	end if

end sub


sub ObjectDraw

	if Object.State == KUMOKUMOWEB_ATTACHED
		// Attached Webs should stick to the Player
		
		Object.XPos = Player.XPos
		Object.YPos = Player.YPos
	end if

	TempValue0 = Object.Frame
	TempValue0 /= 3
	
	DrawSprite(TempValue0)
	
	Object.Frame++
	Object.Frame %= 6
	
end sub


sub ObjectStartup
	LoadSpriteSheet("R5/Objects.gif")

	// Kumo Kumo Web Frames
	SpriteFrame(-12, -8, 24, 16, 69, 1)
	SpriteFrame(-12, -8, 24, 16, 94, 1)
	
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("R5/Objects.gif")
	SpriteFrame(-12, -8, 24, 16, 69, 1)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
