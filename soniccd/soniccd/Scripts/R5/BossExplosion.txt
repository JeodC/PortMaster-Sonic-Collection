//---------------Sonic CD BossExplosion Script----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.Timer
#alias Object.Value1	:	Object.Loops

// Player Aliases
#alias 0	:	PLAYER_SONIC_A
#alias 1	:	PLAYER_TAILS_A

sub ObjectDraw
	
	DrawSprite(Object.Frame)
	
	// Make the Object rise by 3 pixels
	Object.YPos -= 0x30000
	
	// Animate the Explosion
	
	Object.Timer++
	if Object.Timer > 3
		Object.Timer = 0
		
		Object.Frame++
		if Object.Frame > 4
			Object.Frame = 0
			
			Object.Loops++
			if Object.Loops == 2
				// Upon reaching the third loop, unload
				Object.Type = TypeName[Blank Object]
			end if
		end if
	end if
	
end sub


sub ObjectStartup
	if Stage.PlayerListPos == PLAYER_SONIC_A	// PLAYER_SONIC in origins
		LoadSpriteSheet("Global/Items2.gif")
	end if
	if Stage.PlayerListPos == PLAYER_TAILS_A	// PLAYER_TAILS in origins
		LoadSpriteSheet("Global/Items2_t.gif")
	end if
#platform: Use_Origins
	if Stage.PlayerListPos == PLAYER_KNUCKLES
		LoadSpriteSheet("Global/Items2_k.gif")
	end if
	if Stage.PlayerListPos == PLAYER_AMY
		LoadSpriteSheet("Global/Items2_a.gif")
	end if
#endplatform
	
	// Explosion Frames
	
	SpriteFrame(-16, -8, 32, 16, 1, 1)
	SpriteFrame(-16, -16, 32, 32, 1, 84)
	SpriteFrame(-16, -16, 32, 32, 1, 117)
	SpriteFrame(-24, -24, 48, 48, 34, 1)
	SpriteFrame(-24, -24, 48, 48, 34, 50)
	
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Items2.gif")
	SpriteFrame(-16, -8, 32, 16, 1, 1)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
