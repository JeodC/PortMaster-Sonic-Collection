//-----------------Sonic CD Kumo Kumo Script------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0		:	Object.Timer
#alias Object.Value1		:	Object.YVelocity

// Kumo Kumo Web Aliases
#alias Object.Value1		:	Object.XVelocity

#alias Object.PropertyValue	:	Object.Quality

// States
#alias 0	:	KUMOKUMO_AIRBORNE

// Different Ground States for the different conditions a Kumo Kumo can come in
#alias 1	:	KUMOKUMO_GOOD_GROUND
#alias 2	:	KUMOKUMO_BAD_GROUND

// State entered to start shooting a Kumo Kumo Web (aka KWeb)
#alias 3	:	KUMOKUMO_SHOOT

// Badnik Quality / Property Values
#alias 0	:	GOOD_QUALITY
#alias 1	:	BAD_QUALITY

// Collision Sides
#alias 0	:	CSIDE_FLOOR

// Stage SFX
#alias 2	:	SFX_S_SHOT


sub ObjectMain
	
	switch Object.State
	case KUMOKUMO_AIRBORNE
		// This Airborne State isn't only for after jumping, but it's also what a Kumo Kumo spawns with to align it to the ground
		
		// Apply a Gravity of 0.25 pixels per frame
		Object.YVelocity += 0x4000
		
		Object.YPos += Object.YVelocity
		
		// Only check for ground collisions when going downards
		if Object.YVelocity > 0
			ObjectTileCollision(CSIDE_FLOOR, 0, 24, 0)
			
			if CheckResult == true
				// The Kumo Kumo has touched the ground, make it land
			
				// Move the Kumo Kumo 8 pixels down
				Object.YPos += 0x80000
				
				Object.Frame = 0
				
				// Move the Kumo Kumo back to its normal Draw Order
				// (In most cases this won't change anything, but for Good Kumo Kumos, this restores them after they launch a web)
				Object.DrawOrder = 3
				
				if Object.Quality == GOOD_QUALITY
					Object.State = KUMOKUMO_GOOD_GROUND
				else
					Object.State = KUMOKUMO_BAD_GROUND
					Object.Direction = FACING_LEFT
				end if
			end if
		end if
		break
		
	case KUMOKUMO_GOOD_GROUND
		if Object.Timer < 180
			// Pausing before the big jump...
			
			Object.Timer++
			Object.Frame++
			Object.Frame &= 15
		else
			// Make the Kumo Kumo jump
			
			// Move the Kumo Kumo 8 pixels up
			Object.YPos -= 0x80000
			
			Object.Timer = 0
			
			// The Kumo Kumo's Velocity will be 8 pixels upwards as well
			Object.YVelocity = -0x80000
			
			Object.YPos += Object.YVelocity
			Object.YVelocity += 0x4000
			
			Object.State = KUMOKUMO_AIRBORNE
		end if
		break
		
	case KUMOKUMO_BAD_GROUND
		if Object.Timer < 180
			// Getting ready for a jump...
			// Unlike a good Kumo Kumo, Bad Kumo Kumos have a rather slow animation speed when getting ready
			
			Object.Timer++
			Object.Frame++
			Object.Frame &= 31
		else
			// Make the Kumo Kumo jump
			// However, Bad Kumo Kumos jump lower than Good Kumo Kumos
			
			// Move the Kumo Kumo 8 pixels up
			Object.YPos -= 0x80000
			
			Object.Timer = 0
			
			// Since this is a bad Kumo Kumo, it gets a velocity of 5 pixels per frame rather than the Good variant's 8
			Object.YVelocity = -0x50000
			Object.YPos += Object.YVelocity
			
			Object.State = KUMOKUMO_AIRBORNE
		end if
		break
		
	case KUMOKUMO_SHOOT
		if Object.Timer < 62
			// Hold for about a second
			
			Object.Timer++
		else
			// Launch the web!
			
			// Create the Web Object, notably as a temp object meaning it'll be near the end of the Object List
			CreateTempObject(TypeName[Kumo Kumo Web], 0, Object.XPos, Object.YPos)
			
			// Give the Web a high draw order, as it should draw above the Player
			Object[TempObjectPos].DrawOrder = 4
			
			// Set its starting movement properties
			if Object.Direction == FACING_RIGHT
				Object[TempObjectPos].XPos += 0x80000
				Object[TempObjectPos].XVelocity = 0x30000
			else
				Object[TempObjectPos].XPos -= 0x80000
				Object[TempObjectPos].XVelocity = -0x30000
			end if
			
			// And then make the Kumo Kumo jump
			
			// First, move it 8px up
			Object.YPos -= 0x80000
			
			Object.Timer = 0
			
			// Its Velocity should be 8 pixels upwards per frame, as well
			Object.YVelocity = -0x80000
			Object.YPos += Object.YVelocity
			
			// But apply a small starting Gravity of 0.25 pixels, too
			Object.YVelocity += 0x4000
			
			Object.State = KUMOKUMO_AIRBORNE
			
			// Give the Kumo Kumo a high draw order as well, to make it match with its shot Web
			Object.DrawOrder = 4
			
			PlayStageSfx(SFX_S_SHOT, false)
		end if
		break
		
	end switch
	
	// Check to see if the Kumo Kumo should become a Flower
	CallFunction(StageSetup_CheckGoodFuture)
	
end sub


sub ObjectPlayerInteraction
	
	// Check collision for the main Badnik
#platform: Use_Standalone
	PlayerObjectCollision(C_TOUCH, -24, -16, 24, 16)
#endplatform
#platform: Use_Origins
	PlayerObjectCollision(C_ENEMY, -24, -16, 24, 16)
#endplatform
	if CheckResult == true
		CallFunction(Player_BadnikBreak)
	end if
	
	// Only Good Badniks are able to shoot, and they have to be on the ground too
	if Object.State == KUMOKUMO_GOOD_GROUND
	
		// Based on the Kumo Kumo's direction, check to see if the Player's walked in front of it
		if Object.Direction == FACING_RIGHT
			PlayerObjectCollision(C_TOUCH, -64, -32, 0, 16)
			
			if CheckResult == true
				// The Player's stepped into range, turn around and shoot!
				
				Object.Direction = FACING_LEFT
				Object.State = KUMOKUMO_SHOOT
				Object.Frame = 0
				Object.Timer = 0
			end if
		else
			PlayerObjectCollision(C_TOUCH, 0, -32, 64, 16)
			
			if CheckResult == true
				// The Player is within range, start the shooting process
				
				Object.Direction = FACING_RIGHT
				Object.State = KUMOKUMO_SHOOT
				Object.Frame = 0
				Object.Timer = 0
			end if
		end if
	end if
end sub


sub ObjectDraw
	
	switch Object.State
	case KUMOKUMO_AIRBORNE
		if Object.Quality == GOOD_QUALITY
			DrawSpriteFX(2, FX_FLIP, Object.XPos, Object.YPos)
		else
			DrawSpriteFX(5, FX_FLIP, Object.XPos, Object.YPos)
		end if
		break
		
	case KUMOKUMO_GOOD_GROUND
		TempValue0   = Object.Frame
		TempValue0 >>= 3
		DrawSpriteFX(TempValue0, FX_FLIP, Object.XPos, Object.YPos)
		break
		
	case KUMOKUMO_BAD_GROUND
		TempValue0   = Object.Frame
		TempValue0 >>= 4
		TempValue0  += 3
		DrawSpriteFX(TempValue0, FX_FLIP, Object.XPos, Object.YPos)
		break
		
	case KUMOKUMO_SHOOT
		DrawSpriteFX(0, FX_FLIP, Object.XPos, Object.YPos)
		break
		
	end switch
	
end sub


sub ObjectStartup
	LoadSpriteSheet("R5/Objects.gif")
	
	// Kumo Kumo Frames
	
	// Good Standing Frame
	SpriteFrame(-28, -20, 56, 36, 199, 1)
	
	// Good Squatting Frame
	SpriteFrame(-28, -16, 56, 32, 199, 38)
	
	// Good Jumping Frame
	SpriteFrame(-28, -28, 56, 52, 142, 1)
	
	// Bad Standing Frame
	SpriteFrame(-28, -20, 56, 36, 199, 71)
	
	// Bad Squatting Frame
	SpriteFrame(-28, -16, 56, 32, 199, 108)
	
	// Bad Jumping Frame
	SpriteFrame(-28, -28, 56, 52, 142, 54)
	
end sub


// ========================
// Editor Subs
// ========================

sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			CheckResult = Object.PropertyValue
			break
		case 0 // Condition
			CheckResult = Object.PropertyValue
			break
		end switch
	else
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			Object.PropertyValue = Editor.VariableValue
			break
		case 0 // Condition
			Object.PropertyValue = Editor.VariableValue
			break
		end switch
	end if
end sub


sub RSDKDraw
	DrawSprite(Object.PropertyValue)
end sub


sub RSDKLoad
	LoadSpriteSheet("R5/Objects.gif")
	SpriteFrame(-28, -20, 56, 36, 199, 1)
	SpriteFrame(-28, -20, 56, 36, 199, 71)

	AddEditorVariable("Condition")
	SetActiveVariable("Condition")
	AddEnumVariable("Good", GOOD_QUALITY)
	AddEnumVariable("Bad",  BAD_QUALITY)
end sub
