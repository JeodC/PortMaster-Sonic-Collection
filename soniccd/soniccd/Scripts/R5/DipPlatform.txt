//--------------Sonic CD Dipping Platform Script--------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0			:	Object.Ridden
#alias Object.Value1			:	Object.PlatformY
#alias Object.Value2			:	Object.ChangeY

// HUD Alias
#alias Object[24].PropertyValue	:	HUD.CurrentTimePeriod

// Time Period Aliases
#alias 0	:	TIME_PRESENT
#alias 1	:	TIME_PAST
#alias 2	:	TIME_GOOD_FUTURE
#alias 3	:	TIME_BAD_FUTURE


sub ObjectMain
	
	if Object.Ridden == true
		if Object.PlatformY < 16
			Object.PlatformY++
			
			TempValue0 = Object.PlatformY
			TempValue0 &= 3
			
			if TempValue0 == 3
				// The Platform will be moved 1 pixel down
				Object.ChangeY = 0x10000
			else
				Object.ChangeY = 0
			end if
		else
			Object.ChangeY = 0
		end if
	else
		if Object.PlatformY > 0
			Object.PlatformY--
			
			TempValue0 = Object.PlatformY
			TempValue0 &= 3
			
			if TempValue0 == 3
				// The Platform should be moved 1 pixel up
				Object.ChangeY = -0x10000
			else
				Object.ChangeY = 0
			end if
		else
			Object.ChangeY = 0
		end if
	end if
	
	Object.Ridden = false
	
end sub


sub ObjectPlayerInteraction
	
	switch Object.PropertyValue
	case 0
		// Small Platform
		
		PlayerObjectCollision(C_PLATFORM, -16, -16, 16, 12)
		if CheckResult == true
			Object.Ridden = true
			
			// Move the Player along with the Platform
			Player.YPos += Object.ChangeY
		end if
		break
		
	case 1
		// Medium-sized Platform
		
		PlayerObjectCollision(C_PLATFORM, -32, -16, 32, 12)
		if CheckResult == true
			Object.Ridden = true
			
			// Move the Player in accordance with the Platform
			Player.YPos += Object.ChangeY
		end if
		break
		
	case 2
		// Large Platform
		
		PlayerObjectCollision(C_PLATFORM, -48, -16, 48, 12)
		if CheckResult == true
			Object.Ridden = true
			
			// As needed, nudge the Player so that the follow the Platform
			Player.YPos += Object.ChangeY
		end if
		break
		
	case 3
		// Small Conveyor Platform
		
		PlayerObjectCollision(C_PLATFORM, -16, -16, 16, 12)
		if CheckResult == true
			Object.Ridden = true
			
			// Update the Player's Y Position to follow the Platform
			Player.YPos += Object.ChangeY
			
			// Also, update their X Position as needed with the Conveyor Belt too
			if ConveyorBelt_Flag == 0
				Player.XPos += ConveyorBelt_Speed
			else
				Player.XPos -= ConveyorBelt_Speed
			end if
			
		end if
		break
		
	case 4
		// Medium-sized Conveyor Platform
		
		PlayerObjectCollision(C_PLATFORM, -32, -16, 32, 12)
		if CheckResult == true
			Object.Ridden = true
			
			// Make the Player follow the Platform
			Player.YPos += Object.ChangeY
			
			// And make them follow the Conveyor Belt as well
			if ConveyorBelt_Flag == 0
				Player.XPos += ConveyorBelt_Speed
			else
				Player.XPos -= ConveyorBelt_Speed
			end if
			
		end if
		break
		
	case 5
		// Large Conveyor Platform
		
		PlayerObjectCollision(C_PLATFORM, -48, -16, 48, 12)
		if CheckResult == true
			Object.Ridden = true
			
			// The Platform moves vertically, so should the Player!
			Player.YPos += Object.ChangeY
			
			// And don't forget about the Conveyor Belt, too!
			if ConveyorBelt_Flag == 0
				Player.XPos += ConveyorBelt_Speed
			else
				Player.XPos -= ConveyorBelt_Speed
			end if
			
		end if
		break
		
	end switch
	
end sub


sub ObjectDraw
	
	Object.YPos += Object.ChangeY
	
	switch Object.PropertyValue
	case 0
	case 1
	case 2
		// Standard Platform
		DrawSprite(Object.PropertyValue)
		break
		
	// These below 3 are Conveyor Platforms, so they need the Conveyor overlay Sprite drawn ontop of them too
	case 3
		DrawSprite(0)
		DrawSprite(3)
		break
	case 4
		DrawSprite(1)
		DrawSprite(4)
		break
	case 5
		DrawSprite(2)
		DrawSprite(5)
		break
	end switch
	
end sub


sub ObjectStartup
	
	// So a sheet is loaded here, but then it's loaded again for each time zone?
	// Probably just a small oversight...
	LoadSpriteSheet("R5/Objects.gif")
	
	// But anyway...
	// Order of Sprite Frames:
	
	// 0 - Small Platform Frame
	// 1 - Normal Sized Platform Frame
	// 2 - Large Platform Frame
	
	// 3 - Small Conveyor Belt Frame
	// 4 - Medium Sized Conveyor Belt Frame
	// 5 - Large Conveyor Belt Frame
	
	// The Conveyor Belt Frames are overlayed ontop of the normal Platform Frames
	
	// Load the corresponding sprites for each Time Period
	switch HUD.CurrentTimePeriod
	case TIME_PRESENT
		LoadSpriteSheet("R5/Objects.gif")
		SpriteFrame(-16, -16, 32, 32, 1, 51)
		SpriteFrame(-32, -16, 64, 32, 34, 51)
		SpriteFrame(-48, -16, 96, 32, 1, 84)
		SpriteFrame(-16, -16, 32, 16, 65, 208)
		SpriteFrame(-32, -16, 64, 16, 1, 208)
		SpriteFrame(-48, -16, 96, 16, 1, 191)
		break
		
	case TIME_PAST
		LoadSpriteSheet("R5/Objects3.gif")
		SpriteFrame(-16, -16, 32, 32, 1, 170)
		SpriteFrame(-32, -16, 64, 32, 34, 170)
		SpriteFrame(-48, -16, 96, 32, 1, 203)
		SpriteFrame(-16, -16, 32, 16, 223, 148)
		SpriteFrame(-32, -16, 64, 16, 159, 148)
		SpriteFrame(-48, -16, 96, 16, 159, 131)
		break
	
	case TIME_GOOD_FUTURE
		LoadSpriteSheet("R5/Objects3.gif")
		SpriteFrame(-16, -16, 32, 32, 1, 170)
		SpriteFrame(-32, -16, 64, 32, 34, 170)
		SpriteFrame(-48, -16, 96, 32, 1, 203)
		SpriteFrame(-16, -16, 32, 16, 223, 182)
		SpriteFrame(-32, -16, 64, 16, 159, 182)
		SpriteFrame(-48, -16, 96, 16, 159, 165)
		break
		
	case TIME_BAD_FUTURE
		LoadSpriteSheet("R5/Objects3.gif")
		SpriteFrame(-16, -16, 32, 32, 1, 170)
		SpriteFrame(-32, -16, 64, 32, 34, 170)
		SpriteFrame(-48, -16, 96, 32, 1, 203)
		SpriteFrame(-16, -16, 32, 16, 223, 216)
		SpriteFrame(-32, -16, 64, 16, 159, 216)
		SpriteFrame(-48, -16, 96, 16, 159, 199)
		break
		
	end switch
	
end sub


// ========================
// Editor Subs
// ========================

sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			CheckResult = Object.PropertyValue
			break
		case 0 // Type
			CheckResult = Object.PropertyValue
			break
		end switch
	else
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			Object.PropertyValue = Editor.VariableValue
			break
		case 0 // Type
			Object.PropertyValue = Editor.VariableValue
			break
		end switch
	end if
end sub


sub RSDKDraw
	if Object.PropertyValue >= 3
		TempValue0 = -3
		TempValue0 += Object.PropertyValue
		DrawSprite(TempValue0)
	end if
	
	DrawSprite(Object.PropertyValue)
end sub


sub RSDKLoad
	LoadSpriteSheet("R5/Objects.gif")
	
	// TODO: supporting other time periods
	// surely there's some better way to do it than a bunch of unique and boring checkCurrentStageFolder checks?
	SpriteFrame(-16, -16, 32, 32, 1, 51)
	SpriteFrame(-32, -16, 64, 32, 34, 51)
	SpriteFrame(-48, -16, 96, 32, 1, 84)
	SpriteFrame(-16, -16, 32, 16, 65, 208)
	SpriteFrame(-32, -16, 64, 16, 1, 208)
	SpriteFrame(-48, -16, 96, 16, 1, 191)

	AddEditorVariable("Type")
	SetActiveVariable("Type")
	AddEnumVariable("Small", 0)
	AddEnumVariable("Medium", 1)
	AddEnumVariable("Large", 2)
	AddEnumVariable("Small Conveyor", 3)
	AddEnumVariable("Medium Conveyor", 4)
	AddEnumVariable("Large Conveyor", 5)
end sub
