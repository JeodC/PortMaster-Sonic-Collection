//-----------Sonic CD Mission Area Top Right Script-----------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.State	:	Object.SetBounds

// Priority
#alias 1	:	PRIORITY_ACTIVE


sub ObjectMain
	
	// If it hasn't been done already, set the Stage's Top Right boundaries
	if Object.SetBounds == false
		
		// Move the X Bounds to 16 pixels to the right of this Object
		Stage.NewXBoundary2   = Object.XPos
		Stage.NewXBoundary2 >>= 16
		Stage.NewXBoundary2  += 16
		
		// And then move the Y Bounds to be right at this Object's position
		Stage.NewYBoundary1   = Object.YPos
		Stage.NewYBoundary1 >>= 16
		
		// Set the flag, as the bounds have been set now
		Object.SetBounds = true
	end if
	
	if Stage.State == STAGE_RUNNING
		if Player.YPos < Object.YPos
			Player.YVelocity = 0
			Player.Speed	 = 0
			Player.YPos		 = Object.YPos
		end if
		
		if Player.XPos > Object.XPos
			Player.XVelocity = 0
			Player.Speed	 = 0
			Player.XPos		 = Object.XPos
		end if
	end if
	
end sub


sub ObjectStartup
	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[MissionAreaTR]
			Object[ArrayPos0].Priority = PRIORITY_ACTIVE
		end if
		
		ArrayPos0++
	loop
	
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Mission/Objects.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 34)		// #0 - "Top Right" Editor Icon
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
