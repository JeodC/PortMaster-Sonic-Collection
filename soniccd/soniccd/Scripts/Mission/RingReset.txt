//-----------------Sonic CD Ring Reset Script-----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//
//--------Used on Mission "M095 - Ring Challenge 100"---------//

// Aliases
#alias Object.Value0	:	Object.TimeCheckpoint

// This Value's used for by Rings, more specifically by the Ring Sparkle they turn into
#alias Object.Value2	:	Object.MissionTypeEX

// Priority Aliases
#alias 0	:	PRIORITY_BOUNDS

// Mission Function
#alias 10	:	RULE_RESPAWN_RINGS


sub ObjectMain
	
	// Note - this object may never be placed directly into a Scene,
	// but rings from the "Ring Challenge 100" mission will turn into this object in place of unloading
	
	// Make sure the Object's out of bounds right now, we don't want any visible appearing in front of the player
	// -> Do note, this object has a priority of PRIORITY_ACTIVE, which is why this works
	if Object.OutOfBounds == true
		
		// Get the total current stage time
		TempValue0 = Stage.Minutes
		TempValue0 *= 60
		TempValue0 += Stage.Seconds
		
		// See if it's greater than (or equal to) the time this Ring holds
		// (The value the Ring holds will be 20 seconds after the time it was collected)
		if TempValue0 >= Object.TimeCheckpoint
			
			// Turn this object back into a ring, and give it normal Priority again
			Object.Type = TypeName[Ring]
			Object.Priority = PRIORITY_BOUNDS
			
			// And reset the time Value too, of course
			Object.TimeCheckpoint = 0
			
		end if
		
	end if
	
end sub


sub ObjectStartup
	
	// Sets the special rule for this mission
	game.missionFunctionNo = RULE_RESPAWN_RINGS
	
	// Cycle through all rings and set their their special type to that of the Ring Reset object
	
	TempValue0 = TypeName[Ring Reset]
	
	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Ring]
			Object[ArrayPos0].MissionTypeEX = TempValue0
		end if
		
		ArrayPos0++
	loop
	
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	Object.InkEffect = 1
	DrawSpriteFX(0, FX_INK, Object.XPos, Object.YPos)
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Items.gif")
	SpriteFrame(-8, -8, 16, 16, 1, 1)
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub


