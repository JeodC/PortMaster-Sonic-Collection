//---------------Sonic CD Mission Block Script----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.PropertyValue	:	Object.BlocksNo


sub ObjectPlayerInteraction
	
	if Object.BlocksNo <= 1
		
		// If the Block count for this Object is 0 or 1, then it's only a single block
		// From here, check Collision as such
		PlayerObjectCollision(C_BOX, -16, -16, 16, 16)
	else
		
		// This Object is meant to represent several blocks
		
		// Base block width is 32
		TempValue0 = 32
		
		// From here, multiply that width by how many blocks there are
		TempValue0 *= Object.BlocksNo
		
		// Because the left box is already 16 wide, we can shve 16 off from the right
		TempValue0 -= 16
		
		// And now, check collision against all those blocks
		PlayerObjectCollision(C_BOX, -16, -16, TempValue0, 16)
		
	end if
	
end sub


sub ObjectDraw
	
	if Object.BlocksNo <= 1
		// If a single block, then just draw a single sprite
		
		DrawSprite(0)
	else
		// This Object is several blocks, so more has to be done
		
		// Use of values here
		// - TempValue0 is the X Position of the current block
		// - TempValue1 is the Y Position of the current block, this stays the same throughout the whole process
		// - TempValue2 is the current block number, starting from 0
		
		TempValue0 = Object.XPos
		TempValue1 = Object.YPos
		TempValue2 = 0
		
		while TempValue2 < Object.BlocksNo
			// First, draw the Sprite
			// -> This is done first so that the first Block is displayed correctly
			DrawSpriteXY(0, TempValue0, TempValue1)
			
			// Move 32 pixels to the right
			TempValue0 += 0x200000
			
			// Next block!
			TempValue2++
		loop
	end if
	
end sub


sub ObjectStartup
	
	// Loading the Labrynth Zone sheet...
	LoadSpriteSheet("Mission/Objects.gif")
	
	// Well, it's a Mission Block!
	SpriteFrame(-16, -16, 32, 32, 1, 1)
	
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	if Object.PropertyValue < 2
		DrawSprite(0)
	else
		TempValue0 = 0
		TempValue1 = Object.XPos
		
		while TempValue0 < Object.PropertyValue
			DrawSpriteXY(0, TempValue1, Object.YPos)
			
			TempValue1 += 0x200000
			TempValue0++
		loop
	end if
end sub


sub RSDKLoad
	LoadSpriteSheet("Mission/Objects.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 1)
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "BlocksNo")
end sub


