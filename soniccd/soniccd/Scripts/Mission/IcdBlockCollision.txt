//------------Sonic CD Ice Block Collision Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//
//----------Used on Mission "M086 - Twinkle Toes"-------------//

// Aliases
#alias Object.Value0	:	Object.IceBlockEntity

// Aliases
#alias Object.Value1	:	Object.XVelocity
#alias Object.Value2	:	Object.YVelocity

// States
#alias 0	:	ICEBLOCK_FALLING
#alias 1	:	ICEBLOCK_BREAKICE

// Property Values
#alias 0	:	CHARACTER_ICEBLOCK

// Priority
#alias 0	:	PRIORITY_BOUNDS
#alias 1	:	PRIORITY_ACTIVE

// Stage SFX
#alias 4	:	SFX_S_GLASSSMASH


sub ObjectMain
	if Object.IceBlockEntity == 0
		// Search through Temp objects
		ArrayPos0 = 1056
		while ArrayPos0 < 1184
			CheckEqual(Object[ArrayPos0].Type, TypeName[Ice Block])
			TempValue0  = CheckResult
			CheckEqual(Object[ArrayPos0].PropertyValue, CHARACTER_ICEBLOCK)
			TempValue0 &= CheckResult
			CheckEqual(Object[ArrayPos0].State, ICEBLOCK_FALLING)
			TempValue0 &= CheckResult
			if TempValue0 == true
				Object.IceBlockEntity = ArrayPos0
				Object.Priority = PRIORITY_ACTIVE
				ArrayPos0 = 10000
			end if
			ArrayPos0++
		loop
	end if

	if Object.IceBlockEntity != 0
		ArrayPos1 = Object.IceBlockEntity
		ArrayPos0 = 32
		if Object[ArrayPos1].State == ICEBLOCK_BREAKICE
			Object.IceBlockEntity = 0
			Object.Priority = PRIORITY_BOUNDS
			ArrayPos0 = 10000
		end if

		while ArrayPos0 < 1056
			CheckEqual(Object[ArrayPos0].Type, TypeName[MissionBlock])
			TempValue0  = CheckResult
			CheckEqual(Object[ArrayPos0].Type, TypeName[Moving Blocks])
			TempValue0 |= CheckResult
			if TempValue0 == true
				TempValue1  = Object[ArrayPos0].YPos
				TempValue1 -= Object[ArrayPos1].YPos
				// Check vertical distance between mission/moving blocks and the ice block
				CheckLower(TempValue1, 0x280000)
				TempValue0  = CheckResult
				CheckGreater(TempValue1, -0x200000)
				TempValue0 &= CheckResult
				if TempValue0 == true
					TempValue1  = Object[ArrayPos0].XPos
					TempValue1 -= Object[ArrayPos1].XPos
					// Check horizontal distance between mission/moving blocks and the ice block
					CheckLower(TempValue1, 0x200000)
					TempValue0  = CheckResult
					CheckGreater(TempValue1, -0x200000)
					TempValue0 &= CheckResult
					if TempValue0 == true
						// Turns out you collided with a block, must hurt
						Object[ArrayPos1].YPos  = Object[ArrayPos0].YPos
						Object[ArrayPos1].YPos -= 0x280000
						Object[ArrayPos1].State = ICEBLOCK_BREAKICE
						Object[ArrayPos1].YVelocity = 0
						
						PlayStageSfx(SFX_S_GLASSSMASH,0)
						Object.IceBlockEntity = 0
						Object.Priority = PRIORITY_BOUNDS
						ArrayPos0 = 10000
					end if
				end if
			end if
			ArrayPos0++
		loop
	end if
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub


