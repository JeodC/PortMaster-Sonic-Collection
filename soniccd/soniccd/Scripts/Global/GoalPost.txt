//-----------------Sonic CD Goal Post Script------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// States
#alias 0	:	GOALPOST_IDLE
#alias 1	:	GOALPOST_BLANK

// Warp Destination
#alias 0	:	WARPDEST_NONE

// Property Values
#alias 1	:	UPPERGOAL

// Player
#alias 0	:	PLAYER_SONIC_A
#alias 1	:	PLAYER_TAILS_A


sub ObjectMain
	if Object.State == GOALPOST_IDLE
		TempValue0  = Object.YPos
		TempValue0 += 0x600000

		if Player.YPos < TempValue0
			if Player.XPos > Object.XPos
				// Block the player from going back into the stage, reset time warps
				Stage.NewXBoundary1   = Object.XPos
				Stage.NewXBoundary1 >>= 16
				Stage.NewXBoundary1  -= 160

				Warp.Destination = WARPDEST_NONE
				Warp.Timer		 = 0

				Object.State = GOALPOST_BLANK

				if Object.PropertyValue == UPPERGOAL
					PlayerObjectCollision(C_TOUCH, -32, -256, 32, 16)
					if CheckResult == true
						// Take The High Road Unlock Criteria
						if Stage.PlayerListPos == PLAYER_SONIC_A	// PLAYER_SONIC in Origins
							if Stage.DebugMode == false
								SetAchievement(3, 100)
							end if
						end if
					end if
				end if
			end if
		end if
	end if
end sub


sub ObjectDraw
	DrawSprite(0)
end sub


sub ObjectStartup
	if Stage.PlayerListPos == PLAYER_SONIC_A // PLAYER_SONIC in origins
		LoadSpriteSheet("Global/Items2.gif")
	end if
	if Stage.PlayerListPos == PLAYER_TAILS_A // PLAYER_TAILS in origins
		LoadSpriteSheet("Global/Items2_t.gif")
	end if
#platform: Use_Origins
	if Stage.PlayerListPos == PLAYER_KNUCKLES
		LoadSpriteSheet("Global/Items2_k.gif")
	end if
	if Stage.PlayerListPos == PLAYER_AMY
		LoadSpriteSheet("Global/Items2_a.gif")
	end if
#endplatform
	SpriteFrame(-20, -44, 40, 56, 34, 165)	// #0 - Goal Post
end sub


// ========================
// Editor Subs
// ========================

sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			CheckResult = Object.PropertyValue
			break
		case 0 // AwardAchievement
			CheckResult = Object.PropertyValue
			break
		end switch
	else
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			Object.PropertyValue = Editor.VariableValue
			break
		case 0 // AwardAchievement
			Object.PropertyValue = Editor.VariableValue
			break
		end switch
	end if
end sub


sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Items2.gif")
	SpriteFrame(-20, -44, 40, 56, 34, 165)	// #0 - Goal Post

	AddEditorVariable("AwardAchievement")
	SetActiveVariable("AwardAchievement")
	AddEnumVariable("false", false)
	AddEnumVariable("true", true)
end sub
