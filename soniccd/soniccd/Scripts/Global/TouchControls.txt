//----------Sonic CD DebugMode / TouchControls Script---------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.Fade
#alias Object.Value1	:	Object.PauseFade
#alias Object.Value2	:	Object.PauseXPos
#alias Object.Value3	:	Object.AButtonXPos
#alias Object.Value4	:	Object.DPadXPos
#alias Object.Value6	:	Object.HelperSprite
#alias Object.Rotation	:	Object.CurrentSelection

// Ink Effects
#alias 2	:	INK_ALPHA

// Player
#alias 1	:	PLAYER_TAILS_A

// Control Mode
#alias 0	:	CONTROLMODE_NORMAL

// Priority
#alias 1	:	PRIORITY_ACTIVE
#alias 4	:	PRIORITY_BOUNDS_DESTROY

// Function declarations
#function DebugMode_ChangeItem

function DebugMode_ChangeItem
	switch Object.CurrentSelection
	case 0
	case 1
	case 2
	case 3
	case 4
	case 5
	case 6
	case 7
	case 8
	case 9
	case 10
	case 11
	case 12
	case 13
	case 14
	case 15
	case 16
	case 17
	case 18
	case 19
	case 20
	case 21
	case 22
	case 23
	case 24
	case 25
		Object.Frame		= Object.CurrentSelection
		Object.HelperSprite = 0
		break

	case 26
		Object.Frame		= 26
		Object.HelperSprite = 27
		break

	case 27
		Object.Frame		= 28
		Object.HelperSprite = 29
		break
	end switch
end function


sub ObjectMain
// a bit messy, isn't it? blame mobile version for not having debug mode :)
#platform: Standard
	TempValue0 = false
	if KeyDown[1].Left == true
		Player.XPos -= Player.YVelocity
		TempValue0 = true
	end if

	if KeyDown[1].Right == true
		Player.XPos += Player.YVelocity
		TempValue0 = true
	end if

	if KeyDown[1].Up == true
		Player.YPos -= Player.YVelocity
		TempValue0 = true
	end if

	if KeyDown[1].Down == true
		Player.YPos += Player.YVelocity
		TempValue0 = true
	end if

	if TempValue0 == true
		Player.YVelocity += 0xC00
		if Player.YVelocity > 0x100000
			Player.YVelocity = 0x100000
		end if
	else
		Player.YVelocity = 0
	end if
#endplatform

	if KeyPress[1].ButtonB == true
#platform: Standard
		Object.HelperSprite = 0
		Object.Type = TypeName[Player Object]
		// THEY #FIXED HIM!!!
		if Stage.PlayerListPos == PLAYER_TAILS_A // PLAYER_TAILS in origins
			Object[+1].Type = TypeName[Tails Object]
		end if
		Player.Speed	 = 0
		Player.Rotation  = 0
		Player.XVelocity = 0
		Player.YVelocity = 0
#endplatform

#platform: Use_Origins
		Player.State = Player_State_Air_NoDropDash
#endplatform

#platform: Use_Standalone
	#platform: Standard
		Player.State = Player_State_Air
	#endplatform
#endplatform

#platform: Standard
		Player.Animation	  = ANI_WALKING
		Player.AnimationSpeed = 0
		Player.Frame		  = 0

		Player.ControlMode		 = CONTROLMODE_NORMAL
		Player.ObjectInteraction = true
#endplatform
	else
		if KeyPress[0].ButtonA == true
#platform: Standard
			Object.CurrentSelection++
#endplatform
			
#platform: Use_Origins
			// These two checks were introduced in Origins 1.0.4, to prevent the player from spawning Coin Monitors in Debug Mode
			if Object.CurrentSelection == 5
				Object.CurrentSelection++
			end if
			
			if Object.CurrentSelection == 7
				Object.CurrentSelection++
			end if
#endplatform
#platform: Standard
			if Object.CurrentSelection > 27
				Object.CurrentSelection = 0
			end if
			CallFunction(DebugMode_ChangeItem)
#endplatform
		end if

		if KeyDown[0].ButtonA == true
			if KeyPress[0].ButtonC == true
#platform: Standard
				Object.CurrentSelection--
#endplatform
				
#platform: Use_Origins
				// Similarly to above, this part's also new to Origins 1.0.4 and prevents coin inflation
				if Object.CurrentSelection == 5
					Object.CurrentSelection--
				end if
				
				if Object.CurrentSelection == 7
					Object.CurrentSelection--
				end if
#endplatform
#platform: Standard
				if Object.CurrentSelection < 0
					Object.CurrentSelection = 27
				end if
				CallFunction(DebugMode_ChangeItem)
#endplatform
			end if
		else
#platform: Standard
			if KeyPress[0].ButtonC == true
				switch Object.CurrentSelection
				case 0
					CreateTempObject(TypeName[Ring], 0, Player.XPos, Player.YPos)
					Object[TempObjectPos].DrawOrder = 4
					break

				case 1
				case 2
				case 3
				case 4
				case 5
				case 6
				case 7
				case 8
					CreateTempObject(TypeName[Monitor], Object.CurrentSelection, Player.XPos, Player.YPos)
					break

				case 9
				case 10
				case 11
				case 12
				case 13
				case 14
				case 15
				case 16
					TempValue0  = Object.CurrentSelection
					TempValue0 -= 9
					CreateTempObject(TypeName[Red Spring], TempValue0, Player.XPos, Player.YPos)
					break

				case 17
				case 18
				case 19
				case 20
				case 21
				case 22
				case 23
				case 24
					TempValue0  = Object.CurrentSelection
					TempValue0 -= 17
					CreateTempObject(TypeName[Yellow Spring], TempValue0, Player.XPos, Player.YPos)
					break
					
				case 25
					CreateTempObject(TypeName[Lamp Post], 0, Player.XPos, Player.YPos)
					break

				case 26
					CreateTempObject(TypeName[Future Post], 0, Player.XPos, Player.YPos)
					break

				case 27
					CreateTempObject(TypeName[Past Post], 0, Player.XPos, Player.YPos)
					break
				end switch

				Object[TempObjectPos].Priority = PRIORITY_BOUNDS_DESTROY
			end if
#endplatform
		end if
	end if
end sub


sub ObjectDraw
	
#platform: Standard
	DrawSpriteXY(Object.Frame, Player.XPos, Player.YPos)
	
	if Object.HelperSprite > 0
		DrawSpriteXY(Object.HelperSprite, Player.XPos, Player.YPos)
	end if
#endplatform
	
#platform: Mobile
	if Options.TouchControls == false
		TempValue0 = -1
	else
		TempValue0 = Player.ControlMode
	end if
	
	if TempValue0 == false
		if Stage.State != STAGE_PAUSED
			if Object.Fade < 160
				Object.Fade += 4
				Object.PauseFade   = Object.Fade
				Object.PauseFade <<= 1
			end if
			
			Object.Alpha = Object.Fade
			DrawSpriteScreenFX(0, FX_INK, Object.DPadXPos, 160)

			if Player.Left == true
				Object.Alpha = 160
				DrawSpriteScreenFX(7, FX_INK, Object.DPadXPos, 160)
			else
				Object.Alpha = Object.Fade
				DrawSpriteScreenFX(3, FX_INK, Object.DPadXPos, 160)
			end if

			if Player.Down == true
				Object.Alpha = 160
				DrawSpriteScreenFX(6, FX_INK, Object.DPadXPos, 160)
			else
				Object.Alpha = Object.Fade
				DrawSpriteScreenFX(2, FX_INK, Object.DPadXPos, 160)
			end if

			if Player.Right == true
				Object.Alpha = 160
				DrawSpriteScreenFX(8, FX_INK, Object.DPadXPos, 160)
			else
				Object.Alpha = Object.Fade
				DrawSpriteScreenFX(4, FX_INK, Object.DPadXPos, 160)
			end if

			if Player.Up == true
				Object.Alpha = 160
				DrawSpriteScreenFX(5, FX_INK, Object.DPadXPos, 160)
			else
				Object.Alpha = Object.Fade
				DrawSpriteScreenFX(1, FX_INK, Object.DPadXPos, 160)
			end if

			if Player.JumpHold == true
				Object.Alpha = 160
				DrawSpriteScreenFX(10, FX_INK, Object.AButtonXPos, 176)
			else
				Object.Alpha = Object.Fade
				DrawSpriteScreenFX(9, FX_INK, Object.AButtonXPos, 176)
			end if
		else
			Object.Fade = 0
		end if

		if Object.PauseFade < 256
			Object.Alpha = Object.PauseFade
			DrawSpriteScreenFX(11, FX_INK, Object.PauseXPos, 8)
		else
			DrawSpriteScreenXY(11, Object.PauseXPos, 8)
		end if
	else
		if Object.Fade > 0
			Object.Fade		 -= 4
			Object.PauseFade -= 8
		end if

		Object.Alpha = Object.Fade
		if Object.Alpha > 0
			DrawSpriteScreenFX(0, FX_INK, Object.DPadXPos, 160)
			DrawSpriteScreenFX(1, FX_INK, Object.DPadXPos, 160)
			DrawSpriteScreenFX(4, FX_INK, Object.DPadXPos, 160)
			DrawSpriteScreenFX(2, FX_INK, Object.DPadXPos, 160)
			DrawSpriteScreenFX(3, FX_INK, Object.DPadXPos, 160)
			DrawSpriteScreenFX(9, FX_INK, Object.AButtonXPos, 176)
		end if

		if Object.PauseFade < 0
			Object.Alpha = 0
		else
			Object.Alpha = Object.PauseFade
		end if

		if Object.PauseFade < 256
			Object.Alpha = Object.PauseFade
			DrawSpriteScreenFX(11, FX_INK, Object.PauseXPos, 8)
		else
			DrawSpriteScreenXY(11, Object.PauseXPos, 8)
		end if
	end if
#endplatform
end sub


sub ObjectStartup
#platform: Standard
	LoadSpriteSheet("Global/Items.gif")

	SpriteFrame(-8, -8, 16, 16, 1, 1)			// #0  - Ring
	// Monitors
	SpriteFrame(-16, -16, 32, 32, 18, 1)		// #1  - Ring Monitor
	SpriteFrame(-16, -16, 32, 32, 18, 34)		// #2  - Shield Monitor
	SpriteFrame(-16, -16, 32, 32, 18, 67)		// #3  - Invincibility Monitor
	SpriteFrame(-16, -16, 32, 32, 18, 100)		// #4  - Power Sneakers Monitor

#endplatform

#platform: Use_Origins
	CheckEqual(game.coinMode, false)
#endplatform

#platform: Use_Standalone
	CheckResult = true
#endplatform

#platform: Standard
	// Originally game.coinMode == false, adapted for compatibility reasons
	if CheckResult == true
		SpriteFrame(-16, -16, 32, 32, 18, 133)	// #5  - Sonic Monitor
		SpriteFrame(-16, -16, 32, 32, 18, 166)	// #6  - Time Monitor
		SpriteFrame(-16, -16, 32, 32, 51, 100)	// #7  - Tails Monitor
		SpriteFrame(-16, -16, 32, 32, 51, 133)	// #8  - S Monitor
	else
		SpriteFrame(-16, -16, 32, 32, 84, 200)	// #5  - Coin Monitor
		SpriteFrame(-16, -16, 32, 32, 18, 166)	// #6  - Time Monitor
		SpriteFrame(-16, -16, 32, 32, 84, 200)	// #7  - Coin Monitor
		SpriteFrame(-16, -16, 32, 32, 51, 133)	// #8  - S Monitor
	end if

	// Springs
	SpriteFrame(-16, -8, 32, 16, 84, 1)			// #9  - Red Spring facing up
	SpriteFrame(-8, -16, 16, 32, 117, 1)		// #10 - Red Spring facing right
	SpriteFrame(-8, -16, 16, 32, 175, 1)		// #11 - Red Spring facing left
	SpriteFrame(-16, -8, 32, 16, 84, 59)		// #12 - Red Spring facing down
	SpriteFrame(-16, -16, 32, 32, 84, 117)		// #13 - Red Spring facing diagonal right
	SpriteFrame(-16, -16, 32, 32, 117, 165)		// #14 - Red Spring facing diagonal left
	SpriteFrame(-16, -16, 32, 32, 84, 117)		// #15 - Red Spring facing diagonal right 2
	SpriteFrame(-16, -16, 32, 32, 117, 165)		// #16 - Red Spring facing diagonal left 2

	// Springs
	SpriteFrame(-16, -8, 32, 16, 84, 183)		// #17 - Yellow Spring facing up
	SpriteFrame(-8, -16, 16, 32, 215, 116)		// #18 - Yellow Spring facing right
	SpriteFrame(-8, -16, 16, 32, 232, 116)		// #19 - Yellow Spring facing left
	SpriteFrame(-16, -8, 32, 16, 184, 239)		// #20 - Yellow Spring facing down
	SpriteFrame(-16, -16, 32, 32, 183, 165)		// #21 - Yellow Spring facing diagonal right
	SpriteFrame(-16, -16, 32, 32, 216, 182)		// #22 - Yellow Spring facing diagonal left
	SpriteFrame(-16, -16, 32, 32, 183, 165)		// #23 - Yellow Spring facing diagonal right 2
	SpriteFrame(-16, -16, 32, 32, 216, 182)		// #24 - Yellow Spring facing diagonal left 2

	SpriteFrame(-8, -40, 16, 64, 1, 137)		// #25 - Lamp Post
	SpriteFrame(-8, -24, 16, 48, 35, 204)		// #26 - Time Post Body
	SpriteFrame(-16, -40, 32, 16, 52, 239)		// #27 - Future Post
	SpriteFrame(-8, -24, 16, 48, 35, 204)		// #28 - Time Post Body
	SpriteFrame(-16, -40, 32, 16, 118, 239)		// #29 - Past Post
#endplatform

#platform: Mobile
	if Options.AttractMode == false
		LoadSpriteSheet("Global/DPad.gif")

		Object[25].Type = TypeName[Debug Mode]
		Object[25].Priority		 = PRIORITY_ACTIVE
		Object[25].DrawOrder	 = 6
		Object[25].InkEffect	 = INK_ALPHA
		Object[25].Fade 		 = 0
		Object[25].PauseFade	 = 0
		Object[25].PauseXPos	 = Screen.XSize
		Object[25].PauseXPos	-= 68
		Object[25].AButtonXPos	 = Screen.XSize

		if Engine.PlatformID == RETRO_WP7
			Object[25].AButtonXPos  -= 69
			Object[25].DPadXPos		 = 24
			Options.DPadX			 = 56
		else
			Object[25].AButtonXPos  -= 61
			Object[25].DPadXPos		 = 16
			Options.DPadX			 = 48
		end if

		// This script is only ever used in normal stages, Special Stages have their own TouchControls script
		// and yet, this check exists anyway...
		if Stage.ActiveList == SPECIAL_STAGE
			Object[25].PauseXPos += 42
		end if

		SpriteFrame(0, 0, 64, 64, 0, 64)		// #0  - D-Pad
		SpriteFrame(26, 0, 12, 25, 26, 0)		// #1  - Up
		SpriteFrame(26, 38, 12, 26, 26, 38)		// #2  - Down
		SpriteFrame(0, 25, 26, 13, 0, 25)		// #3  - Left
		SpriteFrame(38, 25, 26, 13, 38, 25)		// #4  - Right
		SpriteFrame(26, 0, 12, 25, 116, 64)		// #5  - Pressed Up
		SpriteFrame(26, 38, 12, 26, 116, 102)	// #6  - Pressed Down
		SpriteFrame(0, 25, 26, 13, 89, 101)		// #7  - Pressed Left
		SpriteFrame(38, 25, 26, 13, 89, 115)	// #8  - Presset Right
		SpriteFrame(0, 0, 48, 48, 65, 0)		// #9  - A Button
		SpriteFrame(0, 0, 48, 48, 65, 49)		// #10 - Pressed A Button
		SpriteFrame(0, 0, 16, 16, 72, 111)		// #11 - Pause
	end if
#endplatform
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Items.gif")
	SpriteFrame(-8, -8, 16, 16, 1, 1)			// #0  - Ring

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
