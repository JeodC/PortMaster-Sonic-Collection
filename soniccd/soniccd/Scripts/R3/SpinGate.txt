//-----------------Sonic CD Spin Gate Script------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.Timer
#alias Object.Value1	:	Object.Turning
#alias Object.Value2	:	Object.CurrentLight

// Gate Light Aliases
#alias Object[+1].Type	:	GateLight1.Type
#alias Object[+2].Type	:	GateLight2.Type
#alias Object[+3].Type	:	GateLight3.Type

// States
#alias 0	:	SPINGATE_IDLE
#alias 1	:	SPINGATE_TURNING

// Directions
#alias 2	:	FACING_UPEND_RIGHT


sub ObjectMain
	if Object.State == SPINGATE_TURNING
		if Object.Timer < 96
			Object.Timer++
		else
			Object.Timer = 0
			Object.State = SPINGATE_IDLE
		end if
	end if
end sub


sub ObjectPlayerInteraction
	if Object.State == SPINGATE_IDLE

		PlayerObjectCollision(C_TOUCH, -8, -8, 8, 8)
		if CheckResult == true

			if Object.Turning == false

				Object.State = SPINGATE_TURNING

				Object.CurrentLight++
				Object.CurrentLight &= 3

				Object.Turning = true

				switch Object.CurrentLight
				case 0
					GateLight1.Type = TypeName[Blank Object]
					GateLight2.Type = TypeName[Blank Object]
					GateLight3.Type = TypeName[Blank Object]
					break

				case 1
					Object[+1].Type = TypeName[Gate Light]
					break

				case 2
					Object[+2].Type = TypeName[Gate Light]
					break

				case 3
					Object[+3].Type = TypeName[Gate Light]
					break

				end switch

			end if

		else
			Object.Turning = false
		end if

	end if
end sub


sub ObjectDraw
	TempValue0   = Object.Timer
	TempValue0 >>= 2
	TempValue0  &= 3

	switch TempValue0
	case 0
	case 1
	case 2
		DrawSprite(TempValue0)
		break

	case 3
		Object.Direction = FACING_UPEND_RIGHT
		DrawSpriteFX(3, FX_FLIP, Object.XPos, Object.YPos)
		break

	end switch
end sub


sub ObjectStartup
	LoadSpriteSheet("R3/Objects2.gif")

	SpriteFrame(-16, -8, 32, 16, 18, 50)	// #0 - Spin Gate
	SpriteFrame(-16, -16, 32, 32, 51, 0)	// #1 - Spin Gate Rotate Frame 0
	SpriteFrame(-16, -16, 32, 32, 84, 0)	// #2 - Spin Gate Rotate Frame 1
	SpriteFrame(-16, -16, 32, 32, 51, 0)	// #3 - Spin Gate Rotate Frame 2
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("R3/Objects2.gif")

	SpriteFrame(-16, -8, 32, 16, 18, 50)	// #0 - Spin Gate

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
