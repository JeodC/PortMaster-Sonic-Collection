//---------------Sonic CD Flipout Block Script----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.YOffSet

// Property Values
#alias 0	:	FLIPOUTBLOCK_FLOOR
#alias 1	:	FLIPOUTBLOCK_FLOOR_NO_ATTACH
#alias 2	:	FLIPOUTBLOCK_ROOF
#alias 3	:	FLIPOUTBLOCK_ROOF_NO_ATTACH


sub ObjectPlayerInteraction
	if Object.PropertyValue > 1
		Object.YPos += Object.YOffSet
	else
		Object.YPos -= Object.YOffSet
	end if

	PlayerObjectCollision(C_BOX, -15, -16, 16, 16)
	if Object.PropertyValue > 1
		Object.YPos -= Object.YOffSet
	else
		Object.YPos += Object.YOffSet
	end if
	
end sub


sub ObjectDraw
	TempValue0  = Object.XPos
	TempValue0 += 0x480000
	if Player.XPos > TempValue0
		if Object.YOffSet < 0x200000
			Object.YOffSet += 0x80000
		end if
	end if

	TempValue0  = Object.XPos
	TempValue0 -= 0x100000
	if Player.XPos < TempValue0
		if Object.YOffSet > 0
			Object.YOffSet -= 0x80000
		end if
	end if

	TempValue1  = Object.PropertyValue
	TempValue1 &= 1
	TempValue0  = Object.YPos
	if Object.PropertyValue > 1
		TempValue0 += Object.YOffSet
		if TempValue1 == 0
			Object[+1].YPos  = Object.YPos
			Object[+1].YPos += Object.YOffSet
		end if
	
	else

		TempValue0 -= Object.YOffSet
		if TempValue1 == 0
			Object[+1].YPos  = Object.YPos
			Object[+1].YPos -= Object.YOffSet
		end if

	end if
	DrawSpriteXY(Object.PropertyValue, Object.XPos, TempValue0)
end sub


sub ObjectStartup
	LoadSpriteSheet("R3/Objects.gif")

	SpriteFrame(-16, -16, 32, 32, 34, 1)	// #0 - Flipout Block
	SpriteFrame(-16, -16, 32, 32, 34, 1)	// #1 - Flipout Block
	SpriteFrame(-16, -16, 32, 32, 67, 1)	// #2 - Flipout Block Upside-Down
	SpriteFrame(-16, -16, 32, 32, 67, 1)	// #3 - Flipout Block Upside-Down
end sub


// ========================
// Editor Subs
// ========================

sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
		case 0 // Type
			CheckResult  = Object.PropertyValue
			CheckResult &= 3
			break
		end switch
	else
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
		case 0 // Type
			Object.PropertyValue  = Editor.VariableValue
			Object.PropertyValue &= 3
			break
		end switch
	end if
end sub

sub RSDKDraw
	DrawSprite(Object.PropertyValue)
	if Object.PropertyValue < 2
		TempValue0   = Object.YPos
		TempValue0  -= 0x200000
	else
		TempValue0   = Object.YPos
		TempValue0  += 0x200000
	end if

	TempValue1   = Object.XPos
	TempValue1  += 0x100000

	TempValue2   = Object.XPos
	TempValue2  -= 0x100000

	TempValue3   = Object.YPos
	TempValue3  -= 0x100000


	if Editor.ShowGizmos == true
		switch Object.PropertyValue
		case 0
		case 2
			DrawArrow(TempValue1, Object.YPos, TempValue1, TempValue0, 255, 255, 255, 0)
			DrawRectOutline(TempValue2, TempValue3, 0x40, 0x20, 255, 255, 255, 255)
			break
		case 1
		case 3
			DrawArrow(Object.XPos, Object.YPos, Object.XPos, TempValue0, 255, 255, 255, 0)
			DrawRectOutline(TempValue2, TempValue3, 0x20, 0x20, 255, 255, 255, 255)
			break
		end switch
	end if
end sub


sub RSDKLoad
	LoadSpriteSheet("R3/Objects.gif")

	SpriteFrame(-16, -16, 32, 32, 34, 1)	// #0 - Flipout Block
	SpriteFrame(-16, -16, 32, 32, 34, 1)	// #1 - Flipout Block
	SpriteFrame(-16, -16, 32, 32, 67, 1)	// #2 - Flipout Block Upside-Down
	SpriteFrame(-16, -16, 32, 32, 67, 1)	// #3 - Flipout Block Upside-Down

	AddEditorVariable("Type")
	SetActiveVariable("Type")
	AddEnumVariable("Normal", FLIPOUTBLOCK_FLOOR)
	AddEnumVariable("Without Attachment", FLIPOUTBLOCK_FLOOR_NO_ATTACH)
	AddEnumVariable("Upside-Down", FLIPOUTBLOCK_ROOF)
	AddEnumVariable("Upside-Down w/o Attachment", FLIPOUTBLOCK_ROOF_NO_ATTACH)
end sub
