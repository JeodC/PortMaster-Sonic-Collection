//----------------Sonic CD BG Animation Script----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//
//------------------Only exist in WP7 Versions----------------//

// Aliases
#alias Object.Value1	:	Object.VLLYOffset 			// Vertical Line Left
#alias Object.Value2	:	Object.VLRYOffset 			// Vertical Line Right
#alias Object.Value3	:	Object.ArrowXOffset



sub ObjectMain
	Object.VLLYOffset--
	if Object.VLLYOffset < -31
		Object.VLLYOffset += 32
	end if

	Object.VLRYOffset++
	if Object.VLRYOffset > -1
		Object.VLRYOffset -= 32
	end if

	Object.ArrowXOffset++
	if Object.ArrowXOffset > -1
		Object.ArrowXOffset -= 96
	end if
end sub


sub ObjectDraw
	if Object.DrawOrder == 3
		DrawRect(0, 0, 2, Screen.YSize, 0, 0, 0, 255)

		TempValue0  = Screen.CenterX
		TempValue0 -= 20
		DrawRect(0, 160, TempValue0, 2, 0, 0, 0, 255)

		TempValue0 += 36
		DrawRect(TempValue0, 0, Screen.CenterX, Screen.YSize, 224, 0, 0, 255)
		DrawRect(TempValue0, 175, Screen.CenterX, 38, 0, 0, 96, 255)

		TempValue0 -= 38
		DrawRect(2, 0, TempValue0, 160, 160, 160, 64, 255)
		DrawRect(2, 162, TempValue0, 78, 0, 128, 0, 255)
		DrawRect(0, 31, Screen.XSize, 9, 0, 0, 0, 255)

		TempValue1 = Object.ArrowXOffset
		DrawSpriteScreenXY(3, TempValue1, 226)
		DrawSpriteScreenXY(4, TempValue1, 226)

		TempValue0  += 2
		TempValue1   = Object.VLLYOffset
		TempValue1 >>= 1
		DrawSpriteScreenXY(1, TempValue0, TempValue1)

		TempValue1   = Object.VLRYOffset
		TempValue1 >>= 1
		DrawSpriteScreenXY(2, TempValue0, TempValue1)
		SetDrawListEntityRef(Object.EntityNo, 4, Screen.DrawListSize[4])
		Screen.DrawListSize[4]++
		Object.DrawOrder = 4
	else
		DrawRect(0, 213, Screen.XSize, 13, 0, 0, 0, 255)
		DrawSpriteScreenXY(0, 16, 195)

		TempValue0  = Screen.CenterX
		TempValue0 += 19
		DrawSpriteScreenXY(5, TempValue0, 213)
		Object.DrawOrder = 3
	end if
end sub


sub ObjectStartup
	LoadSpriteSheet("LBoards/Leaderboards.gif")

	LoadPalette("TimeAttack.act", 0, 0, 0, 256)

	MatrixTranslateXYZ(MAT_WORLD, 0, 0, 0)
	MatrixRotateXYZ(MAT_VIEW, 0, 0, 0)

	SpriteFrame(0, 0, 152, 30, 70, 90)		// #0 - Leaderboards
	SpriteFrame(0, 0, 24, 256, 463, 1)		// #1 - BG Vertical Line First Half
	SpriteFrame(24, 0, 24, 256, 487, 1)		// #2 - BG Vertical Line Second Half
	SpriteFrame(0, 0, 448, 14, 1, 125)		// #4 - BG Right Arrow Line
	SpriteFrame(448, 0, 448, 14, 1, 125)	// #5 - BG Left Arrow Line
	SpriteFrame(0, 0, 104, 13, 358, 111)	// #6 - Sonic The Hedgehog CD
	SpriteFrame(0, 0, 64, 16, 326, 188)		// #7 - Next
	SpriteFrame(0, 0, 64, 16, 326, 205)		// #8 - Next Highlight

	Object[31].Type = TypeName[BG Animation]
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("LBoards/Leaderboards.gif")
	SpriteFrame(0, 0, 152, 30, 70, 90)		// #0 - Leaderboards

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
