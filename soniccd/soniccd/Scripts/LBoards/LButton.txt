//--------------Sonic CD Leaderboards Button Script-----------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//
//------------------Only exist in WP7 Versions----------------//

// Aliases
#alias Object.Value1	:	Object.ButtonPressed

// LEntry Aliases
#alias Object[32].State	:	LEntry.State

// MenuControl Aliases
#alias Object[0].State	:	MenuControl.State

// LEntry States
#alias 1	:	LENTRY_MOVE_RIGHT
#alias 4	:	LENTRY_MOVE_LEFT
#alias 7	:	LENTRY_MOVE_DOWN
#alias 10	:	LENTRY_MOVE_UP

// MenuControl States
#alias 2	:	MENUCONTROL_CONTROLS
#alias 3	:	MENUCONTROL_BLANK
#alias 4	:	MENUCONTROL_EXIT

// Global SFX
#alias 23	:	SFX_G_MENUBUTTON
#alias 27	:	SFX_G_SELECT

// Property Values
#alias 0	:	BUTTON_EXIT
#alias 1	:	BUTTON_ARROW_UP
#alias 2	:	BUTTON_ARROW_DOWN
#alias 3	:	BUTTON_ARROW_LEFT
#alias 4	:	BUTTON_ARROW_RIGHT

//Engine & CallBacks
#alias 2	:	MESSAGE_LOSTFOCUS


sub ObjectMain
	if Engine.Message == MESSAGE_LOSTFOCUS
		Object.ButtonPressed = 0
	end if
	
	TempValue0   = Object.XPos
	TempValue0 >>= 16

	TempValue1   = TempValue0

	TempValue2   = Object.YPos
	TempValue2 >>= 16

	TempValue3   = TempValue2
	switch Object.PropertyValue
	case BUTTON_EXIT
		TempValue1 += 64
		TempValue2 -= 8
		TempValue3 += 24
		break

	case BUTTON_ARROW_UP
		TempValue1 += 48
		TempValue2 -= 8
		TempValue3 += 24
		break
		
	case BUTTON_ARROW_DOWN
		TempValue1 += 48
		TempValue2 -= 8
		TempValue3 += 24
		break

	case BUTTON_ARROW_LEFT
		TempValue0 -= 8
		TempValue1 += 32
		TempValue2 -= 8
		TempValue3 += 40
		break
		
	case BUTTON_ARROW_RIGHT
		TempValue0 -= 8
		TempValue1 += 32
		TempValue2 -= 8
		TempValue3 += 40
		break

	end switch
	if MenuControl.State == MENUCONTROL_CONTROLS
		CheckTouchRect(TempValue0, TempValue2, TempValue1, TempValue3)
	else
		CheckResult = -1
	end if
	if CheckResult > -1
		Object.ButtonPressed = 1
	else
		CheckTouchRect(0, 0, Screen.XSize, Screen.YSize)
		if CheckResult < 0
			if Object.ButtonPressed == 1
				switch Object.PropertyValue
				case BUTTON_EXIT
					MenuControl.State = MENUCONTROL_EXIT
					StopMusic()
					PlaySfx(SFX_G_SELECT, false)
					break

				case BUTTON_ARROW_UP
					MenuControl.State = MENUCONTROL_BLANK
					LEntry.State = LENTRY_MOVE_UP
					PlaySfx(SFX_G_MENUBUTTON, false)
					break

				case BUTTON_ARROW_DOWN
					MenuControl.State = MENUCONTROL_BLANK
					LEntry.State = LENTRY_MOVE_DOWN
					PlaySfx(SFX_G_MENUBUTTON, false)
					break

				case BUTTON_ARROW_LEFT
					MenuControl.State = MENUCONTROL_BLANK
					LEntry.State = LENTRY_MOVE_LEFT
					PlaySfx(SFX_G_MENUBUTTON, false)
					break

				case BUTTON_ARROW_RIGHT
					MenuControl.State = MENUCONTROL_BLANK
					LEntry.State = LENTRY_MOVE_RIGHT
					PlaySfx(SFX_G_MENUBUTTON, false)
					break

				end switch
				Object.ButtonPressed = 0
			end if
		else
			Object.ButtonPressed = 0
		end if
	end if
end sub


sub ObjectDraw
	TempValue0   = Object.PropertyValue
	TempValue0 <<= 1
	TempValue0  += Object.ButtonPressed
	DrawSprite(TempValue0)
end sub


sub ObjectStartup
	LoadSpriteSheet("LBoards/Leaderboards.gif")

	SpriteFrame(0, 0, 64, 20, 135, 34)	// #0 - Exit
	SpriteFrame(0, 0, 64, 20, 135, 55)	// #1 - Exit Highlight
	SpriteFrame(0, 0, 48, 20, 200, 34)	// #2 - Up Arrow
	SpriteFrame(0, 0, 48, 20, 200, 55)	// #3 - Up Arrow Highlight
	SpriteFrame(0, 0, 48, 20, 249, 34)	// #4 - Down Arrow
	SpriteFrame(0, 0, 48, 20, 249, 55)	// #5 - Down Arrow Highlight
	SpriteFrame(0, 0, 24, 32, 223, 92)	// #6 - Left Arrow
	SpriteFrame(0, 0, 24, 32, 248, 92)	// #7 - Left Arrow Highlight
	SpriteFrame(0, 0, 24, 32, 273, 92)	// #8 - Right Arrow
	SpriteFrame(0, 0, 24, 32, 298, 92)	// #9 - Right Arrow Highlight
end sub


// ========================
// Editor Subs
// ========================

sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			CheckResult = Object.PropertyValue
			break
		case 0 // buttonType
			CheckResult = Object.PropertyValue
			break
		end switch
	else
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			Object.PropertyValue = Editor.VariableValue
			break
		case 0 // buttonType
			Object.PropertyValue = Editor.VariableValue
			break
		end switch
	end if
end sub

sub RSDKDraw
	DrawSprite(Object.PropertyValue)
end sub


sub RSDKLoad
	LoadSpriteSheet("LBoards/Leaderboards.gif")

	SpriteFrame(0, 0, 64, 20, 135, 34)	// #0 - Exit
	SpriteFrame(0, 0, 48, 20, 200, 34)	// #2 - Up Arrow
	SpriteFrame(0, 0, 48, 20, 249, 34)	// #4 - Down Arrow
	SpriteFrame(0, 0, 24, 32, 223, 92)	// #6 - Left Arrow
	SpriteFrame(0, 0, 24, 32, 273, 92)	// #8 - Right Arrow

	AddEditorVariable("buttonType")
	SetActiveVariable("buttonType")
	AddEnumVariable("Exit Button", 0)
	AddEnumVariable("Up Arrow", 1)
	AddEnumVariable("Down Arrow", 2)
	AddEnumVariable("Left Arrow", 3)
	AddEnumVariable("Right Arrow", 4)
end sub
