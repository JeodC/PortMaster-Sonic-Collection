//-------------------Sonic CD Amy 2 Script--------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.Timer
#alias Object.Value1	:	Object.XVelocity
#alias Object.Value2	:	Object.YVelocity
#alias Object.Value3	:	Object.Bounds.Left
#alias Object.Value4	:	Object.Bounds.Right
#alias Object.Value5	:	Object.InAir
#alias Object.Value6	:	Object.HeartsTimer
#alias Object.Value7	:	Object.FallBackTimer

// States
#alias 0	:	AMY2_CHASEPLAYER
#alias 1	:	AMY2_TRACKPLAYER
#alias 2	:	AMY2_HUGGING
#alias 3	:	AMY2_RECOIL
#alias 4	:	AMY2_WAITNG_R
#alias 5	:	AMY2_WAITNG_L
#alias 7	:	AMY2_UNUSED

// Collision Sides
#alias 0	:	CSIDE_FLOOR

// Gravity
#alias 0	:	GRAVITY_GROUND
#alias 1	:	GRAVITY_AIR

// Control Mode
#alias -1	:	CONTROLMODE_NONE

// Fade Music States
#alias 3	:	FADEMUSIC_SSZAMY

// Ink Effect
#alias 2	:	INK_ALPHA

// Stage SFX
#alias 8	:	SFX_S_GIGGLE


sub ObjectMain
	// Always face Sonic
	if Player.XPos < Object.XPos
		Object.Direction = FACING_LEFT
	else
		Object.Direction = FACING_RIGHT
	end if

	if Object.FallBackTimer < 600
		Object.FallBackTimer++
	else
		if Object.FallBackTimer == 600
			// Can't run away forever!
			
			// It's been 10 seconds of chasing Sonic already so end the level regardless if he wants it or not
			Object.FallBackTimer = 601
			
			// Lock Sonic's controls
			Player.Left  = false
			Player.Right = false
			Player.Up    = false
			Player.Down  = false
			Player.ControlMode = CONTROLMODE_NONE
			
			CreateTempObject(TypeName[Fade Music], FADEMUSIC_SSZAMY, Object.XPos, Object.YPos)
		end if
	end if
	
	switch Object.State
	case AMY2_CHASEPLAYER
		Object.Timer++
		switch Object.Frame
		case 4
		case 6
			if Object.Timer == 4
				Object.Timer = 0
				Object.Frame++
			end if
			break
			
		case 5
		case 7
			if Object.Timer == 3
				Object.Timer = 0
				Object.Frame++
			end if
			break
			
		end switch

		if Object.Frame > 7
			Object.Frame = 4
		end if

		if Object.Direction == FACING_RIGHT
			// We're chasing Sonic, going right of course
			
			if Object.XVelocity < 0x20000
				Object.XVelocity += 0x2000
			end if

			PlayerObjectCollision(C_TOUCH, -32, -360, 4, 360)
			if CheckResult == true
				if Player.Gravity == GRAVITY_GROUND
					if Player.Direction == Object.Direction
						PlayerObjectCollision(C_TOUCH, -32, -20, 4, 20)
						if CheckResult == true
							if Player.Animation == ANI_SPINDASH
								// Sonic's doing a spindash, throw Amy back (!)
								
								Object.State = AMY2_RECOIL
								Object.Frame = 9
								Object.YVelocity = -0x40000
								Object.InAir = true
								Object.XVelocity = -0x10000
							else
								if Object.FallBackTimer < 600
									Object.FallBackTimer = 601
									Player.Left  = false
									Player.Right = false
									Player.Up    = false
									Player.Down  = false
									Player.ControlMode = CONTROLMODE_NONE
									
									CreateTempObject(TypeName[Fade Music], FADEMUSIC_SSZAMY, Object.XPos, Object.YPos)
								end if
								
#platform: Use_Haptics
								// Soft little nudge
								HapticEffect(52, 0, 0, 0)
#endplatform
								
								Object.State = AMY2_HUGGING
								Object.Frame = 8
								Object.Timer = 0
								Player.State = Player_State_Hugged
								PlayStageSfx(SFX_S_GIGGLE, false)

								// Grant the "One Hug Is Enough" achievement
								// -> It's possible that the player may have skipped Amy in PPZ, where the achievement is normally unlocked,
								//    so there's another opportunity here
								SetAchievement(1, 100)
							end if
						else
							Object.State = AMY2_TRACKPLAYER
							Object.Frame = 0
							Object.Timer = 0
							Object.XVelocity = 0
						end if
					else
						// Presumably some code should be here, given that the condition exists and all, and yet there isn't...
					end if
				end if
			end if

			if Object.XPos < Object.Bounds.Right
				Object.XPos += Object.XVelocity
			else
				// Sonic has somehow escaped out of bounds to the left, stand still and patiently await his return
				Object.XVelocity = 0
				Object.XPos = Object.Bounds.Right
				Object.State = AMY2_WAITNG_R
				Object.Frame = 0
				Object.Timer = 0
			end if
			
		else
		
			if Object.XVelocity > -0x20000
				Object.XVelocity -= 0x2000
			end if

			PlayerObjectCollision(C_TOUCH, -4, -360, 32, 360)
			if CheckResult == true
				if Player.Gravity == GRAVITY_GROUND
					if Player.Direction == Object.Direction
						PlayerObjectCollision(C_TOUCH, -32, -20, 4, 20)
						if CheckResult == true
							if Player.Animation == ANI_SPINDASH
								// Sonic's doing a spindash, hit Amy back (!)
								
								Object.State = AMY2_RECOIL
								Object.Frame = 9
								Object.YVelocity = -0x40000
								Object.InAir = true
								Object.XVelocity = 0x10000
							else
								if Object.FallBackTimer < 600
									Object.FallBackTimer = 601
									
									Player.Left  = false
									Player.Right = false
									Player.Up    = false
									Player.Down  = false
									Player.ControlMode = CONTROLMODE_NONE
									
									CreateTempObject(TypeName[Fade Music], FADEMUSIC_SSZAMY, Object.XPos, Object.YPos)
								end if
								
								Object.State = AMY2_HUGGING
								Object.Frame = 8
								Object.Timer = 0
								Player.State = Player_State_Hugged
								PlayStageSfx(SFX_S_GIGGLE, false)

								// Unlock the "One Hug Is Enough" achievement
								// There's a small chance you missed it in PPZ, so now you have a second chance at it here!
								SetAchievement(1, 100)
								
#platform: Use_Haptics
								HapticEffect(52, 0, 0, 0)
#endplatform
								
							end if
						else
							Object.State = AMY2_TRACKPLAYER
							Object.Frame = 0
							Object.Timer = 0
							Object.XVelocity = 0
						end if
					else
						// This condition exists and yet there's no code inside of it...
					end if
				end if
			end if
			
			if Object.XPos > Object.Bounds.Left
				Object.XPos += Object.XVelocity
			else
				// Sonic somehow escaped out of bounds to the right, look at him and wait
				Object.XVelocity = 0
				Object.XPos = Object.Bounds.Left
				Object.State = AMY2_WAITNG_L
				Object.Frame = 0
				Object.Timer = 0
			end if
			
		end if
		break
		
	case AMY2_TRACKPLAYER
		Object.Timer++
		if Object.Timer == 12
			Object.Timer = 0
			Object.Frame++
		end if
		
		if Object.Frame > 3
			Object.Frame = 0
		end if
		
		PlayerObjectCollision(C_TOUCH, -4, -360, 4, 360)
		if CheckResult == false
			if Player.Gravity == GRAVITY_GROUND
				Object.State = AMY2_CHASEPLAYER
				Object.Frame = 4
				Object.Timer = 0
			end if
		else
			if Player.Gravity == GRAVITY_AIR
				Object.State = AMY2_CHASEPLAYER
				Object.Frame = 4
				Object.Timer = 0
			end if
		end if
		break
		
	case AMY2_HUGGING
		Player.Speed = 0
		Player.XVelocity = 0
		Player.Direction = Object.Direction
		break
		
	case AMY2_RECOIL
		if Object.YVelocity > 0
			Object.Frame = 10
		end if
		
		if Object.XVelocity > 0
			Object.XVelocity -= 0x800
		end if
		
		if Object.XVelocity < 0
			Object.XVelocity += 0x800
		end if
		
		Object.XPos += Object.XVelocity
		if Object.InAir == false
			if Object.Timer == 16
				Object.State = AMY2_CHASEPLAYER
				Object.Frame = 4
				Object.Timer = 0
			else
				Object.Timer++
			end if
		end if
		break
		
	case AMY2_WAITNG_R
		// Staring at Sonic to the right
		Object.Timer++
		if Object.Timer == 12
			Object.Timer = 0
			Object.Frame++
		end if
		
		if Object.Frame > 3
			Object.Frame = 0
		end if
		
		if Object.Direction == FACING_LEFT
			Object.State = AMY2_CHASEPLAYER
			Object.Frame = 4
			Object.Timer = 0
		end if
		break
		
	case AMY2_WAITNG_L
		// Staring at Sonic to the Left
		Object.Timer++
		if Object.Timer == 12
			Object.Timer = 0
			Object.Frame++
		end if
		
		if Object.Frame > 3
			Object.Frame = 0
		end if
		
		if Object.Direction == FACING_RIGHT
			Object.State = AMY2_CHASEPLAYER
			Object.Frame = 4
			Object.Timer = 0
		end if
		break
		
	end switch
	
	Object.HeartsTimer++
	
	// Wait what? State 7? Where'd that come from? This object's states end at 5!
	// But I digress, spawn hearts at a faster rate while in state 7
	// (Perhaps it was the hugging state? It is for PPZ's Amy, which is what this object seems to be based on)
	if Object.State == AMY2_UNUSED
		if Object.HeartsTimer > 15
			CreateTempObject(TypeName[Love Heart], 0, Object.XPos, Object.YPos)
			Object[TempObjectPos].iYPos -= 6
			
			Object[TempObjectPos].InkEffect = INK_ALPHA
			Object[TempObjectPos].Alpha = 255
			
			Object.HeartsTimer = 0
		end if
	else
		if Object.HeartsTimer > 41
			CreateTempObject(TypeName[Love Heart], 0, Object.XPos, Object.YPos)
			Object[TempObjectPos].iYPos -= 6
			
			Object[TempObjectPos].InkEffect = INK_ALPHA
			Object[TempObjectPos].Alpha = 255
			
			Object.HeartsTimer = 0
		end if
	end if
	
	if Object.InAir == false
		// Track the ground
		ObjectTileGrip(CSIDE_FLOOR, 0, 19, 0)
	else
		// Got launched into the air by Sonic...
		
		// Apply air movements & gravity
		Object.YVelocity += 0x4000
		Object.YPos += Object.YVelocity
		
		// See if Amy's landed yet
		ObjectTileCollision(CSIDE_FLOOR, 0, 19, 0)
		if CheckResult == true
			Object.YVelocity = 0
			Object.InAir = false
		end if
	end if
	
end sub


sub ObjectDraw
	DrawSpriteFX(Object.Frame, FX_FLIP, Object.XPos, Object.YPos)
end sub


sub ObjectStartup

	LoadSpriteSheet("R7/Objects2.gif")

	// Amy Frames

	// Idle Animation
	SpriteFrame(-11, -20, 22, 40, 34, 208)
	SpriteFrame(-11, -20, 22, 40, 57, 208)
	SpriteFrame(-11, -20, 22, 40, 34, 208)
	SpriteFrame(-11, -20, 22, 40, 126, 167)
	
	// Walking Animation
	SpriteFrame(-15, -20, 30, 40, 68, 167)
	SpriteFrame(-10, -20, 21, 40, 149, 109)
	SpriteFrame(-13, -20, 26, 40, 99, 167)
	SpriteFrame(-10, -20, 21, 40, 149, 109)
	
	// Hugging Frame, hearted eyes
	SpriteFrame(-12, -20, 24, 40, 181, 208)
	
	// Flown into Air Frame
	SpriteFrame(-13, -20, 26, 40, 80, 208)
	
	// Falling Frame
	SpriteFrame(-12, -20, 24, 40, 107, 208)
	
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	// Amy 2 should never appear in a stage layout, only Amy 1 should, but may as well add a render for completeness's sake

	LoadSpriteSheet("R7/Objects2.gif")
	SpriteFrame(-11, -20, 22, 40, 34, 208)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
