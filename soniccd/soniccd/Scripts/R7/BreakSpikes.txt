//----------------Sonic CD Break Spikes Script----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value1 : Object.XVelocity
#alias Object.Value2 : Object.YVelocity
#alias Object.Value3 : Object.YAcceleration

// Player Aliases
#alias Player.Value4 : Player.InvincibleTimer

// Property Values
// All non-zero property values are for individual spike pieces
#alias 0 : BREAKSPIKES_MAIN
#alias 1 : BREAKSPIKES_DEBRIS_1
#alias 2 : BREAKSPIKES_DEBRIS_2
#alias 3 : BREAKSPIKES_DEBRIS_3
#alias 4 : BREAKSPIKES_DEBRIS_4

// Ink Effects
#alias 2 : INK_ALPHA

// Stage SFX
#alias 7 : SFX_S_CRUMBLE


sub ObjectMain
	if Object.PropertyValue == BREAKSPIKES_MAIN
		
		// Compare Metal Sonic's position with that of the spikes' to see if they should be broken
		
		// Take the spike's position and subtract 20 pixels from it
		TempValue0 = Object.XPos
		TempValue0 -= 0x140000
		
		ArrayPos0 = MetalSonic_EntityNo
		
		if Object[ArrayPos0].XPos > TempValue0
			
			// Metal Sonic has passed these spikes, destroy them
			
			// Play the Crumble SFX
			PlayStageSfx(SFX_S_CRUMBLE, false)
			
#platform: Use_Haptics
			// Do a strong shake
			HapticEffect(96, 0, 0, 0)
#endplatform
			
			// Create a bunch of spike fragments
			// Their property value is what frame they should use
			
			CreateTempObject(TypeName[Break Spikes], BREAKSPIKES_DEBRIS_1, Object.XPos, Object.YPos)
			Object[TempObjectPos].XVelocity = 0x8000
			Object[TempObjectPos].YVelocity = -0x20000
			Object[TempObjectPos].YAcceleration = 0x3000
			Object[TempObjectPos].InkEffect = INK_ALPHA
			Object[TempObjectPos].Alpha = 254
			
			CreateTempObject(TypeName[Break Spikes], BREAKSPIKES_DEBRIS_2, Object.XPos, Object.YPos)
			Object[TempObjectPos].XVelocity = 0x10000
			Object[TempObjectPos].YVelocity = -0x40000
			Object[TempObjectPos].YAcceleration = 0x3000
			Object[TempObjectPos].InkEffect = INK_ALPHA
			Object[TempObjectPos].Alpha = 254
			
			CreateTempObject(TypeName[Break Spikes], BREAKSPIKES_DEBRIS_3, Object.XPos, Object.YPos)
			Object[TempObjectPos].XVelocity = 0x18000
			Object[TempObjectPos].YVelocity = -0x20000
			Object[TempObjectPos].YAcceleration = 0x2000
			Object[TempObjectPos].InkEffect = INK_ALPHA
			Object[TempObjectPos].Alpha = 254
			
			CreateTempObject(TypeName[Break Spikes], BREAKSPIKES_DEBRIS_4, Object.XPos, Object.YPos)
			Object[TempObjectPos].XVelocity = 0x20000
			Object[TempObjectPos].YVelocity = -0x40000
			Object[TempObjectPos].YAcceleration = 0x4000
			Object[TempObjectPos].InkEffect = INK_ALPHA
			Object[TempObjectPos].Alpha = 254
			
			// Clear the main spikes object
			ResetObjectEntity(Object.EntityNo, TypeName[Blank Object], 0, 0, 0)
		end if
		
		ArrayPos0 = 0
	else
		// Spike fragment
		
		// Fade out
		if Object.Alpha > 0
			Object.Alpha -= 2
		end if
		
		// Update movement
		Object.XPos += Object.XVelocity
		Object.YPos += Object.YVelocity
		
		// The top and bottom spikes get different gravities, which is why gravity is just stored as an Object Value in this case
		Object.YVelocity += Object.YAcceleration
		
		if Object.OutOfBounds == true
			// Unload as soon as out of view, as at that point the Object's no longer needed
			
			Object.Type = TypeName[Blank Object]
		end if
	end if
end sub


sub ObjectPlayerInteraction
	// If the Spikes are all still together, then they can still damage Sonic
	if Object.PropertyValue == BREAKSPIKES_MAIN
		// Act as a box for the player
		PlayerObjectCollision(C_BOX, -16, -15, 16, 16)
		
		// And now check for actual touching of the sharp spikes
		if Player.YVelocity > -1
			if Player.InvincibleTimer == 0
				PlayerObjectCollision(C_TOUCH, -15, -16, 15, -12)
				
				if CheckResult == true
				
					// Damage the player
					Player.State = Player_State_GotHit
					
					// Since the Player's been hit, they can no longer get the Heavy Metal Achievement
					// (For refrence, the Heavy Metal Achievement has you get through the entire Metal Sonic race without getting hit)
					Player.HeavyMetalFlag = false
					
					// Decide knockback direction based on if the player's behind or in front of this object
					// The X Positions used are based on the centers of both entities
					if Player.XPos > Object.XPos
						Player.Speed = 0x20000
					else
						Player.Speed = -0x20000
					end if
					
				end if
			end if
		end if
	end if
	
	// Spike fragments don't really do anything, so there's nothing for them here
end sub


sub ObjectDraw
	DrawSpriteFX(Object.PropertyValue, FX_INK, Object.XPos, Object.YPos)
end sub


sub ObjectStartup
	
	LoadSpriteSheet("Global/Items3.gif")
	
	// Spike Frames
	
	// 0 - Main Spikes frame
	SpriteFrame(-16, -16, 32, 32, 50, 1)
	
	// 1-3 - Individual Spike Frames
	SpriteFrame(-16, -16, 8, 32, 50, 1)
	SpriteFrame(-8, -16, 8, 32, 50, 1)
	SpriteFrame(0, -16, 8, 32, 50, 1)
	SpriteFrame(8, -16, 8, 32, 50, 1)
	
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Items3.gif")
	SpriteFrame(-16, -16, 32, 32, 50, 1)
	
	// Although used by the object for its broken variants, it shouldn't be set by the editor
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
