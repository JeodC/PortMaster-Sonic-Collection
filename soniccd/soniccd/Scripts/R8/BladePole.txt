//-----------------Sonic CD Blade Pole Script-----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.Timer

// States
#alias 0	:	BLADEPOLE_SETUP
#alias 1	:	BLADEPOLE_MOVE_RIGHT
#alias 2	:	BLADEPOLE_MOVE_LEFT

// Collision Sides
#alias 0	:	CSIDE_FLOOR


sub ObjectMain
	switch Object.State
	case BLADEPOLE_SETUP
		ObjectTileGrip(CSIDE_FLOOR, 0, 48, 0)
		if CheckResult == true
			Object.State = BLADEPOLE_MOVE_RIGHT
			Object.Timer = -64
		else
			Object.YPos += 0x10000
		end if
		break

	case BLADEPOLE_MOVE_RIGHT
		if Object.Timer < 96
			Object.Timer++
			Object.XPos += 0x10000
			ObjectTileGrip(CSIDE_FLOOR, 0, 48, 0)
			if CheckResult == false
				Object.State = BLADEPOLE_MOVE_LEFT
				Object.Timer = -64
			end if
		else
			Object.State = BLADEPOLE_MOVE_LEFT
			Object.Timer = -64
		end if
		break

	case BLADEPOLE_MOVE_LEFT
		if Object.Timer < 96
			Object.Timer++
			Object.XPos -= 0x10000
			ObjectTileGrip(CSIDE_FLOOR, 0, 48, 0)
			if CheckResult == false
				Object.State = BLADEPOLE_MOVE_RIGHT
				Object.Timer = -64
			end if
		else
			Object.State = BLADEPOLE_MOVE_RIGHT
			Object.Timer = -64
		end if
		break
	end switch

	switch Object.Frame
	case 0
	case 2
		if Object.AnimationTimer < 4
			Object.AnimationTimer++
		else
			Object.AnimationTimer = 0
			Object.Frame++
		end if
		break

	case 1
		if Object.AnimationTimer < 72
			Object.AnimationTimer++
		else
			Object.AnimationTimer = 0
			Object.Frame++
		end if
		break

	case 3
		if Object.AnimationTimer < 72
			Object.AnimationTimer++
		else
			Object.AnimationTimer = 0
			Object.Frame = 0
		end if
		break

	end switch
end sub


sub ObjectPlayerInteraction
	switch Object.Frame
	case 1	// Upper Blade
		PlayerObjectCollision(C_TOUCH, -28, -48, 28, -40)
		if CheckResult == true
			CallFunction(Player_Hit)
		end if
		break

	case 3	// Lower Blade
		PlayerObjectCollision(C_TOUCH, -28, 16, 28, 24)
		if CheckResult == true
			CallFunction(Player_Hit)
		end if
		break
	end switch
end sub


sub ObjectDraw
	TempValue0  = Object.Timer
	TempValue0 += 64
	TempValue0 %= 18
	TempValue0 /= 9
	TempValue0 += 16
	DrawSprite(TempValue0)

	switch Object.Frame
	case 0
	case 2
		DrawSprite(0)
		Object.Direction = 2
		DrawSpriteFX(2, FX_FLIP, Object.XPos, Object.YPos)
		break

	case 1
		TempValue0  = Object.AnimationTimer
		TempValue0 /= 3
		TempValue0 %= 6
		TempValue0 += 4
		DrawSprite(TempValue0)
		DrawSpriteFX(3, FX_FLIP, Object.XPos, Object.YPos)
		break

	case 3
		TempValue0  = Object.AnimationTimer
		TempValue0 /= 3
		TempValue0 %= 6
		TempValue0 += 10
		DrawSprite(1)
		DrawSpriteFX(TempValue0, FX_FLIP, Object.XPos, Object.YPos)
		break
	end switch
end sub


sub ObjectStartup
	LoadSpriteSheet("R8/Objects.gif")

	SpriteFrame(-16, -48, 32, 24, 67, 216)	// #0  - Top Half-extended Blade
	SpriteFrame(-12, -48, 24, 32, 34, 213)	// #1  - Top Closed Blade
	SpriteFrame(-16, -24, 32, 24, 67, 216)	// #2  - Bottom Half-open Blade
	SpriteFrame(-12, -24, 24, 32, 34, 213)	// #3  - Bottom Closed Blade

	SpriteFrame(-32, -48, 64, 8, 50, 182)	// #4  - Top Open Blade frame 0
	SpriteFrame(-24, -48, 48, 8, 50, 191)	// #5  - Top Open Blade frame 1
	SpriteFrame(-16, -48, 32, 8, 99, 191)	// #6  - Top Open Blade frame 2
	SpriteFrame(-4, -48, 8, 8, 115, 182)	// #7  - Top Open Blade frame 3
	SpriteFrame(-16, -48, 32, 8, 222, 245)	// #8  - Top Open Blade frame 4
	SpriteFrame(-24, -48, 48, 8, 1, 246)	// #9  - Top Open Blade frame 5

	SpriteFrame(-32, -24, 64, 8, 50, 182)	// #10 - Bottom Open Blade frame 0
	SpriteFrame(-24, -24, 48, 8, 50, 191)	// #11 - Bottom Open Blade frame 1
	SpriteFrame(-16, -24, 32, 8, 99, 191)	// #12 - Bottom Open Blade frame 2
	SpriteFrame(-4, -24, 8, 8, 115, 182)	// #13 - Bottom Open Blade frame 3
	SpriteFrame(-16, -24, 32, 8, 222, 245)	// #14 - Bottom Open Blade frame 4
	SpriteFrame(-24, -24, 48, 8, 1, 246)	// #15 - Bottom Open Blade frame 5

	SpriteFrame(-8, -40, 16, 88, 222, 123)	// #16 - Blade Pole frame 0
	SpriteFrame(-8, -40, 16, 88, 239, 123)	// #17 - Blade Pole frame 1
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(3)
	DrawSprite(0)
	Object.Direction = 2
	DrawSpriteFX(2, FX_FLIP, Object.XPos, Object.YPos)
end sub


sub RSDKLoad
	LoadSpriteSheet("R8/Objects.gif")

	SpriteFrame(-16, -48, 32, 24, 67, 216)	// #0  - Top Half-extended Blade
	SpriteFrame(-12, -48, 24, 32, 34, 213)	// #1  - Top Closed Blade
	SpriteFrame(-16, -24, 32, 24, 67, 216)	// #2  - Bottom Half-open Blade
	SpriteFrame(-8, -40, 16, 88, 222, 123)	// #3  - Blade Pole

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
