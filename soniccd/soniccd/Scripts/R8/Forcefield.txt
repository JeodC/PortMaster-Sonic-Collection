//------------------Sonic CD Forcefield Script----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.PropertyValue	:	Object.ScrollDistance

// States
#alias 0	:	FORCEFIELD_MOVE_TO_SCREEN
#alias 1	:	FORCEFIELD_IDLE
#alias 2	:	FORCEFIELD_VANISH

// Ink Effects
#alias 2	:	INK_ALPHA


sub ObjectMain
	switch Object.State
	case FORCEFIELD_MOVE_TO_SCREEN
		if Object.ScrollDistance > 168
			Object.ScrollDistance--
		else
			Object.State++
		end if

		Object.Frame   = Object.AnimationTimer
		Object.Frame >>= 2

		Object.AnimationTimer++
		Object.AnimationTimer &= 15

		TempValue1   = Oscillation
		TempValue1 <<= 3
		Sin(TempValue0, TempValue1)
		TempValue0 >>= 4
		TempValue0  += 190

		Object.Alpha	 = TempValue0
		Object.InkEffect = INK_ALPHA
		Object.DrawOrder = 4
		break
		
	case FORCEFIELD_IDLE
		Object.Frame   = Object.AnimationTimer
		Object.Frame >>= 2

		Object.AnimationTimer++
		Object.AnimationTimer &= 15

		TempValue1 = Oscillation
		TempValue1 <<= 3
		Sin(TempValue0, TempValue1)
		TempValue0 >>= 4
		TempValue0 += 190
		
		Object.Alpha = TempValue0
		break

	case FORCEFIELD_VANISH
		Object.Frame = 0
		if Object.Alpha > 0
			Object.Alpha -= 2
		end if
		break
	end switch
end sub


sub ObjectPlayerInteraction
	if Object.Alpha > 0
		TempValue0  = Object.ScrollDistance
		TempValue0 -= 8
		PlayerObjectCollision(C_BOX, TempValue0, -96, 240, 96)
		FlipSign(TempValue0)
		PlayerObjectCollision(C_BOX, -240, -96, TempValue0, 96)
	end if
end sub


sub ObjectDraw
	TempValue0  = Object.ScrollDistance
	TempValue0 *= -0x10000
	TempValue0 += Object.XPos
	DrawSpriteFX(4, FX_INK, TempValue0, Object.YPos)
	DrawSpriteFX(5, FX_INK, TempValue0, Object.YPos)

	Object.Direction = FACING_RIGHT
	DrawSpriteFX(Object.Frame, FX_FLIP, TempValue0, Object.YPos)
	Object.Direction = 2
	DrawSpriteFX(Object.Frame, FX_FLIP, TempValue0, Object.YPos)
	
	TempValue0   = Object.ScrollDistance
	TempValue0 <<= 16
	TempValue0  += Object.XPos
	DrawSpriteFX(4, FX_INK, TempValue0, Object.YPos)
	DrawSpriteFX(5, FX_INK, TempValue0, Object.YPos)

	Object.Direction = FACING_RIGHT
	DrawSpriteFX(Object.Frame, FX_FLIP, TempValue0, Object.YPos)
	Object.Direction = 2
	DrawSpriteFX(Object.Frame, FX_FLIP, TempValue0, Object.YPos)
end sub


sub ObjectStartup
	LoadSpriteSheet("R8/Objects3.gif")

	SpriteFrame(-8, 80, 16, 16, 90, 35)		// #0 - Forcefield machine frame 0
	SpriteFrame(-8, 80, 16, 16, 107, 35)	// #1 - Forcefield machine frame 1
	SpriteFrame(-8, 80, 16, 16, 124, 35)	// #2 - Forcefield machine frame 2
	SpriteFrame(-8, 80, 16, 16, 107, 35)	// #3 - Forcefield machine frame 3

	SpriteFrame(-8, -82, 16, 82, 74, 114)	// #4 - Forcefield Beam frame 0
	SpriteFrame(-8, 0, 16, 82, 74, 114)		// #5 - Forcefield Beam frame 1
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	TempValue0  = 168
	TempValue0 *= -0x10000
	TempValue0 += Object.XPos
	DrawSpriteFX(1, FX_INK, TempValue0, Object.YPos)
	DrawSpriteFX(2, FX_INK, TempValue0, Object.YPos)

	Object.Direction = FACING_RIGHT
	DrawSpriteFX(0, FX_FLIP, TempValue0, Object.YPos)
	Object.Direction = 2
	DrawSpriteFX(0, FX_FLIP, TempValue0, Object.YPos)
	
	TempValue0   = 168
	TempValue0 <<= 16
	TempValue0  += Object.XPos
	DrawSpriteFX(1, FX_INK, TempValue0, Object.YPos)
	DrawSpriteFX(2, FX_INK, TempValue0, Object.YPos)

	Object.Direction = FACING_RIGHT
	DrawSpriteFX(0, FX_FLIP, TempValue0, Object.YPos)
	Object.Direction = 2
	DrawSpriteFX(0, FX_FLIP, TempValue0, Object.YPos)
end sub


sub RSDKLoad
	LoadSpriteSheet("R8/Objects3.gif")

	SpriteFrame(-8, 80, 16, 16, 90, 35)		// #0 - Forcefield machine frame 0

	SpriteFrame(-8, -82, 16, 82, 74, 114)	// #1 - Forcefield Beam frame 0
	SpriteFrame(-8, 0, 16, 82, 74, 114)		// #2 - Forcefield Beam frame 1

	SetVariableAlias(ALIAS_VAR_PROPVAL, "scrollDistance")
end sub
