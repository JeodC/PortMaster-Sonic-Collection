//-----------------Sonic CD Tube Path Script------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.Moving

// Property Values
#alias 0	:	PATH_SELECT
#alias 1	:	PATH_GATE
#alias 2	:	PATH_UP_OR_RIGHT
#alias 3	:	PATH_DOWN_OR_LEFT
#alias 4	:	PATH_UP_OR_LEFT
#alias 5	:	PATH_DOWN_OR_RIGHT
#alias 6	:	PATH_DOWNRIGHT_OR_LEFT
#alias 7	:	PATH_UPLEFT_OR_RIGHT
#alias 8	:	PATH_UPRIGHT_OR_LEFT
#alias 9	:	PATH_DOWNLEFT_OR_RIGHT
#alias 10	:	PATH_LEVEL_TUNNEL
#alias 11	:	PATH_UPRIGHT_OR_DOWNLEFT
#alias 12	:	PATH_DOWNRIGHT_OR_UPLEFT

// Global SFX
#alias 7	:	SFX_G_RELEASE

// Gravity
#alias 0	:	GRAVITY_GROUND
#alias 1	:	GRAVITY_AIR

// Collision Planes
#alias 0	:	COLLISION_PLANE_A
#alias 1	:	COLLISION_PLANE_B


sub ObjectPlayerInteraction
	PlayerObjectCollision(C_TOUCH, -4, -4, 4, 4)
	if CheckResult == true
		switch Object.PropertyValue
		case PATH_SELECT

			if Object.Moving == false
				Player.XPos = Object.XPos
				Player.YPos = Object.YPos
				Player.XVelocity = 0
				Player.YVelocity = 0
			end if

			if Player.XVelocity == 0
				if Player.YVelocity == 0
					if Player.Up == true
						Player.XVelocity = 0
						Player.YVelocity = -0x100000
						PlaySfx(SFX_G_RELEASE, false)
					end if
					
					if Player.Down == true
						Player.XVelocity = 0
						Player.YVelocity = 0x100000
						PlaySfx(SFX_G_RELEASE, false)
					end if
					
					if Player.Left == true
						Player.XVelocity = -0x100000
						Player.YVelocity = 0
						PlaySfx(SFX_G_RELEASE, false)
					end if
					
					if Player.Right == true
						Player.XVelocity = 0x100000
						Player.YVelocity = 0
						PlaySfx(SFX_G_RELEASE, false)
					end if
				end if
			end if
			break

		case PATH_GATE
			if Object.Moving == false
				Player.AnimationSpeed = 240
				if Player.YVelocity > 0
					Player.XPos = Object.XPos
					Player.YPos = Object.YPos
					Player.Timer = 0
					Player.State = Player_State_Static
					Player.Animation = ANI_JUMPING
					Player.Speed = 0
					Player.TileCollisions = false
					Player.CollisionPlane = COLLISION_PLANE_B
					Player.XVelocity = 0
					Player.YVelocity = 0x100000
					PlaySfx(SFX_G_RELEASE, false)
				else
					
#platform: Use_Origins
					Player.State = Player_State_Air_NoDropDash
#endplatform

#platform: Use_Standalone
					Player.State = Player_State_Air
#endplatform
					Player.TileCollisions = true
					Player.CollisionPlane = COLLISION_PLANE_A
					Player.YVelocity = -0x80000
				end if
			end if
			break

		case PATH_UP_OR_RIGHT
			if Object.Moving == false
				Player.AnimationSpeed = 240
				Player.XPos = Object.XPos
				Player.YPos = Object.YPos
				if Player.YVelocity > 0
					Player.XVelocity = 0x100000
					Player.YVelocity = 0
				else
					Player.XVelocity = 0
					Player.YVelocity = -0x100000
				end if
			end if
			break

		case PATH_DOWN_OR_LEFT
			if Object.Moving == false
				Player.AnimationSpeed = 240
				Player.XPos = Object.XPos
				Player.YPos = Object.YPos
				if Player.YVelocity < 0
					Player.XVelocity = -0x100000
					Player.YVelocity = 0
				else
					Player.XVelocity = 0
					Player.YVelocity = 0x100000
				end if
			end if
			break

		case PATH_UP_OR_LEFT
			if Object.Moving == false
				Player.AnimationSpeed = 240
				Player.XPos = Object.XPos
				Player.YPos = Object.YPos
				if Player.YVelocity > 0
					Player.XVelocity = -0x100000
					Player.YVelocity = 0
				else
					Player.XVelocity = 0
					Player.YVelocity = -0x100000
				end if
			end if
			break

		case PATH_DOWN_OR_RIGHT
			if Object.Moving == false
				Player.AnimationSpeed = 240
				Player.XPos = Object.XPos
				Player.YPos = Object.YPos
				if Player.YVelocity < 0
					Player.XVelocity = 0x100000
					Player.YVelocity = 0
				else
					Player.XVelocity = 0
					Player.YVelocity = 0x100000
				end if
			end if
			break

		case PATH_DOWNRIGHT_OR_LEFT
			if Object.Moving == false
				if Player.State == Player_State_Static
					Player.AnimationSpeed = 240
					Player.XPos = Object.XPos
					Player.YPos = Object.YPos
					if Player.XVelocity > 0
						Player.XVelocity = 0xC0000
						Player.YVelocity = 0xC0000
					else
						Player.XVelocity = -0x100000
						Player.YVelocity = 0
					end if
				end if
			end if
			break

		case PATH_UPLEFT_OR_RIGHT
			if Object.Moving == false
				if Player.State == Player_State_Static
					Player.AnimationSpeed = 240
					Player.XPos = Object.XPos
					Player.YPos = Object.YPos
					if Player.XVelocity > 0
						Player.XVelocity = 0x100000
						Player.YVelocity = 0
					else
						Player.XVelocity = -0xC0000
						Player.YVelocity = -0xC0000
					end if
				end if
			end if
			break

		case PATH_UPRIGHT_OR_LEFT
			if Object.Moving == false
				if Player.State == Player_State_Static
					Player.AnimationSpeed = 240
					Player.XPos = Object.XPos
					Player.YPos = Object.YPos
					if Player.XVelocity > 0
						Player.XVelocity = 0xC0000
						Player.YVelocity = -0xC0000
					else
						Player.XVelocity = -0x100000
						Player.YVelocity = 0
					end if
				end if
			end if
			break

		case PATH_DOWNLEFT_OR_RIGHT
			if Object.Moving == false
				if Player.State == Player_State_Static
					Player.AnimationSpeed = 240
					Player.XPos = Object.XPos
					Player.YPos = Object.YPos
					if Player.XVelocity > 0
						Player.XVelocity = 0x100000
						Player.YVelocity = 0
					else
						Player.XVelocity = -0xC0000
						Player.YVelocity = 0xC0000
					end if
				end if
			end if
			break

		case PATH_LEVEL_TUNNEL
			if Player.Gravity == GRAVITY_GROUND
				Player.Gravity = GRAVITY_AIR
				Player.Animation = ANI_JUMPING
			end if
			break

		case PATH_UPRIGHT_OR_DOWNLEFT
			if Object.Moving == false
				if Player.State == Player_State_Static
					Player.AnimationSpeed = 240
					Player.XPos = Object.XPos
					Player.YPos = Object.YPos
					if Player.XVelocity > 0
						Player.XVelocity = 0xC0000
						Player.YVelocity = -0xC0000
					else
						Player.XVelocity = -0xC0000
						Player.YVelocity = -0xC0000
					end if
				end if
			end if
			break

		case PATH_DOWNRIGHT_OR_UPLEFT
			if Object.Moving == false
				if Player.State == Player_State_Static
					Player.AnimationSpeed = 240
					Player.XPos = Object.XPos
					Player.YPos = Object.YPos
					if Player.XVelocity > 0
						Player.XVelocity = 0xC0000
						Player.YVelocity = 0xC0000
					else
						Player.XVelocity = -0xC0000
						Player.YVelocity = 0xC0000
					end if
				end if
			end if
			break
		end switch

		Object.Moving = true
	else
		Object.Moving = false
	end if
end sub


// ========================
// Editor Subs
// ========================

sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			CheckResult = Object.PropertyValue
			break
		case 0 // condition
			CheckResult = Object.PropertyValue
			break
		end switch
	else
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			Object.PropertyValue = Editor.VariableValue
			break
		case 0 // condition
			Object.PropertyValue = Editor.VariableValue
			break
		end switch
	end if
end sub

sub RSDKDraw
	TempValue0  = Object.XPos
	TempValue0 -= 0x80000
	TempValue1  = Object.YPos
	TempValue1 -= 0x80000

	DrawSpriteXY(0, TempValue0, TempValue1)

	TempValue0 += 0x100000
	DrawSpriteXY(0, TempValue0, TempValue1)

	TempValue1 += 0x100000
	DrawSpriteXY(0, TempValue0, TempValue1)

	TempValue0 -= 0x100000
	DrawSpriteXY(0, TempValue0, TempValue1)

	if Editor.ShowGizmos == true
		Editor.DrawingOverlay = true
		switch Object.PropertyValue
		case PATH_SELECT
			TempValue0  = Object.XPos
			TempValue0 -= 0x300000
			DrawArrow(Object.XPos, Object.YPos, TempValue0, Object.YPos, 255, 255, 0)
			TempValue0 += 0x600000
			DrawArrow(Object.XPos, Object.YPos, TempValue0, Object.YPos, 255, 255, 0)

			TempValue0  = Object.YPos
			TempValue0 -= 0x300000
			DrawArrow(Object.XPos, Object.YPos, Object.XPos, TempValue0, 255, 255, 0)
			TempValue0 += 0x600000
			DrawArrow(Object.XPos, Object.YPos, Object.XPos, TempValue0, 255, 255, 0)
			break

		case PATH_GATE
			TempValue0  = Object.YPos
			TempValue0 -= 0x300000
			DrawArrow(Object.XPos, Object.YPos, Object.XPos, TempValue0, 255, 255, 0)
			TempValue0 += 0x600000
			DrawArrow(Object.XPos, Object.YPos, Object.XPos, TempValue0, 255, 255, 0)
			break

		case PATH_UP_OR_RIGHT
			TempValue0  = Object.YPos
			TempValue0 -= 0x300000
			DrawArrow(Object.XPos, Object.YPos, Object.XPos, TempValue0, 255, 255, 0)

			TempValue0  = Object.XPos
			TempValue0 += 0x300000
			DrawArrow(Object.XPos, Object.YPos, TempValue0, Object.YPos, 255, 255, 0)
			break

		case PATH_DOWN_OR_LEFT
			TempValue0  = Object.YPos
			TempValue0 += 0x300000
			DrawArrow(Object.XPos, Object.YPos, Object.XPos, TempValue0, 255, 255, 0)

			TempValue0  = Object.XPos
			TempValue0 -= 0x300000
			DrawArrow(Object.XPos, Object.YPos, TempValue0, Object.YPos, 255, 255, 0)
			break
			
		case PATH_UP_OR_LEFT
			TempValue0  = Object.YPos
			TempValue0 -= 0x300000
			DrawArrow(Object.XPos, Object.YPos, Object.XPos, TempValue0, 255, 255, 0)

			TempValue0  = Object.XPos
			TempValue0 -= 0x300000
			DrawArrow(Object.XPos, Object.YPos, TempValue0, Object.YPos, 255, 255, 0)
			break
			
		case PATH_DOWN_OR_RIGHT
			TempValue0  = Object.YPos
			TempValue0 += 0x300000
			DrawArrow(Object.XPos, Object.YPos, Object.XPos, TempValue0, 255, 255, 0)

			TempValue0  = Object.XPos
			TempValue0 += 0x300000
			DrawArrow(Object.XPos, Object.YPos, TempValue0, Object.YPos, 255, 255, 0)
			break

		case PATH_DOWNRIGHT_OR_LEFT
			TempValue0  = Object.XPos
			TempValue0 += 0x300000
			TempValue1  = Object.YPos
			TempValue1 += 0x300000
			DrawArrow(Object.XPos, Object.YPos, TempValue0, TempValue1, 255, 255, 0)

			TempValue0 -= 0x600000
			DrawArrow(Object.XPos, Object.YPos, TempValue0, Object.YPos, 255, 255, 0)
			break

		case PATH_UPLEFT_OR_RIGHT
			TempValue0  = Object.XPos
			TempValue0 -= 0x300000
			TempValue1  = Object.YPos
			TempValue1 -= 0x300000
			DrawArrow(Object.XPos, Object.YPos, TempValue0, TempValue1, 255, 255, 0)

			TempValue0 += 0x600000
			DrawArrow(Object.XPos, Object.YPos, TempValue0, Object.YPos, 255, 255, 0)
			break

		case PATH_UPRIGHT_OR_LEFT
			TempValue0  = Object.XPos
			TempValue0 += 0x300000
			TempValue1  = Object.YPos
			TempValue1 -= 0x300000
			DrawArrow(Object.XPos, Object.YPos, TempValue0, TempValue1, 255, 255, 0)

			TempValue0 -= 0x600000
			DrawArrow(Object.XPos, Object.YPos, TempValue0, Object.YPos, 255, 255, 0)
			break

		case PATH_DOWNLEFT_OR_RIGHT
			TempValue0  = Object.XPos
			TempValue0 -= 0x300000
			TempValue1  = Object.YPos
			TempValue1 += 0x300000
			DrawArrow(Object.XPos, Object.YPos, TempValue0, TempValue1, 255, 255, 0)

			TempValue0 += 0x600000
			DrawArrow(Object.XPos, Object.YPos, TempValue0, Object.YPos, 255, 255, 0)
			break

		case PATH_UPRIGHT_OR_DOWNLEFT
			TempValue0  = Object.XPos
			TempValue0 += 0x300000
			TempValue1  = Object.YPos
			TempValue1 -= 0x300000
			DrawArrow(Object.XPos, Object.YPos, TempValue0, TempValue1, 255, 255, 0)

			TempValue0  = Object.XPos
			TempValue0 -= 0x300000
			TempValue1  = Object.YPos
			TempValue1 += 0x300000
			DrawArrow(Object.XPos, Object.YPos, TempValue0, TempValue1, 255, 255, 0)
			break

		case PATH_DOWNRIGHT_OR_UPLEFT
			TempValue0  = Object.XPos
			TempValue0 += 0x300000
			TempValue1  = Object.YPos
			TempValue1 += 0x300000
			DrawArrow(Object.XPos, Object.YPos, TempValue0, TempValue1, 255, 255, 0)

			TempValue0  = Object.XPos
			TempValue0 -= 0x300000
			TempValue1  = Object.YPos
			TempValue1 -= 0x300000
			DrawArrow(Object.XPos, Object.YPos, TempValue0, TempValue1, 255, 255, 0)
			break
		end switch
		Editor.DrawingOverlay = false
	end if
end sub

sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-8, -8, 16, 16, 173, 67)		// #0 - "T" Icon

	AddEditorVariable("pathType")
	SetActiveVariable("pathType")
	AddEnumVariable("Select Path", PATH_SELECT)
	AddEnumVariable("Path Gate", PATH_GATE)
	AddEnumVariable("Path Up/Right", PATH_UP_OR_RIGHT)
	AddEnumVariable("Path Down/Left", PATH_DOWN_OR_LEFT)
	AddEnumVariable("Path Up/Left", PATH_UP_OR_LEFT)
	AddEnumVariable("Path Down/Right", PATH_DOWN_OR_RIGHT)
	AddEnumVariable("Path Down-Right/Left", PATH_DOWNRIGHT_OR_LEFT)
	AddEnumVariable("Path Up-Left/Right", PATH_UPLEFT_OR_RIGHT)
	AddEnumVariable("Path Up-Right/Left", PATH_UPRIGHT_OR_LEFT)
	AddEnumVariable("Path Down-Left/Right", PATH_DOWNLEFT_OR_RIGHT)
	AddEnumVariable("Path Level Tunnel", PATH_LEVEL_TUNNEL)
	AddEnumVariable("Path Up-Right/Down-Left", PATH_UPRIGHT_OR_DOWNLEFT)
	AddEnumVariable("Path Down-Right/Up-Left", PATH_DOWNRIGHT_OR_UPLEFT)
end sub
