//---------------Sonic CD Boundary Adjust Script--------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// States
#alias 0	:	BOUNDARYADJUST_SET_BOUNDS
#alias 1	:	BOUNDARYADJUST_RESTORE_BOUNDS


sub ObjectMain
	if Object.State == BOUNDARYADJUST_SET_BOUNDS
#platform: Use_Origins
		if Object.PropertyValue == 0
			if game.playMode == BOOT_PLAYMODE_BOSSRUSH
				Object.Type = TypeName[Blank Object]
			else
#endplatform
				if Player.XPos > Object.XPos
					// Set Left Bound
					TempValue0  = 3120
					TempValue0 -= Screen.CenterX
					Stage.NewXBoundary1 = TempValue0

					// Set Right Bound
					TempValue0 += Screen.XSize
					Stage.NewXBoundary2 = TempValue0

					// Set Top Bound
					TempValue0  = Object.iYPos
					TempValue0 -= Screen.YSize
					Stage.NewYBoundary1 = TempValue0

					// Set Bottom Bound
					Stage.YBoundary2  = Screen.YOffset
					Stage.YBoundary2 += Screen.YSize
					if Stage.YBoundary2 < Object.iYPos
						Stage.YBoundary2 = Object.iYPos
					end if

					Object.State++
				end if
#platform: Use_Origins
			end if
		else
			if game.playMode != BOOT_PLAYMODE_BOSSRUSH
				Object.Type = TypeName[Blank Object]
			else
				if Player.XPos > Object.XPos
					// Set Left Bound
					TempValue0  = 3840
					TempValue0 -= Screen.CenterX
					Stage.NewXBoundary1 = TempValue0

					// Set Right Bound
					TempValue0 += Screen.XSize
					Stage.NewXBoundary2 = TempValue0

					Screen.XOffset = 3260

					// Set Top Bound
					TempValue0  = Object.iYPos
					TempValue0 -= Screen.YSize
					Stage.NewYBoundary1 = TempValue0

					// Set Bottom Bound
					Stage.YBoundary2  = Screen.YOffset
					Stage.YBoundary2 += Screen.YSize
					Stage.YBoundary2  = Object.iYPos

					Object.State++
				end if
			end if
		end if
#endplatform
	else
		Stage.YBoundary2 -= 3
		if Stage.YBoundary2 <= Object.iYPos
			Stage.YBoundary2 = Object.iYPos
			Object.Type = TypeName[Blank Object]
		end if
	end if
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	if Editor.ShowGizmos == true
		Editor.DrawingOverlay = true
		// Draw a little indicator of where the bounds will be moved to
		
		 // Let's assume it's the original width res of the game, 424
		TempValue0   = 3120
		TempValue0  -= 242
		TempValue0 <<= 16
		TempValue1   = Object.iYPos
		TempValue1  -= 240
		TempValue1 <<= 16
		DrawRectOutline(TempValue0, TempValue1, 424, 240, 255, 255, 0, 255)
		Editor.DrawingOverlay = false
	end if
	
	TempValue0  = Object.XPos
	TempValue0 -= 0x80000
	TempValue1  = Object.YPos
	TempValue1 -= 0x80000

	DrawSpriteXY(0, TempValue0, TempValue1)

	TempValue0 += 0x100000
	DrawSpriteXY(0, TempValue0, TempValue1)

	TempValue1 += 0x100000
	DrawSpriteXY(0, TempValue0, TempValue1)

	TempValue0 -= 0x100000
	DrawSpriteXY(0, TempValue0, TempValue1)
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-8, -8, 16, 16, 173, 67)		// #0 - "T" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
