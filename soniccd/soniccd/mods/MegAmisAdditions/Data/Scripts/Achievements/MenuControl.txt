//----------------Sonic CD Menu Control Script----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0			:	Object.Timer				// Also used for fading
#alias Object.Value2			:	Object.SelectedButton		// Only used for Mobile
#alias Object.Value3			:	Object.SavedRound			// This is used AFTER you play in time attack
#alias Object.Value4 			:	Object.SMYPos				// Also used for the XPos of the zone portrait
#alias Object.Value5 			:	Object.SMCurrentButton
#alias Object.Value6			:	Object.BeatedRecord
#alias Object.Value7			:	Object.SSUnlocked
#alias Object.Scale 			:	Object.OffSet

// Large Icon / Special Large Icon Aliases
#alias Object[40].State			:	LargeIcon.State
#alias Object[40].XPos			:	LargeIcon.XPos
#alias Object[40].PropertyValue	:	LargeIcon.Portrait

// Time Stuff
#alias Object.Value1			:	Object.Minutes
#alias Object.Value2			:	Object.Seconds
#alias Object.Value3			:	Object.Milliseconds
#alias Object.Value4			:	Object.ZoneLocked

// States
// *SM = Sub Menu
#alias 0	:	MENUCONTROL_SETUP
#alias 1	:	MENUCONTROL_FADEIN
#alias 2	:	MENUCONTROL_CONTROLS
#alias 3	:	MENUCONTROL_BLANK
#alias 4	:	MENUCONTROL_RETURN_TO_MENU
#alias 5	:	MENUCONTROL_ENTER_SM
#alias 6	:	MENUCONTROL_CALL_RECORD_MENU
#alias 7	:	MENUCONTROL_SM_CONTROLS
#alias 8	:	MENUCONTROL_SM_SCROLL_DOWN
#alias 9	:	MENUCONTROL_SM_SCROLL_UP
#alias 10	:	MENUCONTROL_SM_CONFIRMED
#alias 11	:	MENUCONTROL_SM_LOAD_LEVEL
#alias 12	:	MENUCONTROL_SM_LEVEL_RETURN
#alias 13	:	MENUCONTROL_SM_COMFIRMED_LEAVE
#alias 14	:	MENUCONTROL_SM_EXIT
#alias 15	:	MENUCONTROL_SM_RECORD_BEATED
#alias 16	:	MENUCONTROL_SCROLL_DOWN
#alias 17	:	MENUCONTROL_SCROLLING_1
#alias 18	:	MENUCONTROL_SCROLL_UP
#alias 19	:	MENUCONTROL_SCROLLING_2

// Large Icon State
#alias 1	:	LARGEICON_ENTER_SCREEN
#alias 2	:	LARGEICON_LEAVE_SCREEN

// Round Icon States
#alias 1	:	ROUNDICON_SELECTED_ZONE

// Presentation Stages
#alias 0	:	STAGE_P_TITLE
#alias 1	:	STAGE_P_MENU

// Players
#alias 0	:	PLAYER_SONIC

// Soundtrack
#alias 0	:	OST_JP

// Global SFX
#alias 21	:	SFX_G_ACHIEVEMENT
#alias 23	:	SFX_G_MENUBUTTON
#alias 27	:	SFX_G_SELECT

// Stage SFX
#alias 0	:	SFX_S_BACKBUTTON

// Engine & Callbacks
#alias 2	:	CALLBACK_TATTACK_NOTIFY_ENTER
#alias 3	:	CALLBACK_TATTACK_NOTIFY_EXIT

// Engine Messages
#alias 2	:	MESSAGE_LOSTFOCUS

// Device Type
#alias 1	:	RETRO_MOBILE

// Game Mode Aliases
#alias 2	:	MODE_TIMEATTACK

// SaveRAM[39] is how many zones are unlocked for Time Attack

// Function declarations
#function MenuControl_IconTransitions
#function MenuControl_DefaultRecord
#function MenuControl_ZoneUnlockCheck
#function MenuControl_TotalRegularRecord
#function MenuControl_TotalSpecialRecord


function MenuControl_IconTransitions
	ArrayPos0 = 32

	//Top Half
	TempValue2   = Screen.CenterX
	TempValue2 <<= 16

	TempValue0 = -210
	TempValue1 = -46
	while ArrayPos0 < 36
		// Horizontal Movement
		Object[ArrayPos0].XPos   = TempValue0
		Object[ArrayPos0].XPos  *= Object.OffSet
		Object[ArrayPos0].XPos >>= 1
		Object[ArrayPos0].XPos  += TempValue2
		// Vertical Movement
		Object[ArrayPos0].YPos  = TempValue1
		Object[ArrayPos0].YPos *= Object.OffSet
		Object[ArrayPos0].YPos += 7782400
		TempValue0 += 140
		ArrayPos0++
	loop
	
	if TimeAttack.MenuSection == 0
		TempValue0 = -210
		TempValue1 = 46
		while ArrayPos0 < 40
			Object[ArrayPos0].XPos   = TempValue0
			Object[ArrayPos0].XPos  *= Object.OffSet
			Object[ArrayPos0].XPos >>= 1
			Object[ArrayPos0].XPos  += TempValue2
			Object[ArrayPos0].YPos   = TempValue1
			Object[ArrayPos0].YPos  *= Object.OffSet
			Object[ArrayPos0].YPos  += 6553600
			TempValue0 += 140
			ArrayPos0++
		loop
	else
		TempValue0 = -210
		TempValue1 = 46
		while ArrayPos0 < 40
			Object[ArrayPos0].YPos  = TempValue1
			Object[ArrayPos0].YPos *= Object.OffSet
			Object[ArrayPos0].YPos += 6553600
			TempValue0 += 140
			ArrayPos0++
		loop
	end if
end function


function MenuControl_DefaultRecord
	
end function


function MenuControl_ZoneUnlockCheck
	
end function


function MenuControl_TotalRegularRecord
	Object.SSUnlocked = true
end function


function MenuControl_TotalSpecialRecord
	Object.SSUnlocked = true
end function


sub ObjectMain
	switch Object.State
	case MENUCONTROL_SETUP
		ReadSaveRAM()

		Object.State  = MENUCONTROL_FADEIN
		Object.Timer  = 384
		Object.OffSet = 0x40000
		SetScreenFade(0, 0, 0, 255)

#platform: Use_Origins
		if Engine.DeviceType == RETRO_MOBILE
			Object.SavedRound		= -1
			Object.SelectedButton	= -1
			Object.SMCurrentButton	= -1
		end if
#endplatform

#platform: Mobile
		Object.SavedRound		= -1
		Object.SelectedButton	= -1
		Object.SMCurrentButton	= -1
#endplatform

		CallFunction(MenuControl_ZoneUnlockCheck)
		TimeAttack.MenuSection = 0
		Object.SSUnlocked = true
		WriteSaveRAM()

		CallFunction(MenuControl_IconTransitions)
		EngineCallback(CALLBACK_TATTACK_NOTIFY_ENTER)
		PlayMusic(0)
		break

	case MENUCONTROL_FADEIN
		if Object.Timer > 0
			Object.Timer  -= 8
			Object.OffSet -= 0x1000
		else
			Object.State = MENUCONTROL_CONTROLS
		end if
		SetScreenFade(0, 0, 0, Object.Timer)
		CallFunction(MenuControl_IconTransitions)
		break

	case MENUCONTROL_CONTROLS
#platform: Standard
		CheckResult = true
#endplatform

#platform: Mobile
		CheckEqual(Options.PhysicalControls, true)
#endplatform


		if CheckResult == true
			ArrayPos0  = 32
			ArrayPos0 += Object.SavedRound
			CheckResult = false
			if Object[ArrayPos0].State != ROUNDICON_SELECTED_ZONE
				if KeyPress[0].Left == true
					if TimeAttack.MenuSection == 1
						if Object.SavedRound > 3
							Object.SavedRound = 4
						end if
					end if
					Object.SavedRound--
					if Object.SavedRound < 0
						Object.SavedRound = 7
					end if
					PlaySfx(SFX_G_MENUBUTTON, false) 
				end if
				
				if KeyPress[0].Right == true
					if TimeAttack.MenuSection == 1
						if Object.SavedRound == 3
							Object.SavedRound = 6
						end if
					end if
					Object.SavedRound++
					if Object.SavedRound > 7
						Object.SavedRound = 0
					end if
					PlaySfx(SFX_G_MENUBUTTON, false) 
				end if

				if KeyPress[0].Up == true
					if TimeAttack.MenuSection == 0
						Object.SavedRound -= 4
					else
						if Object.SavedRound > 3
							Object.SavedRound = 0
						else
							Object.SavedRound -= 4
						end if
					end if
					if Object.SavedRound < 0
						if TimeAttack.MenuSection == 1
							Object.SavedRound += 8
						else
							Object.SavedRound = 7
						end if
						if Object.SSUnlocked == true
							Object.State = MENUCONTROL_SCROLL_UP
						end if
					end if
					PlaySfx(SFX_G_MENUBUTTON, false) 
				end if

				if KeyPress[0].Down == true
					if TimeAttack.MenuSection == 0
						Object.SavedRound += 4
					else
						if Object.SavedRound < 4
							Object.SavedRound = 7
						else
							Object.SavedRound += 4
						end if
					end if
					if Object.SavedRound > 7
						if TimeAttack.MenuSection == 0
							Object.SavedRound -= 8
						else
							Object.SavedRound = 0
						end if
						if Object.SSUnlocked == true
							Object.State = MENUCONTROL_SCROLL_DOWN
						end if
					end if
					PlaySfx(SFX_G_MENUBUTTON, false) 
				end if

				if KeyPress[0].Start == true
					CheckResult = true
				end if

				if KeyPress[0].ButtonA == true
					CheckResult = true
				end if
				
				if KeyPress[0].ButtonB == true
					PlayStageSfx(SFX_S_BACKBUTTON, false)
					Object.State = MENUCONTROL_BLANK
					StopMusic()
				endif
			end if

			if CheckResult == true
				ArrayPos0  = 32
				ArrayPos0 += Object.SavedRound
				if Object[ArrayPos0].ZoneLocked == 0
					Object[ArrayPos0].State = ROUNDICON_SELECTED_ZONE
					Object.SMCurrentButton = 0
					switch Object.SavedRound
					case -1
						break
					case 0
						PlaySfx(SFX_G_SELECT, false) 
						break
					case 1
						PlaySfx(SFX_G_SELECT, false) 
						break
					case 2
						PlaySfx(SFX_G_SELECT, false) 
						break
					case 3
						PlaySfx(SFX_G_SELECT, false) 
						break
					case 4
						PlaySfx(SFX_G_SELECT, false)
						if TimeAttack.MenuSection == 1
							Object.State = MENUCONTROL_BLANK
							StopMusic()
						endif
						break
					case 5
						PlaySfx(SFX_G_SELECT, false)
						if TimeAttack.MenuSection == 1
							Object.State = MENUCONTROL_BLANK
							StopMusic()
						endif
						break
					case 6
						PlaySfx(SFX_G_SELECT, false)
						if TimeAttack.MenuSection == 1
							Object.State = MENUCONTROL_BLANK
							StopMusic()
						endif
						break
					case 7
						PlaySfx(SFX_G_SELECT, false)
						if TimeAttack.MenuSection == 1
							Object.State = MENUCONTROL_BLANK
							StopMusic()
						endif
						break
					end switch
				end if
			end if
		end if

#platform: Mobile
		if Options.PhysicalControls == true
			CheckTouchRect(0, 0, Screen.XSize, Screen.YSize)
			if CheckResult > -1
				Options.PhysicalControls = false
				Object.SavedRound = -1
			end if
		else
			if Engine.Message == MESSAGE_LOSTFOCUS
				Object.SelectedButton = -1
			end if

			if Object.SSUnlocked == true
				TempValue0  = Screen.XSize
				TempValue0 -= 72
				CheckTouchRect(TempValue0, 192, Screen.XSize, Screen.YSize)
				if CheckResult > -1
					Object.SelectedButton = 8
				else
					CheckTouchRect(0, 0, Screen.XSize, Screen.YSize)
					if CheckResult < 0
						if Object.SelectedButton == 8
							PlaySfx(SFX_G_MENUBUTTON, false) 
							Object.State = MENUCONTROL_SCROLL_DOWN
							Object.SelectedButton = -1
						end if
					else
						if Object.SelectedButton == 8
							Object.SelectedButton = -1
						end if
					end if
				end if
			end if

			if Object.SelectedButton > -1
				Object.SavedRound = Object.SelectedButton
			else
				Object.SavedRound = -1
			end if

			if KeyPress[0].Left == true
				Object.SavedRound	  = 7
				Object.SelectedButton = -1

				Options.PhysicalControls = true
			end if

			if KeyPress[0].Right == true
				Object.SavedRound	  = 0
				Object.SelectedButton = -1

				Options.PhysicalControls = true
			end if

			if KeyPress[0].Up == true
				Object.SavedRound	  = 7
				Object.SelectedButton = -1

				Options.PhysicalControls = true
			end if

			if KeyPress[0].Down == true
				Object.SavedRound	  = 0
				Object.SelectedButton = -1

				Options.PhysicalControls = true
			end if
		end if
#endplatform
		break

	case MENUCONTROL_BLANK
		if Object.Timer < 384
			Object.Timer += 8
			Object.OffSet += 4096
		else
			Stage.ListPos = STAGE_P_MENU
			LoadStage()
		endif
		SetScreenFade(0, 0, 0, Object.Timer)
		CallFunction(MenuControl_IconTransitions)
		EngineCallback(CALLBACK_TATTACK_NOTIFY_EXIT)
		break

	case MENUCONTROL_RETURN_TO_MENU
		if Object.Timer < 384
			Object.Timer  += 8
			Object.OffSet += 0x1000
		else
			Stage.ListPos = STAGE_P_MENU
			LoadStage()
		end if
		SetScreenFade(0, 0, 0, Object.Timer)

		CallFunction(MenuControl_IconTransitions)
		EngineCallback(CALLBACK_TATTACK_NOTIFY_EXIT)
		break

	case MENUCONTROL_ENTER_SM
		if Object.Timer < 256
			Object.Timer  += 8
			Object.OffSet += 0x2000
		else
			if TimeAttack.MenuSection == 0
				Object[40].Type = TypeName[Large Icon]
				Object[41].Type = TypeName[Records]
			else
				Object[40].Type = TypeName[S Large Icon]
				Object[41].Type = TypeName[S Records]
			end if
			LargeIcon.Portrait = Object.SavedRound
			LargeIcon.State	   = LARGEICON_ENTER_SCREEN

			Object[41].DrawOrder = 2
			Object[42].DrawOrder = 2
			Object[43].DrawOrder = 2

			Object.Timer  = 0
			Object.SMYPos = 0

			Object.State++
		end if
		CallFunction(MenuControl_IconTransitions)
		break
	case MENUCONTROL_CALL_RECORD_MENU
		if Object.SMYPos < 192
			Object.SMYPos += 8
		else
			Object.State++
		end if

#platform: Standard
		Object.SMCurrentButton = 0
#endplatform

#platform: Mobile
		if Options.PhysicalControls == true
			Object.SMCurrentButton = 0
		else
			Object.SMCurrentButton = -1
		end if
#endplatform
		break

	case MENUCONTROL_SM_CONTROLS
#platform: Standard
		CheckResult = true
#endplatform

#platform: Mobile
		CheckEqual(Options.PhysicalControls, true)
#endplatform

		if CheckResult == true
			CheckResult = false
			if KeyPress[0].Start == true
				CheckResult = true
			end if

			if KeyPress[0].ButtonA == true
				CheckResult = true
			end if

			if KeyPress[0].ButtonB == true
				PlayStageSfx(SFX_S_BACKBUTTON, false)
				LargeIcon.State = LARGEICON_LEAVE_SCREEN
				Object.State	= MENUCONTROL_SM_COMFIRMED_LEAVE

				Object[41].DrawOrder = 8
				Object[42].DrawOrder = 8
				Object[43].DrawOrder = 8

				ArrayPos0  = Object.SMYPos
				ArrayPos0 /= 192
				ArrayPos0 += 40
				Object[ArrayPos0].DrawOrder = 2
			end if

			if CheckResult == true
				PlayStageSfx(SFX_S_BACKBUTTON, false)
				LargeIcon.State = LARGEICON_LEAVE_SCREEN
				Object.State	= MENUCONTROL_SM_COMFIRMED_LEAVE

				Object[41].DrawOrder = 8
				Object[42].DrawOrder = 8
				Object[43].DrawOrder = 8

				ArrayPos0  = Object.SMYPos
				ArrayPos0 /= 192
				ArrayPos0 += 40
				Object[ArrayPos0].DrawOrder = 2
			end if
		end if

#platform: Mobile
		if Options.PhysicalControls == true
			CheckTouchRect(0, 0, Screen.XSize, Screen.YSize)
			if CheckResult > -1
				Options.PhysicalControls = false
				Object.SMCurrentButton = -1
			end if
		else
			if Engine.Message == MESSAGE_LOSTFOCUS
				Object.SMCurrentButton = -1
			end if

			TempValue0  = Screen.CenterX
			TempValue0 -= 140
			TempValue1  = TempValue0
			TempValue1 += 66
			CheckTouchRect(TempValue0, 0, TempValue1, 40)
			if CheckResult > -1
				Object.SMCurrentButton = 0
			else
				CheckTouchRect(0, 0, Screen.XSize, Screen.YSize)
				if CheckResult < 0
					if Object.SMCurrentButton == 0
						PlayStageSfx(SFX_S_BACKBUTTON, false)
						LargeIcon.State = LARGEICON_LEAVE_SCREEN
						Object.State	= MENUCONTROL_SM_COMFIRMED_LEAVE

						Object[41].DrawOrder = 8
						Object[42].DrawOrder = 8
						Object[43].DrawOrder = 8

						ArrayPos0  = Object.SMYPos
						ArrayPos0 /= 192
						ArrayPos0 += 40
						Object[ArrayPos0].DrawOrder = 2
					end if
				else
					if Object.SMCurrentButton == 0
						Object.SMCurrentButton = -1
					end if
				end if
			end if

			if KeyPress[0].ButtonB == true
				PlayStageSfx(SFX_S_BACKBUTTON, false)
				LargeIcon.State = LARGEICON_LEAVE_SCREEN
				Object.State	= MENUCONTROL_SM_COMFIRMED_LEAVE

				Object[41].DrawOrder = 8
				Object[42].DrawOrder = 8
				Object[43].DrawOrder = 8

				ArrayPos0  = Object.SMYPos
				ArrayPos0 /= 192
				ArrayPos0 += 40
				Object[ArrayPos0].DrawOrder = 2
			end if

			if KeyPress[0].Left == true
				Object.SMCurrentButton = 0
				Options.PhysicalControls = true
			end if

			if KeyPress[0].Right == true
				Object.SMCurrentButton = 0
				Options.PhysicalControls = true
			end if
		end if
#endplatform
		break

	case MENUCONTROL_SM_SCROLL_UP
		if Object.Timer < 192
			Object.Timer  += 8
			Object.SMYPos += 8
			if Object.SMYPos > 767
				Object.SMYPos -= 576
			end if
		else
			Object.Timer = 0
			Object.State = MENUCONTROL_SM_CONTROLS
#platform: Mobile
			if Options.PhysicalControls == false
				Object.SMCurrentButton = -1
			end if
#endplatform
		end if

		break
		
	case MENUCONTROL_SM_SCROLL_DOWN
		if Object.Timer < 192
			Object.Timer  += 8
			Object.SMYPos -= 8
			if Object.SMYPos < 192
				Object.SMYPos += 576
			end if
		else
			Object.Timer = 0
			Object.State = MENUCONTROL_SM_CONTROLS
#platform: Mobile
			if Options.PhysicalControls == false
				Object.SMCurrentButton = -1
			end if
#endplatform
		end if
		break

	case MENUCONTROL_SM_CONFIRMED
		if Object.Timer < 30
			Object.Timer++
		else
			Object.Timer = 0
			Object.State++
		end if
		break

	case MENUCONTROL_SM_LOAD_LEVEL
		if Object.Timer < 320
			Object.Timer += 8
		else
			if TimeAttack.MenuSection == 0
				Stage.ActiveList = REGULAR_STAGE
				Stage.ListPos	 = LargeIcon.Portrait
				TimeAttack.Round = LargeIcon.Portrait
				Stage.ListPos   *= 10

				TempValue0  = Object.SMYPos
				TempValue0 -= 192
				TempValue0 /= 192

				TimeAttack.Zone = TempValue0
				TempValue0    <<= 2
				Stage.ListPos  += TempValue0
				if TempValue0 == 8
					Stage.ListPos++
				end if
			else
				Stage.ActiveList = SPECIAL_STAGE
#platform: Use_Origins
				Stage.ActiveList = 3
#endplatform
				Stage.ListPos	 = LargeIcon.Portrait
				TimeAttack.Round = LargeIcon.Portrait
				TimeAttack.Zone	 = 0
			end if

			Player.Lives	 = 1
			Player.Score	 = 0
			Options.GameMode = MODE_TIMEATTACK

			LoadStage()
		end if
		SetScreenFade(0, 0, 0, Object.Timer)
		break

	case MENUCONTROL_SM_LEVEL_RETURN
		if Object.Timer > 0
			Object.Timer -= 8
		else
			TimeAttack.Round = -1
			if Object.BeatedRecord > 0
				PlaySfx(SFX_G_ACHIEVEMENT, false) 
				Object.State = MENUCONTROL_SM_RECORD_BEATED
				Object[0].DrawOrder = 4
			else
				Object.State = MENUCONTROL_SM_CONTROLS
			end if
		end if
		SetScreenFade(0, 0, 0, Object.Timer)
		break

	case MENUCONTROL_SM_COMFIRMED_LEAVE
		if Object.Timer < 30
			Object.Timer++
		else
			Object.OffSet = 0x50000
			Object.Timer  = 0
			Object.State++
		end if

		if Object.SMYPos > 0
			Object.SMYPos -= 8
		end if
		break

	case MENUCONTROL_SM_EXIT
		if Object.Timer < 256
			Object.Timer  += 8
			Object.OffSet -= 0x2000
		else
			Object.Timer = 0
			Object.State = MENUCONTROL_CONTROLS
		end if
		CallFunction(MenuControl_IconTransitions)
		break
		
	case MENUCONTROL_SM_RECORD_BEATED
		if Object.Timer < 120
			Object.Timer++
		else
			Object.Timer = 0
			Object.State = MENUCONTROL_SM_CONTROLS

			Object.BeatedRecord = 0
			Object[0].DrawOrder = 1
		end if
		break

	case MENUCONTROL_SCROLL_DOWN
		if Object.Timer < 20
			Object.Timer++

			ArrayPos0 = 32
			while ArrayPos0 < 40
				Object[ArrayPos0].YPos -= 0xC0000
				ArrayPos0++
			loop
		else
			Object.Timer = 0

			ArrayPos0 = 32
			if TimeAttack.MenuSection == 0
				while ArrayPos0 < 40
					Object[ArrayPos0].YPos += 0x1E00000
					Object[ArrayPos0].Type  = TypeName[Special Icon]
					ArrayPos0++
				loop
				TimeAttack.MenuSection = 1
				CallFunction(MenuControl_TotalSpecialRecord)
			else
				while ArrayPos0 < 40
					Object[ArrayPos0].YPos += 0x1E00000
					Object[ArrayPos0].Type = TypeName[Round Icon]
					ArrayPos0++
				loop
				TimeAttack.MenuSection = 0
				CallFunction(MenuControl_TotalRegularRecord)
			end if
			Object.State++
		end if
		break

	case MENUCONTROL_SCROLLING_1		// Follow-up to MENUCONTROL_SCROLL_DOWN
		if Object.Timer < 20
			Object.Timer++

			ArrayPos0 = 32
			while ArrayPos0 < 40
				Object[ArrayPos0].YPos -= 0xC0000
				ArrayPos0++
			loop
		else
			Object.Timer = 0
			Object.State = MENUCONTROL_CONTROLS
		end if
		break

	case MENUCONTROL_SCROLL_UP
		if Object.Timer < 20
			Object.Timer++

			ArrayPos0 = 32
			while ArrayPos0 < 40
				Object[ArrayPos0].YPos += 0xC0000
				ArrayPos0++
			loop
		else
			Object.Timer = 0

			ArrayPos0 = 32
			if TimeAttack.MenuSection == 0
				while ArrayPos0 < 40
					Object[ArrayPos0].YPos -= 0x1E00000
					Object[ArrayPos0].Type  = TypeName[Special Icon]
					ArrayPos0++
				loop
				TimeAttack.MenuSection = 1
				CallFunction(MenuControl_TotalSpecialRecord)
			else
				while ArrayPos0 < 40
					Object[ArrayPos0].YPos -= 0x1E00000
					Object[ArrayPos0].Type  = TypeName[Round Icon]
					ArrayPos0++
				loop
				TimeAttack.MenuSection = 0
				CallFunction(MenuControl_TotalRegularRecord)
			end if
			Object.State++
		end if
		break

	case MENUCONTROL_SCROLLING_2		// Follow-up to MENUCONTROL_SCROLL_UP
		if Object.Timer < 20
			Object.Timer++

			ArrayPos0 = 32
			while ArrayPos0 < 40
				Object[ArrayPos0].YPos += 0xC0000
				ArrayPos0++
			loop
		else
			Object.Timer = 0
			Object.State = MENUCONTROL_CONTROLS
		end if
		break

	end switch
end sub


sub ObjectDraw
	switch Object.State
	case MENUCONTROL_SM_COMFIRMED_LEAVE
		TempValue0  = Screen.CenterX
		TempValue0 += 16
		DrawRect(TempValue0, 31, Screen.CenterX, 9, 0, 0, 0, 255)
		DrawRect(TempValue0, 40, Screen.CenterX, 135, 224, 0, 0, 255)
		DrawRect(TempValue0, 175, Screen.CenterX, 38, 0, 0, 96, 255)
		break
		
	case MENUCONTROL_SM_RECORD_BEATED
		TempValue0   = Object.Timer
		TempValue0 >>= 2
		TempValue0  &= 1
		if TempValue0 == 0
			TempValue0  = Screen.CenterX
			TempValue0 += 16
			TempValue1  = Object.BeatedRecord
			TempValue1 *= 24
			TempValue1 += 105
			DrawRect(TempValue0, TempValue1, 34, 24, 8, 0, 104, 255)
		end if
		break

	end switch
end sub


sub ObjectStartup
	if Options.Soundtrack == OST_JP
		SetMusicTrack("JP/TimeAttack.ogg", 0, 100512)
	else
		SetMusicTrack("US/DAGarden.ogg", 0, 117382)
	end if

	Object[0].Type		= TypeName[Menu Control]
	Object[0].DrawOrder = 1

#platform: Use_Origins
	if Engine.DeviceType == RETRO_MOBILE
		Options.PhysicalControls = true
	end if
#endplatform

#platform: Mobile
	Options.PhysicalControls = false
#endplatform
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
