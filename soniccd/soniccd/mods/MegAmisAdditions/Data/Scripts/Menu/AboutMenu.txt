//-----------------Sonic CD About Menu Script-----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//
//---This Object is normally only used by Mobile Data files---//

// Aliases
#alias Object.Value1		:	Object.ButtonSelected
#alias Object.Value2		:	Object.DrawHeight
#alias Object[-10].Alpha	:	Object.OptionsAlpha	//a.k.a the alpha value from the menu that called this

// States
#alias 0	:	ABOUTMENU_CONTROLS
#alias 1	:	ABOUTMENU_OPTION_SELECTED

// Global SFX
#alias 23	:	SFX_G_MENUBUTTON
#alias 27	:	SFX_G_SELECT

// Engine States & Callbacks
#alias 9	:	CALLBACK_TERMS_SELECTED
#alias 10	:	CALLBACK_PRIVACY_SELECTED

// Text Info type
#alias 0	:	TEXTINFO_TEXTDATA
#alias 1	:	TEXTINFO_TEXTSIZE


sub ObjectMain
	Object.Alpha = Object.OptionsAlpha
	if Object.Alpha > 254
		CheckResult = true
#platform: Mobile
		CheckResult = Options.PhysicalControls
#endplatform
#platform: Use_Origins
		CheckResult = Options.PhysicalControls
#endplatform
		if CheckResult == true
			CallFunction(MenuHeading_CheckTouchBack)
		end if
	else
#platform: Mobile
		if Options.PhysicalControls == false
			Object.ButtonSelected = -1
		end if
#endplatform

#platform: Use_Origins
		if Options.PhysicalControls == false
			Object.ButtonSelected = -1
		end if
#endplatform
		Object.AnimationTimer = 0
		Object.State = ABOUTMENU_CONTROLS
	end if
end sub


sub ObjectDraw
	TempValue0  = Screen.CenterX
	TempValue0 -= 120
	DrawSpriteScreenFX(0, FX_INK, TempValue0, 72)

	TempValue0  = Screen.CenterX
	TempValue0 += 72
	DrawSpriteScreenFX(1, FX_INK, TempValue0, 102)
	
	ArrayPos0 = 0
	GetTextInfo(TempValue1, MENU_1, TEXTINFO_TEXTSIZE, 0, 0)	//Gets version numbers
	TempValue2   = TempValue1
	TempValue2 <<= 2
	FlipSign(TempValue2)
	TempValue2 += Screen.CenterX
	TempValue2 += 72
	Object.DrawHeight = 122
	
	//This handles the drawing of the game version
	while TempValue1 > 0
		GetTextInfo(TempValue0, MENU_1, TEXTINFO_TEXTDATA, 0, ArrayPos0)
		switch TempValue0
		default
			Object.Frame = 0
			if TempValue0 > 64
				if TempValue0 < 91
					Object.Frame  = TempValue0
					Object.Frame -= 63
				end if
			end if
			if TempValue0 > 96
				if TempValue0 < 123
					Object.Frame  = TempValue0
					Object.Frame -= 95
				end if
			end if
			if TempValue0 > 47
				if TempValue0 < 58
					Object.Frame  = TempValue0
					Object.Frame -= 18
				end if
			end if
			if Object.Frame > 0
				DrawSpriteScreenFX(Object.Frame, FX_INK, TempValue2, Object.DrawHeight)
			end if
			break
			
		case 38
			DrawSpriteScreenFX(33, FX_INK, TempValue2, Object.DrawHeight)
			break
			
		case 39
			DrawSpriteScreenFX(31, FX_INK, TempValue2, Object.DrawHeight)
			break

		case 44
			DrawSpriteScreenFX(29, FX_INK, TempValue2, Object.DrawHeight)
			break

		case 46
			DrawSpriteScreenFX(28, FX_INK, TempValue2, Object.DrawHeight)
			break
		end switch
		ArrayPos0++
		TempValue1--
		TempValue2 += 8
	loop
end sub


sub ObjectStartup
	LoadSpriteSheet("Menu/MenuGfx1_EN.gif")
	
	SpriteFrame(0, 0, 132, 113, 154, 235)	//	#0 - Sonic CD Mini Logo
	SpriteFrame(-28, 0, 56, 16, 154, 349)	//	#1 - Version

	TempValue1 = 366
	while TempValue1 < 417
		TempValue0 = 154
		while TempValue0 < 280
			SpriteFrame(-3, 0, 8, 16, TempValue0, TempValue1)	//	#2 to #44 - Alphabet and Numbers
			TempValue0 += 9
		loop
		TempValue1 += 17
	loop
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
