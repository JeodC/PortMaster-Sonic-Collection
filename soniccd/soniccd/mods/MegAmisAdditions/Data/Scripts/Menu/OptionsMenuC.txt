//--------------Sonic CD Soundtrack Menu Script---------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0			:	Object.FadeValue
#alias Object.Value1			:	Object.SelectedOption

//Object[33] is MenuHeading
#alias Object[33].State			:	MenuHeading.State
#alias Object[33].PropertyValue	:	MenuHeading.Header

//Object[-1] is MenuWindow
#alias Object[-1].State			:	MenuWindow.State

// Soundtrack Options
#alias -1	:	OPTION_NONE // Touching nothing on mobile
#alias  0	:	OPTION_NO
#alias  1	:	OPTION_YES

// States
#alias 0	:	EXITMENU_INITIALIZE
#alias 1	:	EXITMENU_OPTION_SELECTION
#alias 2	:	EXITMENU_OPTION_SELECTED
#alias 3	:	EXITMENU_BACK
#alias 4	:	EXITMENU_EXIT
#alias 5	:	EXITMENU_RETURN_TO_TITLE

// Menu Heading States
#alias 1	:	MENUHEADING_LEAVE_SCREEN

// Menu Heading Property Values
#alias 5	:	MAINMENU_HEADER

// Menu Window States
#alias 3	:	MENUWINDOW_SHRINK

// Menu Button States
#alias 1	:	MENUBUTTON_BUTTONS_IN

#alias 3	:	ENGINE_EXITGAME

// Global SFX
#alias 23	:	SFX_G_MENUBUTTON
#alias 27	:	SFX_G_SELECT

// Stage SFX
#alias 1	:	SFX_S_BACKBUTTON

// Languages
#alias 0	:	LANG_ENGLISH
#alias 1	:	LANG_FRENCH
#alias 2	:	LANG_ITALIAN
#alias 3	:	LANG_DEUTSCH
#alias 4	:	LANG_SPANISH
#alias 5	:	LANG_JAPANESE


sub ObjectMain
	switch Object.State
	case EXITMENU_INITIALIZE
#platform: Standard
		Object.SelectedOption = OPTION_NO
#endplatform

#platform: Mobile
		if Options.PhysicalControls == false
			Object.SelectedOption = OPTION_NONE
		else
			Object.SelectedOption = OPTION_NO
		end if
#endplatform

#platform: Use_Origins
		if Options.PhysicalControls == false
			Object.SelectedOption = OPTION_NONE
		else
			Object.SelectedOption = OPTION_NO
		end if
#endplatform


		if Object.FadeValue < 256
			Object.Alpha	  = Object.FadeValue
			Object.FadeValue += 6
		else
			Object.FadeValue = 0
			Object.Alpha	 = 255
			Object.State	 = EXITMENU_OPTION_SELECTION
		end if
		break
		
	case EXITMENU_OPTION_SELECTION
#platform: Standard
		CheckResult = true
#endplatform

#platform: Mobile
		CheckEqual(Options.PhysicalControls, true)
#endplatform

#platform: Use_Origins
		CheckEqual(Options.PhysicalControls, true)
#endplatform

		// 2011 Steam lacks Options.PhysicalControls, so we force it here first and the other platforms can override it
		if CheckResult == true
			// Physical Controls
			if KeyPress[0].Left == true
				Object.SelectedOption--
				if Object.SelectedOption < OPTION_NO
					Object.SelectedOption = OPTION_YES
				end if
				PlaySfx(SFX_G_MENUBUTTON, false) 
			end if
			
			if KeyPress[0].Right == true
				Object.SelectedOption++
				if Object.SelectedOption > OPTION_YES
					Object.SelectedOption = OPTION_NO
				end if
				PlaySfx(SFX_G_MENUBUTTON, false) 
			end if
			// Enable touch controls if the screen is touched
			CheckTouchRect(0, 0, Screen.XSize, Screen.YSize)
			if CheckResult > -1
#platform: Mobile
				Options.PhysicalControls = false
				Object.SelectedOption = OPTION_NONE
#endplatform
#platform: Use_Origins
				Options.PhysicalControls = false
				Object.SelectedOption = OPTION_NONE
#endplatform
			end if
		else
			// Touch Controls
			
			// Check if JP button is touched
			CheckTouchRect(0, 0, Screen.XSize, Screen.YSize)
			TempValue3  = CheckResult
			TempValue0  = Screen.CenterX
			TempValue0 -= 90
			TempValue1  = Screen.CenterX
			TempValue1 -= 22
			CheckTouchRect(TempValue0, 112, TempValue1, 144)
			if CheckResult > -1
				Object.SelectedOption = OPTION_NO
			else
				if TempValue3 < 0
					if Object.SelectedOption == OPTION_NO
						KeyPress[0].Start = true
					end if
				else
					if Object.SelectedOption == OPTION_NO
						Object.SelectedOption = OPTION_NONE
					end if
				end if
			end if
			
			// Check if US button is touched
			TempValue0  = Screen.CenterX
			TempValue0 += 22
			TempValue1  = Screen.CenterX
			TempValue1 += 90
			CheckTouchRect(TempValue0, 112, TempValue1, 144)
			if CheckResult > -1
				Object.SelectedOption = OPTION_YES
			else
				if TempValue3 < 0
					if Object.SelectedOption == OPTION_YES
						KeyPress[0].Start = true
					end if
				else
					if Object.SelectedOption == OPTION_YES
						Object.SelectedOption = OPTION_NONE
					end if
				end if
			end if
			
			CallFunction(MenuHeading_CheckTouchBack)
			
			// Enable physical controls if a button is pressed
			if KeyPress[0].Left == true
				Object.SelectedOption = OPTION_YES

#platform: Use_Origins
				Options.PhysicalControls = true
#endplatform
#platform: Mobile
				Options.PhysicalControls = true
#endplatform

			end if
			if KeyPress[0].Right == true
				Object.SelectedOption = OPTION_NO

#platform: Use_Origins
				Options.PhysicalControls = true
#endplatform
#platform: Mobile
				Options.PhysicalControls = true
#endplatform
			end if
		end if

		CheckResult = false

		if Object.SelectedOption > OPTION_NONE // If a button was touched
			if KeyPress[0].Start == true
				CheckResult = true
			end if

			if KeyPress[0].ButtonA == true
				CheckResult = true
			end if
		end if

		if CheckResult == true
			Object.State = EXITMENU_OPTION_SELECTED
			if Object.SelectedOption == OPTION_YES
				StopMusic()
			end if
			PlaySfx(SFX_G_SELECT, false)
		end if
		
		if KeyPress[0].ButtonB == true
			Object.FadeValue = 248
			Object.State = EXITMENU_BACK
			PlayStageSfx(SFX_S_BACKBUTTON, false)
		end if
		break
		
	case EXITMENU_OPTION_SELECTED
		if Object.AnimationTimer < 30
			Object.AnimationTimer++
		else
			Object.AnimationTimer = 0
			if Object.SelectedOption == OPTION_NO
				Object.State = EXITMENU_BACK
			else
				Object.State = EXITMENU_EXIT
				Object.FadeValue = 0
			end if
		end if
		break
		
	case EXITMENU_BACK
		if Object.FadeValue > 0
			Object.Alpha	  = Object.FadeValue
			Object.FadeValue -= 8
		else
			// Tell the other objects the menu is being exited
			Object.Type = TypeName[Blank Object]
			MenuWindow.State = MENUWINDOW_SHRINK
			//All of this is MenuButton Objects
			Object[34].State = MENUBUTTON_BUTTONS_IN
			Object[35].State = MENUBUTTON_BUTTONS_IN
			Object[36].State = MENUBUTTON_BUTTONS_IN
			Object[37].State = MENUBUTTON_BUTTONS_IN
			Object[38].State = MENUBUTTON_BUTTONS_IN
			Object[39].State = MENUBUTTON_BUTTONS_IN
			Object[40].State = MENUBUTTON_BUTTONS_IN
			Object[41].State = MENUBUTTON_BUTTONS_IN
			Object[42].State = MENUBUTTON_BUTTONS_IN
			//End
			MenuHeading.State  = MENUHEADING_LEAVE_SCREEN
			MenuHeading.Header = MAINMENU_HEADER // Set the heading to "MAIN MENU"
		end if
		break
		
	case EXITMENU_EXIT
		if Object.FadeValue < 320
			Object.FadeValue += 8
		else
			Engine.State = ENGINE_EXITGAME
		end if
		SetScreenFade(0, 0, 0, Object.FadeValue)
		break
	end switch
end sub


sub ObjectDraw
	DrawSpriteScreenFX(0, FX_INK, Screen.CenterX, Screen.CenterY)
	
	// Set up the positioning for the buttons
	TempValue0  = Screen.CenterX
	TempValue0 -= 56
	TempValue1  = Screen.CenterX
	TempValue1 += 56
	TempValue3  = Screen.CenterY
	TempValue3 += 8
	
	// Draw the No button
	if Object.SelectedOption == OPTION_NO
		TempValue2   = Object.AnimationTimer
		TempValue2  &= 3
		TempValue2 >>= 1
		TempValue2++
		DrawSpriteScreenFX(TempValue2, FX_INK, TempValue0, TempValue3)
	else
		DrawSpriteScreenFX(2, FX_INK, TempValue0, TempValue3)
	end if
	
	// Draw the Yes button
	if Object.SelectedOption == OPTION_YES
		TempValue2   = Object.AnimationTimer
		TempValue2  &= 3
		TempValue2 >>= 1
		TempValue2  += 3
		DrawSpriteScreenFX(TempValue2, FX_INK, TempValue1, TempValue3)
	else
		DrawSpriteScreenFX(4, FX_INK, TempValue1, TempValue3)
	end if
end sub


sub ObjectStartup
	switch Engine.Language
	case LANG_ENGLISH
		LoadSpriteSheet("Menu/MenuGfx2_EN.gif")
		break
	case LANG_FRENCH
		LoadSpriteSheet("Menu/MenuGfx2_FR.gif")
		break
	case LANG_ITALIAN
		LoadSpriteSheet("Menu/MenuGfx2_IT.gif")
		break
	case LANG_DEUTSCH
		LoadSpriteSheet("Menu/MenuGfx2_DE.gif")
		break
	case LANG_SPANISH
		LoadSpriteSheet("Menu/MenuGfx2_ES.gif")
		break
	case LANG_JAPANESE
		LoadSpriteSheet("Menu/MenuGfx2_JP.gif")
		break
	end switch
	
	SpriteFrame(-40, -36, 80, 16, 300, 581) // #0 - Exit Game?
	SpriteFrame(-32, -10, 64, 21, 404, 385) // #1 - No Selected
	SpriteFrame(-32, -10, 64, 21, 404, 363) // #2 - No Unselected
	SpriteFrame(-32, -10, 64, 21, 404, 341) // #3 - Yes Selected
	SpriteFrame(-32, -10, 64, 21, 404, 319) // #4 - Yes Unselected
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Menu/MenuGfx2_EN.gif")
	SpriteFrame(-20, -56, 40, 40, 259, 513) // #0 - Eggman 1

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
