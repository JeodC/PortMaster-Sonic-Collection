//-----------------Sonic CD BGEffects Script------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
// Values 0-1 are unused...
#alias Object.Value2 : Object.PrevAngle
#alias Object.Value3 : Object.FansTimer
#alias Object.Value4 : Object.CrunchersTimer
#alias Object.Value5 : Object.FansSprite
#alias Object.Value6 : Object.CrunchersSprite

// Priority
#alias 1 : PRIORITY_ACTIVE


sub ObjectDraw
	// Animate the fans in the level
	Object.FansTimer++
	if Object.FansTimer > 2
		Object.FansTimer = 0
		Object.FansSprite++
		Object.FansSprite %= 3
		switch Object.FansSprite
		case 0
			Copy16x16Tile(269, 484)
			Copy16x16Tile(270, 485)
			Copy16x16Tile(271, 486)
			Copy16x16Tile(272, 487)
			Copy16x16Tile(273, 488)
			Copy16x16Tile(274, 489)
			Copy16x16Tile(275, 490)
			Copy16x16Tile(276, 491)
			break
			
		case 1
			Copy16x16Tile(269, 492)
			Copy16x16Tile(270, 493)
			Copy16x16Tile(271, 494)
			Copy16x16Tile(272, 495)
			Copy16x16Tile(273, 496)
			Copy16x16Tile(274, 497)
			Copy16x16Tile(275, 498)
			Copy16x16Tile(276, 499)
			break
			
		case 2
			Copy16x16Tile(269, 500)
			Copy16x16Tile(270, 501)
			Copy16x16Tile(271, 502)
			Copy16x16Tile(272, 503)
			Copy16x16Tile(273, 504)
			Copy16x16Tile(274, 505)
			Copy16x16Tile(275, 506)
			Copy16x16Tile(276, 507)
			break
			
		end switch
	end if
	
	// Animate the crunchers in the level
	Object.CrunchersTimer++
	if Object.CrunchersTimer > 5
		Object.CrunchersTimer = 0
		Object.CrunchersSprite++
		Object.CrunchersSprite &= 3
		switch Object.CrunchersSprite
		case 0
			Copy16x16Tile(129, 508)
			Copy16x16Tile(130, 509)
			Copy16x16Tile(134, 512)
			Copy16x16Tile(135, 513)
			break
			
		case 1
			Copy16x16Tile(129, 510)
			Copy16x16Tile(130, 511)
			Copy16x16Tile(134, 514)
			Copy16x16Tile(135, 515)
			break
			
		case 2
			Copy16x16Tile(129, 516)
			Copy16x16Tile(130, 517)
			Copy16x16Tile(134, 520)
			Copy16x16Tile(135, 521)
			break
			
		case 3
			Copy16x16Tile(129, 518)
			Copy16x16Tile(130, 519)
			Copy16x16Tile(134, 522)
			Copy16x16Tile(135, 523)
			break
			
		end switch
	end if
	
	// Move all the BG layers based on the scroll difference from last frame
	
	TempValue1 = Object.PrevAngle
	TempValue2 = TileLayer[0].Angle
	if TempValue1 < TempValue2
		TempValue3 = TempValue2
		TempValue3 -= TempValue1
		if TempValue3 > 256
			TempValue1 += 512
		end if
	else
		TempValue3 = TempValue1
		TempValue3 -= TempValue2
		if TempValue3 > 256
			TempValue2 += 512
		end if
	end if
	
	TempValue0 = TempValue1
	TempValue0 -= TempValue2
	
	// Store the current angle so that the same thing can be done next frame
	Object.PrevAngle = TileLayer[0].Angle
	
	TempValue1 = 0x10000
	TempValue2 = 0x20000
	TempValue3 = 0x30000
	
	TempValue1 *= TempValue0
	TempValue2 *= TempValue0
	TempValue3 *= TempValue0
	
	HParallax[0].ScrollPos += TempValue1
	HParallax[1].ScrollPos += TempValue2
	HParallax[2].ScrollPos += TempValue3
	
end sub


sub ObjectStartup
	
	Stage.XBoundary1 = 0x2800000 // left bounds of 640 pixels
	Stage.XBoundary2 = 0xD800000 // right bounds of 3456 pixels
	Stage.YBoundary1 = 0x2800000 // upper bounds of 640 pixels
	Stage.YBoundary2 = 0xD800000 // lower bounds of 3456 pixels
	
#platform: HW_Rendering
	// Load the Sprite version of the stage map if needed
	
	LoadSpriteSheet("Special/SSMap7.gif")
#endplatform
	
	// Set the initial BG positions, offset by 128 pixels
	HParallax[0].ScrollPos += 0x800000
	HParallax[2].ScrollPos -= 0x800000
	
	// Setup any and all BGEffects objects that may be present in the level
	// Though, there should only ever be one
	
	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[BGEffects]
			
			// Set the object to always active
			Object[ArrayPos0].Priority = PRIORITY_ACTIVE
			
			// Give it a low draw order
			Object[ArrayPos0].DrawOrder = 0
			
			// And set its starting angle
			// But all other stages use 368... why is this one different?
			Object[ArrayPos0].Value2 = 256
		
		end if
		
		ArrayPos0++
	loop
	
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Special/SSMap7.gif")
	SpriteFrame(-256, -256, 512, 512, 0, 0)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
