//-----------------Sonic CD Flip Door Script------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.Timer

// States
#alias 0	:	FLIPDOOR_CLOSED
#alias 1	:	FLIPDOOR_HALF_OPEN
#alias 2	:	FLIPDOOR_OPEN
#alias 3	:	FLIPDOOR_HALF_CLOSED

// Stages SFX
#alias 9	:	SFX_S_DOOR


sub ObjectMain

	if Object.Frame == 0
		if Player.ObjectInteraction == true
			PlayerObjectCollision(C_BOX, -4, -32, 4, 32)
		end if
	end if

	if Object.Direction == FACING_RIGHT

		switch Object.State
		case FLIPDOOR_CLOSED
			Object.Frame = 0
			PlayerObjectCollision(C_TOUCH, -48, -32, 0, 32)
			if CheckResult == true
				Object.State = FLIPDOOR_HALF_OPEN
				PlayStageSfx(SFX_S_DOOR, false)
			end if
			break

		case FLIPDOOR_HALF_OPEN
			if Object.Timer < 4
				Object.Timer++
				Object.Frame = 1
			else
				Object.Timer = 0
				Object.State = FLIPDOOR_OPEN
			end if
			break

		case FLIPDOOR_OPEN
			Object.Frame = 2
			PlayerObjectCollision(C_TOUCH, -48, -32, 64, 64)
			if CheckResult == false
				Object.State = FLIPDOOR_HALF_CLOSED
			end if
			break

		case FLIPDOOR_HALF_CLOSED
			if Object.Timer < 4
				Object.Timer++
				Object.Frame = 1
			else
				Object.Timer = 0
				Object.State = FLIPDOOR_CLOSED
			end if
			break
		end switch
	else

		switch Object.State
		case FLIPDOOR_CLOSED
			Object.Frame = 0
			PlayerObjectCollision(C_TOUCH, 0, -32, 48, 32)
			if CheckResult == true
				Object.State = FLIPDOOR_HALF_OPEN
				PlayStageSfx(SFX_S_DOOR, false)
			end if
			break

		case FLIPDOOR_HALF_OPEN
			if Object.Timer < 4
				Object.Timer++
				Object.Frame = 1
			else
				Object.Timer = 0
				Object.State = FLIPDOOR_OPEN
			end if
			break

		case FLIPDOOR_OPEN
			Object.Frame = 2
			PlayerObjectCollision(C_TOUCH, -64, -32, 48, 64)
			if CheckResult == false
				Object.State = FLIPDOOR_HALF_CLOSED
			end if
			break

		case FLIPDOOR_HALF_CLOSED
			if Object.Timer < 4
				Object.Timer++
				Object.Frame = 1
			else
				Object.Timer = 0
				Object.State = FLIPDOOR_CLOSED
			end if
			break
		end switch
	end if
end sub


sub ObjectDraw
	DrawSpriteFX(Object.Frame, FX_FLIP, Object.XPos, Object.YPos)
end sub


sub ObjectStartup
	LoadSpriteSheet("R6/Objects.gif")

	// Sprite frames
	SpriteFrame(-4, -32, 8, 64, 230, 1)		// #0 - FlipDoor Closed
	SpriteFrame(-4, -32, 48, 48, 83, 34)	// #1 - FlipDoor Half-Open
	SpriteFrame(-4, -32, 64, 8, 132, 34)	// #2 - FlipDoor Open


	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Flip Door]
			Object[ArrayPos0].DrawOrder = 4
			Object[ArrayPos0].Direction = Object[ArrayPos0].PropertyValue
		end if
		ArrayPos0++
	loop
end sub


// ========================
// Editor Subs
// ========================

sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			CheckResult = Object.PropertyValue
			break
		case 0 // direction
			CheckResult = Object.PropertyValue
			CheckResult &= 1
			break
		end switch
	else
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			Object.PropertyValue = Editor.VariableValue
			break
		case 0 // direction
			Object.PropertyValue = Editor.VariableValue
			Object.PropertyValue &= 1
			break
		end switch
	end if
end sub


sub RSDKDraw
	Object.Direction = Object.PropertyValue
	DrawSpriteFX(0, FX_FLIP, Object.XPos, Object.YPos)
end sub


sub RSDKLoad
	LoadSpriteSheet("R6/Objects.gif")

	// Sprite frames
	SpriteFrame(-4, -32, 8, 64, 230, 1)		// #0 - FlipDoor Closed

	AddEditorVariable("direction")
	SetActiveVariable("direction")
	AddEnumVariable("right", 0)
	AddEnumVariable("left", 1)
end sub
