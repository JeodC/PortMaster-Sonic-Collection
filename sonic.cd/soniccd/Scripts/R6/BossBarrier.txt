//---------------Sonic CD Boss Barrier Script-----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0		:	Object.BarrierHeight

// Boss Camera Aliases
#alias Object[+1].Priority	:	BossCamera.Priority
#alias Object[+1].Value0	:	BossCamera.CameraHeight

// Eggman Mobile Aliases
#alias Object[+7].Priority	:	EggMobile.Priority
#alias Object[+7].State		:	EggMobile.State

// Floor Control Aliases
#alias Object[+8].Priority	:	FloorControl.Priority
#alias Object[+8].Value6	:	FloorControl.CurrentFloor

// States
#alias 0	:	BOSSBARRIER_IDLE
#alias 1	:	BOSSBARRIER_SETUP_BOSS
#alias 2	:	BOSSBARRIER_CLOSE_DOOR
#alias 3	:	BOSSBARRIER_CLOSED

// Eggman Mobile States
#alias 1	:	R6_EGG_START

// Fade Music Property Values
#alias 0	:	FADEMUSIC_TO_BOSS

// Priority
#alias 1	:	PRIORITY_ACTIVE


sub ObjectMain
	switch Object.State
	case BOSSBARRIER_IDLE
		TempValue0  = Player.XPos
		TempValue0 -= Object.XPos
		if TempValue0 > 0x600000
			Object.State = BOSSBARRIER_SETUP_BOSS
		end if
		break

	case BOSSBARRIER_SETUP_BOSS
		if Object.BarrierHeight < 0x400000
			Object.BarrierHeight += 0x20000
			Object.YPos += 0x20000
		end if

		TempValue0  = Player.XPos
		TempValue0 -= Object.XPos
		if TempValue0 > 0x800000
			Object.State = BOSSBARRIER_CLOSE_DOOR
			
			CreateTempObject(TypeName[Fade Music], FADEMUSIC_TO_BOSS, Object.XPos, Object.YPos)
			Stage.NewXBoundary1  = Object.iXPos
			Stage.NewXBoundary1 -= 16

			Stage.NewXBoundary2  = Stage.NewXBoundary1
			Stage.NewXBoundary2 += 448

			Object[+1].Type = TypeName[Boss Camera]
			BossCamera.Priority 	= PRIORITY_ACTIVE
			BossCamera.CameraHeight = -1

			EggMobile.State		= R6_EGG_START
			EggMobile.Priority  = PRIORITY_ACTIVE

			Object[+8].Type = TypeName[Floor Control]
			FloorControl.Priority	  = PRIORITY_ACTIVE
			FloorControl.CurrentFloor = -1
		end if
		break

	case BOSSBARRIER_CLOSE_DOOR
		if Object.BarrierHeight < 0x400000
			Object.BarrierHeight += 0x20000
			Object.YPos			 += 0x20000
		else
			Object.State = BOSSBARRIER_CLOSED
		end if
		break

	end switch
end sub


sub ObjectPlayerInteraction
	if Object.BarrierHeight < 0x400000
		PlayerObjectCollision(C_BOX, -16, -32, 16, 32)
	else
		PlayerObjectCollision(C_BOX, -16, -32, 16, 64)
	end if
end sub


sub ObjectDraw
	DrawSprite(0)
	DrawSprite(1)
	DrawSprite(2)
end sub


sub ObjectStartup
	LoadSpriteSheet("R6/Objects2.gif")
	
	SpriteFrame(-16, -32, 32, 32, 173, 223)	// #0 - Barrier Top
	SpriteFrame(-16, 0, 32, 32, 173, 223)	// #1 - Barrier Bottom
	SpriteFrame(16, -6, 16, 32, 239, 183)	// #2 - Boss Electrode
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
	DrawSprite(1)
	DrawSprite(2)
end sub


sub RSDKLoad
	LoadSpriteSheet("R6/Objects2.gif")
	
	SpriteFrame(-16, -32, 32, 32, 173, 223)	// #0 - Barrier Top
	SpriteFrame(-16, 0, 32, 32, 173, 223)	// #1 - Barrier Bottom
	SpriteFrame(16, -6, 16, 32, 239, 183)	// #2 - Boss Electrode

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
