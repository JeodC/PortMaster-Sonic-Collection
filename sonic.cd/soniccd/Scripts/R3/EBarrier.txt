//---------------Sonic CD Eggman Barrier Script---------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.Timer

// States
#alias 0	:	EBARRIER_IDLE
#alias 1	:	EBARRIER_HIT
#alias 2	:	EBARRIER_DESTROYED

// Global SFX
#alias 22	:	SFX_G_EXPLOSION


sub ObjectMain
	switch Object.State
	case EBARRIER_HIT
		if Object.YPos > 0x5100000
			Object.YPos -= 0x28000
		else
			Object.YPos = 0x5100000
			Object.State = EBARRIER_DESTROYED

#platform: Use_Haptics
			HapticEffect(76, 0, 0, 0)
#endplatform

		end if
		Object.Frame++
		Object.Frame %= 9
		break

	case EBARRIER_DESTROYED
		TempValue0  = Object.Timer
		TempValue0 &= 3
		if TempValue0 == 0

			Rand(TempValue0, 96)
			TempValue0  -= 48
			TempValue0 <<= 16
			TempValue0  += Object.XPos

			Rand(TempValue1, 40)
			TempValue1  -= 20
			TempValue1 <<= 16
			TempValue1  += Object.YPos

			CreateTempObject(TypeName[Explosion], 0, TempValue0, TempValue1)
			PlaySfx(SFX_G_EXPLOSION, false)
		end if
		
		Object.Timer++
		if Object.Timer == 120
			Object.Type = TypeName[Blank Object]
		end if
		break
	end switch
end sub


sub ObjectPlayerInteraction
	if Object.State == EBARRIER_IDLE
		PlayerObjectCollision(C_TOUCH, -48, -16, 48, 16)
		if CheckResult == true
			Object.State = EBARRIER_HIT
			Player.YVelocity = 0
			PlaySfx(SFX_G_EXPLOSION, false)
		end if
	end if
end sub


sub ObjectDraw
	DrawSprite(3)

	TempValue0  = Object.Frame
	TempValue0 /= 3
	DrawSprite(TempValue0)
end sub


sub ObjectStartup
	LoadSpriteSheet("R3/Objects2.gif")

	SpriteFrame(-64, -16, 32, 32, 134, 0)	// #0 - Gear Frame 0
	SpriteFrame(-64, -16, 32, 32, 167, 0)	// #1 - Gear Frame 1
	SpriteFrame(-64, -16, 32, 32, 200, 0)	// #2 - Gear Frame 2

	SpriteFrame(-48, -16, 96, 32, 1, 67)	// #3 - Eggman Barrier
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
	DrawSprite(1)
end sub


sub RSDKLoad
	LoadSpriteSheet("R3/Objects2.gif")

	SpriteFrame(-64, -16, 32, 32, 134, 0)	// #0 - Gear Frame 0
	SpriteFrame(-48, -16, 96, 32, 1, 67)	// #3 - Eggman Barrier

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
