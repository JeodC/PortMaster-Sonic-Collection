//------------------Sonic CD Eggman Script--------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0		:	Object.FrameTimer
#alias Object.Value1		:	Object.Timer

// Boss Body Aliases
#alias Object[+1].Value5	:	BossBody.DamageTimer
#alias Object[+1].State		:	BossBody.State

// States
#alias 0	:	EGGMAN_IDLE
#alias 1	:	EGGMAN_HURT
#alias 2	:	EGGMAN_NERVOUS
#alias 3	:	EGGMAN_FLEE

// BossBody Aliases
#alias 5	:	BOSSBODY_DRAG_EGGMAN_2


sub ObjectMain
	switch Object.State
	case EGGMAN_IDLE
		Object.FrameTimer++
		Object.FrameTimer %= 40
		break

	case EGGMAN_HURT
		if Object.Timer < 41
			Object.Timer++
		else
			Object.Timer = 0
			Object.FrameTimer = 0
			if BossBody.State < BOSSBODY_DRAG_EGGMAN_2
				Object.State = EGGMAN_IDLE
			else
				Object.State = EGGMAN_NERVOUS
			end if
		end if
		break

	case EGGMAN_NERVOUS
		Object.FrameTimer++
		Object.FrameTimer &= 31
		break

	case EGGMAN_FLEE
		Object.FrameTimer++
		Object.FrameTimer &= 7
		break

	end switch
end sub


sub ObjectDraw
	switch Object.State
	case EGGMAN_IDLE
		DrawSprite(4)

		TempValue0  = Object.FrameTimer
		TempValue0 /= 10
		DrawSprite(TempValue0)
		break

	case EGGMAN_HURT
		TempValue1   = Object.Timer
		TempValue1 <<= 8
		TempValue1  /= 40

		Sin(TempValue0, TempValue1)
		TempValue0 <<= 11
		FlipSign(TempValue0)
		TempValue0  += Object.YPos

		TempValue1   = Object.Timer
		TempValue1  &= 15
		TempValue1 >>= 3
		TempValue1  += 7
		DrawSpriteXY(TempValue1, Object.XPos, TempValue0)

		TempValue0  = BossBody.DamageTimer
		TempValue0 &= 1
		TempValue0 += 4
		DrawSprite(TempValue0)

		DrawSprite(6)
		break

	case EGGMAN_NERVOUS
		TempValue0   = Object.FrameTimer
		TempValue0 >>= 4
		TempValue0  += 9
		DrawSprite(TempValue0)
		break

	case EGGMAN_FLEE
		TempValue0   = Object.FrameTimer
		TempValue0 >>= 2
		TempValue0  += 11
		DrawSprite(TempValue0)

		Object.XPos += 0x40000
		
		if Object.OutOfBounds == true
			ResetObjectEntity(Object.EntityNo, TypeName[Blank Object], 0, 0, 0)
		end if
		break
	end switch
end sub


sub ObjectStartup
	LoadSpriteSheet("R3/Objects2.gif")

	// Eggman Idle
	SpriteFrame(-24, -56, 48, 24, 1, 166)	// #0  - Eggman Idle Frame 0
	SpriteFrame(-24, -56, 48, 24, 50, 166)	// #1  - Eggman Idle Frame 1
	SpriteFrame(-24, -56, 48, 24, 148, 174)	// #2  - Eggman Idle Frame 2
	SpriteFrame(-24, -56, 48, 24, 99, 166)	// #3  - Eggman Idle Frame 3

	// Egg Tilter Controller
	SpriteFrame(-24, -32, 48, 16, 148, 107)	// #4  - Egg Tilter Controller
	SpriteFrame(-24, -32, 48, 16, 148, 124)	// #5  - Egg Tilter Controller Flicker

	// Egg Mobile window
	SpriteFrame(-16, -48, 32, 16, 84, 50)	// #6  - Egg Mobile Window

	// Eggman Hurt
	SpriteFrame(-32, -67, 56, 64, 199, 134)	// #7  - Eggman Hurt Frame 0
	SpriteFrame(-23, -67, 56, 64, 199, 69)	// #8  - Eggman Hurt Frame 1

	// Eggman Nervous
	SpriteFrame(-24, -56, 48, 56, 197, 199)	// #9  - Eggman Nervous Frame 0
	SpriteFrame(-24, -56, 48, 56, 147, 199)	// #10 - Eggman Nervous Frame 1

	// Eggman Flee
	SpriteFrame(-16, -56, 72, 64, 1, 191)	// #11 - Eggman Flee Frame 0
	SpriteFrame(-16, -56, 72, 64, 74, 191)	// #12 - Eggman Flee Frame 1
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
	DrawSprite(1)
	DrawSprite(2)
end sub


sub RSDKLoad
	LoadSpriteSheet("R3/Objects2.gif")

	// Eggman Idle
	SpriteFrame(-24, -56, 48, 24, 1, 166)	// #0  - Eggman Idle Frame 0

	// Egg Tilter Controller
	SpriteFrame(-24, -32, 48, 16, 148, 107)	// #1  - Egg Tilter Controller
	// Egg Mobile window
	SpriteFrame(-16, -48, 32, 16, 84, 50)	// #2  - Egg Mobile Window

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
