//---------------Sonic CD R Plane Shifter Script--------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value1	:	Object.AnimFrameTimer
#alias Object.Value2	:	Object.OnObject

// States
#alias 0	:	RPLANESHIFTER_IDLE
// Facing Right
#alias 1	:	RPLANESHIFTER_PLANE_A_TO_B_R
#alias 2	:	RPLANESHIFTER_PLANE_B_TO_A_R
// Facing Left
#alias 3	:	RPLANESHIFTER_PLANE_A_TO_B_L
#alias 4	:	RPLANESHIFTER_PLANE_B_TO_A_L

// Property Values
#alias 0	:	GIMMICK_PARENT
#alias 1	:	GIMMICK_CHILD

// Collision Planes
#alias 0	:	COLLISION_PLANE_A
#alias 1	:	COLLISION_PLANE_B


sub ObjectMain
	if Player.ObjectInteraction == true
		if Object.PropertyValue == GIMMICK_PARENT
			switch Object.State
			case RPLANESHIFTER_PLANE_A_TO_B_R
				if Object.AnimFrameTimer < 256
					Object.AnimFrameTimer += 8
				else
					Object.State = RPLANESHIFTER_IDLE
					Player.State = Player_State_Ground
					Player.Frame = 0
					Player.DrawOrder = 2
					Player.CollisionPlane = COLLISION_PLANE_B
				end if

				TempValue0   = Object.AnimFrameTimer
				TempValue0 >>= 6
				switch TempValue0
				case 0
					Player.Frame = 1
					break
				case 1
					Player.Frame = 2
					break
				case 2
					Player.Frame = 3
					break
				case 3
					Player.Frame = 0
					Player.Direction = FACING_LEFT
					break
				end switch

				Object.AnimationTimer++
				Object.AnimationTimer %= 6
				break

			case RPLANESHIFTER_PLANE_B_TO_A_R
				if Object.AnimFrameTimer > 0
					Object.AnimFrameTimer -= 8
				else
					Object.State = RPLANESHIFTER_IDLE
					Player.State = Player_State_Ground
					Player.Frame = 0
					Player.DrawOrder = 4
					Player.CollisionPlane = COLLISION_PLANE_A
				end if

				TempValue0 = Object.AnimFrameTimer
				TempValue0 >>= 6
				switch TempValue0
				case 0
					Player.Frame = 0
					Player.Direction = FACING_LEFT
					break
				case 1
					Player.Frame = 4
					break
				case 2
					Player.Frame = 0
					break
				case 3
					Player.Frame = 1
					break
				end switch

				Object.AnimationTimer++
				Object.AnimationTimer %= 6
				break

			case RPLANESHIFTER_PLANE_A_TO_B_L
				if Object.AnimFrameTimer > 256
					Object.AnimFrameTimer -= 8
				else
					Object.State = RPLANESHIFTER_IDLE
					Player.State = Player_State_Ground
					Player.Frame = 0
					Player.DrawOrder = 2
					Player.CollisionPlane = COLLISION_PLANE_B
				end if

				TempValue0   = 512
				TempValue0  -= Object.AnimFrameTimer
				TempValue0 >>= 6
				switch TempValue0
				case 0
					Player.Frame = 1
					break
				case 1
					Player.Frame = 2
					break
				case 2
					Player.Frame = 3
					break
				case 3
					Player.Frame = 0
					Player.Direction = FACING_RIGHT
					break
				end switch

				Object.AnimationTimer++
				Object.AnimationTimer %= 6
				break

			case RPLANESHIFTER_PLANE_B_TO_A_L
				if Object.AnimFrameTimer < 512
					Object.AnimFrameTimer += 8
				else
					Object.AnimFrameTimer = 0
					Object.State = RPLANESHIFTER_IDLE
					Player.State = Player_State_Ground
					Player.Frame = 0
					Player.DrawOrder = 4
					Player.CollisionPlane = COLLISION_PLANE_A
				end if

				TempValue0   = 512
				TempValue0  -= Object.AnimFrameTimer
				TempValue0 >>= 6
				switch TempValue0
				case 0
					Player.Frame = 0
					Player.Direction = FACING_RIGHT
					break
				case 1
					Player.Frame = 4
					break
				case 2
					Player.Frame = 0
					break
				case 3
					Player.Frame = 1
					break
				end switch

				Object.AnimationTimer++
				Object.AnimationTimer %= 6
				break
			end switch

			// First Bar
			Sin(Object[+1].XPos, Object.AnimFrameTimer)
			Object[+1].XPos *= 0xC00
			Object[+1].XPos += Object.XPos
			Object[+1].YPos  = Object.YPos
			if Object.AnimFrameTimer > 128
				if Object.AnimFrameTimer < 384
					Object[+1].DrawOrder = 1
				else
					Object[+1].DrawOrder = 4
				end if
			else
				Object[+1].DrawOrder = 4
			end if

			// Second Bar
			TempValue0  = Object.AnimFrameTimer
			TempValue0 += 170
			TempValue0 &= 511
			Sin(Object[+2].XPos, TempValue0)
			Object[+2].XPos *= 0xC00
			Object[+2].XPos += Object.XPos
			Object[+2].YPos  = Object.YPos
			if TempValue0 > 128
				if TempValue0 < 384
					Object[+2].DrawOrder = 1
				else
					Object[+2].DrawOrder = 4
				end if
			else
				Object[+2].DrawOrder = 4
			end if

			// Third Bar
			TempValue0  = Object.AnimFrameTimer
			TempValue0 += 341
			TempValue0 &= 511
			Sin(Object[+3].XPos, TempValue0)
			Object[+3].XPos *= 0xC00
			Object[+3].XPos += Object.XPos
			Object[+3].YPos  = Object.YPos
			if TempValue0 > 128
				if TempValue0 < 384
					Object[+3].DrawOrder = 1
				else
					Object[+3].DrawOrder = 4
				end if
			else
				Object[+3].DrawOrder = 4
			end if
		end if
	end if
end sub


sub ObjectPlayerInteraction
	if Object.PropertyValue == GIMMICK_PARENT
		PlayerObjectCollision(C_PLATFORM, -40, -8, 40, 24)
		TempValue1 = CheckResult
		PlayerObjectCollision(C_BOX, -40, -184, 40, -168)
		PlayerObjectCollision(C_TOUCH, -4, -48, 4, 8)
		if CheckResult == true
			if Object.OnObject == false
				if TempValue1 == true
					Object.OnObject = true
					Player.State = Player_State_Static
					Player.Animation = ANI_STOPPED
					Player.XPos = Object.XPos
					Player.Speed = 0
					Player.XVelocity = 0
					Player.YVelocity = 0
					Player.TrackScroll = false
					if Player.Direction == FACING_RIGHT
						if Player.CollisionPlane == COLLISION_PLANE_A
							Object.State = RPLANESHIFTER_PLANE_A_TO_B_R
							Object.AnimFrameTimer = 0
						else
							Object.State = RPLANESHIFTER_PLANE_B_TO_A_R
							Object.AnimFrameTimer = 256
						end if
					else
						if Player.CollisionPlane == COLLISION_PLANE_A
							Object.State = RPLANESHIFTER_PLANE_A_TO_B_L
							Object.AnimFrameTimer = 512
						else
							Object.State = RPLANESHIFTER_PLANE_B_TO_A_L
							Object.AnimFrameTimer = 256
						end if
					end if
				end if
			end if
		else
			Object.OnObject = false
		end if
	end if
end sub


sub ObjectDraw
	if Object.PropertyValue == GIMMICK_PARENT
		TempValue0  = Object.AnimationTimer
		TempValue0 /= 3
		DrawSprite(TempValue0)
		TempValue0 += 2
		DrawSprite(TempValue0)
	else
		DrawSprite(4)
		DrawSprite(5)
		DrawSprite(6)
		DrawSprite(7)
		DrawSprite(8)
	end if
end sub


sub ObjectStartup
	LoadSpriteSheet("R8/Objects2.gif")

	SpriteFrame(-40, -8, 80, 16, 1, 200)	// #0 - R Plane Shifter Base frame 0
	SpriteFrame(-40, -8, 80, 16, 1, 217)	// #1 - R Plane Shifter Base frame 1

	SpriteFrame(-40, -184, 80, 16, 1, 200)	// #2 - R Plane Shifter Roof frame 0
	SpriteFrame(-40, -184, 80, 16, 1, 217)	// #3 - R Plane Shifter Roof frame 1

	SpriteFrame(-4, -40, 8, 32, 100, 35)	// #4 - R Plane Shifter Bars 0
	SpriteFrame(-4, -72, 8, 32, 100, 35)	// #5 - R Plane Shifter Bars 1
	SpriteFrame(-4, -104, 8, 32, 100, 35)	// #6 - R Plane Shifter Bars 2
	SpriteFrame(-4, -136, 8, 32, 100, 35)	// #7 - R Plane Shifter Bars 3
	SpriteFrame(-4, -168, 8, 32, 100, 35)	// #8 - R Plane Shifter Bars 4
end sub


// ========================
// Editor Subs
// ========================

sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			CheckResult = Object.PropertyValue
			CheckResult &= 1
			break
		case 0 // type
			CheckResult = Object.PropertyValue
			CheckResult &= 1
			break
		end switch
	else
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			Object.PropertyValue = Editor.VariableValue
			Object.PropertyValue &= 1
			break
		case 0 // type
			Object.PropertyValue = Editor.VariableValue
			Object.PropertyValue &= 1
			break
		end switch
	end if
end sub

sub RSDKDraw
	if Object.PropertyValue == GIMMICK_PARENT
		TempValue0  = Object.AnimationTimer
		TempValue0 /= 3
		DrawSprite(TempValue0)
		TempValue0 += 2
		DrawSprite(TempValue0)
	else
		ArrayPos0  = Object.EntityNo
		TempValue0 = 0
		TempValue1 = 0
		while TempValue1 == false
			if TempValue0 == 3
				TempValue1 = true
			end if
			ArrayPos0--
			TempValue0++
			CheckEqual(Object[ArrayPos0].Type, TypeName[R Plane Shifter])
			TempValue1  = CheckResult
			CheckNotEqual(Object[ArrayPos0].PropertyValue, GIMMICK_CHILD)
			TempValue1 &= CheckResult
		loop
		ArrayPos1  = Object.EntityNo
		ArrayPos1 -= Object[ArrayPos0].EntityNo
		switch ArrayPos1
		case 0
			TempValue0  = 0
			break
		case 1
			TempValue0  = 0
			TempValue0 += 170
			TempValue0 &= 511
			break
		case 2
			TempValue0  = 0
			TempValue0 += 341
			TempValue0 &= 511
			break
		end switch
		Sin(TempValue2, TempValue0)
		TempValue2 *= 0xC00
		TempValue2 += Object[ArrayPos0].XPos
		DrawSpriteXY(4, TempValue2, Object.YPos)
		DrawSpriteXY(5, TempValue2, Object.YPos)
		DrawSpriteXY(6, TempValue2, Object.YPos)
		DrawSpriteXY(7, TempValue2, Object.YPos)
		DrawSpriteXY(8, TempValue2, Object.YPos)
	end if
end sub


sub RSDKLoad
	LoadSpriteSheet("R8/Objects2.gif")

	SpriteFrame(-40, -8, 80, 16, 1, 200)	// #0 - R Plane Shifter Base frame 0
	SpriteFrame(-40, -8, 80, 16, 1, 217)	// #1 - R Plane Shifter Base frame 1

	SpriteFrame(-40, -184, 80, 16, 1, 200)	// #2 - R Plane Shifter Roof frame 0
	SpriteFrame(-40, -184, 80, 16, 1, 217)	// #3 - R Plane Shifter Roof frame 1

	SpriteFrame(-4, -40, 8, 32, 100, 35)	// #4 - R Plane Shifter Bars 0
	SpriteFrame(-4, -72, 8, 32, 100, 35)	// #5 - R Plane Shifter Bars 1
	SpriteFrame(-4, -104, 8, 32, 100, 35)	// #6 - R Plane Shifter Bars 2
	SpriteFrame(-4, -136, 8, 32, 100, 35)	// #7 - R Plane Shifter Bars 3
	SpriteFrame(-4, -168, 8, 32, 100, 35)	// #8 - R Plane Shifter Bars 4

	AddEditorVariable("type")
	SetActiveVariable("type")
	AddEnumVariable("Parent Object", GIMMICK_PARENT)
	AddEnumVariable("Child Object", GIMMICK_CHILD)
end sub
