//-----------------Sonic CD Spikes Script---------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Player.Value4	:	Player.InvincibleTimer

// Property Values
#alias 0	:	SPIKE_DIRECTION_UP
#alias 1	:	SPIKE_DIRECTION_RIGHT
#alias 2	:	SPIKE_DIRECTION_LEFT
#alias 3	:	SPIKE_DIRECTION_DOWN


sub ObjectPlayerInteraction
	switch Object.PropertyValue
	case SPIKE_DIRECTION_UP
		PlayerObjectCollision(C_BOX, -16, -12, 16, 16)
#platform: Use_Origins
		if Player.Animation == ANI_LEDGEPULLUP
			PlayerObjectCollision(C_BOX, -14, -12, 14, 16)
		end if
#endplatform
		if Player.YVelocity > -1
			if Player.InvincibleTimer == 0
				PlayerObjectCollision(C_TOUCH, -15, -13, 15, -12)
				if CheckResult == true
					Player.State = Player_State_GotHit
					if Player.XPos > Object.XPos
						Player.Speed =  0x20000
					else
						Player.Speed = -0x20000
					end if
				end if
			end if
		end if
		break

	case SPIKE_DIRECTION_RIGHT
		PlayerObjectCollision(C_BOX, -16, -16, 15, 16)
		if Player.XVelocity <= 0
			if Player.InvincibleTimer == 0
				PlayerObjectCollision(C_TOUCH, 12, -15, 16, 15)
				if CheckResult == true
					Player.State = Player_State_GotHit
					Player.Speed = 0x20000
				end if
			end if
		end if
		break

	case SPIKE_DIRECTION_LEFT
		PlayerObjectCollision(C_BOX, -15, -16, 16, 16)
		if Player.InvincibleTimer == 0
			PlayerObjectCollision(C_TOUCH, -16, -15, -12, 15)
			if CheckResult == true
				Player.State = Player_State_GotHit
				Player.Speed = -0x20000
			end if
		end if
		break

	case SPIKE_DIRECTION_DOWN
		TempValue0 = Player.YVelocity
		PlayerObjectCollision(C_BOX, -16, -16, 16, 15)
		if TempValue0 < 1
			if Player.InvincibleTimer == 0
				PlayerObjectCollision(C_TOUCH, -15, 12, 15, 16)
				if CheckResult == true
					Player.State = Player_State_GotHit
					if Player.XPos > Object.XPos
						Player.Speed =  0x20000
					else
						Player.Speed = -0x20000
					end if
				end if
			end if
		end if
		break

	end switch
end sub


sub ObjectDraw
	DrawSprite(Object.PropertyValue)
end sub


sub ObjectStartup
	LoadSpriteSheet("Global/Items3.gif")

	SpriteFrame(-16, -16, 32, 32, 50, 1)	// #0 - Spikes facing up
	SpriteFrame(-16, -16, 32, 32, 50, 34)	// #1 - Spikes facing right
	SpriteFrame(-16, -16, 32, 32, 50, 67)	// #2 - Spikes facing left
	SpriteFrame(-16, -16, 32, 32, 50, 100)	// #3 - Spikes facing down
end sub


// ========================
// Editor Subs
// ========================

sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			CheckResult = Object.PropertyValue
			break
		case 0 // Orientation
			CheckResult = Object.PropertyValue
			break
		end switch
	else
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			Object.PropertyValue = Editor.VariableValue
			break
		case 0 // Orientation
			Object.PropertyValue = Editor.VariableValue
			break
		end switch
	end if
end sub


sub RSDKDraw
	DrawSprite(Object.PropertyValue)
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Items3.gif")
	SpriteFrame(-16, -16, 32, 32, 50, 1)	// #0 - Spikes facing up
	SpriteFrame(-16, -16, 32, 32, 50, 34)	// #1 - Spikes facing right
	SpriteFrame(-16, -16, 32, 32, 50, 67)	// #2 - Spikes facing left
	SpriteFrame(-16, -16, 32, 32, 50, 100)	// #3 - Spikes facing down

	AddEditorVariable("Orientation")
	SetActiveVariable("Orientation")
	AddEnumVariable("Up", 0)
	AddEnumVariable("Right", 1)
	AddEnumVariable("Left", 2)
	AddEnumVariable("Down", 3)
end sub
