//--------------Sonic CD Static Platform Script---------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// HUD Alias
#alias Object[24].PropertyValue	:	HUD.CurrentTimePeriod

// Time Period Aliases
#alias 0	:	TIME_PRESENT
#alias 1	:	TIME_PAST
#alias 2	:	TIME_GOOD_FUTURE
#alias 3	:	TIME_BAD_FUTURE

// Property Values
#alias 0	:	PLATFORM_SMALL
#alias 1	:	PLATFORM_MEDIUM
#alias 2	:	PLATFORM_LARGE
#alias 3	:	PLATFORM_SMALL_CONVEYOR
#alias 4	:	PLATFORM_MEDIUM_CONVEYOR
#alias 5	:	PLATFORM_LARGE_CONVEYOR


sub ObjectPlayerInteraction

	switch Object.PropertyValue
	case PLATFORM_SMALL
		PlayerObjectCollision(C_PLATFORM, -16, -16, 16, 12)
		break
		
	case PLATFORM_MEDIUM
		PlayerObjectCollision(C_PLATFORM, -32, -16, 32, 12)
		break
		
	case PLATFORM_LARGE
		PlayerObjectCollision(C_PLATFORM, -48, -16, 48, 12)
		break
		
	case PLATFORM_SMALL_CONVEYOR
		// (with the Conveyor code removed, functionally identical to the normal Small Platform)
		PlayerObjectCollision(C_PLATFORM, -16, -16, 16, 12)
		break
		
	case PLATFORM_MEDIUM_CONVEYOR
		// (Also with no Conveyor code associated with it... pretty much the same as the normal Medium Platform type)
		PlayerObjectCollision(C_PLATFORM, -32, -16, 32, 12)
		break
		
	case PLATFORM_LARGE_CONVEYOR
		// (You may notice some corresponding code is missing, that's because the took the Conveyor out of the name!)
		PlayerObjectCollision(C_PLATFORM, -48, -16, 48, 12)
		break
		
	end switch

end sub


sub ObjectDraw

	switch Object.PropertyValue
	case PLATFORM_SMALL
	case PLATFORM_MEDIUM
	case PLATFORM_LARGE
		// Standard Platform
		DrawSprite(Object.PropertyValue)
		break
		
	// These next 3 types are for Conveyor Belt types, where the Conveyor Belt Frame is overlayed atop the normal Platform Sprite

	case PLATFORM_SMALL_CONVEYOR
		DrawSprite(0)
		DrawSprite(3)
		break
		
	case PLATFORM_MEDIUM_CONVEYOR
		DrawSprite(1)
		DrawSprite(4)
		break
		
	case PLATFORM_LARGE_CONVEYOR
		DrawSprite(2)
		DrawSprite(5)
		break
		
	end switch
	
end sub


sub ObjectStartup

	// So an initial sheet is loaded here...
	// but then more sheets are loaded down below? Huh?
	LoadSpriteSheet("R5/Objects.gif")
	
	// But anyway, this Object's Sprite Frames are the same as Dip Platform's, including Conveyor Frames, so the order is:
	
	// 0 - Small Platform Frame
	// 1 - Normal Sized Platform Frame
	// 2 - Large Platform Frame
	
	// 3 - Small Conveyor Belt Frame
	// 4 - Medium Sized Conveyor Belt Frame
	// 5 - Large Conveyor Belt Frame
	
	// The essentially unused Conveyor Belt Frames are to be layered on top of the Platform Frames, though they aren't really used from this Object
	
	switch HUD.CurrentTimePeriod
	case TIME_PRESENT
		LoadSpriteSheet("R5/Objects.gif")
		SpriteFrame(-16, -16, 32, 32, 1, 51)
		SpriteFrame(-32, -16, 64, 32, 34, 51)
		SpriteFrame(-48, -16, 96, 32, 1, 84)
		SpriteFrame(-16, -16, 32, 16, 65, 208)
		SpriteFrame(-32, -16, 64, 16, 1, 208)
		SpriteFrame(-48, -16, 96, 16, 1, 191)
		break
		
	case TIME_PAST
		LoadSpriteSheet("R5/Objects3.gif")
		SpriteFrame(-16, -16, 32, 32, 1, 170)
		SpriteFrame(-32, -16, 64, 32, 34, 170)
		SpriteFrame(-48, -16, 96, 32, 1, 203)
		SpriteFrame(-16, -16, 32, 16, 223, 148)
		SpriteFrame(-32, -16, 64, 16, 159, 148)
		SpriteFrame(-48, -16, 96, 16, 159, 131)
		break
		
	case TIME_GOOD_FUTURE
		LoadSpriteSheet("R5/Objects3.gif")
		SpriteFrame(-16, -16, 32, 32, 1, 170)
		SpriteFrame(-32, -16, 64, 32, 34, 170)
		SpriteFrame(-48, -16, 96, 32, 1, 203)
		SpriteFrame(-16, -16, 32, 16, 223, 182)
		SpriteFrame(-32, -16, 64, 16, 159, 182)
		SpriteFrame(-48, -16, 96, 16, 159, 165)
		break
		
	case TIME_BAD_FUTURE
		LoadSpriteSheet("R5/Objects3.gif")
		SpriteFrame(-16, -16, 32, 32, 1, 170)
		SpriteFrame(-32, -16, 64, 32, 34, 170)
		SpriteFrame(-48, -16, 96, 32, 1, 203)
		SpriteFrame(-16, -16, 32, 16, 223, 216)
		SpriteFrame(-32, -16, 64, 16, 159, 216)
		SpriteFrame(-48, -16, 96, 16, 159, 199)
		break
		
	end switch
	
end sub


// ========================
// Editor Subs
// ========================

sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			CheckResult = Object.PropertyValue
			break
		case 0 // Type
			CheckResult = Object.PropertyValue
			break
		end switch
	else
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			Object.PropertyValue = Editor.VariableValue
			break
		case 0 // Type
			Object.PropertyValue = Editor.VariableValue
			break
		end switch
	end if
end sub


sub RSDKDraw
	if Object.PropertyValue >= 3
		TempValue0 = -3
		TempValue0 += Object.PropertyValue
		DrawSprite(TempValue0)
	end if
	
	DrawSprite(Object.PropertyValue)
end sub


sub RSDKLoad
	LoadSpriteSheet("R5/Objects.gif")
	
	// TODO: support for other time periods, this is just present
	// certainly there's some better way to do it rather than a bunch of unique and boring checkCurrentStageFolder checks?
	SpriteFrame(-16, -16, 32, 32, 1, 51)
	SpriteFrame(-32, -16, 64, 32, 34, 51)
	SpriteFrame(-48, -16, 96, 32, 1, 84)
	SpriteFrame(-16, -16, 32, 16, 65, 208)
	SpriteFrame(-32, -16, 64, 16, 1, 208)
	SpriteFrame(-48, -16, 96, 16, 1, 191)

	AddEditorVariable("Type")
	SetActiveVariable("Type")
	AddEnumVariable("Small", 0)
	AddEnumVariable("Medium", 1)
	AddEnumVariable("Large", 2)
	AddEnumVariable("Small Conveyor", 3)
	AddEnumVariable("Medium Conveyor", 4)
	AddEnumVariable("Large Conveyor", 5)
end sub
