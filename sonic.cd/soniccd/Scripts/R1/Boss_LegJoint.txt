//---------------Sonic CD Boss Leg Joint Script---------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.BodyEntity
#alias Object.Value1	:	Object.LegsEntity
#alias Object.Value1	:	Object.LegREntity	// Alt. for Right leg joint
#alias Object.Value2	:	Object.XOffSet
#alias Object.Value4	:	Object.LegSide
#alias Object.Value5	:	Object.YOffSet
#alias Object.Value6	:	Object.XVelocity
#alias Object.Value7	:	Object.YVelocity

// Boss Body aliases
#alias Object.Value0	:	Body.FaceEntity
#alias Object.State		:	Body.State
#alias Object.XPos		:	Body.XPos
#alias Object.YPos		:	Body.YPos
#alias Object.Value1	:	Body.LegJointREntity
#alias Object.Value2	:	Body.LegJointLEntity

// Boss Face aliases
#alias Object.XPos		:	Face.XPos
#alias Object.YPos		:	Face.YPos

// Boss Foot aliases
#alias Object.XPos		:	Foot.XPos
#alias Object.YPos		:	Foot.YPos
#alias Object.State		:	Foot.State
#alias Object.Value2	:	Foot.XOffSet
#alias Object.Value4	:	Foot.LegSide

// Boss Legs aliases
#alias Object.Value1	:	Legs.FootEntity
#alias Object.State		:	Legs.State

// Boss Foot (Right) aliases
#alias Object.XPos		:	FootR.XPos
#alias Object.YPos		:	FootR.YPos
#alias Object.State		:	FootR.State
#alias Object.Value2	:	FootR.XOffSet
#alias Object.Value4	:	FootR.LegSide

// Boss Legs (Right) aliases
#alias Object.Value1	:	LegR.FootREntity
#alias Object.State		:	LegR.State

// Boss Leg Joint (Right) Aliases
#alias Object.Priority	:	LegJointR.Priority
#alias Object.Value0	:	LegJointR.BodyEntity
#alias Object.Value1	:	LegJointR.LegREntity
#alias Object.Value2	:	LegJointR.XOffSet
#alias Object.Value4	:	LegJointR.LegSide
#alias Object.XPos		:	LegJointR.XPos
#alias Object.YPos		:	LegJointR.YPos

// Boss Leg Joint (Left) Aliases
#alias Object.Priority	:	LegJointL.Priority
#alias Object.DrawOrder	:	LegJointL.DrawOrder
#alias Object.Value0	:	LegJointL.BodyEntity
#alias Object.Value1	:	LegJointL.LegLEntity

// States
#alias 0	:	BOSSLEGJOINT_SETUP
#alias 1	:	BOSSLEGJOINT_STEP_START
#alias 2	:	BOSSLEGJOINT_STEP
#alias 3	:	BOSSLEGJOINT_STEP_END
#alias 4	:	BOSSLEGJOINT_WAIT_LEG
#alias 5	:	BOSSLEGJOINT_LANDING
#alias 6	:	BOSSLEGJOINT_DESCEND
#alias 7	:	BOSSLEGJOINT_STAND
#alias 8	:	BOSSLEGJOINT_MIMIC_RIGHT
#alias 9	:	BOSSLEGJOINT_STEP_START_BW
#alias 10	:	BOSSLEGJOINT_STEP_BW
#alias 11	:	BOSSLEGJOINT_WAIT_LEG_BW
#alias 12	:	BOSSLEGJOINT_DESTROYED

// Boss Foot States
#alias 1	:	BOSSFOOT_STEP
#alias 2	:	BOSSFOOT_HANDLE_STEP
#alias 3	:	BOSSFOOT_STEP_OPPOSITE
#alias 4	:	BOSSFOOT_HANDLE_STEP_OPPOSITE

// Priority
#alias 0	:	PRIORITY_BOUNDS

// Function declarations
#function BossLegJoint_JointRotation
#function BossLegJoint_HandleMovement
#function BossLegJoint_HandleLanding


function BossLegJoint_JointRotation
	ArrayPos0 = Object.BodyEntity
	Object.XPos  = Body[ArrayPos0].XPos
	Object.XPos += 0xC0000
	Object.XPos += TempValue7
	if Object.DrawOrder == 2
		Object.XPos -= 0xA0000
	end if
	Object.YPos  = Body[ArrayPos0].YPos
	Object.YPos += 0x140000
	Object.YPos += TempValue6
end function


function BossLegJoint_HandleMovement
	Sin256(TempValue6, Object.XOffSet)
	TempValue6 <<= 12
	Cos256(TempValue7, Object.XOffSet)
	TempValue7 <<= 12
	GetBit(TempValue0, Object.LegSide, 4)
	if TempValue0 == false
		SetBit(Object.LegSide, 1, 0)
		CallFunction(BossLegJoint_JointRotation)
	else
		GetBit(TempValue0, Object.LegSide, 5)
		if TempValue0 == false
			CallFunction(BossLegJoint_JointRotation)
		else
			GetBit(TempValue0, Object.LegSide, 1)
			if TempValue0 == false
				SetBit(Object.LegSide, 1, 1)
				Object.XVelocity = TempValue6
				Object.YVelocity = TempValue7
			else
				ArrayPos0 = Object.BodyEntity
				ArrayPos1 = Body[ArrayPos0].FaceEntity

				TempValue0 = TempValue7
				TempValue0 -= Object.YVelocity
				Object.YVelocity = TempValue7

				Body[ArrayPos0].XPos -= TempValue0
				Face[ArrayPos1].XPos -= TempValue0

				TempValue0 = TempValue6
				TempValue0 -= Object.XVelocity
				Object.XVelocity = TempValue6

				Body[ArrayPos0].YPos -= TempValue0
				Face[ArrayPos1].YPos -= TempValue0
			end if
		end if
	end if
end function


function BossLegJoint_HandleLanding
	ArrayPos0 = Object.LegsEntity
	ArrayPos1 = Legs[ArrayPos0].FootEntity
	if Foot[ArrayPos1].State == BOSSFOOT_HANDLE_STEP
		if Object.XOffSet >= 88
			GetBit(TempValue0, Foot[ArrayPos1].LegSide, 0)

			if TempValue0 == true
				SetBit(Foot[ArrayPos1].LegSide, 0, 0)
				SetBit(Object.LegSide, 0, 1)
			end if

			CallFunction(BossLegJoint_HandleMovement)
		end if

	else
		Foot[ArrayPos1].State = BOSSFOOT_STEP
		if Object.XOffSet < 88
			Object.XOffSet += Object.YOffSet
		end if
		CallFunction(BossLegJoint_HandleMovement)

	end if
end function


sub ObjectMain
	switch Object.State
	case BOSSLEGJOINT_SETUP
		Object.XOffSet = 88
		Object.YOffSet = 2
		Object.State   = BOSSLEGJOINT_DESCEND
		break

	case BOSSLEGJOINT_STEP_START
		ArrayPos0 = Object.LegsEntity
		ArrayPos1 = Legs[ArrayPos0].FootEntity
		if Foot[ArrayPos1].State != BOSSFOOT_STEP_OPPOSITE
			if Foot[ArrayPos1].State != BOSSFOOT_HANDLE_STEP_OPPOSITE
				SetBit(Foot[ArrayPos1].LegSide, 0, 0)
				Foot[ArrayPos1].State = BOSSFOOT_STEP_OPPOSITE
			end if
		end if

		TempValue0 = false
		if Object.XOffSet > 0
			Object.XOffSet -= Object.YOffSet
			if Object.XOffSet > 0
				CallFunction(BossLegJoint_HandleMovement)
				TempValue0 = true
			end if
		end if
		
		if TempValue0 == false
			Object.XOffSet = 0
			if Foot[ArrayPos1].State != BOSSFOOT_HANDLE_STEP_OPPOSITE
				GetBit(TempValue0, Foot[ArrayPos1].LegSide, 0)
				if TempValue0 == false
					CallFunction(BossLegJoint_HandleMovement)
				else
					SetBit(Foot[ArrayPos1].LegSide, 0, 0)
					Object.State = BOSSLEGJOINT_STEP
					CallFunction(BossLegJoint_HandleMovement)
				end if
			else
				Object.State = BOSSLEGJOINT_STEP
				CallFunction(BossLegJoint_HandleMovement)
			end if
		end if
		break

	case BOSSLEGJOINT_STEP
		ArrayPos0 = Object.LegsEntity
		ArrayPos1 = Legs[ArrayPos0].FootEntity
		Object.XOffSet += Object.YOffSet
		if Object.XOffSet >= 88
			Object.XOffSet = 88
			Object.State   = BOSSLEGJOINT_STEP_END
			Foot[ArrayPos1].State = BOSSFOOT_STEP
		end if
		CallFunction(BossLegJoint_HandleMovement)
		break

	case BOSSLEGJOINT_STEP_END
		ArrayPos0 = Object.LegsEntity
		ArrayPos1 = Legs[ArrayPos0].FootEntity
		GetBit(TempValue0, Foot[ArrayPos1].LegSide, 4)
		if TempValue0 == 1
			SetBit(Object.LegSide, 0, 1)
		end if
		CallFunction(BossLegJoint_HandleMovement)
		break

	case BOSSLEGJOINT_WAIT_LEG	// wait for the other leg to move
		GetBit(TempValue0, Object.LegSide, 0)
		if TempValue0 == 0
			Object.XOffSet -= Object.YOffSet
			if Object.XOffSet < 24
				Object.XOffSet = 24
				SetBit(Object.LegSide, 0, 1)
			end if
		end if
		CallFunction(BossLegJoint_HandleMovement)
		break
		
	case BOSSLEGJOINT_LANDING	// Only the right onse uses this, the left mimics
		ArrayPos0 = Object.LegREntity
		ArrayPos1 = LegR[ArrayPos0].FootREntity
		if FootR[ArrayPos1].State == BOSSFOOT_HANDLE_STEP_OPPOSITE
			if Object.XOffSet == 0
				GetBit(TempValue0, FootR[ArrayPos1].LegSide, 0)
				if TempValue0 == true
					SetBit(FootR[ArrayPos1].LegSide, 0, 0)
					SetBit(Object.LegSide, 0, 1)
				end if

				CallFunction(BossLegJoint_HandleMovement)
			else
				if Object.XOffSet != 0
					Object.XOffSet -= Object.YOffSet
				end if

				CallFunction(BossLegJoint_HandleMovement)
			end if
		else
			FootR[ArrayPos1].State = BOSSFOOT_STEP_OPPOSITE

			if Object.XOffSet != 0
				Object.XOffSet -= Object.YOffSet
			end if

			CallFunction(BossLegJoint_HandleMovement)
		end if
		break

	case BOSSLEGJOINT_DESCEND	// Only the right onse uses this, the left mimics
		CallFunction(BossLegJoint_HandleLanding)
		break

	case BOSSLEGJOINT_STAND
		if Object.XOffSet < 24
			Object.XOffSet += Object.YOffSet

			ArrayPos0 = Object.LegsEntity
			ArrayPos1 = Legs[ArrayPos0].FootEntity
			SetBit(Foot[ArrayPos1].LegSide, 7, 1)

			ArrayPos0 = Object.BodyEntity
			ArrayPos1 = Body[ArrayPos0].LegJointLEntity

			ArrayPos0 = LegJointL[ArrayPos1].LegLEntity
			ArrayPos1 = Body[ArrayPos0].LegJointREntity
			SetBit(LegJointR[ArrayPos1].LegSide, 7, 1)

			CallFunction(BossLegJoint_HandleLanding)
		else
			if Object.XOffSet > 24

				Object.XOffSet -= Object.YOffSet
				ArrayPos0 = Object.LegsEntity
				ArrayPos1 = Legs[ArrayPos0].FootEntity
				if Foot[ArrayPos1].State == BOSSFOOT_HANDLE_STEP_OPPOSITE

					if Object.XOffSet == 0

						GetBit(TempValue0, Foot[ArrayPos1].LegSide, 0)
						if TempValue0 == true
							SetBit(Foot[ArrayPos1].LegSide, 0, 0)
							SetBit(Object.LegSide, 0, 1)
						end if

						CallFunction(BossLegJoint_HandleMovement)
					else
						if Object.XOffSet != 0
							Object.XOffSet -= Object.YOffSet
						end if

						CallFunction(BossLegJoint_HandleMovement)
					end if

				else
					Foot[ArrayPos1].State = BOSSFOOT_STEP_OPPOSITE
					if Object.XOffSet != 0
						Object.XOffSet -= Object.YOffSet
					end if

					CallFunction(BossLegJoint_HandleMovement)
				end if

			else
				ArrayPos0 = Object.LegsEntity
				ArrayPos1 = Legs[ArrayPos0].FootEntity
				GetBit(TempValue0, Foot[ArrayPos1].LegSide, 0)
				if TempValue0 == true
					SetBit(Object.LegSide, 0, 1)
				end if

				CallFunction(BossLegJoint_HandleMovement)
			end if

		end if
		break

	case BOSSLEGJOINT_MIMIC_RIGHT	//	only the left leg uses this
		ArrayPos0 = Object.BodyEntity
		ArrayPos1 = Body[ArrayPos0].LegJointREntity
		Object.XPos  = LegJointR[ArrayPos1].XPos
		Object.XPos -= 0xA0000
		Object.YPos  = LegJointR[ArrayPos1].YPos
		Object.XOffSet = LegJointR[ArrayPos1].XOffSet
		break

	case BOSSLEGJOINT_STEP_START_BW
		ArrayPos0  = Object.LegsEntity
		ArrayPos1  = Legs[ArrayPos0].FootEntity
		TempValue1 = false
		if Foot[ArrayPos1].State != BOSSFOOT_STEP_OPPOSITE

			if Foot[ArrayPos1].State != BOSSFOOT_HANDLE_STEP_OPPOSITE
				SetBit(Foot[ArrayPos1].LegSide, 0, 0)
				Foot[ArrayPos1].State = BOSSFOOT_STEP_OPPOSITE
			else
				if Object.XOffSet != 32

					Object.XOffSet -= Object.YOffSet
					if Object.XOffSet > 0
						TempValue1 = true
					end if

				end if

				if TempValue1 == false
					Object.XOffSet = 32
					SetBit(Foot[ArrayPos1].LegSide, 0, 0)
					Object.State = BOSSLEGJOINT_STEP_BW
				end if
				
			end if

		end if

		CallFunction(BossLegJoint_HandleMovement)
		break

	case BOSSLEGJOINT_STEP_BW
		ArrayPos0 = Object.LegsEntity
		ArrayPos1 = Legs[ArrayPos0].FootEntity

		Object.XOffSet += Object.YOffSet
		if Object.XOffSet >= 48
			Object.XOffSet = 48
			Object.State = BOSSLEGJOINT_STEP_END
			Foot[ArrayPos1].State = BOSSFOOT_STEP
		end if

		CallFunction(BossLegJoint_HandleMovement)
		break
		
	case BOSSLEGJOINT_WAIT_LEG_BW
		GetBit(TempValue0, Object.LegSide, 0)
		if TempValue0 == false

			ArrayPos0 = Object.LegsEntity
			ArrayPos1 = Legs[ArrayPos0].FootEntity
			if Foot[ArrayPos1].State != BOSSFOOT_STEP

				if Foot[ArrayPos1].State != BOSSFOOT_HANDLE_STEP
					Foot[ArrayPos1].State = BOSSFOOT_STEP
				else
					Object.XOffSet += Object.YOffSet
					if Object.XOffSet >= 80
						SetBit(Object.LegSide, 0, 1)
					end if

				end if

			end if

		end if
		CallFunction(BossLegJoint_HandleMovement)
		break

	case BOSSLEGJOINT_DESTROYED
		GetBit(TempValue0, Object.LegSide, 4)
		if TempValue0 == false
			Object.XOffSet -= 0x600
		else
			Object.XOffSet += 0x600
		end if
		Object.XPos += Object.XOffSet

		Object.YOffSet += 0x1860
		Object.YPos += Object.YOffSet

		if Object.OutOfBounds == true
			Object.Type = TypeName[Blank Object]
			Object.Priority = PRIORITY_BOUNDS
		end if
		break

	end switch
end sub


sub ObjectDraw
	DrawSprite(0)
end sub


sub ObjectStartup
	LoadSpriteSheet("R1/Objects3.gif")

	SpriteFrame(-8, -8, 16, 16, 74, 42)	// #0 - Leg Joint
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("R1/Objects3.gif")
	SpriteFrame(-8, -8, 16, 16, 74, 42)	// #0 - Leg Joint

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
