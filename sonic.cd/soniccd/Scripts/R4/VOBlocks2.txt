//--------------Sonic CD Vertical Blocks 2 Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value1			:	Object.YDrawPos
#alias Object.Value2			:	Object.YOffset
#alias Object.Value3			:	Object.Angle

// HUD alias
#alias Object[24].PropertyValue	:	HUD.CurrentTimePeriod

// Time Periods
#alias 0	:	TIME_PRESENT
#alias 1	:	TIME_PAST
#alias 2	:	TIME_GOOD_FUTURE
#alias 3	:	TIME_BAD_FUTURE

// Collision
#alias 1	:	PLAYER_COL_FLOOR
#alias 4	:	PLAYER_COL_ROOF

// Gravity
#alias 0	:	GRAVITY_GROUND


sub ObjectMain
	Object.Angle   = Oscillation
	Object.Angle  += Object.PropertyValue
	Object.Angle <<= 1

	Sin(Object.YOffset, Object.Angle)
	Object.YOffset <<= 13
	Object.YOffset += Object.YPos
	Object.YOffset &= 0xFFFF0000 // Truncate the value
	Object.YOffset -= Object.YDrawPos
end sub


sub ObjectPlayerInteraction
	TempValue0  = Object.YPos
	Object.YPos = Object.YDrawPos
	PlayerObjectCollision(C_BOX, -16, -64, 16, 64)
	switch CheckResult
	case PLAYER_COL_FLOOR
		Player.YPos += Object.YOffset
		break
#platform: Use_Origins
	case PLAYER_COL_ROOF
		if Player.Gravity == GRAVITY_GROUND
			if Object.YOffset > 0
				CallFunction(Player_Kill)
			end if
		end if
		break
#endplatform
	end switch
	Object.YPos = TempValue0
end sub


sub ObjectDraw
	Sin(Object.YDrawPos, Object.Angle)
	Object.YDrawPos <<= 13
	Object.YDrawPos  += Object.YPos
	Object.YDrawPos  &= 0xFFFF0000 // Truncate the value
	
	DrawSpriteXY(0, Object.XPos, Object.YDrawPos)
	DrawSpriteXY(1, Object.XPos, Object.YDrawPos)
	DrawSpriteXY(2, Object.XPos, Object.YDrawPos)
	DrawSpriteXY(3, Object.XPos, Object.YDrawPos)
end sub


sub ObjectStartup
	switch HUD.CurrentTimePeriod
	case TIME_PRESENT
		LoadSpriteSheet("R4/Objects.gif")
		TempValue0 = 163
		TempValue1 = 1
		TempValue2 = 196
		break

	case TIME_PAST
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 157
		TempValue2 = 34
		break

	case TIME_GOOD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 190
		TempValue2 = 34
		break

	case TIME_BAD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 223
		TempValue2 = 34
		break

	end switch

	// R4 Blocks
	SpriteFrame(-16, -64, 32, 32, TempValue0, TempValue1)
	SpriteFrame(-16, -32, 32, 32, TempValue2, TempValue1)
	SpriteFrame(-16, 0, 32, 32, TempValue0, TempValue1)
	SpriteFrame(-16, 32, 32, 32, TempValue2, TempValue1)
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	TempValue1   = Oscillation
	TempValue1  += Object.PropertyValue
	TempValue1 <<= 1
	Sin(TempValue0, TempValue1)
	TempValue0 <<= 13
	TempValue0  += Object.YPos
	TempValue0  &= 0xFFFF0000 // Truncate the value
	
	DrawSpriteXY(0, Object.XPos, TempValue0)
	DrawSpriteXY(1, Object.XPos, TempValue0)
	DrawSpriteXY(2, Object.XPos, TempValue0)
	DrawSpriteXY(3, Object.XPos, TempValue0)
end sub


sub RSDKLoad
	CallFunction(EditorHelpers_FindTimePeriod)
	switch CheckResult
	case TIME_PRESENT
		LoadSpriteSheet("R4/Objects.gif")
		TempValue0 = 163
		TempValue1 = 1
		TempValue2 = 196
		break

	case TIME_PAST
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 157
		TempValue2 = 34
		break

	case TIME_GOOD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 190
		TempValue2 = 34
		break

	case TIME_BAD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 223
		TempValue2 = 34
		break

	end switch

	// R4 Blocks
	SpriteFrame(-16, -64, 32, 32, TempValue0, TempValue1)
	SpriteFrame(-16, -32, 32, 32, TempValue2, TempValue1)
	SpriteFrame(-16, 0, 32, 32, TempValue0, TempValue1)
	SpriteFrame(-16, 32, 32, 32, TempValue2, TempValue1)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "startingAngle")
end sub
