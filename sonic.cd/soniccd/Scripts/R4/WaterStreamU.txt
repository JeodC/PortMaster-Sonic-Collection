//---------------Sonic CD Water Stream U Script---------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value1		:	Object.HitboxUp
#alias Object.Value2		:	Object.HitboxBottom

#alias Object.PropertyValue	:	Object.ActiveWaterStream

// Previous Object alias
#alias Object.PropertyValue	:	PrevObject.Active

// Control Mode
#alias 0	:	CONTROLMODE_NORMAL


sub ObjectPlayerInteraction
	// Water streams are activated through a chain, originated from a blank object or a fan
	if PrevObject[-1].Active == true
		Object.ActiveWaterStream = true
		PlayerObjectCollision(C_TOUCH, -32, Object.HitboxUp, 32, Object.HitboxBottom)
		if CheckResult == true
			if Player.State != Player_State_Hurt
				if Player.State != Player_State_Death
					if Player.State != Player_State_Drown
						if Player.State != Player_State_Static
							Player.State	 = Player_State_WaterCurrent
							Player.Animation = ANI_FANROTATE

							if Player.Left == false
								if Player.Right == false
									if Player.XPos < Object.XPos
										Player.XPos += 0x10000
									end if
									if Player.XPos > Object.XPos
										Player.XPos -= 0x10000
									end if
								end if
							end if
							
							Player.YVelocity = -0x40000
							if Player.WaterStream == false
								Player.XVelocity   = 0
								Player.Speed	   = 0
								Player.WaterStream = true
							end if
						end if
					end if
				end if
			end if
		else
			if Player.State == Player_State_WaterCurrent
				if Player.WaterStream == false

#platform: Use_Origins
					Player.State = Player_State_Air_NoDropDash
#endplatform

#platform: Use_Standalone
					Player.State = Player_State_Air
#endplatform

					Player.ControlMode = CONTROLMODE_NORMAL
					Player.Animation   = ANI_WALKING
				end if
			end if
		end if
	end if
end sub


sub ObjectStartup
	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Water Stream U]
			Object[ArrayPos0].HitboxUp     = Object[ArrayPos0].PropertyValue
			Object[ArrayPos0].HitboxUp   <<= 3
			Object[ArrayPos0].HitboxBottom = Object[ArrayPos0].HitboxUp
			FlipSign(Object[ArrayPos0].HitboxUp)
			Object[ArrayPos0].ActiveWaterStream = false
		end if
		ArrayPos0++
	loop
end sub


// ========================
// Editor Subs
// ========================


sub RSDKDraw
	if Editor.ShowGizmos == true
		DrawLine(Object.XPos, Object.YPos, Object[-1].XPos, Object.YPos, 0, 255, 255)
		DrawArrow(Object[-1].XPos, Object.YPos, Object[-1].XPos, Object[-1].YPos, 0, 255, 255)
	end if
	Object.InkEffect = 2
	Object.Alpha = 198
	DrawSpriteFX(0, FX_INK, Object.XPos, Object.YPos)
	TempValue0  = Object.YPos
	TempValue0 -= 0x400000
	DrawArrow(Object.XPos, Object.YPos, Object.XPos, TempValue0, 255, 255, 0)
	
end sub

sub RSDKLoad
	LoadSpriteSheet("Players/Sonic3.gif")
	SpriteFrame(-24, -12, 48, 24, 207, 125)		// #0 - Sonic Fan Rotate frame 1

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
