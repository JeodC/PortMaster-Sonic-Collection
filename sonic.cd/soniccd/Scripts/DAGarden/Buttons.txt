//---------------Sonic CD Touch Buttons Script----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value1			:	Object.ButtonPressed
#alias Object.PropertyValue		:	Object.ButtonID

// These are also Touch Buttons
#alias Object[35].PropertyValue	:	Object.Button1Value
#alias Object[36].PropertyValue	:	Object.Button2Value
#alias Object[37].PropertyValue	:	Object.Button3Value

// Little Planet Aliases
#alias Object[32].Value1		:	LittlePlanet.ZoomDepth
#alias Object[32].Value3		:	LittlePlanet.RotationSpeed

// Music Selector Aliases
#alias Object[33].Frame			:	MusicSelector.Frame
#alias Object[33].XPos			:	MusicSelector.XPos
#alias Object[33].State			:	MusicSelector.State
#alias Object[33].DrawOrder		:	MusicSelector.DrawOrder
#alias Object[33].Priority		:	MusicSelector.Priority

// MenuControl Aliases
#alias Object[0].State			:	MenuControl.State

// Music Selector States
#alias 1	:	MUSICSELECTOR_STOP_MOVE_L
#alias 2	:	MUSICSELECTOR_MOVE_L
#alias 4	:	MUSICSELECTOR_MOVE_R

// MenuControl States
#alias 1	:	MENUCONTROL_FADE_IN
#alias 3	:	MENUCONTROL_MUSIC_CONTROLS
#alias 4	:	MENUCONTROL_SET_MUSIC
#alias 6	:	MENUCONTROL_EXIT

// Global SFX
#alias 23	:	SFX_G_MENUBUTTON
#alias 27	:	SFX_G_SELECT

//Engine & CallBacks
#alias 2	:	MESSAGE_LOSTFOCUS

// Priority
#alias 1	:	PRIORITY_ACTIVE


sub ObjectMain
	if MenuControl.State > MENUCONTROL_FADE_IN
		if MenuControl.State < MENUCONTROL_SET_MUSIC
			if Engine.Message == MESSAGE_LOSTFOCUS
				Object.ButtonPressed = 0
			end if

			TempValue0   = Object.XPos
			TempValue0 >>= 16
			TempValue1   = TempValue0

			TempValue0  -= 32
			TempValue0  -= Screen.XOffset

			TempValue1  += 32
			TempValue1  -= Screen.XOffset
			CheckTouchRect(TempValue0, 0, TempValue1, 32)
			if CheckResult > -1
				Object.ButtonPressed = Object.ButtonID
				Object.ButtonPressed++
				switch Object.ButtonID
				case 0
					if LittlePlanet.ZoomDepth < 0x4000
						LittlePlanet.ZoomDepth += 8
					end if
					break
				case 1
					if LittlePlanet.ZoomDepth > 48
						LittlePlanet.ZoomDepth -= 8
					end if
					break
				case 2
					if LittlePlanet.RotationSpeed < 256
						LittlePlanet.RotationSpeed += 2
					end if
					break
				case 3
					if LittlePlanet.RotationSpeed > -256
						LittlePlanet.RotationSpeed -= 2
					end if
					break
				end switch
			else
				CheckTouchRect(0, 0, Screen.XSize, Screen.YSize)
				if CheckResult < 0
					switch Object.ButtonID
					// Zooming
					case 0
						if Object.ButtonPressed == 1
							Object.ButtonPressed = 0
							Object.Button1Value	 = 1
						end if
						break

					case 1
						if Object.ButtonPressed == 2
							Object.ButtonPressed = 0
							Object.Button1Value  = 0
						end if
						break
					// Rotation
					case 2
						if Object.ButtonPressed == 3
							Object.ButtonPressed = 0
						end if
						break

					case 3
						if Object.ButtonPressed == 4
							Object.ButtonPressed = 0
						end if
						break
					// Music Selector
					case 4
						if Object.ButtonPressed == 5
							Object.ButtonPressed = 0
							
							PlaySfx(SFX_G_MENUBUTTON, false)

							MusicSelector.State = MUSICSELECTOR_MOVE_R
						end if
						break

					case 5
						if Object.ButtonPressed == 6
							Object.ButtonPressed = 0
							MenuControl.State	 = MENUCONTROL_SET_MUSIC
							StopMusic()
						end if
						break

					case 6
						if Object.ButtonPressed == 7
							Object.ButtonPressed = 0

							PlaySfx(SFX_G_MENUBUTTON, false)

							MusicSelector.State = MUSICSELECTOR_MOVE_L
						end if
						break

					case 7
						if Object.ButtonPressed == 8
							Object.ButtonPressed = 0

							PlaySfx(SFX_G_SELECT, false)

							MenuControl.State	 = MENUCONTROL_MUSIC_CONTROLS
							MusicSelector.XPos	 = Screen.XSize
							MusicSelector.XPos	+= 96
							MusicSelector.XPos <<= 16

							MusicSelector.State		= MUSICSELECTOR_STOP_MOVE_L
							MusicSelector.DrawOrder = 5
							MusicSelector.Priority  = PRIORITY_ACTIVE

							Object.Button1Value = 4
							Object.Button2Value = 5
							Object.Button3Value = 6
						end if
						break
					// Exit
					case 8
						if Object.ButtonPressed == 9
							Object.ButtonPressed = 0
							StopMusic()
							PlaySfx(SFX_G_SELECT, false)
							MenuControl.State = MENUCONTROL_EXIT
						end if
						break
					end switch
				else	// Reset buttons
					switch Object.ButtonID
					case 0
						if Object.ButtonPressed == 1
							Object.ButtonPressed = 0
						end if
						break
					case 1
						if Object.ButtonPressed == 2
							Object.ButtonPressed = 0
						end if
						break
					case 2
						if Object.ButtonPressed == 3
							Object.ButtonPressed = 0
						end if
						break
					case 3
						if Object.ButtonPressed == 4
							Object.ButtonPressed = 0
						end if
						break
					case 4
						if Object.ButtonPressed == 5
							Object.ButtonPressed = 0
						end if
						break
					case 5
						if Object.ButtonPressed == 6
							Object.ButtonPressed = 0
						end if
						break
					case 6
						if Object.ButtonPressed == 7
							Object.ButtonPressed = 0
						end if
						break
					case 7
						if Object.ButtonPressed == 8
							Object.ButtonPressed = 0
						end if
						break
					case 8
						if Object.ButtonPressed == 9
							Object.ButtonPressed = 0
						end if
						break
					end switch
				end if
			end if
		end if
	end if
end sub


sub ObjectDraw
	TempValue0   = Object.ButtonID
	TempValue0 <<= 1

	TempValue1 = Object.ButtonID
	TempValue1++
	if Object.ButtonPressed == TempValue1
		TempValue0++
	end if
	DrawSprite(TempValue0)
end sub


sub ObjectStartup
	LoadSpriteSheet("DAGarden/Objects.gif")

	SpriteFrame(-32, -16, 63, 31, 146, 577)		// #0 - Zoom Out
	SpriteFrame(-32, -16, 63, 31, 146, 609)		// #1 - Zoom Out Highlight
	SpriteFrame(-32, -16, 63, 31, 210, 577)		// #2 - Zoom In
	SpriteFrame(-32, -16, 63, 31, 210, 609)		// #3 - Zoom In Highlight
	SpriteFrame(-32, -16, 63, 31, 274, 577)		// #4 - Rotation Right
	SpriteFrame(-32, -16, 63, 31, 274, 609)		// #5 - Rotation Right Highlight
	SpriteFrame(-32, -16, 63, 31, 338, 577)		// #6 - Rotation Left
	SpriteFrame(-32, -16, 63, 31, 338, 609)		// #7 - Rotation Left Highlight
	SpriteFrame(-32, -16, 63, 31, 210, 641)		// #8 - Prior Theme
	SpriteFrame(-32, -16, 63, 31, 210, 673)		// #9 - Prior Theme Highlight
	SpriteFrame(-32, -16, 63, 31, 274, 641)		// #10 - Play
	SpriteFrame(-32, -16, 63, 31, 274, 673)		// #11 - Play Highlight
	SpriteFrame(-32, -16, 63, 31, 338, 641)		// #12 - Next Theme
	SpriteFrame(-32, -16, 63, 31, 338, 673)		// #13 - Next Theme Highlight
	SpriteFrame(-32, -16, 63, 31, 402, 641)		// #14 - Music Toggle
	SpriteFrame(-32, -16, 63, 31, 402, 673)		// #15 - Music Toggle Highlight
	SpriteFrame(-32, -16, 63, 31, 402, 577)		// #16 - Exit
	SpriteFrame(-32, -16, 63, 31, 402, 609)		// #17 - Exit Highlight
end sub


// ========================
// Editor Subs
// ========================

sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			CheckResult = Object.PropertyValue
			break
		case 0 // ButtonID
			CheckResult = Object.PropertyValue
			break
		end switch
	else
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			Object.PropertyValue = Editor.VariableValue
			break
		case 0 // ButtonID
			Object.PropertyValue = Editor.VariableValue
			break
		end switch
	end if
end sub


sub RSDKDraw
	TempValue0 = Object.ButtonID
	TempValue0 <<= 1
	DrawSprite(TempValue0)
end sub


sub RSDKLoad
	LoadSpriteSheet("DAGarden/Objects.gif")
	SpriteFrame(-32, -16, 63, 31, 146, 577)		// #0 - Zoom Out
	SpriteFrame(-32, -16, 63, 31, 146, 609)		// #1 - Zoom Out Highlight
	SpriteFrame(-32, -16, 63, 31, 210, 577)		// #2 - Zoom In
	SpriteFrame(-32, -16, 63, 31, 210, 609)		// #3 - Zoom In Highlight
	SpriteFrame(-32, -16, 63, 31, 274, 577)		// #4 - Rotate Right
	SpriteFrame(-32, -16, 63, 31, 274, 609)		// #5 - Rotate Right Highlight
	SpriteFrame(-32, -16, 63, 31, 338, 577)		// #6 - Rotate Left
	SpriteFrame(-32, -16, 63, 31, 338, 609)		// #7 - Rotate Left Highlight
	SpriteFrame(-32, -16, 63, 31, 210, 641)		// #8 - Previous Track
	SpriteFrame(-32, -16, 63, 31, 210, 673)		// #9 - Previous Track Highlight
	SpriteFrame(-32, -16, 63, 31, 274, 641)		// #10 - Play Track
	SpriteFrame(-32, -16, 63, 31, 274, 673)		// #11 - Play Track Highlight
	SpriteFrame(-32, -16, 63, 31, 338, 641)		// #12 - Next Track
	SpriteFrame(-32, -16, 63, 31, 338, 673)		// #13 - Next Track Highlight
	SpriteFrame(-32, -16, 63, 31, 402, 641)		// #14 - Music Toggle
	SpriteFrame(-32, -16, 63, 31, 402, 673)		// #15 - Music Toggle Highlight
	SpriteFrame(-32, -16, 63, 31, 402, 577)		// #16 - Exit
	SpriteFrame(-32, -16, 63, 31, 402, 609)		// #17 - Exit Highlight

	AddEditorVariable("ButtonID")
	SetActiveVariable("ButtonID")
	AddEnumVariable("Zoom Out", 0)
	AddEnumVariable("Zoom In", 1)
	AddEnumVariable("Rotate Right", 2)
	AddEnumVariable("Rotate Left", 3)
	AddEnumVariable("Previous Track", 4)
	AddEnumVariable("Play Track ", 5)
	AddEnumVariable("Next Track", 6)
	AddEnumVariable("Music Toggle", 7)
	AddEnumVariable("Exit", 8)
end sub
