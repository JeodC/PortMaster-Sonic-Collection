//---------------Sonic CD Text Font 1 Script------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value1	:	Object.TargetLine

// Text Info type
#alias 0	:	TEXTINFO_TEXTDATA
#alias 1	:	TEXTINFO_TEXTSIZE


sub ObjectDraw

	// Only show letters 4th and onwards, as the first 3 letters are for identifying the text type
	ArrayPos0 = 3

	// Get the length of the line
	GetTextInfo(TempValue1, MENU_1, TEXTINFO_TEXTSIZE, Object.TargetLine, 0)

	// Taking away 3 for the reason mentioned above...
	TempValue1 -= 3

	// Base Position to start drawing text at
	TempValue2   = TempValue1
	TempValue2 <<= 18
	FlipSign(TempValue2)
	TempValue2  += Object.XPos

	// Loop for drawing all the letters
	while TempValue1 > 0

		// Get the letter at the current text position
		GetTextInfo(TempValue0, MENU_1, TEXTINFO_TEXTDATA, Object.TargetLine, ArrayPos0)

		// Jump to the according special case, if needed for certain exceptions
		switch TempValue0
		default
			Object.Frame = 0

			// Translate lowercase letters to Sprite Frames
			if TempValue0 > 64
				if TempValue0 < 91
					Object.Frame  = TempValue0
					Object.Frame -= 64
				end if
			end if
			
			// Translate uppercase letters to Sprite Frames
			if TempValue0 > 96
				if TempValue0 < 123
					Object.Frame  = TempValue0
					Object.Frame -= 96
				end if
			end if

			if Object.Frame > 0
				// If the letter is valid, draw it
				DrawSpriteXY(Object.Frame, TempValue2, Object.YPos)
			end if
			break

		case 38
			// Ampersand (&)
			DrawSpriteXY(32, TempValue2, Object.YPos)
			break

		case 39
			// Tick mark (')
			DrawSpriteXY(30, TempValue2, Object.YPos)
			break

		case 44
			// Comma (,)
			DrawSpriteXY(28, TempValue2, Object.YPos)
			break

		case 46
			// Period (.)
			DrawSpriteXY(27, TempValue2, Object.YPos)
			break

		case 47
			// Forward slash (/)
			DrawSpriteXY(31, TempValue2, Object.YPos)
			break

		case 58
			// Colon (;)
			DrawSpriteXY(29, TempValue2, Object.YPos)
			break

		case 94
			// Caret (^)
			DrawSpriteXY(36, TempValue2, Object.YPos)
			break

		case 225
			// Accent mark A (á)
			DrawSpriteXY(33, TempValue2, Object.YPos)
			break

		case 233
			// Accent mark E (é)
			DrawSpriteXY(34, TempValue2, Object.YPos)
			break

		case 243
			// Accent mark O (ó)
			DrawSpriteXY(35, TempValue2, Object.YPos)
			break

		end switch

		// Move the cursor to the next letter
		ArrayPos0++

		// Decrease the remaining letter count
		TempValue1--

		// Each letter is 8 pixels apart
		TempValue2 += 0x80000
	loop

end sub


sub ObjectStartup
	LoadSpriteSheet("Menu/CreditsText.gif")
	
	// Setup all the letter Sprite Frames, primarily using loops to automate the process
	SpriteFrame(-3, -9, 8, 16, 37, 35)

	TempValue1 = 1
	while TempValue1 < 35
		TempValue0 = 1
		while TempValue0 < 127
			SpriteFrame(-3, -6, 8, 16, TempValue0, TempValue1)
			TempValue0 += 9
		loop
		TempValue1 += 17
	loop

	TempValue0 = 1
	while TempValue0 < 37
		SpriteFrame(-3, -6, 8, 16, TempValue0, TempValue1)
		TempValue0 += 9
	loop

	SpriteFrame(-3, -9, 8, 16, 37, 35)
	SpriteFrame(-3, -9, 8, 16, 46, 35)
	SpriteFrame(-3, -9, 8, 16, 55, 35)
	SpriteFrame(-3, -6, 8, 16, 73, 35)

end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	TempValue0 = 0
	while TempValue0 < 4
		DrawSprite(TempValue0)
		TempValue0++
	loop
end sub


sub RSDKLoad
	LoadSpriteSheet("Menu/CreditsText.gif")
	SpriteFrame(0, -6, 8, 16, 46, 18)
	SpriteFrame(9, -6, 8, 16, 37, 1)
	SpriteFrame(18, -6, 8, 16, 82, 18)
	SpriteFrame(27, -6, 8, 16, 46, 18)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
