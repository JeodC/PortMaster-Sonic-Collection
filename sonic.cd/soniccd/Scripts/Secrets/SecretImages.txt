//---------------Sonic CD Secret Images Script----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.Timer

// States
#alias 0	:	SECRETIMAGES_SETUP
#alias 1	:	SECRETIMAGES_FADE_IN
#alias 2	:	SECRETIMAGES_CONTROL_DELAY
#alias 3	:	SECRETIMAGES_CONTROL_CHECK
#alias 4	:	SECRETIMAGES_EXIT

// Presentation Stages
#alias 1	:	STAGE_P_MENU

// Game Mode Aliases
#alias 0	:	MODE_NOSAVE


sub ObjectMain
	switch Object.State
	case SECRETIMAGES_SETUP
		if Object.Timer < 8
			// Yield on the black screen for a moment
			
			Object.Timer++
		else
			// Transition to the next state now!
			
			Object.Timer = 288
			Object.State++
		end if
		
		SetScreenFade(0, 0, 0, 255)
		break

	case SECRETIMAGES_FADE_IN
		if Object.Timer > 0
			Object.Timer -= 16
		else
			PlayMusic(Options.Soundtrack)
			Object.State++
		end if
		
		SetScreenFade(0, 0, 0, Object.Timer)
		break

	case SECRETIMAGES_CONTROL_DELAY
		if Object.Timer < 60
			Object.Timer++
		else
			Object.Timer = 0
			Object.State++
		end if
		break

	case SECRETIMAGES_CONTROL_CHECK
		if KeyPress[0].ButtonA == true
			Object.State++
		end if

		if KeyPress[0].ButtonB == true
			Object.State++
		end if

		if KeyPress[0].ButtonC == true
			Object.State++
		end if

		if KeyPress[0].Start == true
			Object.State++
		end if

		CheckTouchRect(0, 0, Screen.XSize, Screen.YSize)
		if CheckResult > -1
			Object.State++
		end if
		break

	case SECRETIMAGES_EXIT
		if Object.Timer < 288
			Object.Timer += 8
			Music.Volume -= 5
		else
			StopMusic()
			Options.GameMode = MODE_NOSAVE
			Stage.ActiveList = PRESENTATION_STAGE
			Stage.ListPos = STAGE_P_MENU
			LoadStage()
		end if
		
		TempValue0 = Object.Timer
		if TempValue0 > 255
			TempValue0 = 255
		end if
		SetScreenFade(0, 0, 0, TempValue0)
		break
	end switch
end sub


sub ObjectDraw
	switch Object.PropertyValue
	case 0	// Batman Parody
		DrawRect(0, 0, Screen.XSize, Screen.YSize, 0, 0, 0, 255)
		DrawSpriteScreenXY(0, Screen.CenterX, Screen.CenterY)
		break

	case 1	// Fun is Infinite
		DrawRect(0, 0, Screen.XSize, Screen.YSize, 0, 0, 0, 255)
		DrawSpriteScreenXY(1, Screen.CenterX, Screen.CenterY)
		break

	case 2	// You Are Cool
		DrawRect(0, 0, Screen.XSize, Screen.YSize, 238, 238, 204, 255)
		DrawSpriteScreenXY(2, Screen.CenterX, Screen.CenterY)
		break

	case 3	// Fastest DJ Alive
		DrawRect(0, 0, Screen.XSize, Screen.YSize, 0, 0, 0, 255)
		DrawSpriteScreenXY(3, Screen.CenterX, Screen.CenterY)
		break

	case 4	// See You Next Game
		DrawRect(0, 0, Screen.CenterX, Screen.YSize, 0, 34, 221, 255)
		DrawRect(Screen.CenterX, 0, Screen.CenterX, Screen.YSize, 0, 34, 204, 255)
		DrawSpriteScreenXY(4, Screen.CenterX, Screen.CenterY)
		DrawSpriteScreenXY(5, Screen.CenterX, Screen.CenterY)
		break
	end switch
end sub


sub ObjectStartup
	LoadSpriteSheet("Secrets/SecretImages.gif")

	SpriteFrame(-160, -112, 320, 224, 0, 0)		// #0 - Batman Parody
	SpriteFrame(-160, -112, 320, 224, 0, 225)	// #1 - Fun is Infinite
	SpriteFrame(-160, -112, 320, 224, 0, 450)	// #2 - You Are Cool
	SpriteFrame(-160, -112, 320, 224, 0, 675)	// #3 - Fastest DJ Alive
	SpriteFrame(-160, -112, 160, 224, 321, 0)	// #4 - See You Next Game first half
	SpriteFrame(0, -112, 160, 224, 321, 225)	// #5 - See You Next Game second half
end sub


// ========================
// Editor Subs
// ========================

sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			CheckResult = Object.PropertyValue
			break
		case 0 // type
			CheckResult = Object.PropertyValue
			break
		end switch
	else
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			Object.PropertyValue = Editor.VariableValue
			break
		case 0 // type
			Object.PropertyValue = Editor.VariableValue
			break
		end switch
	end if
end sub


sub RSDKDraw
	switch Object.PropertyValue
	case 4
		DrawSprite(5)
	default
		DrawSprite(Object.PropertyValue)
		break
	end switch
end sub


sub RSDKLoad
	LoadSpriteSheet("Secrets/SecretImages.gif")
	SpriteFrame(-160, -112, 320, 224, 0, 0)		// #0 - Batman Parody
	SpriteFrame(-160, -112, 320, 224, 0, 225)	// #1 - Fun is Infinite
	SpriteFrame(-160, -112, 320, 224, 0, 450)	// #2 - You Are Cool
	SpriteFrame(-160, -112, 320, 224, 0, 675)	// #3 - Fastest DJ Alive
	SpriteFrame(-160, -112, 160, 224, 321, 0)	// #4 - See You Next Game first half
	SpriteFrame(0, -112, 160, 224, 321, 225)	// #5 - See You Next Game second half

	// note - this object isn't normally placed in a scene normally, it's spawned via the Sound Test object when needed instead
	AddEditorVariable("frame")
	SetActiveVariable("frame")
	AddEnumVariable("Batman Parody", 0)
	AddEnumVariable("Fun is Infinite", 1)
	AddEnumVariable("You Are Cool", 2)
	AddEnumVariable("Fastest DJ Alive", 3)
	AddEnumVariable("See You Next Game", 4)
end sub
