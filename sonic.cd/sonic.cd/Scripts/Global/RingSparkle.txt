//----------------Sonic CD Ring Sparkle Script----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.Timer
// Value1 is skipped over
#alias Object.Value2	:	Object.MissionTypeEX

// Ring Reset Aliases
#alias Object.Value0	:	Object.TimeCheckpoint

// Priority Aliases
#alias 1	:	PRIORITY_ACTIVE

// Priority Aliases
#alias 10	:	RULE_RESPAWN_RINGS


sub ObjectMain
	
	Object.Timer++
	if Object.Timer == 6
		
		Object.Timer = 0
		Object.Frame++
		
		// There are only 4 Ring Sparkle frames, unload once attempting to progress from the final one
		if Object.Frame == 4
			Object.Type = TypeName[Blank Object]
			Object.Frame = 0

#platform: Use_Origins
			if game.playMode == BOOT_PLAYMODE_MISSION
				// Set during "M079 - Ring Challenge 100"
				if game.missionFunctionNo == RULE_RESPAWN_RINGS
					
					// Turn into the Ring Reset object
					// -> MissionTypeEX is set in the Ring Reset script
					Object.Type = Object.MissionTypeEX
					
					// Take the current time + 20 and set it to this object's Time Checkpoint
					Object.TimeCheckpoint  = Stage.Minutes
					Object.TimeCheckpoint *= 60
					Object.TimeCheckpoint += Stage.Seconds
					Object.TimeCheckpoint += 20
					
					// While as a Ring Reset object, this object should always be updating before turning back into a Ring object
					Object.Priority = PRIORITY_ACTIVE
					
				end if
			end if
#endplatform
		end if
	end if
	
end sub


sub ObjectDraw
	DrawSprite(Object.Frame)
end sub


sub ObjectStartup
	
	LoadSpriteSheet("Global/Items.gif")
	
	// 0-3 - Ring Sparkle Frames
	SpriteFrame(-8, -8, 16, 16, 1, 202)
	SpriteFrame(-8, -8, 16, 16, 18, 202)
	SpriteFrame(-8, -8, 16, 16, 1, 219)
	SpriteFrame(-8, -8, 16, 16, 18, 219)
	
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Items.gif")
	SpriteFrame(-8, -8, 16, 16, 1, 202)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
