//---------------Sonic CD Spike Barrier Script----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.Timer
#alias Object.Value1	:	Object.LeftBound
#alias Object.Value2	:	Object.RightBound

// States
#alias 0	:	SPIKEBARRIER_MOVE_LEFT
#alias 1	:	SPIKEBARRIER_MOVE_RIGHT

// Collision Plane
#alias 0	:	COLLISION_PLANE_A


sub ObjectMain
	if Object.State == SPIKEBARRIER_MOVE_LEFT
		Object.XPos -= 0x5000
		if Object.XPos <= Object.LeftBound
			Object.State = SPIKEBARRIER_MOVE_RIGHT
			Object.Direction = FACING_RIGHT
		end if
	else
		Object.XPos += 0x5000
		if Object.XPos >= Object.RightBound
			Object.State = SPIKEBARRIER_MOVE_LEFT
			Object.Direction = FACING_LEFT
		end if
	end if
end sub


sub ObjectPlayerInteraction
	if Player.CollisionPlane == COLLISION_PLANE_A
		PlayerObjectCollision(C_TOUCH, -14, -76, 14, 76)
		if CheckResult == true
			CallFunction(Player_Hit)
		end if
	end if
end sub


sub ObjectDraw
	TempValue0   = Object.Timer
	TempValue0 >>= 1
	DrawSpriteFX(TempValue0, FX_FLIP, Object.XPos, Object.YPos)
	Object.Timer++
	Object.Timer &= 3
end sub


sub ObjectStartup
	LoadSpriteSheet("R8/Objects2.gif")

	SpriteFrame(-16, -76, 32, 152, 190, 1)	// #0 - Spike Barrier frame 0
	SpriteFrame(-16, -76, 32, 152, 223, 1)	// #1 - Spike Barrier frame 1

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Spike Barrier]
			Object[ArrayPos0].DrawOrder = 4
			Object[ArrayPos0].RightBound = Object[ArrayPos0].XPos
			Object[ArrayPos0].LeftBound  = Object[ArrayPos0].RightBound
			Object[ArrayPos0].LeftBound -= 0x800000
			Object[ArrayPos0].Direction  = FACING_LEFT
		end if
		ArrayPos0++
	loop
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("R8/Objects2.gif")
	SpriteFrame(-16, -76, 32, 152, 190, 1)	// #0 - Spike Barrier frame 0
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
