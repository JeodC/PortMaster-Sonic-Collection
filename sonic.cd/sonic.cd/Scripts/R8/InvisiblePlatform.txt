//------------Sonic CD Invisible Platform Script--------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.FirstPlatTimer
#alias Object.Value1	:	Object.FirstPlatMaxTimer

#alias Object.Value2	:	Object.SecondPlatTimer
#alias Object.Value3	:	Object.SecondPlatMaxTimer
#alias Object.Value4	:	Object.SecondPlatFrame

#alias Object.Value5	:	Object.ThirdPlatTimer
#alias Object.Value6	:	Object.ThirdPlatMaxTimer
#alias Object.Value7	:	Object.ThirdPlatFrame

// Property Values
#alias 0	:	ONE_BLOCK
#alias 1	:	TWO_BLOCKS	// Seems unused in the game
#alias 2	:	THREE_BLOCK_STAIR

// Function declarations
#function InvPlatform_SetMaxTimer

function InvPlatform_SetMaxTimer
	switch TempValue0
	case 0
		TempValue1 = 60
		break
	case 1
	case 3
	case 5
	case 7
	case 9
	case 11
	case 14
	case 16
	case 18
	case 20
	case 22
	case 24
		TempValue1 = 5
		break
	case 2
	case 4
	case 6
	case 8
	case 10
	case 12
	case 15
	case 17
	case 19
	case 21
	case 23
	case 25
		TempValue1 = 3
		break
	case 13
		TempValue1 = 160
		break
	end switch
end function


sub ObjectMain
	if Object.FirstPlatTimer < Object.FirstPlatMaxTimer
		Object.FirstPlatTimer++
	else
		Object.FirstPlatTimer = 0
		Object.Frame++
		Object.Frame %= 26
		TempValue0 = Object.Frame
		CallFunction(InvPlatform_SetMaxTimer)
		Object.FirstPlatMaxTimer = TempValue1
	end if

	if Object.PropertyValue == THREE_BLOCK_STAIR
		if Object.SecondPlatTimer < Object.SecondPlatMaxTimer
			Object.SecondPlatTimer++
		else
			Object.SecondPlatTimer = 0
			Object.SecondPlatFrame++
			Object.SecondPlatFrame %= 26
			TempValue0 = Object.SecondPlatFrame
			CallFunction(InvPlatform_SetMaxTimer)
			Object.SecondPlatMaxTimer = TempValue1
		end if

		if Object.ThirdPlatTimer < Object.ThirdPlatMaxTimer
			Object.ThirdPlatTimer++
		else
			Object.ThirdPlatTimer = 0
			Object.ThirdPlatFrame++
			Object.ThirdPlatFrame %= 26
			TempValue0 = Object.ThirdPlatFrame
			CallFunction(InvPlatform_SetMaxTimer)
			Object.ThirdPlatMaxTimer = TempValue1
		end if
	end if
end sub


sub ObjectPlayerInteraction
	switch Object.PropertyValue
	case ONE_BLOCK
		if Object.Frame == 13
			PlayerObjectCollision(C_PLATFORM, -16, -16, 16, 0)
		end if
		break
	case TWO_BLOCKS
		if Object.Frame == 13
			PlayerObjectCollision(C_PLATFORM, -16, -16, 48, 0)
		end if
		break
	case THREE_BLOCK_STAIR
		if Object.Frame == 13
			PlayerObjectCollision(C_PLATFORM, -16, -16, 16, 0)
		end if
		if Object.SecondPlatFrame == 13
			PlayerObjectCollision(C_PLATFORM, 16, 0, 48, 16)
		end if
		if Object.ThirdPlatFrame == 13
			PlayerObjectCollision(C_PLATFORM, 48, 16, 80, 32)
		end if
		break
	end switch
end sub


sub ObjectDraw
	switch Object.PropertyValue
	case ONE_BLOCK
		DrawSprite(Object.Frame)
		break
	case TWO_BLOCKS
		TempValue0 = Object.XPos
		DrawSpriteXY(Object.Frame, TempValue0, Object.YPos)
		TempValue0 += 0x200000
		DrawSpriteXY(Object.Frame, TempValue0, Object.YPos)
		break
	case THREE_BLOCK_STAIR
		TempValue0 = Object.XPos
		TempValue1 = Object.YPos
		DrawSpriteXY(Object.Frame, TempValue0, TempValue1)
		TempValue0 += 0x200000
		TempValue1 += 0x100000
		DrawSpriteXY(Object.SecondPlatFrame, TempValue0, TempValue1)
		TempValue0 += 0x200000
		TempValue1 += 0x100000
		DrawSpriteXY(Object.ThirdPlatFrame, TempValue0, TempValue1)
		break
	end switch
end sub


sub ObjectStartup
	LoadSpriteSheet("R8/Objects2.gif")

	SpriteFrame(-4, -16, 0, 0, 190, 223)	//	#0  - Blank

	// 1/3 width
	SpriteFrame(-4, -16, 8, 32, 190, 223)	//	#1  - Vanishing Platform
	SpriteFrame(-4, -16, 8, 32, 223, 223)	//	#2  - Solid Platform
	SpriteFrame(-4, -16, 8, 32, 190, 223)	//	#3  - Vanishing Platform
	SpriteFrame(-4, -16, 8, 32, 223, 223)	//	#4  - Solid Platform

	// 2/3 width
	SpriteFrame(-8, -16, 16, 32, 190, 223)	//	#5  - Vanishing Platform
	SpriteFrame(-8, -16, 16, 32, 223, 223)	//	#6  - Solid Platform
	SpriteFrame(-8, -16, 16, 32, 190, 223)	//	#7  - Vanishing Platform
	SpriteFrame(-8, -16, 16, 32, 223, 223)	//	#8  - Solid Platform

	// Full width
	SpriteFrame(-16, -16, 32, 32, 190, 223)	//	#9  - Vanishing Platform
	SpriteFrame(-16, -16, 32, 32, 223, 223)	//	#10 - Solid Platform
	SpriteFrame(-16, -16, 32, 32, 190, 223)	//	#11 - Vanishing Platform
	SpriteFrame(-16, -16, 32, 32, 223, 223)	//	#12 - Solid Platform
	SpriteFrame(-16, -16, 32, 32, 223, 223)	//	#13 - Solid Platform
	SpriteFrame(-16, -16, 32, 32, 190, 223)	//	#14 - Vanishing Platform
	SpriteFrame(-16, -16, 32, 32, 223, 223)	//	#15 - Solid Platform
	SpriteFrame(-16, -16, 32, 32, 190, 223)	//	#16 - Vanishing Platform
	SpriteFrame(-16, -16, 32, 32, 223, 223)	//	#17 - Solid Platform

	// 2/3 width
	SpriteFrame(-8, -16, 16, 32, 190, 223)	//	#18 - Vanishing Platform
	SpriteFrame(-8, -16, 16, 32, 223, 223)	//	#19 - Solid Platform
	SpriteFrame(-8, -16, 16, 32, 190, 223)	//	#20 - Vanishing Platform
	SpriteFrame(-8, -16, 16, 32, 223, 223)	//	#21 - Solid Platform

	// 1/3 width
	SpriteFrame(-4, -16, 8, 32, 190, 223)	//	#22 - Vanishing Platform
	SpriteFrame(-4, -16, 8, 32, 223, 223)	//	#23 - Solid Platform
	SpriteFrame(-4, -16, 8, 32, 190, 223)	//	#24 - Vanishing Platform
	SpriteFrame(-4, -16, 8, 32, 223, 223)	//	#25 - Solid Platform

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Inv Platform]
			switch Object[ArrayPos0].PropertyValue
			case ONE_BLOCK
			case TWO_BLOCKS
				Object[ArrayPos0].FirstPlatMaxTimer = 60
				break
			case THREE_BLOCK_STAIR
				Object[ArrayPos0].FirstPlatMaxTimer = 60
				Object[ArrayPos0].SecondPlatMaxTimer = 108
				Object[ArrayPos0].ThirdPlatMaxTimer = 156
				break
			end switch
		end if
		ArrayPos0++
	loop
end sub


// ========================
// Editor Subs
// ========================

sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			CheckResult  = Object.PropertyValue
			CheckResult %= 3
			break
		case 0 // type
			CheckResult  = Object.PropertyValue
			CheckResult %= 3
			break
		end switch
	else
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // Property Value
			Object.PropertyValue  = Editor.VariableValue
			Object.PropertyValue %= 3
			break
		case 0 // type
			Object.PropertyValue = Editor.VariableValue
			Object.PropertyValue %= 3
			break
		end switch
	end if
end sub

sub RSDKDraw
	switch Object.PropertyValue
	case ONE_BLOCK
		DrawSprite(9)
		break
	case TWO_BLOCKS
		TempValue0 = Object.XPos
		DrawSpriteXY(9, TempValue0, Object.YPos)
		TempValue0 += 0x200000
		DrawSpriteXY(9, TempValue0, Object.YPos)
		break
	case THREE_BLOCK_STAIR
		TempValue0 = Object.XPos
		TempValue1 = Object.YPos
		DrawSpriteXY(9, TempValue0, TempValue1)
		TempValue0 += 0x200000
		TempValue1 += 0x100000
		DrawSpriteXY(9, TempValue0, TempValue1)
		TempValue0 += 0x200000
		TempValue1 += 0x100000
		DrawSpriteXY(9, TempValue0, TempValue1)
		break
	end switch
end sub


sub RSDKLoad
	LoadSpriteSheet("R8/Objects2.gif")

	SpriteFrame(-4, -16, 0, 0, 190, 223)	//	#0  - Blank

	// 1/3 width
	SpriteFrame(-4, -16, 8, 32, 190, 223)	//	#1  - Vanishing Platform
	SpriteFrame(-4, -16, 8, 32, 223, 223)	//	#2  - Solid Platform
	SpriteFrame(-4, -16, 8, 32, 190, 223)	//	#3  - Vanishing Platform
	SpriteFrame(-4, -16, 8, 32, 223, 223)	//	#4  - Solid Platform

	// 2/3 width
	SpriteFrame(-8, -16, 16, 32, 190, 223)	//	#5  - Vanishing Platform
	SpriteFrame(-8, -16, 16, 32, 223, 223)	//	#6  - Solid Platform
	SpriteFrame(-8, -16, 16, 32, 190, 223)	//	#7  - Vanishing Platform
	SpriteFrame(-8, -16, 16, 32, 223, 223)	//	#8  - Solid Platform

	// Full width
	SpriteFrame(-16, -16, 32, 32, 190, 223)	//	#9  - Vanishing Platform
	SpriteFrame(-16, -16, 32, 32, 223, 223)	//	#10 - Solid Platform
	SpriteFrame(-16, -16, 32, 32, 190, 223)	//	#11 - Vanishing Platform
	SpriteFrame(-16, -16, 32, 32, 223, 223)	//	#12 - Solid Platform
	SpriteFrame(-16, -16, 32, 32, 223, 223)	//	#13 - Solid Platform
	SpriteFrame(-16, -16, 32, 32, 190, 223)	//	#14 - Vanishing Platform
	SpriteFrame(-16, -16, 32, 32, 223, 223)	//	#15 - Solid Platform
	SpriteFrame(-16, -16, 32, 32, 190, 223)	//	#16 - Vanishing Platform
	SpriteFrame(-16, -16, 32, 32, 223, 223)	//	#17 - Solid Platform

	// 2/3 width
	SpriteFrame(-8, -16, 16, 32, 190, 223)	//	#18 - Vanishing Platform
	SpriteFrame(-8, -16, 16, 32, 223, 223)	//	#19 - Solid Platform
	SpriteFrame(-8, -16, 16, 32, 190, 223)	//	#20 - Vanishing Platform
	SpriteFrame(-8, -16, 16, 32, 223, 223)	//	#21 - Solid Platform

	// 1/3 width
	SpriteFrame(-4, -16, 8, 32, 190, 223)	//	#22 - Vanishing Platform
	SpriteFrame(-4, -16, 8, 32, 223, 223)	//	#23 - Solid Platform
	SpriteFrame(-4, -16, 8, 32, 190, 223)	//	#24 - Vanishing Platform
	SpriteFrame(-4, -16, 8, 32, 223, 223)	//	#25 - Solid Platform

	AddEditorVariable("type")
	SetActiveVariable("type")
	AddEnumVariable("1 Block", ONE_BLOCK)
	AddEnumVariable("2 Blocks", TWO_BLOCKS)
	AddEnumVariable("3-Block Stair", THREE_BLOCK_STAIR)
end sub
