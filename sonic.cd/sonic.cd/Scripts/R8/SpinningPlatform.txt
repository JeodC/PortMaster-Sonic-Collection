//-------------Sonic CD Spinning Platform Script--------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Alias
#alias Object[19].Rotation	:	R8Setup.Rotation


sub ObjectMain
	// All Spinning Platforms are synced together, via the Object in slot 19 which
	// should be the R8 Setup Object
	Object.Rotation = R8Setup.Rotation
	
	// And then bump that rotation as needed
	Object.Rotation += Object.PropertyValue
	
	// Keep the rotation limited to 143, as with the R8 Setup's rotation value as well
	Object.Rotation %= 144
	
end sub


sub ObjectPlayerInteraction
	// Only check for Collision when the Platform is in its normal position
	if Object.Rotation < 121
		PlayerObjectCollision(C_PLATFORM, -16, -5, 16, 11)
	end if
	
end sub


sub ObjectDraw
	if Object.Rotation < 121
		// Draw the normal Platform frame
		DrawSprite(0)
	else
		
		// Use different drawing routines for the Platform's different possible angles
		
		TempValue0 = Object.Rotation
		TempValue0 -= 120
		TempValue0 >>= 1
		
		switch TempValue0
		case 0
		case 6
			DrawSprite(1)
			break
			
		case 1
		case 7
			DrawSprite(2)
			break
			
		case 2
		case 8
			DrawSprite(3)
			break
			
		case 3
		case 9
			Object.Direction = FACING_LEFT
			DrawSpriteFX(4, FX_FLIP, Object.XPos, Object.YPos)
			break
			
		case 4
		case 10
			Object.Direction = FACING_LEFT
			DrawSpriteFX(5, FX_FLIP, Object.XPos, Object.YPos)
			break
			
		case 5
		case 11
			DrawSprite(0)
			break
			
		end switch
	end if
	
end sub


sub ObjectStartup
	LoadSpriteSheet("R8/Objects.gif")
	
	SpriteFrame(-16, -8, 32, 16, 107, 98)	// #0 - Platform Horizontal
	SpriteFrame(-16, -12, 32, 24, 107, 115)	// #1 - Platform Tilted frame 0
	SpriteFrame(-12, -16, 24, 32, 230, 34)	// #2 - Platform Tilted frame 1

	SpriteFrame(-8, -16, 16, 32, 239, 212)	// #3 - Platform Vertical
	SpriteFrame(-12, -16, 24, 32, 230, 34)	// #4 - Platform Tilted frame 0
	SpriteFrame(-16, -12, 32, 24, 107, 115)	// #5 - Platform Tilted frame 1
	
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("R8/Objects.gif")
	SpriteFrame(-16, -8, 32, 16, 107, 98)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "SpinOffset")
end sub
