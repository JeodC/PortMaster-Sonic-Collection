//----------------Sonic CD Amy Rose Script--------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.Timer
// Value1 is unused, likely just XVelocity
#alias Object.Value2	:	Object.YVelocity

// Boss Curtain alias
#alias Object[-1].XPos	:	BossCurtain.XPos

// States
#alias 0	:	AMY_ENTER_SCREEN
#alias 1	:	AMY_JUMP_1
#alias 2	:	AMY_JUMP_2
#alias 3	:	AMY_TRIGGER_FADE
#alias 4	:	AMY_FADE_WALK

// Players
#alias 0	:	PLAYER_SONIC_A

// Collision Sides
#alias 0	:	CSIDE_FLOOR


sub ObjectMain
	switch Object.State
	case AMY_ENTER_SCREEN
		Object.Frame   = Object.Timer
		Object.Frame >>= 2

		Object.Timer++
		Object.Timer %= 24

		Object.XPos  += 0x14000
		ObjectTileGrip(CSIDE_FLOOR, 0, 19, 0)

		TempValue0  = BossCurtain.XPos
		TempValue0 -= 0x600000
		if Object.XPos > TempValue0
			Object.Frame = 6

			Object.XPos = TempValue0
			Object.YVelocity = -0x30000

			Object.State++
		end if
		break
		
	case AMY_JUMP_1
		Object.YVelocity += 0x4000
		Object.YPos += Object.YVelocity
		ObjectTileCollision(CSIDE_FLOOR, 0, 19, 0)
		if Object.YVelocity > 0
			Object.Frame = 7
		end if
		
		if CheckResult == true
			Object.Frame = 6
			Object.YVelocity = -0x30000
			Object.State++
		end if
		break
		
	case AMY_JUMP_2
		Object.YVelocity += 0x4000
		Object.YPos += Object.YVelocity
		ObjectTileCollision(CSIDE_FLOOR, 0, 19, 0)
		if Object.YVelocity > 0
			Object.Frame = 7
		end if
		
		if CheckResult == true
			Object.YVelocity = 0
			Object.State++
		end if
		break
		
	case AMY_TRIGGER_FADE
		Object.Frame   = Object.Timer
		Object.Frame >>= 2

		Object.Timer++
		Object.Timer %= 24

		Object.XPos += 0x14000
		ObjectTileGrip(CSIDE_FLOOR, 0, 19, 0)

		TempValue0  = BossCurtain.XPos
		TempValue0 -= 0x100000
		if Object.XPos > TempValue0
			Object[+1].Type = TypeName[FadeScreen]
			Object.State++
		end if
		break
		
	case AMY_FADE_WALK
		Object.Frame   = Object.Timer
		Object.Frame >>= 2

		Object.Timer++
		Object.Timer %= 24

		Object.XPos += 0x14000
		ObjectTileGrip(CSIDE_FLOOR, 0, 19, 0)
		break
		
	end switch
	
end sub


sub ObjectDraw
	if Stage.PlayerListPos == PLAYER_SONIC_A // PLAYER_SONIC in origins
		DrawSpriteFX(Object.Frame, FX_FLIP, Object.XPos, Object.YPos)
	end if
end sub


sub ObjectStartup
	
	LoadSpriteSheet("R8/Objects3.gif")
	
	// Amy Frames
	
	// 0-5 - Walking Frames
	SpriteFrame(-16, -20, 32, 40, 124, 174)
	SpriteFrame(-16, -20, 32, 40, 157, 174)
	SpriteFrame(-16, -20, 32, 40, 190, 174)
	SpriteFrame(-16, -20, 32, 40, 223, 174)
	SpriteFrame(-16, -20, 32, 40, 138, 215)
	SpriteFrame(-16, -20, 32, 40, 171, 215)
	
	// 6 - Airborne Frame
	SpriteFrame(-13, -20, 26, 40, 204, 215)
	
	// 7 - Hugging Frame
	SpriteFrame(-12, -20, 24, 40, 231, 215)
	
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("R8/Objects3.gif")
	SpriteFrame(-16, -20, 32, 40, 124, 174)
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
