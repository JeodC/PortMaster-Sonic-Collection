//--------------Sonic CD Leaderboards Entry Script------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//
//------------------Only exist in WP7 Versions----------------//

// Aliases
#alias Object.Value0	:	Object.HTimer
#alias Object.Value3	:	Object.Index
#alias Object.Value4	:	Object.VTimer
#alias Object.Value5	:	Object.SavedXPos

// Menu Control Alias
#alias Object[0].State	:	MenuControl.State

// Next and previous LEntry's
#alias Object.State		:	PrevLEntry.State
#alias Object.State		:	NextLEntry.State

// States
#alias 0	:	LENTRY_BLANK
#alias 1	:	LENTRY_MOVE_RIGHT
#alias 2	:	LENTRY_SCROLL_ENTRIES_R
#alias 3	:	LENTRY_STOP_SCROLL_R
#alias 4	:	LENTRY_MOVE_LEFT
#alias 5	:	LENTRY_SCROLL_ENTRIES_L
#alias 6	:	LENTRY_STOP_SCROLL_L
#alias 7	:	LENTRY_MOVE_DOWN
#alias 8	:	LENTRY_SCROLL_ENTRIES_D
#alias 9	:	LENTRY_STOP_SCROLL_D
#alias 10	:	LENTRY_MOVE_UP
#alias 11	:	LENTRY_SCROLL_ENTRIES_U
#alias 12	:	LENTRY_STOP_SCROLL_U

// Menu Control States
#alias 2	:	MENUCONTROL_CONTROLS

// Priority
#alias 1	:	PRIORITY_ACTIVE

// Ink Effect
#alias 2	:	INK_ALPHA

// Text Info type
#alias 0	:	TEXTINFO_TEXTDATA
#alias 1	:	TEXTINFO_TEXTSIZE

// Online Menus
#alias 1	:	ONLINEMENU_LEADERBOARDS

// Property Values
#alias 0	:	ENTRY_1
#alias 1	:	ENTRY_2
#alias 2	:	ENTRY_3
#alias 3	:	ENTRY_4
#alias 4	:	ENTRY_5


sub ObjectMain
	switch Object.State
	case LENTRY_BLANK
		break

	case LENTRY_MOVE_RIGHT
		Object.SavedXPos = Object.XPos
		Object.Priority  = PRIORITY_ACTIVE
		Object.State++
		break

	case LENTRY_SCROLL_ENTRIES_R
		Object.XPos -= 0x300000
		if Object.HTimer < 25
			Object.HTimer++
			if Object.PropertyValue < 4
				if Object.HTimer == 5
					PrevLEntry[+1].State = LENTRY_MOVE_RIGHT
					if Object.PropertyValue == ENTRY_2
						Leaderboard.ID++
						Leaderboard.ID %= 22
						LoadOnlineMenu(ONLINEMENU_LEADERBOARDS)
					end if
				end if
			end if
		else
			Object.HTimer = 0
			Object.iXPos  = Screen.XSize
			Object.XPos  += 0x100000
			Object.State++
		end if
		break

	case LENTRY_STOP_SCROLL_R
		Object.XPos -= 0x300000
		if Object.XPos < Object.SavedXPos
			Object.XPos  = Object.SavedXPos
			Object.State = LENTRY_BLANK
			if Object.PropertyValue == ENTRY_5
				MenuControl.State = MENUCONTROL_CONTROLS
			end if
		end if
		break

	case LENTRY_MOVE_LEFT
		Object.SavedXPos = Object.XPos
		Object.Priority  = PRIORITY_ACTIVE
		Object.State++
		break

	case LENTRY_SCROLL_ENTRIES_L
		Object.XPos += 0x300000
		if Object.HTimer < 25
			Object.HTimer++
			if Object.PropertyValue < 4
				if Object.HTimer == 5
					PrevLEntry[+1].State = LENTRY_MOVE_LEFT
					if Object.PropertyValue == ENTRY_3
						Leaderboard.ID--
						if Leaderboard.ID < 0
							Leaderboard.ID = 21
						end if
						LoadOnlineMenu(ONLINEMENU_LEADERBOARDS)
					end if
				end if
			end if
		else
			Object.HTimer = 0
			Object.iXPos  = -232
			Object.XPos  -= 0x100000
			Object.State++
		end if
		break

	case LENTRY_STOP_SCROLL_L
		Object.XPos += 0x300000
		if Object.XPos > Object.SavedXPos
			Object.XPos  = Object.SavedXPos
			Object.State = LENTRY_BLANK
			if Object.PropertyValue == ENTRY_5
				MenuControl.State = MENUCONTROL_CONTROLS
			end if
		end if
		break

	case LENTRY_MOVE_DOWN
		if Object.VTimer < 95
			Object.VTimer += 5
		end if
		Object.Alpha = 255
		Object.State++
		break

	case LENTRY_SCROLL_ENTRIES_D
		Object.InkEffect = INK_ALPHA
		Object.Alpha = 224
		Object.State++
		break

	case LENTRY_STOP_SCROLL_D
		if Object.Alpha > 0
			Object.Alpha -= 32
			if Object.PropertyValue < 4
				if Object.Alpha == 128
					PrevLEntry[+1].State = LENTRY_MOVE_DOWN
				end if
			end if
		else
			Object.State = LENTRY_BLANK
			if Object.PropertyValue == ENTRY_5
				MenuControl.State = MENUCONTROL_CONTROLS
			end if
		end if
		break

	case LENTRY_MOVE_UP
		if Object.VTimer > 0
			Object.VTimer -= 5
		end if
		Object.Alpha = 255
		Object.State++
		break

	case LENTRY_SCROLL_ENTRIES_U
		Object.InkEffect = INK_ALPHA
		Object.Alpha = 224
		Object.State++
		break

	case LENTRY_STOP_SCROLL_U
		if Object.Alpha > 0
			Object.Alpha -= 32
			if Object.PropertyValue < 4
				if Object.Alpha == 128
					PrevLEntry[+1].State = LENTRY_MOVE_UP
				end if
			end if
		else
			Object.State = LENTRY_BLANK
			if Object.PropertyValue == ENTRY_5
				MenuControl.State = MENUCONTROL_CONTROLS
			end if
		end if
		break

	end switch
end sub


sub ObjectDraw
	DrawSprite(142)

	Object.Index  = Object.PropertyValue
	Object.Index += Object.VTimer
	ArrayPos0 = 0

	GetTextInfo(TempValue1, MENU_1, TEXTINFO_TEXTSIZE, Object.Index, 0)
	TempValue2  = Object.XPos
	TempValue2 += 0x80000
	TempValue3  = 0
	while TempValue3 < 4
		GetTextInfo(TempValue0, MENU_1, TEXTINFO_TEXTDATA, Object.Index, ArrayPos0)
		switch TempValue0
		default
			Object.Frame = 0
			if TempValue0 > 47
				if TempValue0 < 58
					Object.Frame  = TempValue0
					Object.Frame -= 15
				end if
			end if

			if Object.Frame > 0
				Object.Frame += 71
				DrawSpriteXY(Object.Frame, TempValue2, Object.YPos)
			end if
			break

		case 34
			DrawSpriteXY(100, TempValue2, Object.YPos)
			break

		case 39
			DrawSpriteXY(99, TempValue2, Object.YPos)
			break

		case 46
			DrawSpriteXY(98, TempValue2, Object.YPos)
			break

		end switch
		ArrayPos0++
		TempValue3++
		TempValue2 += 0x80000
	loop

	TempValue1 -= 4
	while TempValue1 > 0
		GetTextInfo(TempValue0, MENU_1, TEXTINFO_TEXTDATA, Object.Index, ArrayPos0)
		switch TempValue0
		default
			Object.Frame = 0
			if TempValue0 > 64
				if TempValue0 < 91
					Object.Frame  = TempValue0
					Object.Frame -= 64
				end if
			end if

			if TempValue0 > 96
				if TempValue0 < 123
					Object.Frame  = TempValue0
					Object.Frame -= 54
				end if
			end if

			if TempValue0 > 47
				if TempValue0 < 58
					Object.Frame  = TempValue0
					Object.Frame -= 15
				end if
			end if

			if Object.Frame > 0
				DrawSpriteXY(Object.Frame, TempValue2, Object.YPos)
			end if
			break

		case 34
			DrawSpriteXY(29, TempValue2, Object.YPos)
			break

		case 39
			DrawSpriteXY(28, TempValue2, Object.YPos)
			break

		case 45
			DrawSpriteXY(69, TempValue2, Object.YPos)
			break

		case 46
			DrawSpriteXY(27, TempValue2, Object.YPos)
			break

		end switch
		ArrayPos0++
		TempValue1--
		TempValue2 += 0x80000
	loop

	if Object.State > LENTRY_STOP_SCROLL_L
		DrawSpriteFX(143, FX_INK, Object.XPos, Object.YPos)
	end if
end sub


sub ObjectStartup
	LoadSpriteSheet("LBoards/Leaderboards.gif")
	LoadTextFile(MENU_1, "Data/Game/LeaderboardText.txt", 0)
	Leaderboard.ID = 0
	LoadOnlineMenu(ONLINEMENU_LEADERBOARDS)

	SpriteFrame(0, 0, 8, 10, 118, 298)		// #0 - Blank Space

	// (#1 - #70) - Alphabet
	TempValue1 = 254
	while TempValue1 < 308
		TempValue0 = 127
		while TempValue0 < 253
			SpriteFrame(-3, -5, 8, 10, TempValue0, TempValue1)
			TempValue0 += 9
		loop
		TempValue1 += 11
	loop

	SpriteFrame(0, 0, 8, 10, 118, 298)		// #72 - Blank Space

	// (#73 - #141) Alphabet Highlight
	TempValue1 = 254
	while TempValue1 < 308
		TempValue0 = 1
		while TempValue0 < 127
			SpriteFrame(-3, -5, 8, 10, TempValue0, TempValue1)
			TempValue0 += 9
		loop
		TempValue1 += 11
	loop

	SpriteFrame(0, -10, 232, 21, 1, 309)	// #142 - Button
	SpriteFrame(0, -10, 232, 21, 1, 232)	// #143 - Button Flash
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("LBoards/Leaderboards.gif")

	SpriteFrame(0, -10, 232, 21, 1, 309)	// #142 - Button

	SetVariableAlias(ALIAS_VAR_PROPVAL, "entry")
end sub
