//--------------Sonic CD Button Blocks 1 Script---------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value1			:	Object.MinHeight
#alias Object.Value2			:	Object.YVelocity
#alias Object.Value3			:	Object.MaxHeight

// Push Button aliases
#alias Object.PropertyValue	:	PushButton.Pressed
#alias Object.XPos			:	PushButton.XPos
#alias Object.YPos			:	PushButton.YPos

// HUD alias
#alias Object[24].PropertyValue	:	HUD.CurrentTimePeriod

// Gravity
#alias 0	:	GRAVITY_GROUND

// Player Collision
#alias 1	:	PLAYER_COL_FLOOR
#alias 4	:	PLAYER_COL_ROOF

// Time Periods
#alias 0	:	TIME_PRESENT
#alias 1	:	TIME_PAST
#alias 2	:	TIME_GOOD_FUTURE
#alias 3	:	TIME_BAD_FUTURE


sub ObjectMain
	if PushButton[+1].Pressed == true
		if Object.YVelocity > -0x10000
			Object.YVelocity -= 0x200
		end if
		Object.YPos += Object.YVelocity

		if Object.YPos < Object.MaxHeight
			Object.YPos = Object.MaxHeight
			Object.YVelocity = 0
		end if
	else
		if Object.YVelocity < 0x10000
			Object.YVelocity += 0x200
		end if
		Object.YPos += Object.YVelocity

		if Object.YPos > Object.MinHeight
			Object.YPos = Object.MinHeight
			Object.YVelocity = 0
		end if
	end if

	PushButton[+1].XPos  = Object.XPos
	PushButton[+1].XPos += 0x100000

	PushButton[+1].YPos  = Object.YPos
	PushButton[+1].YPos -= 0x480000
end sub


sub ObjectPlayerInteraction
	PlayerObjectCollision(C_BOX, -32, -1, 0, 64)
	switch CheckResult
	case PLAYER_COL_FLOOR
		Player.YPos += 0x10000
		break

	case PLAYER_COL_ROOF
		if Player.Gravity == GRAVITY_GROUND
			CallFunction(Player_Kill)
		end if
		
	end switch

	PlayerObjectCollision(C_BOX, 0, -73, 32, 64)
	switch CheckResult
	case PLAYER_COL_FLOOR
		Player.YPos += 0x10000
		break

	case PLAYER_COL_ROOF
		if Player.Gravity == GRAVITY_GROUND
			CallFunction(Player_Kill)
		end if
		
	end switch
end sub



sub ObjectDraw
	DrawSprite(0)
	DrawSprite(1)
	DrawSprite(2)
	DrawSprite(3)
	DrawSprite(4)
	DrawSprite(5)
end sub


sub ObjectStartup
	switch HUD.CurrentTimePeriod
	case TIME_PRESENT
		LoadSpriteSheet("R4/Objects.gif")
		TempValue0 = 163
		TempValue1 = 1
		TempValue2 = 196
		break

	case TIME_PAST
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 157
		TempValue2 = 34
		break

	case TIME_GOOD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 190
		TempValue2 = 34
		break

	case TIME_BAD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 223
		TempValue2 = 34
		break

	end switch

	// R4 Blocks
	SpriteFrame(0, -64, 32, 32, TempValue0, TempValue1)
	SpriteFrame(0, -32, 32, 32, TempValue0, TempValue1)
	SpriteFrame(-32, 0, 32, 32, TempValue0, TempValue1)
	SpriteFrame(0, 0, 32, 32, TempValue0, TempValue1)
	SpriteFrame(-32, 32, 32, 32, TempValue0, TempValue1)
	SpriteFrame(0, 32, 32, 32, TempValue0, TempValue1)

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Button Blocks 1]
			Object[ArrayPos0].MinHeight  = Object[ArrayPos0].YPos
			Object[ArrayPos0].MaxHeight  = Object[ArrayPos0].YPos
			Object[ArrayPos0].MaxHeight -= 0x800000
		end if
		ArrayPos0++
	loop
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
	DrawSprite(1)
	DrawSprite(2)
	DrawSprite(3)
	DrawSprite(4)
	DrawSprite(5)
end sub


sub RSDKLoad
	CallFunction(EditorHelpers_FindTimePeriod)
	switch CheckResult
	case TIME_PRESENT
		LoadSpriteSheet("R4/Objects.gif")
		TempValue0 = 163
		TempValue1 = 1
		TempValue2 = 196
		break

	case TIME_PAST
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 157
		TempValue2 = 34
		break

	case TIME_GOOD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 190
		TempValue2 = 34
		break

	case TIME_BAD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 223
		TempValue2 = 34
		break

	end switch

	// R4 Blocks
	SpriteFrame(0, -64, 32, 32, TempValue0, TempValue1)
	SpriteFrame(0, -32, 32, 32, TempValue0, TempValue1)
	SpriteFrame(-32, 0, 32, 32, TempValue0, TempValue1)
	SpriteFrame(0, 0, 32, 32, TempValue0, TempValue1)
	SpriteFrame(-32, 32, 32, 32, TempValue0, TempValue1)
	SpriteFrame(0, 32, 32, 32, TempValue0, TempValue1)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
