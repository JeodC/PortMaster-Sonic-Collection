//----------------Sonic CD Block Rows A Script----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value1			:	Object.XVelocity
#alias Object.Value2			:	Object.OnObject

// HUD Alias
#alias Object[24].PropertyValue	:	HUD.CurrentTimePeriod

// Time Periods
#alias 0	:	TIME_PRESENT
#alias 1	:	TIME_PAST
#alias 2	:	TIME_GOOD_FUTURE
#alias 3	:	TIME_BAD_FUTURE

// Priority
#alias 0	:	PRIORITY_BOUNDS

// Collision
#alias 1	:	PLAYER_COL_FLOOR
#alias 2	:	PLAYER_COL_LWALL
#alias 3	:	PLAYER_COL_RWALL
#alias 4	:	PLAYER_COL_ROOF

// Gravity
#alias 1	:	GRAVITY_AIR


sub ObjectMain
	Object.XVelocity += 0x8000
	Object.XVelocity %= 0x900000

	Object.Priority   = PRIORITY_BOUNDS
end sub


sub ObjectPlayerInteraction
	TempValue0   = Object.XPos

	Object.XPos -= 0xA00000
	Object.XPos -= Object.XVelocity

	TempValue1   = 0
	TempValue2   = 0
	TempValue3   = 0

	while TempValue1 < 4
		PlayerObjectCollision(C_BOX, -32, -64, 32, 0)
		switch CheckResult
		case PLAYER_COL_FLOOR
			Player.XPos -= 0x8000
			if Player.Speed == 0
				if Object.OnObject == false
					Object.OnObject = true
					// Round these values
					Player.XPos		 &= 0xFFFF0000
					Object.XVelocity &= 0xFFFF0000
				end if
			else
				Object.OnObject = false
			end if
			break

		case PLAYER_COL_LWALL
			TempValue2 = true
			break

		case PLAYER_COL_RWALL
			TempValue3 = true
			break

		end switch
		Object.XPos += 0x900000
		TempValue1++
	loop

	Object.XPos  = TempValue0
	Object.XPos -= 0x1300000
	Object.XPos += Object.XVelocity

	TempValue1 = 0
	while TempValue1 < 4
		PlayerObjectCollision(C_BOX, -32, 0, 32, 64)
		switch CheckResult
		case PLAYER_COL_FLOOR
			Player.XPos += 0x8000
			if Player.Speed == 0
				if Object.OnObject == false
					Object.OnObject = true
					// Truncate these values
					Player.XPos		 &= 0xFFFF0000
					Object.XVelocity &= 0xFFFF0000
				end if
			else
				Object.OnObject = false
			end if
			break

		case PLAYER_COL_LWALL
			TempValue2 = true
			break

		case PLAYER_COL_RWALL
			TempValue3 = true
			break

		end switch
		Object.XPos += 0x900000
		TempValue1++
	loop

	if Player.Gravity == GRAVITY_AIR
		Object.OnObject = false
	end if
	
	Object.XPos = TempValue0
	PlayerObjectCollision(C_BOX, -224, -128, -192, 128)
	switch CheckResult
	case PLAYER_COL_LWALL
		TempValue2 = true
		break

	case PLAYER_COL_RWALL
		TempValue3 = true
		break

	end switch

	PlayerObjectCollision(C_BOX, 192, -128, 224, 128)
	switch CheckResult
	case PLAYER_COL_LWALL
		TempValue2 = true
		break

	case PLAYER_COL_RWALL
		TempValue3 = true
		break

	end switch

	if TempValue2 == true
		if TempValue3 == true
			CallFunction(Player_Kill)
		end if
	end if
end sub


sub ObjectDraw
	TempValue0  = Object.XPos
	TempValue0 -= 0xA00000
	TempValue0 -= Object.XVelocity

	TempValue1  = Object.YPos
	TempValue1 -= 0x200000
	TempValue2  = 0
	while TempValue2 < 4
		DrawSpriteXY(0, TempValue0, TempValue1)
		DrawSpriteXY(1, TempValue0, TempValue1)
		DrawSpriteXY(2, TempValue0, TempValue1)
		DrawSpriteXY(3, TempValue0, TempValue1)
		TempValue0 += 0x900000
		TempValue2++
	loop

	TempValue0  = Object.XPos
	TempValue0 -= 0x1300000
	TempValue0 += Object.XVelocity

	TempValue1  = Object.YPos
	TempValue1 += 0x200000
	TempValue2  = 0
	while TempValue2 < 4
		DrawSpriteXY(0, TempValue0, TempValue1)
		DrawSpriteXY(1, TempValue0, TempValue1)
		DrawSpriteXY(2, TempValue0, TempValue1)
		DrawSpriteXY(3, TempValue0, TempValue1)
		TempValue0 += 0x900000
		TempValue2++
	loop
end sub


sub ObjectStartup
	switch HUD.CurrentTimePeriod
	case TIME_PRESENT
		LoadSpriteSheet("R4/Objects.gif")
		TempValue0 = 163
		TempValue1 = 1
		TempValue2 = 196
		break
		
	case TIME_PAST
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 157
		TempValue2 = 34
		break

	case TIME_GOOD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 190
		TempValue2 = 34
		break

	case TIME_BAD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 223
		TempValue2 = 34
		break

	end switch

	// Cube of blocks
	SpriteFrame(-32, -32, 32, 32, TempValue0, TempValue1)
	SpriteFrame(0, -32, 32, 32, TempValue0, TempValue1)
	SpriteFrame(-32, 0, 32, 32, TempValue0, TempValue1)
	SpriteFrame(0, 0, 32, 32, TempValue0, TempValue1)
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	TempValue0  = Object.XPos
	TempValue0 -= 0xA00000

	TempValue1  = Object.YPos
	TempValue1 -= 0x200000
	TempValue2  = 0
	while TempValue2 < 4
		DrawSpriteXY(0, TempValue0, TempValue1)
		DrawSpriteXY(1, TempValue0, TempValue1)
		DrawSpriteXY(2, TempValue0, TempValue1)
		DrawSpriteXY(3, TempValue0, TempValue1)
		TempValue0 += 0x900000
		TempValue2++
	loop
	TempValue3  = TempValue0
	TempValue3 -= 0x900000
	
	TempValue0  = Object.XPos
	TempValue0 -= 0x1300000
	DrawArrow(Object.XPos, TempValue1, TempValue0, TempValue1, 255, 128, 0)

	TempValue1  = Object.YPos
	TempValue1 += 0x200000
	TempValue2  = 0
	while TempValue2 < 4
		DrawSpriteXY(0, TempValue0, TempValue1)
		DrawSpriteXY(1, TempValue0, TempValue1)
		DrawSpriteXY(2, TempValue0, TempValue1)
		DrawSpriteXY(3, TempValue0, TempValue1)
		TempValue0 += 0x900000
		TempValue2++
	loop
	DrawArrow(Object.XPos, TempValue1, TempValue0, TempValue1, 255, 128, 0)

	if Editor.ShowGizmos == true
		Editor.DrawingOverlay = true
		TempValue2  = Object.YPos
		TempValue2 -= 0x200000
		DrawArrow(TempValue3, TempValue2, Object.XPos, TempValue2, 255, 255, 255)
		TempValue2  = Object.XPos
		TempValue2 -= 0x1300000
		DrawArrow(TempValue2, TempValue1, Object.XPos, TempValue1, 255, 255, 255)
		TempValue0  = Object.XPos
		TempValue0 -= 0x1300000
		TempValue1  = Object.YPos
		TempValue1 -= 0x400000
		DrawRectOutline(TempValue0, TempValue1, 0x240, 0x80, 255, 255, 0, 255)
		Editor.DrawingOverlay = false
	end if
end sub


sub RSDKLoad
	CallFunction(EditorHelpers_FindTimePeriod)
	switch CheckResult
	case TIME_PRESENT
		LoadSpriteSheet("R4/Objects.gif")
		TempValue0 = 163
		TempValue1 = 1
		TempValue2 = 196
		break
		
	case TIME_PAST
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 157
		TempValue2 = 34
		break

	case TIME_GOOD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 190
		TempValue2 = 34
		break

	case TIME_BAD_FUTURE
		LoadSpriteSheet("R4/Objects2.gif")
		TempValue0 = 1
		TempValue1 = 223
		TempValue2 = 34
		break

	end switch

	// Cube of blocks
	SpriteFrame(-32, -32, 32, 32, TempValue0, TempValue1)
	SpriteFrame(0, -32, 32, 32, TempValue0, TempValue1)
	SpriteFrame(-32, 0, 32, 32, TempValue0, TempValue1)
	SpriteFrame(0, 0, 32, 32, TempValue0, TempValue1)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub

