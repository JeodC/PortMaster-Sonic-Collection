//------------------Sonic CD L Blocks Script------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.Timer
#alias Object.Value1	:	Object.Rotation

// States
#alias 0	:	LBLOCKS_INACTIVE
#alias 1	:	LBLOCKS_STEP_LEFT
#alias 2	:	LBLOCKS_STEP_RIGHT
#alias 3	:	LBLOCKS_ROTATE_LEFT
#alias 4	:	LBLOCKS_ROTATE_RIGHT

// Player Collision
#alias 1	:	PLAYER_COL_FLOOR

// Stage SFX
#alias 8	:	SFX_S_SWITCH


sub ObjectMain
	switch Object.State
	case LBLOCKS_STEP_LEFT
		if Object.Timer < 14
			Object.Timer++
		else
			Object.Timer = 0
			Object.State = LBLOCKS_ROTATE_LEFT
			PlayStageSfx(SFX_S_SWITCH, false)
		end if
		break

	case LBLOCKS_STEP_RIGHT
		if Object.Timer < 14
			Object.Timer++
		else
			Object.Timer = 0
			Object.State = LBLOCKS_ROTATE_RIGHT
			PlayStageSfx(SFX_S_SWITCH, false)
		end if
		break

	case LBLOCKS_ROTATE_LEFT
		if Object.Timer < 8
			Object.Timer++
			Object.Rotation++
		else
			Object.Timer = 0
			Object.State = LBLOCKS_INACTIVE
			PlayStageSfx(SFX_S_SWITCH, false)
		end if
		break

	case LBLOCKS_ROTATE_RIGHT
		if Object.Timer < 8
			Object.Timer++
			Object.Rotation--
		else
			Object.Timer = 0
			Object.State = LBLOCKS_INACTIVE
			PlayStageSfx(SFX_S_SWITCH, false)
		end if
		break
	end switch
end sub


sub ObjectPlayerInteraction
	PlayerObjectCollision(C_BOX, -16, -16, 16, 16)
	
	if Object.State == LBLOCKS_INACTIVE
		TempValue1 = CheckResult
	else
		TempValue1 = 1
	end if

	if Object.State < LBLOCKS_ROTATE_LEFT
		TempValue0   = Object.Rotation
		TempValue0 >>= 3
		switch TempValue0
		case 0
			PlayerObjectCollision(C_BOX, -16, -48, 16, -16)
			PlayerObjectCollision(C_BOX, -48, -16, -16, 16)
			if CheckResult == PLAYER_COL_FLOOR
				if TempValue1 == 0
					Object.State = LBLOCKS_STEP_LEFT
				end if
			end if
			break

		case 1
			PlayerObjectCollision(C_BOX, -16, 16, 16, 48)
			PlayerObjectCollision(C_BOX, -48, -16, -16, 16)
			if CheckResult == PLAYER_COL_FLOOR
				if TempValue1 == 0
					Object.State = LBLOCKS_STEP_LEFT
				end if
			end if
			break

		case 2
			PlayerObjectCollision(C_BOX, -16, 16, 16, 48)
			PlayerObjectCollision(C_BOX, 16, -16, 48, 16)
			if CheckResult == PLAYER_COL_FLOOR
				if TempValue1 == 0
					Object.State = LBLOCKS_STEP_RIGHT
				end if
			end if
			break

		case 3
			PlayerObjectCollision(C_BOX, -16, -48, 16, -16)
			PlayerObjectCollision(C_BOX, 16, -16, 48, 16)
			if CheckResult == PLAYER_COL_FLOOR
				if TempValue1 == 0
					Object.State = LBLOCKS_STEP_RIGHT
				end if
			end if
			break

		end switch
	end if
end sub


sub ObjectDraw
	TempValue0 = Object.Rotation
	if Object.State == LBLOCKS_ROTATE_RIGHT
		TempValue0 += 3
	end if

	TempValue0 >>= 2
	TempValue0  *= 3

	TempValue0++
	DrawSprite(TempValue0)
	
	TempValue0++
	DrawSprite(TempValue0)

	TempValue0++
	DrawSprite(TempValue0)

	DrawSprite(0)
end sub


sub ObjectStartup
	LoadSpriteSheet("R4/Objects3.gif")

	SpriteFrame(-8, -8, 16, 16, 67, 46)		// #0  - LBlock Center
	// Rotation Set 1
	SpriteFrame(-16, -16, 32, 32, 34, 34)	// #1  - LBlock 1
	SpriteFrame(-48, -16, 32, 32, 1, 34)	// #2  - LBlock 2
	SpriteFrame(-16, -48, 32, 32, 34, 1)	// #3  - LBlock 3
	SpriteFrame(-22, -22, 44, 44, 157, 1)	// #4  - LBlock Diagonal 1
	SpriteFrame(-44, -44, 44, 44, 112, 1)	// #5  - LBlock Diagonal 2
	SpriteFrame(-44, 0, 44, 44, 202, 1)		// #6  - LBlock Diagonal 3
	// Rotation Set 2
	SpriteFrame(-16, -16, 32, 32, 34, 1)	// #7  - LBlock 1
	SpriteFrame(-48, -16, 32, 32, 1, 1)		// #8  - LBlock 2
	SpriteFrame(-16, 16, 32, 32, 34, 34)	// #9  - LBlock 3
	SpriteFrame(-22, -22, 44, 44, 112, 1)	// #10 - LBlock Diagonal 1
	SpriteFrame(-44, 0, 44, 44, 67, 1)		// #11 - LBlock Diagonal 2
	SpriteFrame(0, 0, 44, 44, 157, 1)		// #12 - LBlock Diagonal 3
	// Rotation Set 3
	SpriteFrame(-16, -16, 32, 32, 1, 1)		// #13 - LBlock 1
	SpriteFrame(-16, 16, 32, 32, 1, 34)		// #14 - LBlock 2
	SpriteFrame(16, -16, 32, 32, 34, 1)		// #15 - LBlock 3
	SpriteFrame(-22, -22, 44, 44, 67, 1)	// #16 - LBlock Diagonal 1
	SpriteFrame(0, -44, 44, 44, 112, 1)		// #17 - LBlock Diagonal 2
	SpriteFrame(0, 0, 44, 44, 202, 1)		// #18 - LBlock Diagonal 3
	// Rotation Set 4
	SpriteFrame(-16, -16, 32, 32, 1, 34)	// #19 - LBlock 1
	SpriteFrame(-16, -48, 32, 32, 1, 1)		// #20 - LBlock 2
	SpriteFrame(16, -16, 32, 32, 34, 34)	// #21 - LBlock 3
	SpriteFrame(-22, -22, 44, 44, 202, 1)	// #22 - LBlock Diagonal 1
	SpriteFrame(-44, -44, 44, 44, 67, 1)	// #23 - LBlock Diagonal 2
	SpriteFrame(0, -44, 44, 44, 157, 1)		// #24 - LBlock Diagonal 3

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[L Blocks]
			Object[ArrayPos0].Rotation   = Object[ArrayPos0].PropertyValue
			Object[ArrayPos0].Rotation <<= 3
		end if
		ArrayPos0++
	loop
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	TempValue0   = Object.PropertyValue
	TempValue0 <<= 3

	TempValue0 >>= 2
	TempValue0  *= 3

	TempValue0++
	DrawSprite(TempValue0)
	
	TempValue0++
	DrawSprite(TempValue0)

	TempValue0++
	DrawSprite(TempValue0)

	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("R4/Objects3.gif")

	SpriteFrame(-8, -8, 16, 16, 67, 46)		// #0  - LBlock Center
	// Rotation Set 1
	SpriteFrame(-16, -16, 32, 32, 34, 34)	// #1  - LBlock 1
	SpriteFrame(-48, -16, 32, 32, 1, 34)	// #2  - LBlock 2
	SpriteFrame(-16, -48, 32, 32, 34, 1)	// #3  - LBlock 3
	SpriteFrame(-22, -22, 44, 44, 157, 1)	// #4  - LBlock Diagonal 1
	SpriteFrame(-44, -44, 44, 44, 112, 1)	// #5  - LBlock Diagonal 2
	SpriteFrame(-44, 0, 44, 44, 202, 1)		// #6  - LBlock Diagonal 3
	// Rotation Set 2
	SpriteFrame(-16, -16, 32, 32, 34, 1)	// #7  - LBlock 1
	SpriteFrame(-48, -16, 32, 32, 1, 1)		// #8  - LBlock 2
	SpriteFrame(-16, 16, 32, 32, 34, 34)	// #9  - LBlock 3
	SpriteFrame(-22, -22, 44, 44, 112, 1)	// #10 - LBlock Diagonal 1
	SpriteFrame(-44, 0, 44, 44, 67, 1)		// #11 - LBlock Diagonal 2
	SpriteFrame(0, 0, 44, 44, 157, 1)		// #12 - LBlock Diagonal 3
	// Rotation Set 3
	SpriteFrame(-16, -16, 32, 32, 1, 1)		// #13 - LBlock 1
	SpriteFrame(-16, 16, 32, 32, 1, 34)		// #14 - LBlock 2
	SpriteFrame(16, -16, 32, 32, 34, 1)		// #15 - LBlock 3
	SpriteFrame(-22, -22, 44, 44, 67, 1)	// #16 - LBlock Diagonal 1
	SpriteFrame(0, -44, 44, 44, 112, 1)		// #17 - LBlock Diagonal 2
	SpriteFrame(0, 0, 44, 44, 202, 1)		// #18 - LBlock Diagonal 3
	// Rotation Set 4
	SpriteFrame(-16, -16, 32, 32, 1, 34)	// #19 - LBlock 1
	SpriteFrame(-16, -48, 32, 32, 1, 1)		// #20 - LBlock 2
	SpriteFrame(16, -16, 32, 32, 34, 34)	// #21 - LBlock 3
	SpriteFrame(-22, -22, 44, 44, 202, 1)	// #22 - LBlock Diagonal 1
	SpriteFrame(-44, -44, 44, 44, 67, 1)	// #23 - LBlock Diagonal 2
	SpriteFrame(0, -44, 44, 44, 157, 1)		// #24 - LBlock Diagonal 3

	AddEditorVariable("block set")
	SetActiveVariable("block set")
	AddEnumVariable("Set 1", 0)
	AddEnumVariable("Set 2", 1)
	AddEnumVariable("Set 3", 2)
	AddEnumVariable("Set 4", 3)
end sub
