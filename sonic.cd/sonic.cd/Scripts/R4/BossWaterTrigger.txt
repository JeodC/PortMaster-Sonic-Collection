//-------------Sonic CD Boss Water Trigger Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.WaterEntity
#alias Object.YPos		:	Water.YPos

// Property Values
#alias 0	:	BOSSWATERTRIGGER_RAISE_WATER
#alias 1	:	BOSSWATERTRIGGER_SET_Y_BOUNDS


sub ObjectMain
	switch Object.PropertyValue
	case BOSSWATERTRIGGER_RAISE_WATER
		if Player.XPos > Object.XPos
			// Y - 1172 in map coords
			if Stage.NewWaterLevel > 0x4940000
				Stage.NewWaterLevel = 0x4940000
				
				ArrayPos0 = Object.WaterEntity

				Water[ArrayPos0].YPos   = Screen.YOffset
				Water[ArrayPos0].YPos  += 256
				Water[ArrayPos0].YPos <<= 16

				Stage.YBoundary2 = 1496
			end if
		else
			// Y - 3072 in map coords
			if Stage.NewWaterLevel < 0xC000000
				Stage.NewWaterLevel = 0xC000000
				Stage.NewYBoundary2 = 2304
			end if
			
			ArrayPos0 = Object.WaterEntity

			TempValue0   = Screen.YOffset
			TempValue0  += 256
			TempValue0 <<= 16
			if Water[ArrayPos0].YPos >= TempValue0
				Water[ArrayPos0].YPos = 0xC000000
			end if
		end if
		break

	case BOSSWATERTRIGGER_SET_Y_BOUNDS
		if Player.XPos > Object.XPos
			Stage.NewYBoundary2 = 1448
		else
			Stage.NewYBoundary2 = 1496
		end if
		break
	end switch
end sub


sub ObjectStartup
	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Water]
			TempValue0 = ArrayPos0
		end if
		ArrayPos0++
	loop

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[BossWaterTrigger]
			Object[ArrayPos0].WaterEntity = TempValue0
		end if
		ArrayPos0++
	loop
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
