//----------------Sonic CD Message Text Script----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value1	:	Object.Index

// Text Info type
#alias 0	:	TEXTINFO_TEXTDATA
#alias 1	:	TEXTINFO_TEXTSIZE


sub ObjectDraw

	ArrayPos0 = 0
	
	// Get how long the target text line is
	GetTextInfo(TempValue1, MENU_1, TEXTINFO_TEXTSIZE, Object.Index, 0)
	
	// The text should be just about aligned to the center, so do some maths based on the character length to make that happen
	TempValue2   = TempValue1
	TempValue2 <<= 18
	FlipSign(TempValue2)
	TempValue2  += Object.XPos
	
	// Loop through all the characters in the message
	while TempValue1 > 0
	
		// Get the actual character at the current char pos
		GetTextInfo(TempValue0, MENU_1, TEXTINFO_TEXTDATA, Object.Index, ArrayPos0)
		
		Object.Frame = 0
		
		// Take uppercase letters and turn them into a matching Sprite Frame
		if TempValue0 > 64
			if TempValue0 < 91
				Object.Frame  = TempValue0
				Object.Frame -= 64
			end if
		end if
		
		// Take lowercase letters and turn them into a matching Sprite Frame
		if TempValue0 > 96
			if TempValue0 < 123
				Object.Frame  = TempValue0
				Object.Frame -= 96
			end if
		end if
		
		// Notably, the space character (32) is skipped over here, as it shouldn't draw anything
		
		if Object.Frame > 0
			// The the letter, but only if it's a valid one
			DrawSpriteXY(Object.Frame, TempValue2, Object.YPos)
		end if
		
		// Move to the next letter
		ArrayPos0++
		
		// And decrease the remaining letter count, as well
		TempValue1--
		
		// Each letter should be spaced 8 pixels apart
		TempValue2 += 0x80000
	loop
end sub


sub ObjectStartup
	LoadSpriteSheet("Secrets/SecretMenus.gif")

	SpriteFrame(0, 0, 0, 0, 0, 0)		// #0 - Dummy Sprite
	
	// #1 -> #26 - Alphabet
	// (Using loops to automate Sprite Framing)
	TempValue1 = 49
	while TempValue1 < 100
		TempValue0 = 37
		while TempValue0 < 136
			SpriteFrame(-3, -6, 8, 16, TempValue0, TempValue1)
			TempValue0 += 9
		loop

		TempValue1 += 17
	loop
	
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	TempValue0 = 0
	while TempValue0 < 4
		DrawSprite(TempValue0)
		TempValue0++
	loop
end sub


sub RSDKLoad
	LoadSpriteSheet("Secrets/SecretMenus.gif")
	SpriteFrame(0, -6, 8, 16, 109, 117)
	SpriteFrame(9, -6, 8, 16, 73, 100)
	SpriteFrame(18, -6, 8, 16, 46, 134)
	SpriteFrame(27, -6, 8, 16, 109, 117)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
