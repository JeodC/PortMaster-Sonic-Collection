//----------------Sonic CD Tails Unlock Script----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.Timer

// States
#alias 0	:	TAILSUNLOCK_SETUP
#alias 1	:	TAILSUNLOCK_FADE_IN
#alias 2	:	TAILSUNLOCK_CONTROL_DELAY
#alias 3	:	TAILSUNLOCK_CONTROL_CHECK
#alias 4	:	TAILSUNLOCK_EXIT

// Stage SFX
#alias 41	:	SFX_S_EVENT

// Presentation Stage
#alias 1	:	STAGE_P_MENU

// Engine callbacks
#alias 1	:	ENGINE_MAINGAME
#alias 8	:	ENGINE_WAIT


sub ObjectMain
	switch Object.State
	case TAILSUNLOCK_SETUP
		if Object.Timer < 16
			// Yield on the black screen for a few moments
			Object.Timer++
		else
			if Stage.DebugMode == false
				// Set the All Stages Clear Achievement
				SetAchievement(7, 100)
			end if

			Object.Timer = 288
			Object.State++
		end if
		
		// Solid Black screen
		SetScreenFade(0, 0, 0, 255)
		break

	case TAILSUNLOCK_FADE_IN
		if Object.Timer > 0
			Object.Timer -= 8
		else
			// Fully faded in now, play the special SFX
			PlayStageSfx(SFX_S_EVENT, false)
			
			Object.State++
		end if
		
		SetScreenFade(0, 0, 0, Object.Timer)
		break

	case TAILSUNLOCK_CONTROL_DELAY
		if Object.Timer < 60
			Object.Timer++
		else
			Object.Timer = 0
			Object.State++
		end if
		break

	case TAILSUNLOCK_CONTROL_CHECK
		if KeyPress[0].ButtonA == true
			Object.State++
		end if

		if KeyPress[0].ButtonB == true
			Object.State++
		end if

		if KeyPress[0].ButtonC == true
			Object.State++
		end if

		if KeyPress[0].Start == true
			Object.State++
		end if

		CheckTouchRect(0, 0, Screen.XSize, Screen.YSize)
		if CheckResult > -1
			Object.State++
		end if

		if Object.Timer < 300
			Object.Timer++
		else
			Object.Timer = 0
			Object.State = TAILSUNLOCK_EXIT
		end if
		break

	case TAILSUNLOCK_EXIT
		if Object.Timer < 288
			// Fade the screen out
			Object.Timer += 8
		else
#platform: Use_Origins
			Engine.State = ENGINE_WAIT
#endplatform

#platform: Use_Decomp
			// Actually can we not softlock the game thanks
			if Engine.State == ENGINE_WAIT
				Engine.State = ENGINE_MAINGAME
				Stage.ActiveList = PRESENTATION_STAGE
				Stage.ListPos = STAGE_P_MENU
				LoadStage()
			end if
#endplatform

#platform: Use_Standalone
			// The Player's looked enough at the screen by now, send them back to the Menu
			Stage.ActiveList = PRESENTATION_STAGE
			Stage.ListPos = STAGE_P_MENU
			LoadStage()
#endplatform
		end if
		
		TempValue0 = Object.Timer
		if TempValue0 > 255
			TempValue0 = 255
		end if
		SetScreenFade(0, 0, 0, TempValue0)
		break

	end switch
end sub


sub ObjectDraw

	// The scene doesn't have a background, so draw a black rectangle one here
	DrawRect(0, 0, Screen.XSize, Screen.YSize, 0, 0, 0, 255)
	
	// And then draw the actual See You Next Game sprite
	DrawSpriteScreenXY(0, Screen.CenterX, Screen.CenterY)
	
end sub


sub ObjectStartup
	LoadSpriteSheet("Secrets/SecretImages.gif")
	
	SpriteFrame(-117, -36, 234, 64, 0, 900)		// #0 - See You Next Game
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Secrets/SecretImages.gif")
	SpriteFrame(-117, -36, 234, 64, 0, 900)		// #0 - See You Next Game

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
