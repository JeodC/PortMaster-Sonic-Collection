//---------------Sonic CD Tile Trigger V Script---------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// This is a chunk swapper for loops

// Aliases
#alias Object.Value0		:	Object.HitboxTop
#alias Object.Value1		:	Object.HitboxBottom
#alias Object.Value2		:	Object.ChunkX
#alias Object.Value3		:	Object.ChunkY
#alias Object.Value4		:	Object.NextChunkX
#alias Object.Value5		:	Object.NextChunkY
#alias Object.Value6		:	Object.StartupChunk
#alias Object.Value7		:	Object.NextChunk

#alias Object.PropertyValue	:	Object.Size

// Helper aliases
#alias Object.PropertyValue	:	Helper.StoredChunk	// This is a blank object

// Property Values
#alias 0	:	STARTUP_CHUNK
#alias 1	:	NEXT_CHUNK


sub ObjectPlayerInteraction
	PlayerObjectCollision(C_TOUCH, -12, Object.HitboxTop, 12, Object.HitboxBottom)
	if CheckResult == true

		if Helper[+1].StoredChunk == STARTUP_CHUNK
			TempValue0 = Object.StartupChunk
		else
			TempValue0 = Object.NextChunk
		end if

		SetTileLayerEntry(TempValue0, 0, Object.ChunkX, Object.ChunkY)
		TempValue0++

		SetTileLayerEntry(TempValue0, 0, Object.NextChunkX, Object.ChunkY)
		TempValue0++

		SetTileLayerEntry(TempValue0, 0, Object.ChunkX, Object.NextChunkY)
		TempValue0++

		SetTileLayerEntry(TempValue0, 0, Object.NextChunkX, Object.NextChunkY)
		TempValue0++
	end if
end sub


sub ObjectStartup
	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Tile Trigger V]
			ArrayPos1 = ArrayPos0
			ArrayPos1++	// This should always be a blank object
			Object[ArrayPos0].HitboxTop     = Object[ArrayPos0].PropertyValue
			Object[ArrayPos0].HitboxTop   <<= 3
			Object[ArrayPos0].HitboxBottom  = Object[ArrayPos0].HitboxTop
			FlipSign(Object[ArrayPos0].HitboxTop)
			Object[ArrayPos0].HitboxTop    -= 8
			Object[ArrayPos0].HitboxBottom += 8

			Object[ArrayPos0].ChunkX   = Object[ArrayPos1].iXPos
			Object[ArrayPos0].ChunkX >>= 7

			Object[ArrayPos0].NextChunkX = Object[ArrayPos0].ChunkX
			Object[ArrayPos0].NextChunkX++

			Object[ArrayPos0].ChunkY   = Object[ArrayPos1].iYPos
			Object[ArrayPos0].ChunkY >>= 7

			Object[ArrayPos0].NextChunkY = Object[ArrayPos0].ChunkY
			Object[ArrayPos0].NextChunkY++

			GetTileLayerEntry(Object[ArrayPos0].StartupChunk, 0, Object[ArrayPos0].ChunkX, Object[ArrayPos0].ChunkY)

			Object[ArrayPos0].NextChunk  = Object[ArrayPos0].StartupChunk
			Object[ArrayPos0].NextChunk += 4
		end if
		ArrayPos0++
	loop
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	TempValue0 = Object.PropertyValue
	TempValue0 <<= 19
	FlipSign(TempValue0)
	TempValue0 += Object.YPos
	TempValue0 += 0x80000

	TempValue4 = 0
	while TempValue4 < Object.PropertyValue
		DrawSpriteXY(0, Object.XPos, TempValue0)
		TempValue0 += 0x100000
		TempValue4++
	loop

	if Object.PropertyValue == 0
		DrawSprite(0)
	end if

	if Editor.ShowGizmos == true
		DrawArrow(Object.XPos, Object.YPos, Object[+1].XPos, Object[+1].YPos, 255, 255, 0)
	end if
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-8, -8, 16, 16, 173, 67)		// #0 - "T" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "size")
end sub
