//---------------Sonic CD Crumble Ledge 2 Script--------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.Timer
#alias Object.Value1	:	Object.YVelocity

// States
#alias 0	:	CLEDGE2_IDLE
#alias 1	:	CLEDGE2_DEBRIS
#alias 2	:	CLEDGE2_RESTORE	// Unused

// Stage SFX
#alias 0	:	SFX_S_CRUMBLE

// Priority
#alias 0	:	PRIORITY_BOUNDS


sub ObjectMain
	switch Object.State
	case CLEDGE2_DEBRIS
		if Object.Timer > 0
			Object.Timer--
		else
			Object.YVelocity += 0x5000
			Object.YPos += Object.YVelocity
		end if
		if Object.OutOfBounds == true
			Object.Type = TypeName[Blank Object]
		end if
		break

	case CLEDGE2_RESTORE

		if Object.OutOfBounds == true
			Object.State = CLEDGE2_IDLE
			Object.Priority = PRIORITY_BOUNDS
		end if

		break
	end switch
end sub


sub ObjectPlayerInteraction
	switch Object.PropertyValue
	case 0
		if Object.State == CLEDGE2_IDLE
			PlayerObjectCollision(C_PLATFORM, -40, -24, 40, -8)
			if CheckResult == true
				Object.Priority = PRIORITY_BOUNDS
				Object.Type = TypeName[Blank Object]
				// TempValue0	:	Debris chunk value
				// TempValue1	:	Ledge section value
				// TempValue2	:	Debris for ledge section
				// TempValue3	:	Fall Timer
				// TempValue4	:	XPos
				// TempValue5	:	YPos
				TempValue0  = 1
				TempValue1  = 0
				TempValue4  = Object.XPos
				TempValue4 -= 0x200000
				while TempValue1 < 5
					TempValue2  = 0
					TempValue5  = Object.YPos
					TempValue5 += 0x200000
					switch TempValue1
					case 0
						TempValue3 = 58
						break
					case 1
						TempValue3 = 46
						break
					case 2
						TempValue3 = 34
						break
					case 3
						TempValue3 = 27
						break
					case 4
						TempValue3 = 10
						break
					end switch

					while TempValue2 < 5
						CreateTempObject(TypeName[Crumble Ledge 2], TempValue0, TempValue4, TempValue5)
						Object[TempObjectPos].Timer = TempValue3
						Object[TempObjectPos].State = CLEDGE2_DEBRIS
						TempValue3 += 5
						TempValue0++
						TempValue2++
						TempValue5 -= 0x100000
					loop

					TempValue3 -= 8
					TempValue1++
					TempValue4 += 0x100000
				loop
				PlayStageSfx(SFX_S_CRUMBLE, false)
			end if
		end if
		break
	// The five debris chunks at the very top retains collision
	case 4
	case 9
	case 14
	case 19
	case 24
		if Object.Timer > 0
			PlayerObjectCollision(C_PLATFORM, -8, -8, 8, 8)
		end if
		break

	end switch
end sub


sub ObjectDraw
	if Object.State < CLEDGE2_RESTORE
		DrawSprite(Object.PropertyValue)
	end if
end sub


sub ObjectStartup
	LoadSpriteSheet("R1/Objects2.gif")

	SpriteFrame(-40, -40, 80, 80, 82, 1)	// #0 - Ledge

	// Debris chunks
	TempValue1 = 82
	while TempValue1 < 162
		TempValue0 = 65
		while TempValue0 > 0
			SpriteFrame(-8, -8, 16, 16, TempValue1, TempValue0)
			TempValue0 -= 16
		loop
		TempValue1 += 16
	loop
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("R1/Objects2.gif")

	SpriteFrame(-40, -40, 80, 80, 82, 1)	// #0 - Ledge

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
