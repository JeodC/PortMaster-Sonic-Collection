//-------------------Sonic CD Flower Script-------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0 : Object.Timer

// States
#alias 0 : FLOWER_SEED_SPAWN
#alias 1 : FLOWER_SEED_FALL

#alias 2 : FLOWER_SEED_PLANTED_1
#alias 3 : FLOWER_SEED_PLANTED_2
#alias 4 : FLOWER_SEED_PLANTED_3

#alias 5 : FLOWER_ROOT_GROWTH_1
#alias 6 : FLOWER_ROOT_GROWTH_2
#alias 7 : FLOWER_ROOT_GROWTH_3

#alias 8 : FLOWER_ANIM_2
#alias 9 : FLOWER_ANIM_3

// Collision Sides
#alias 0	:	CSIDE_FLOOR


sub ObjectMain
	switch Object.State
	case FLOWER_SEED_SPAWN
		// Hover as a seed for a brief moment
		
		// Frame 8 is the invisible frame
		Object.Frame = 8
		Object.Timer++
		if Object.Timer == 20
			Object.Timer = 0
			Object.State++
		end if
		break
		
	case FLOWER_SEED_FALL
		Object.Frame   = Object.Timer
		Object.Frame >>= 2

		Object.Timer++
		Object.Timer &= 7
		
		// Fall at a graceful speed of 2 pixels per frame
		Object.YPos += 0x20000
		
		// Check if the Flower has touched the ground
		ObjectTileCollision(CSIDE_FLOOR, 0, 8, 0)
		if CheckResult == true
			// If it has, then turn it into a full-on Flower
			
			// First, move the seed 8 pixels down since Flowers' offsets are further than that of seeds
			Object.YPos += 0x80000
			
			Object.State++
			Object.Timer = 0
			Object.Frame = 2
		end if
		break
		
	// All the growth animation frames are their own mini-state, just about the same thing
	
	case FLOWER_SEED_PLANTED_1
		Object.Timer++
		if Object.Timer == 4
			Object.Timer = 0
			Object.State++
			Object.Frame = 3
		end if
		break
		
	case FLOWER_SEED_PLANTED_2
		Object.Timer++
		if Object.Timer == 4
			Object.Timer = 0
			Object.State++
			Object.Frame = 2
		end if
		break
		
	case FLOWER_SEED_PLANTED_3
		Object.Timer++
		if Object.Timer == 4
			Object.Timer = 0
			Object.State++
			Object.Frame = 3
		end if
		break
		
	case FLOWER_ROOT_GROWTH_1
		Object.Timer++
		if Object.Timer == 5
			Object.Timer = 0
			Object.State++
			Object.Frame = 4
		end if
		break
		
	case FLOWER_ROOT_GROWTH_2
		Object.Timer++
		if Object.Timer == 4
			Object.Timer = 0
			Object.State++
			Object.Frame = 5
		end if
		break
	
	case FLOWER_ROOT_GROWTH_3
		Object.Timer++
		if Object.Timer == 2
			Object.Timer = 0
			Object.State++
			Object.Frame = 6
		end if
		break
		
	// Similarly, the main flower animation is also comprised of two near-identical states
		
	case FLOWER_ANIM_2
		Object.Timer++
		if Object.Timer == 20
			Object.Timer = 0
			Object.State++
			Object.Frame = 7
		end if
		break
		
	case FLOWER_ANIM_3
		Object.Timer++
		if Object.Timer == 20
			Object.Timer = 0
			Object.State--
			Object.Frame = 6
		end if
		break
		
	end switch
end sub


sub ObjectDraw
	DrawSprite(Object.Frame)
end sub


sub ObjectStartup
	LoadSpriteSheet("R7/Objects2.gif")
	
	SpriteFrame(-4, -8, 8, 16, 26, 170)		// #0 - Seed Frame 0
	SpriteFrame(-4, -8, 8, 16, 35, 170)		// #1 - Seed Frame 1
	
	SpriteFrame(-8, -8, 16, 16, 44, 170)	// #2 - Planted Seed Frame 0
	SpriteFrame(-8, -8, 16, 16, 26, 187)	// #3 - Planted Seed Frame 1
	
	SpriteFrame(0, -8, 8, 16, 66, 99) 		// #4 - Flower Growth Frame 0
	SpriteFrame(-12, -24, 24, 24, 66, 25) 	// #5 - Flower Growth Frame 1
	
	SpriteFrame(-12, -48, 24, 48, 66, 1) 	// #6 - Flower Frame 0
	SpriteFrame(-12, -48, 24, 48, 66, 50) 	// #7 - Flower Frame 1
	
	SpriteFrame(0, 0, 0, 0, 73, 132)		// #8 - Cheat Sprite, blank frame
	
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("R7/Objects2.gif")
	SpriteFrame(-12, -48, 24, 48, 66, 1)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
