//--------------Sonic CD R7 D Boss Setup Script---------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.Timer
#alias Object.Value1	:	Object.ActivePalette

// Collision Modes
#alias 0	:	CMODE_FLOOR

// Priority
#alias 1	:	PRIORITY_ACTIVE

// Tile Info
#alias 0	:	TILEINFO_INDEX


// Function declarations
#function R7Setup_GetLoopTileInfo


function R7Setup_GetLoopTileInfo
	// Gets info from out of bounds chunks, located past the signpost

	// Precondition:
	//   - TempValue0 is the chunk ID of the chunk currentbly being checked against

	TempValue3 = Player.XPos
	TempValue3 >>= 16
	TempValue3 &= 255
	TempValue3 += 0x4000
	TempValue0 -= 16
	TempValue0 >>= 2
	TempValue0 <<= 8

	TempValue4 = Player.YPos
	TempValue4 >>= 16
	TempValue4 &= 255
	TempValue4 += TempValue0

	Get16x16TileInfo(TempValue0, TempValue3, TempValue4, TILEINFO_INDEX)
end function


sub ObjectMain

	Object.Timer++
	if Object.Timer == 5
		Object.Timer = 0
		Object.ActivePalette++
		Object.ActivePalette %= 6
		SetActivePalette(Object.ActivePalette, 0, 240)
	end if

end sub


sub ObjectPlayerInteraction

	// Manage stage layers

	// Get the chunk ID of the chunk the player's currently in
	TempValue1 = Player.XPos
	TempValue1 >>= 23
	TempValue2 = Player.YPos
	TempValue2 >>= 23
	GetTileLayerEntry(TempValue0, 0, TempValue1, TempValue2)

	// If possible, jump to its corresponding entry over here now
	switch TempValue0
	case 16
	case 17
	case 18
	case 19
		// Loop chunks, opening on left side
		CallFunction(R7Setup_GetLoopTileInfo)
		if Player.CollisionMode == CMODE_FLOOR
			if TempValue0 == 1001
				TempValue1 &= 1022
				TempValue2 &= 1022
				SetTileLayerEntry(20, 0, TempValue1, TempValue2)
				TempValue1++
				SetTileLayerEntry(21, 0, TempValue1, TempValue2)
				TempValue1--
				TempValue2++
				SetTileLayerEntry(22, 0, TempValue1, TempValue2)
				TempValue1++
				SetTileLayerEntry(23, 0, TempValue1, TempValue2)
			end if
		else
			if Player.Speed > 0
				if TempValue0 == 1003
					TempValue1 &= 1022
					TempValue2 &= 1022
					SetTileLayerEntry(20, 0, TempValue1, TempValue2)
					TempValue1++
					SetTileLayerEntry(21, 0, TempValue1, TempValue2)
					TempValue1--
					TempValue2++
					SetTileLayerEntry(22, 0, TempValue1, TempValue2)
					TempValue1++
					SetTileLayerEntry(23, 0, TempValue1, TempValue2)
				end if
			end if
		end if
		break

	case 20
	case 21
	case 22
	case 23
		// Loop chunks, opening on right side
		CallFunction(R7Setup_GetLoopTileInfo)
		if Player.CollisionMode == CMODE_FLOOR
			if TempValue0 == 1000
				TempValue1 &= 1022
				TempValue2 &= 1022
				SetTileLayerEntry(16, 0, TempValue1, TempValue2)
				TempValue1++
				SetTileLayerEntry(17, 0, TempValue1, TempValue2)
				TempValue1--
				TempValue2++
				SetTileLayerEntry(18, 0, TempValue1, TempValue2)
				TempValue1++
				SetTileLayerEntry(19, 0, TempValue1, TempValue2)
			end if
		else
			if Player.Speed < 0
				if TempValue0 == 1002
					TempValue1 &= 1022
					TempValue2 &= 1022
					SetTileLayerEntry(16, 0, TempValue1, TempValue2)
					TempValue1++
					SetTileLayerEntry(17, 0, TempValue1, TempValue2)
					TempValue1--
					TempValue2++
					SetTileLayerEntry(18, 0, TempValue1, TempValue2)
					TempValue1++
					SetTileLayerEntry(19, 0, TempValue1, TempValue2)
				end if
			end if
		end if
		break

	case 24
	case 25
	case 26
	case 27
		// Wavy chunks, opening on left side... or at least they should be
		// However!
		// These don't exist in Act 3, so this function call is actually checking against blank chunks...
		CallFunction(R7Setup_GetLoopTileInfo)
		if TempValue0 == 1001
			TempValue1 &= 1022
			TempValue2 &= 1022
			SetTileLayerEntry(28, 0, TempValue1, TempValue2)
			TempValue1++
			SetTileLayerEntry(29, 0, TempValue1, TempValue2)
			TempValue1--
			TempValue2++
			SetTileLayerEntry(30, 0, TempValue1, TempValue2)
			TempValue1++
			SetTileLayerEntry(31, 0, TempValue1, TempValue2)
		end if
		break

	case 28
	case 29
	case 30
	case 31
		// Wavy chunks, opening on right side, but see note above
		// Same story here
		CallFunction(R7Setup_GetLoopTileInfo)
		if TempValue0 == 1000
			TempValue1 &= 1022
			TempValue2 &= 1022
			SetTileLayerEntry(24, 0, TempValue1, TempValue2)
			TempValue1++
			SetTileLayerEntry(25, 0, TempValue1, TempValue2)
			TempValue1--
			TempValue2++
			SetTileLayerEntry(26, 0, TempValue1, TempValue2)
			TempValue1++
			SetTileLayerEntry(27, 0, TempValue1, TempValue2)
		end if
		break

	end switch

end sub


sub ObjectDraw
	// Find the XPos to draw the lamp at, since it's supposed to have parallax and loop around the stage
	Object.XPos = Screen.XOffset
	Object.XPos /= 420
	Object.XPos *= 420
	Object.XPos += 320
	Object.XPos *= 3
	Object.XPos >>= 1
	TempValue0 = Screen.XOffset
	TempValue0 *= 3
	TempValue0 >>= 1
	Object.XPos -= TempValue0
	Object.YPos = 520
	
	// And now find the YPos to draw it at, there's also vertical parallax too
	TempValue0 = Screen.YOffset
	TempValue0 *= 23
	TempValue0 >>= 4
	Object.YPos -= TempValue0

	DrawSpriteScreenXY(0, Object.XPos, Object.YPos)
	DrawSpriteScreenXY(1, Object.XPos, Object.YPos)
	DrawSpriteScreenXY(2, Object.XPos, Object.YPos)
	DrawSpriteScreenXY(3, Object.XPos, Object.YPos)

end sub


sub ObjectStartup

	LoadSpriteSheet("R7/Objects2.gif")

	// Place the Setup object into slot 19, make it always active, and give it a high draworder so that it's in front of Sonic
	Object[19].Type = TypeName[R7 Setup]
	Object[19].Priority = PRIORITY_ACTIVE
	Object[19].DrawOrder = 5

	// Lamp Post Head
	SpriteFrame(-24, -24, 48, 24, 173, 26)

	// Lamp Post Post
	SpriteFrame(-8, 0, 16, 224, 222, 26)
	SpriteFrame(-8, 224, 16, 224, 222, 26)
	SpriteFrame(-8, 448, 16, 224, 222, 26)

	// Set initial stage bounds
	Stage.XBoundary2 -= 288

	// Setup the stage's palette cycle
	CopyPalette(0, 1)
	CopyPalette(0, 2)
	CopyPalette(0, 3)
	CopyPalette(0, 4)
	CopyPalette(0, 5)

	TempValue0 = 0
	TempValue1 = 0
	TempValue2 = 16
	while TempValue0 < 6
		LoadPalette("R7D_PalCycle.act", TempValue0, 160, TempValue1, TempValue2)
		TempValue0++
		TempValue1 += 16
		TempValue2 += 16
	loop

end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
	DrawSprite(1)
end sub


sub RSDKLoad
	LoadSpriteSheet("R7/Objects2.gif")
	SpriteFrame(-24, -24, 48, 24, 173, 26)
	SpriteFrame(-8, 0, 16, 224, 222, 26)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
