//--------------Sonic CD R7 C Boss Setup Script---------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Priority
#alias 1 : PRIORITY_ACTIVE

// Collision Mode
#alias 0 : CMODE_FLOOR

// Tile Info
#alias 0 : TILEINFO_INDEX

// Function declarations
#function R7Setup_GetLoopTileInfo


function R7Setup_GetLoopTileInfo
	// Gets info from out of bounds chunks, located past the signpost

	// Precondition:
	//   - TempValue0 is the chunk ID of the chunk currentbly being checked against
	
	TempValue3 = Player.XPos
	TempValue3 >>= 16
	TempValue3 &= 255
	TempValue3 += 0x4000

	TempValue0 -= 16
	TempValue0 >>= 2
	TempValue0 <<= 8
	TempValue4 = Player.YPos
	TempValue4 >>= 16
	TempValue4 &= 255
	TempValue4 += TempValue0

	Get16x16TileInfo(TempValue0, TempValue3, TempValue4, TILEINFO_INDEX)
end function


sub ObjectPlayerInteraction
	// "Layers" are managed here
	// (In reality, chunks are swapped on-the-fly in order to make it appear as if the player's moving behind/in front of them)

	// Get the position of the chunk the player's currently in
	TempValue1 = Player.XPos
	TempValue1 >>= 23
	TempValue2 = Player.YPos
	TempValue2 >>= 23

	// And find what chunk ID it holds
	GetTileLayerEntry(TempValue0, 0, TempValue1, TempValue2)

	// If applicable, jump to the loop chunk set it corresponds to
	switch TempValue0
	case 16
	case 17
	case 18
	case 19
		// Loop chunks, open from left side
		CallFunction(R7Setup_GetLoopTileInfo)
		if Player.CollisionMode == CMODE_FLOOR
			if TempValue0 == 1001
				TempValue1 &= 1022
				TempValue2 &= 1022
				SetTileLayerEntry(20, 0, TempValue1, TempValue2)
				TempValue1++
				SetTileLayerEntry(21, 0, TempValue1, TempValue2)
				TempValue1--
				TempValue2++
				SetTileLayerEntry(22, 0, TempValue1, TempValue2)
				TempValue1++
				SetTileLayerEntry(23, 0, TempValue1, TempValue2)
			end if
		else
			if Player.Speed > 0
				if TempValue0 == 1003
					TempValue1 &= 1022
					TempValue2 &= 1022
					SetTileLayerEntry(20, 0, TempValue1, TempValue2)
					TempValue1++
					SetTileLayerEntry(21, 0, TempValue1, TempValue2)
					TempValue1--
					TempValue2++
					SetTileLayerEntry(22, 0, TempValue1, TempValue2)
					TempValue1++
					SetTileLayerEntry(23, 0, TempValue1, TempValue2)
				end if
			end if
		end if
		break

	case 20
	case 21
	case 22
	case 23
		// Loop chunks, open from right side
		CallFunction(R7Setup_GetLoopTileInfo)
		if Player.CollisionMode == CMODE_FLOOR
			if TempValue0 == 1000
				TempValue1 &= 1022
				TempValue2 &= 1022
				SetTileLayerEntry(16, 0, TempValue1, TempValue2)
				TempValue1++
				SetTileLayerEntry(17, 0, TempValue1, TempValue2)
				TempValue1--
				TempValue2++
				SetTileLayerEntry(18, 0, TempValue1, TempValue2)
				TempValue1++
				SetTileLayerEntry(19, 0, TempValue1, TempValue2)
			end if
		else
			if Player.Speed < 0
				if TempValue0 == 1002
					TempValue1 &= 1022
					TempValue2 &= 1022
					SetTileLayerEntry(16, 0, TempValue1, TempValue2)
					TempValue1++
					SetTileLayerEntry(17, 0, TempValue1, TempValue2)
					TempValue1--
					TempValue2++
					SetTileLayerEntry(18, 0, TempValue1, TempValue2)
					TempValue1++
					SetTileLayerEntry(19, 0, TempValue1, TempValue2)
				end if
			end if
		end if
		break

	case 24
	case 25
	case 26
	case 27
		// Wavy chunks, open from left side
		// Note - These don't actually exist in Act 3! So the function is comparing against blank chunks...
		CallFunction(R7Setup_GetLoopTileInfo)
		if TempValue0 == 1001
			TempValue1 &= 1022
			TempValue2 &= 1022
			SetTileLayerEntry(28, 0, TempValue1, TempValue2)
			TempValue1++
			SetTileLayerEntry(29, 0, TempValue1, TempValue2)
			TempValue1--
			TempValue2++
			SetTileLayerEntry(30, 0, TempValue1, TempValue2)
			TempValue1++
			SetTileLayerEntry(31, 0, TempValue1, TempValue2)
		end if
		break

	case 28
	case 29
	case 30
	case 31
		// Wavy chunks, open from right side, but they don't actually exist
		// See note above
		CallFunction(R7Setup_GetLoopTileInfo)
		if TempValue0 == 1000
			TempValue1 &= 1022
			TempValue2 &= 1022
			SetTileLayerEntry(24, 0, TempValue1, TempValue2)
			TempValue1++
			SetTileLayerEntry(25, 0, TempValue1, TempValue2)
			TempValue1--
			TempValue2++
			SetTileLayerEntry(26, 0, TempValue1, TempValue2)
			TempValue1++
			SetTileLayerEntry(27, 0, TempValue1, TempValue2)
		end if
		break

	end switch

end sub


sub ObjectDraw

	// Find the XPos to draw it at, keeping the screen width in mind
	Object.XPos = Screen.XOffset
	Object.XPos /= 420
	Object.XPos *= 420
	Object.XPos += 320
	Object.XPos *= 3
	Object.XPos >>= 1
	TempValue0 = Screen.XOffset
	TempValue0 *= 3
	TempValue0 >>= 1
	Object.XPos -= TempValue0

	Object.YPos = 520
	TempValue0 = Screen.YOffset
	TempValue0 *= 23
	TempValue0 >>= 4
	Object.YPos -= TempValue0

	// Draw the street lamp
	DrawSpriteScreenXY(0, Object.XPos, Object.YPos)
	DrawSpriteScreenXY(1, Object.XPos, Object.YPos)
	DrawSpriteScreenXY(2, Object.XPos, Object.YPos)
	DrawSpriteScreenXY(3, Object.XPos, Object.YPos)

end sub


sub ObjectStartup
	
	LoadSpriteSheet("R7/Objects2.gif")
	
	// Place a Setup object in reserved object slot 19 and make it always active
	Object[19].Type = TypeName[R7 Setup]
	Object[19].Priority = PRIORITY_ACTIVE
	Object[19].DrawOrder = 5
	
	// Street Lamps
	SpriteFrame(-24, -24, 48, 24, 207, 1)

	// Street Lamp Post Frames
	SpriteFrame(-8, 0, 16, 224, 239, 26)
	SpriteFrame(-8, 224, 16, 224, 239, 26)
	SpriteFrame(-8, 448, 16, 224, 239, 26)

	// Set initial stage bounds
	Stage.XBoundary2 -= 288

end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
	DrawSprite(1)
end sub


sub RSDKLoad
	LoadSpriteSheet("R7/Objects2.gif")
	SpriteFrame(-24, -24, 48, 24, 207, 1)
	SpriteFrame(-8, 0, 16, 224, 239, 26)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
