//--------------Sonic CD Character Spawn Script---------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0		:	Object.Timer
#alias Object.Value1		:	Object.SpawnList
#alias Object.Value2		:	Object.SpawnType		// Set by BGAnimation

// Blue Bird/ Slow Bird Alias
#alias Object.Value2		:	Object.Wobble
#alias Object.PropertyValue	:	Object.WobbleIncrease

// Spawn List
// 0		- Blue Bird
// 1, 4, 10	- Tornado
// 2, 5, 9	- Blue Bird/Shooting Star
// 3, 8		- Metal Sonic
// 6, 11	- UFO
// 7, 12	- Eggman

// States
#alias 0	:	CHARACTERSPAWN_SETUP
#alias 1	:	CHARACTERSPAWN_SPAWNER

// UFO Property Values
#alias 0	:	GREEN_UFO
#alias 3	:	YELLOW_UFO

// Shooting Star Property Values
#alias 0	:	WHITE_STAR
#alias 6	:	GREEN_STAR

#function CharacterSpawn_SpawnBlueBird
#function CharacterSpawn_SpawnShootStar
#function CharacterSpawn_SpawnSetup

function CharacterSpawn_SpawnBlueBird
	//Base YPos
	Rand(TempValue4, 64)
	TempValue4 -= 32
	TempValue4 += 120

	TempValue5 = 0
	while TempValue1 > 1
		// Random XPos
		Rand(TempValue2, 64)
		TempValue2  -= 32
		TempValue2  += TempValue0
		TempValue2 <<= 16
		// Random YPos
		Rand(TempValue3, 96)
		TempValue3  -= 48
		TempValue3  += TempValue4
		TempValue3 <<= 16

		CreateTempObject(TypeName[Blue Bird], 0, TempValue2, TempValue3)
		if TempValue0 < Screen.CenterX
			Object[TempObjectPos].Direction = FACING_RIGHT
		else
			Object[TempObjectPos].Direction = FACING_LEFT
		end if
		Object[TempObjectPos].Wobble = TempValue5	// Set the base wobble value
		TempValue5 += 8
		// Set the wobble increment value
		Rand(Object[TempObjectPos].WobbleIncrease, 10)
		Object[TempObjectPos].WobbleIncrease += 3
		TempValue1--
	loop
	// Spawn one slower blue bird
	// Random XPos
	Rand(TempValue2, 64)
	TempValue2  -= 32
	TempValue2  += TempValue0
	TempValue2 <<= 16
	// Random YPos
	Rand(TempValue3, 96)
	TempValue3  -= 48
	TempValue3  += TempValue4
	TempValue3 <<= 16

	CreateTempObject(TypeName[Slow Bird], 0, TempValue2, TempValue3)
	if TempValue0 < Screen.CenterX
		Object[TempObjectPos].Direction = FACING_RIGHT
	else
		Object[TempObjectPos].Direction = FACING_LEFT
	end if
	Object[TempObjectPos].Wobble = TempValue5	// Set the base wobble value
	TempValue5 += 8
	// Set the wobble increment value
	Rand(Object[TempObjectPos].WobbleIncrease, 10)
	Object[TempObjectPos].WobbleIncrease += 3
	TempValue1--
end function


function CharacterSpawn_SpawnShootStar
	while TempValue1 > 0
		// Random XPos
		Rand(TempValue2, 256)
		TempValue2  -= 64
		TempValue2  += Screen.CenterX
		TempValue2 <<= 16

		// Random YPos
		Rand(TempValue3, 80)
		TempValue3  -= 96
		TempValue3 <<= 16

		// Define the color of the shooting star
		Rand(TempValue4, 10)
		if TempValue4 > 5
			CreateTempObject(TypeName[Shooting Star], GREEN_STAR, TempValue2, TempValue3)
		else
			CreateTempObject(TypeName[Shooting Star], WHITE_STAR, TempValue2, TempValue3)
		end if
		TempValue1--
	loop
end function


function CharacterSpawn_SpawnSetup
	switch Object.SpawnList
	case 0
		// Set Base XPos
		TempValue0  = Screen.XSize
		TempValue0 += 96
		TempValue1  = 8
		CallFunction(CharacterSpawn_SpawnBlueBird)
		Object.SpawnList++
		break

	case 1
		// Set Base XPos
		TempValue0   = Screen.XSize
		TempValue0  += 48
		TempValue0 <<= 16
		CreateTempObject(TypeName[Tails Biplane], 0, TempValue0, Object.YPos)
		Object[TempObjectPos].Direction = FACING_LEFT
		Object.SpawnList++
		break

	case 2
		if Object.SpawnType == 0
			// Set Base XPos
			TempValue0  = 0
			TempValue0 -= 96
			TempValue1  = 6
			CallFunction(CharacterSpawn_SpawnBlueBird)
		else
			TempValue1 = 3	// Set the amount of stars to spawn
			CallFunction(CharacterSpawn_SpawnShootStar)
		end if
		Object.SpawnList++
		break

	case 3
		// Set XPos
		Rand(TempValue0, 320)
		TempValue0  -= 160
		TempValue0  += Screen.CenterX
		TempValue0 <<= 16
		// Determine if spawns from the top or bottom of the screen
		Rand(TempValue1, 64)
		if TempValue1 > 40
			CreateTempObject(TypeName[Metal Sonic], 0, TempValue0, 0x10C0000)
		else
			CreateTempObject(TypeName[Metal Sonic], 0, TempValue0, -0x400000)
		end if
		Object.SpawnList++
		break

	case 4
		CreateTempObject(TypeName[Tails Biplane], 0, Object.XPos, Object.YPos)
		Object.SpawnList++
		break

	case 5
		if Object.SpawnType == 0
			// Set Base XPos
			TempValue0  = 0
			TempValue0 -= 96
			TempValue1  = 4
			CallFunction(CharacterSpawn_SpawnBlueBird)
		else
			TempValue1 = 4	// Set the amount of stars to spawn
			CallFunction(CharacterSpawn_SpawnShootStar)
		end if
		Object.SpawnList++
		break

	case 6
		// Set Base XPos
		TempValue0   = Screen.XSize
		TempValue0  += 48
		TempValue0 <<= 16
		CreateTempObject(TypeName[UFO], YELLOW_UFO, TempValue0, Object.YPos)
		Object[TempObjectPos].Direction = FACING_LEFT
		Object.SpawnList++
		break
		
	case 7
		// Set Base XPos
		TempValue0   = Screen.XSize
		TempValue0  += 48
		TempValue0 <<= 16
		CreateTempObject(TypeName[Dr Eggman], 6, TempValue0, 0xC00000)
		Object[TempObjectPos].Direction = FACING_RIGHT
		Object.SpawnList++
		break

	case 8
		// Set Base XPos
		Rand(TempValue0, 320)
		TempValue0  -= 160
		TempValue0  += Screen.CenterX
		TempValue0 <<= 16
		// Determine if spawns from the top or bottom of the screen
		Rand(TempValue1, 64)
		if TempValue1 > 40
			CreateTempObject(TypeName[Metal Sonic], 0, TempValue0, 0x10C0000)
		else
			CreateTempObject(TypeName[Metal Sonic], 0, TempValue0, -0x400000)
		end if
		Object.SpawnList++
		break

	case 9
		if Object.SpawnType == 0
			// Set Base XPos
			TempValue0  = Screen.XSize
			TempValue0 += 96
			TempValue1  = 8
			CallFunction(CharacterSpawn_SpawnBlueBird)
		else
			TempValue1 = 2	// Set the amount of stars to spawn
			CallFunction(CharacterSpawn_SpawnShootStar)
		end if
		Object.SpawnList++
		break

	case 10
		// Set Base XPos
		TempValue0   = Screen.XSize
		TempValue0  += 48
		TempValue0 <<= 16
		CreateTempObject(TypeName[Tails Biplane], 0, TempValue0, Object.YPos)
		Object[TempObjectPos].Direction = FACING_LEFT
		Object.SpawnList++
		break

	case 11
		CreateTempObject(TypeName[UFO], GREEN_UFO, Object.XPos, Object.YPos)
		CreateTempObject(TypeName[UFO], YELLOW_UFO, Object.XPos, Object.YPos)
		Object.SpawnList++
		break

	case 12
		// Set Base XPos
		TempValue0   = -48
		TempValue0 <<= 16
		CreateTempObject(TypeName[Dr Eggman], 6, TempValue0, 0xC00000)
		Object[TempObjectPos].Direction = FACING_LEFT
		Object.SpawnList = 0
		break

	end switch
end function


sub ObjectMain
	switch Object.State
	case CHARACTERSPAWN_SETUP
		CreateTempObject(TypeName[Tails Biplane], 0, Object.XPos, Object.YPos)
		Object.Timer = 380
		Object.State++
		break

	case CHARACTERSPAWN_SPAWNER
		if Object.Timer > 0
			Object.Timer--
		else
			Rand(Object.Timer, 480)
			Object.Timer += 240
			CallFunction(CharacterSpawn_SpawnSetup)
		end if
		break

	end switch
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
