//---------------Sonic CD Eggman Escape Script----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.Timer
#alias Object.Value1	:	Object.XVelocity

// Destroyed Egg Mobile Alias
#alias Object[-1].XPos	:	DestroyedMobile.XPos

// States
#alias 0	:	EGG_ESCAPE_FADE_IN
#alias 1	:	EGG_ESCAPE_FLEE

// Priority
#alias 1	:	PRIORITY_ACTIVE


sub ObjectMain
	switch Object.State
	case EGG_ESCAPE_FADE_IN
		Player.XPos = DestroyedMobile.XPos
		Player.Speed = 0
		if Object.Timer < 120
			Object.Timer++
		else
			Player.Score += 1000
			Object.Timer	 = 0
			Object.Priority  = PRIORITY_ACTIVE
			Object.XVelocity = 0x8000
			Object.State++
		end if
		break
		
	case EGG_ESCAPE_FLEE
		Object.XVelocity += 0xC00
		Object.XPos		 += Object.XVelocity
		if Object.OutOfBounds == true
			ResetObjectEntity(Object.EntityNo, TypeName[Blank Object], 0, 0, 0)
		end if
		break
	end switch
end sub


sub ObjectDraw
	TempValue0  = Object.Frame
	TempValue0 /= 6
	DrawSprite(TempValue0)

	Object.Frame++
	Object.Frame %= 24

	TempValue0  = Object.AnimationTimer
	TempValue0 /= 6
	TempValue0 += 4

	TempValue1  = Object.AnimationTimer
	TempValue1 %= 6
	if TempValue1 < 3
		DrawSprite(TempValue0)
	end if

	Object.AnimationTimer++
	Object.AnimationTimer %= 12
end sub


sub ObjectStartup
	LoadSpriteSheet("R6/Objects2.gif")

	SpriteFrame(-32, -25, 64, 48, 1, 170)	// #0 - Eggman Running Frame 0
	SpriteFrame(-32, -23, 48, 56, 66, 131)	// #1 - Eggman Running Frame 1
	SpriteFrame(-32, -21, 56, 56, 66, 199)	// #2 - Eggman Running Frame 2
	SpriteFrame(-32, -23, 48, 56, 66, 131)	// #3 - Eggman Running Frame 3

	SpriteFrame(-32, -36, 16, 16, 237, 1)	// #4 - Sweat Frame 0
	SpriteFrame(-32, -36, 16, 16, 181, 207)	// #5 - Sweat Frame 1
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("R6/Objects2.gif")

	SpriteFrame(-32, -25, 64, 48, 1, 170)	// #0 - Eggman Running Frame 0

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
