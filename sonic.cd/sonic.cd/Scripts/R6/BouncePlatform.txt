//---------------Sonic CD Bounce Platform Script--------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value1	:	Object.YOriginPos
#alias Object.Value2	:	Object.YVelocity
#alias Object.Value3	:	Object.FloorYPos
#alias Object.Value4	:	Object.FallSpeed

// R6 Setup alias
#alias Object[19].Scale :	R6Setup.BounceFloorFlag

// Collision Sides
#alias 0	:	CSIDE_FLOOR

// Stage SFX
#alias 3	:	SFX_S_IMPACT1


sub ObjectMain
	Object.YOriginPos = Object.YPos

	Object.YVelocity += Object.FallSpeed
	Object.YPos		 += Object.YVelocity

	ObjectTileCollision(CSIDE_FLOOR, 0, 16, 0)
	if CheckResult == true
		if Object.FallSpeed == 0x1000
			PlayStageSfx(SFX_S_IMPACT1, false)
		end if
		if R6Setup.BounceFloorFlag == true
			Object.YVelocity = -0x60000
			Object.FallSpeed =  0x1000
		else
			Object.YVelocity = -0x20000
			Object.FallSpeed =  0x1488
		end if
	end if

	Object.FloorYPos  = Object.YPos
	Object.FloorYPos -= Object.YOriginPos
end sub


sub ObjectPlayerInteraction
	TempValue0  = Object.YPos
	Object.YPos = Object.YOriginPos

	TempValue1  = Object.XPos
	TempValue1 -= Player.XPos
	if TempValue1 < 0
		FlipSign(TempValue1)
	end if
	if TempValue1 > 0x280000
		PlayerObjectCollision(C_BOX, -32, -16, 32, 16)
	end if

	PlayerObjectCollision(C_PLATFORM, -32, -18, 32, 16)
	if CheckResult == true
		Player.YPos += Object.FloorYPos
		Player.YPos += 0x20000
	end if

	Object.YPos = TempValue0
end sub


sub ObjectDraw
	DrawSprite(0)
end sub


sub ObjectStartup
	LoadSpriteSheet("R6/Objects.gif")

	SpriteFrame(-32, -16, 64, 32, 132, 43)	// #0 - Bounce Platform

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Bounce Platform]
			Object[ArrayPos0].FallSpeed = 0x1000
		end if
		ArrayPos0++
	loop
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
	if Editor.ShowGizmos == true
		Editor.DrawingOverlay = true
		TempValue1  = Object.XPos
		TempValue1 -= 0x100000
		TempValue0  = Object.YPos
		TempValue0 -= 0x200000
		DrawArrow(TempValue1, Object.YPos, TempValue1, TempValue0, 255, 255, 0)
		TempValue1  = Object.XPos
		TempValue1 += 0x100000
		TempValue0  = Object.YPos
		TempValue0 -= 0x600000
		DrawArrow(TempValue1, Object.YPos, TempValue1, TempValue0, 255, 255, 0)
		Editor.DrawingOverlay = false
	end if
end sub


sub RSDKLoad
	LoadSpriteSheet("R6/Objects.gif")

	SpriteFrame(-32, -16, 64, 32, 132, 43)	// #0 - Bounce Platform

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
