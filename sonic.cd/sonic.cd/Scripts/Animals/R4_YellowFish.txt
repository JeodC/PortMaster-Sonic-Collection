//---------------Sonic CD Yellow Fish Script------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0			:	Object.Timer
#alias Object.Value1			:	Object.Angle

// HUD alias
#alias Object[24].PropertyValue	:	HUD.CurrentTimePeriod

// States
#alias 0	:	YELLOWFISH_SWIMRIGHT
#alias 1	:	YELLOWFISH_SWIMLEFT

// Time Period Aliases
#alias 2	:	TIME_GOOD_FUTURE


sub ObjectMain
	switch Object.State
	case YELLOWFISH_SWIMRIGHT
		if Object.Angle < 512
			Object.Angle += 2
			
			// Move the fish right a pixel
			Object.XPos += 0x10000
		else
			// Turn around
			
			Object.State	 = YELLOWFISH_SWIMLEFT
			Object.Direction = FACING_LEFT
		end if
		break
		
	case YELLOWFISH_SWIMLEFT
		if Object.Angle > 0
			Object.Angle -= 2
			
			// Move the fish a pixel left
			Object.XPos -= 0x10000
		else
			// Turn once back around
			
			Object.State	 = YELLOWFISH_SWIMRIGHT
			Object.Direction = FACING_RIGHT
		end if
		break
		
	end switch
	
	// Animate the Fish
	Object.Timer++
	if Object.Timer > 19
		Object.Timer = 0
		
		Object.Frame++
		Object.Frame &= 1
	end if
	
end sub


sub ObjectDraw
	if MetalSonic_Destroyed == true
		Sin(TempValue1, Object.Angle)
		TempValue1 <<= 12
		TempValue1  += Object.YPos
		
		DrawSpriteFX(Object.Frame, FX_FLIP, Object.XPos, TempValue1)
	end if
	
	if HUD.CurrentTimePeriod == TIME_GOOD_FUTURE
		Sin(TempValue1, Object.Angle)
		TempValue1 <<= 12
		TempValue1  += Object.YPos
		
		DrawSpriteFX(Object.Frame, FX_FLIP, Object.XPos, TempValue1)
	end if
	
end sub


sub ObjectStartup
	
	LoadSpriteSheet("R4/Objects3.gif")
	
	// Fish Frames
	
	SpriteFrame(-8, -12, 16, 24, 1, 92)
	SpriteFrame(-8, -12, 16, 24, 18, 92)
	
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("R4/Objects3.gif")
	SpriteFrame(-8, -12, 16, 24, 1, 92)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
