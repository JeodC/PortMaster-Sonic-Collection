//----------------Sonic CD SignPost 2 Script------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0		:	Object.Timer
#alias Object.Value1		:	Object.FrameTimer
#alias Object.Value2		:	Object.SignPostBound

// Player Aliases
#alias Player.Value4		:	Player.InvincibleTimer

// ActFinish Aliases
#alias Object[30].DrawOrder	:	ActFinish.DrawOrder
#alias Object[30].Priority	:	ActFinish.Priority

// States
#alias 0	:	SIGNPOST2_CHECK_BOUNDS
#alias 1	:	SIGNPOST2_FLIP
#alias 2	:	SIGNPOST2_MOVE_PLAYER
#alias 3	:	SIGNPOST2_STOP_PLAYER
#alias 4	:	SIGNPOST2_BLANK

// Control Mode
#alias -1	:	CONTROLMODE_NONE

// Global SFX
#alias 15	:	SFX_G_SIGNPOST

// Priority
#alias 1	:	PRIORITY_ACTIVE
#alias 3	:	PRIORITY_XBOUNDS


sub ObjectMain
	switch Object.State
	case SIGNPOST2_CHECK_BOUNDS
		Object.Frame = 1

		TempValue0  = Object.YPos
		TempValue0 += 0x600000

		if Player.YPos < TempValue0
			if Object.SignPostBound > 0
				TempValue0   = Object.XPos
				TempValue0  -= Player.XPos
				TempValue0 >>= 16
				CheckGreater(TempValue0, 0)
				TempValue1   = CheckResult
				CheckLower(TempValue0, Object.SignPostBound)
				CheckResult &= TempValue1
				if CheckResult == true
					Stage.NewXBoundary1   = Object.iXPos
					Stage.NewXBoundary1  -= Object.SignPostBound
					Stage.NewXBoundary2   = Object.XPos
					Stage.NewXBoundary2 >>= 16
					Stage.NewXBoundary2  += Screen.CenterX
				end if
			else
				Stage.NewXBoundary1   = Object.XPos
				Stage.NewXBoundary1 >>= 16
				Stage.NewXBoundary1  -= Screen.CenterX
				Stage.NewXBoundary1  -= Screen.CenterX

				Stage.NewXBoundary2   = Object.XPos
				Stage.NewXBoundary2 >>= 16
				Stage.NewXBoundary2  += Screen.CenterX
			end if

			if Player.XPos > Object.XPos
				Stage.NewXBoundary1   = Object.XPos
				Stage.NewXBoundary1 >>= 16
				Stage.NewXBoundary1  -= Screen.CenterX

				EngineCallback(NOTIFY_TOUCH_SIGNPOST)

				Stage.TimeEnabled = false
				Object.State	  = SIGNPOST2_FLIP
				Object.Timer	  = 8

				PlaySfx(SFX_G_SIGNPOST, false)
				
			end if
		end if
		break

	case SIGNPOST2_FLIP
		Music.Volume--
		Object.FrameTimer++
		if Object.FrameTimer == 2

			Object.FrameTimer = 0
			Object.Frame++
			if Object.Frame == 5
				// Spawn a ring sparkle at random location close to the sign post
				Rand(TempValue0, 48)
				TempValue0 <<= 16
				TempValue0  += Object.XPos
				TempValue0  -= 0x180000

				Rand(TempValue1, 32)
				TempValue1 <<= 16
				TempValue1  += Object.YPos
				TempValue1  -= 0x1C0000
				CreateTempObject(TypeName[Ring Sparkle], 0, TempValue0, TempValue1)

				Object.Timer--
				if Object.Timer == 0
					if Object[0].Type == TypeName[Blank Object]	// Check if the player already leave by itself
						Object.State = SIGNPOST2_BLANK

						if Object.OutOfBounds == false
							PlayMusic(1)
						end if

						Object[30].Type 	= TypeName[ActFinish]
						ActFinish.DrawOrder = 6
						ActFinish.Priority	= PRIORITY_ACTIVE
					else	// Otherwise the signpost will do it itself.
						Player.ControlMode  = CONTROLMODE_NONE
						Player.Up 			= false
						Player.Down 		= false
						Player.Left 		= false
						Player.JumpPress 	= false
						Player.JumpHold 	= false
						Object.State		= SIGNPOST2_MOVE_PLAYER
					end if
				end if
			end if

			if Object.Frame == 9
				// Spawn a ring sparkle at random location close to the sign post
				Rand(TempValue0, 48)
				TempValue0 <<= 16
				TempValue0  += Object.XPos
				TempValue0  -= 0x180000
				
				Rand(TempValue1, 32)
				TempValue1 <<= 16
				TempValue1  += Object.YPos
				TempValue1  -= 0x1C0000
				CreateTempObject(TypeName[Ring Sparkle], 0, TempValue0, TempValue1)
				// Resets the animation cycle
				Object.Frame = 1
			end if

		end if
		// Freezes the player after leaving the screen
		TempValue0   = 40
		TempValue0  += Screen.CenterX
		TempValue0 <<= 16
		TempValue0  += Object.XPos
		if Player.XPos > TempValue0
			Player.XPos 	 = TempValue0
			Player.Speed	 = 0
			Player.XVelocity = 0
		end if
		break

	case SIGNPOST2_MOVE_PLAYER
		if Object[0].Type == TypeName[Blank Object]
			Object.State = SIGNPOST2_BLANK
			
			if Object.OutOfBounds == false
				PlayMusic(1)
			end if
			
			Object[30].Type		= TypeName[ActFinish]
			ActFinish.DrawOrder = 6
			ActFinish.Priority	= PRIORITY_ACTIVE
		end if

		if Object.PropertyValue > 127
			if Player.ControlMode == CONTROLMODE_NONE
				Player.Right = true
			end if
		end if

		TempValue0   = Screen.CenterX
		TempValue0 <<= 16
		TempValue0  += Object.XPos
		CheckGreater(Player.XPos, TempValue0)
		TempValue1   = CheckResult
		CheckLower(Object.PropertyValue, 128)
		CheckResult |= TempValue1
		if CheckResult == true
			if Object.OutOfBounds == false
				PlayMusic(1)
			end if

			Object.State		= SIGNPOST2_STOP_PLAYER
			Object[30].Type 	= TypeName[ActFinish]
			ActFinish.DrawOrder = 6
			ActFinish.Priority	= PRIORITY_ACTIVE
		else
			if Player.Animation == ANI_PUSHING
				PlayMusic(1)
				Object.State   		= SIGNPOST2_BLANK
				Object[30].Type		= TypeName[ActFinish]
				ActFinish.DrawOrder = 6
				ActFinish.Priority  = PRIORITY_ACTIVE
			end if
		end if
		break

	case SIGNPOST2_STOP_PLAYER
		if Object.PropertyValue > 127
			if Player.ControlMode == CONTROLMODE_NONE
				Player.Right = true
			end if
		end if

		TempValue0   = 40
		TempValue0  += Screen.CenterX
		TempValue0 <<= 16
		TempValue0  += Object.XPos

		if Player.XPos > TempValue0
			Player.XPos		 = TempValue0
			Player.Speed	 = 0
			Player.XVelocity = 0
		end if
		break
	end switch

	if Object.State > SIGNPOST2_FLIP
		if Object.PropertyValue < 128
			CheckEqual(Player.State, Player_State_Ground)
			TempValue0  = CheckResult
			CheckEqual(Player.OutOfBounds, true)
			TempValue0 |= CheckResult

			TempValue1  = Screen.XOffset
			TempValue1 += Screen.XSize
			TempValue1 += 32
			CheckGreater(Player.iXPos, TempValue1)
			TempValue0 |= CheckResult
			if TempValue0 == true
				Player.ControlMode   = CONTROLMODE_NONE
				Player.JumpHold		 = false
				Player.Right		 = false
				Player.Left			 = false
				Player.Up			 = false
				Player.Down			 = false

				if Player.State == Player_State_Ground
					Player.XVelocity = 0
					Player.YVelocity = 0
				end if

				Player.Speed 		 = 0
				Screen.CameraEnabled = false
			end if
		end if
	end if

	if Object.State > SIGNPOST2_CHECK_BOUNDS
		Player.InvincibleTimer = 80
	end if
end sub


sub ObjectDraw
	DrawSprite(0)
	DrawSprite(Object.Frame)
end sub


sub ObjectStartup
	if Stage.PlayerListPos == PLAYER_SONIC
		LoadSpriteSheet("Global/Items2.gif")
	end if
	if Stage.PlayerListPos == PLAYER_TAILS
		LoadSpriteSheet("Global/Items2_t.gif")
	end if
	if Stage.PlayerListPos == PLAYER_KNUCKLES
		LoadSpriteSheet("Global/Items2_K.gif")
	end if
	if Stage.PlayerListPos == PLAYER_AMY
		LoadSpriteSheet("Global/Items2_A.gif")
	end if

	SpriteFrame(-12, -12, 24, 24, 0, 150)		// #0 - Sign Post
	SpriteFrame(-24, -44, 48, 32, 34, 99)		// #1 - Sign Post Head (Eggman)
	SpriteFrame(-16, -44, 32, 32, 1, 183)		// #2 - Sign Post Head (Flip 1)
	SpriteFrame(-4, -44, 8, 32, 25, 150)		// #3 - Sign Post Head (Flip 2)
	SpriteFrame(-16, -44, 32, 32, 1, 216)		// #4 - Sign Post Head (Flip 3)
	SpriteFrame(-24, -44, 48, 32, 34, 132)		// #5 - Sign Post Head (Player)
	SpriteFrame(-16, -44, 32, 32, 1, 183)		// #6 - Sign Post Head (Flip 1)
	SpriteFrame(-4, -44, 8, 32, 25, 150)		// #7 - Sign Post Head (Flip 2)
	SpriteFrame(-16, -44, 32, 32, 1, 216)		// #8 - Sign Post Head (Flip 3)

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[SignPost2]
			Object[ArrayPos0].Priority = PRIORITY_XBOUNDS
			if Object[ArrayPos0].PropertyValue < 128
				Object[ArrayPos0].SignPostBound  = Object[ArrayPos0].PropertyValue
			else
				Object[ArrayPos0].SignPostBound  = 256
				Object[ArrayPos0].SignPostBound -= Object[ArrayPos0].PropertyValue
			end if
			Object[ArrayPos0].SignPostBound <<= 4
		end if
		ArrayPos0++
	loop
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
	DrawSprite(1)
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Items2.gif")
	SpriteFrame(-12, -12, 24, 24, 0, 150)		// #0 - Sign Post
	SpriteFrame(-24, -44, 48, 32, 34, 99)		// #1 - Sign Post Head (Eggman)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub


