//----------------Sonic CD Boss Bumper 2 Script---------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//
//---------Used on Mission "M099 - Eggman: Speed Up"----------//

// Aliases
#alias Object.Value0	:	Object.ArmEntity
#alias Object.Value2	:	Object.XOffSet
#alias Object.Value5	:	Object.YOffSet

// Arm Aliases
#alias Object.Frame		:	Arm.Frame
#alias Object.XPos		:	Arm.XPos
#alias Object.YPos		:	Arm.YPos

// States
#alias 0	:	BOSSBUMPER2_SETUP
#alias 1	:	BOSSBUMPER2_IDLE
#alias 2	:	BOSSBUMPER2_DESTROYED

// Global SFX
#alias 11	:	SFX_G_SPRING

// Priority
#alias 0	:	PRIORITY_BOUNDS


sub ObjectMain
	switch Object.State
	case BOSSBUMPER2_SETUP
		Object.State = BOSSBUMPER2_IDLE
		// Fall-Through
	case BOSSBUMPER2_IDLE
		ArrayPos0 = Object.ArmEntity
		Object.XPos  = Arm[ArrayPos0].XPos
		Object.XPos -= 0x130000
		Object.YPos  = Arm[ArrayPos0].YPos
		if Arm[ArrayPos0].Frame != 0
			if Arm[ArrayPos0].Frame == 1
				Object.XPos += 0x80000
			else
				Object.XPos += 0x100000
			end if
		end if
		break

	case BOSSBUMPER2_DESTROYED
		Object.XOffSet -= 0x500
		Object.XPos += Object.XOffSet

		Object.YOffSet += 0x1800
		Object.YPos += Object.YOffSet

		if Object.OutOfBounds == true
			Object.Type = TypeName[Blank Object]
			Object.Priority = PRIORITY_BOUNDS
		end if
		break
	end switch
end sub


sub ObjectPlayerInteraction
	if Object.State < BOSSBUMPER2_DESTROYED
		if Player.Animation != ANI_HURT
			ArrayPos0 = Object.ArmEntity

			if Arm[ArrayPos0].Frame == 0
				PlayerObjectCollision(C_TOUCH, -10, -16, 14, 16)
				if CheckResult == true

					Player.Speed	 = -0x40000
					Player.XVelocity = -0x40000

					if Object.Frame == 0
						PlaySfx(SFX_G_SPRING, false)
					end if
					Object.Frame = 2
					
				end if
			end if
		end if
	end if
end sub


sub ObjectDraw
	DrawSprite(Object.Frame)

	if Object.Frame > 0
		Object.Frame--
	end if
end sub


sub ObjectStartup
	LoadSpriteSheet("R1/Objects3.gif")

	SpriteFrame(-14, -16, 24, 32, 99, 26)	// #0 - EGG-HVC-001 Bumper
	SpriteFrame(-18, -16, 24, 32, 99, 26)	// #1 - EGG-HVC-001 Bumper (different offset)
	SpriteFrame(-18, -16, 24, 32, 99, 26)	// #2 - EGG-HVC-001 Bumper (same offset as frame 1, here to pad out animation length)
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("R1/Objects3.gif")
	SpriteFrame(-14, -16, 24, 32, 99, 26)	// #0 - EGG-HVC-001 Bumper

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub


