//------------------Sonic CD Sasuri 2 Script------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//
//-------------Used on Mission "M093 - Fast Foes"-------------//

// Aliases
#alias Object.Value0	:	Object.MoveTimer
#alias Object.Value1	:	Object.CannonTimer
#alias Object.Value2	:	Object.LeftPlatformBound
#alias Object.Value3	:	Object.RightPlatformBound

// Sasuri Bullet (SBullet) Aliases
#alias Object.Value1	:	Object.XVelocity

// States
#alias 0	:	SASURI2_SETBOUNDS
#alias 1	:	SASURI2_CRAWLLEFT
#alias 2	:	SASURI2_CRAWLRIGHT
#alias 3	:	SASURI2_SPOTPLAYER
#alias 4	:	SASURI2_SHOOT
#alias 5	:	SASURI2_RETRACTCANNON

// Stage SFX
#alias 2	:	SFX_S_SHOT


sub ObjectMain
	Object.Frame++
	Object.Frame %= 60
	switch Object.State
	case SASURI2_SETBOUNDS
		// Object[+1] is whatever platform Sasuri is on top
		Object.YPos   = Object[+1].YPos
		Object.iYPos -= 28

		Object.RightPlatformBound   = 32
		Object.RightPlatformBound <<= 16

		Object.LeftPlatformBound    = 0
		Object.LeftPlatformBound   -= Object.RightPlatformBound

		Object.RightPlatformBound  += Object[+1].XPos
		Object.LeftPlatformBound   += Object[+1].XPos

		Object.State = SASURI2_CRAWLLEFT
		break

	case SASURI2_CRAWLLEFT
		// Move the Sasuri left by one pixel
		Object.XPos -= 0x10000

		// Increment its movement timer, and see if it's time to turn around now
		Object.MoveTimer--
		if Object.MoveTimer <= -80
			Object.State = SASURI2_CRAWLRIGHT
			Object.Direction=FACING_LEFT
		end if

		if Object.XPos < Object.LeftPlatformBound
			Object.State = SASURI2_CRAWLRIGHT
			Object.MoveTimer = 0
			Object.Direction=FACING_LEFT
		end if
		break

	case SASURI2_CRAWLRIGHT
		Object.XPos += 0x10000
		
		// As with above, do timer stuff and see if the Sasuri should turn around now
		Object.MoveTimer++
		if Object.MoveTimer >= 80
			Object.State = SASURI2_CRAWLLEFT
			Object.Direction = FACING_RIGHT
		end if

		if Object.XPos > Object.RightPlatformBound
			Object.State = SASURI2_CRAWLLEFT
			Object.MoveTimer = 0
			Object.Direction=FACING_RIGHT
		end if
		break
			
	case SASURI2_SPOTPLAYER
		// This state's set in ObjectPlayerInteraction if the Player is within shooting range
		
		if Object.CannonTimer < 30
			Object.CannonTimer++
		else
			Object.State = SASURI2_SHOOT
			Object.CannonTimer = 0
		end if
		break
		
	case SASURI2_SHOOT
		if Object.CannonTimer < 40
			if Object.CannonTimer == 10
				PlayStageSfx(SFX_S_SHOT, false)
				
				// Create the Bullet that the Sasuri shot
				CreateTempObject(TypeName[Sasuri Bullet], 0, Object.XPos, Object.YPos)
				
				// Move the Bullet 24 pixels up, to match where the Sasuri's cannon is drawn
				Object[TempObjectPos].YPos -= 0x180000
				
				// Move it 24 pixels in the Sasuri's direction, as well as giving it a velocity of 3px per frame in that very same direction too
				if Object.Direction == FACING_RIGHT
					Object[TempObjectPos].XPos -= 0x70000
					Object[TempObjectPos].XVelocity = -0x30000
				else
					Object[TempObjectPos].XPos += 0x70000
					Object[TempObjectPos].XVelocity = 0x30000
				end if
				
				// Move the Sasuri up a Draw Order in order to make it drawn on top of the shot Bullet
				Object.DrawOrder = 4
			end if
			
			Object.CannonTimer++
		else
			// Pull the cannon back in
			Object.State = SASURI2_RETRACTCANNON
			Object.CannonTimer = 0
			
			// Move the Sasuri back to its original Draw Order, as at this point
			// the Bullet's definitely beyond the Sasuri now
			Object.DrawOrder = 3
		end if
		break

	case SASURI2_RETRACTCANNON
		if Object.CannonTimer < 30
			Object.CannonTimer++
		else
		
			// ...what?
			// Why make it shoot again? The State's set proper in a couple of lines after anyway...
			Object.State = SASURI2_SHOOT
			
			if Object.Direction == FACING_RIGHT
				Object.State = SASURI2_CRAWLLEFT
			else
				Object.State = SASURI2_CRAWLRIGHT
			end if
		end if
		break
	end switch

	CallFunction(StageSetup_CheckGoodFuture)
end sub


sub ObjectPlayerInteraction
	
	PlayerObjectCollision(C_ENEMY, -22, -14, 22, 14)
	if CheckResult == true
		CallFunction(Player_BadnikBreak)
	end if
	
	switch Object.State
	case SASURI2_CRAWLLEFT
	case SASURI2_CRAWLRIGHT
		// If in a movement state, then see if the Sasuri
		if Object.CannonTimer == 0
			PlayerObjectCollision(C_TOUCH, -64, -24, 64, 24)
			if CheckResult == true
				Object.State = SASURI2_SPOTPLAYER
				
				if Player.XPos < Object.XPos
					Object.Direction = FACING_RIGHT
				else
					Object.Direction = FACING_LEFT
				end if
			end if
		else
			Object.CannonTimer = 0
		end if
		break
		
	end switch
end sub


sub ObjectDraw
	if Object.Frame < 30
		// In this order, draw the:
		// - back paw
		// - main body
		// - front paw
		
		DrawSpriteFX(2, FX_FLIP, Object.XPos, Object.YPos)
		DrawSpriteFX(0, FX_FLIP, Object.XPos, Object.YPos)
		DrawSpriteFX(3, FX_FLIP, Object.XPos, Object.YPos)
	else
		// These are the same order as detailed above, just with different frames for the paws
		
		DrawSpriteFX(4, FX_FLIP, Object.XPos, Object.YPos)
		DrawSpriteFX(0, FX_FLIP, Object.XPos, Object.YPos)
		DrawSpriteFX(5, FX_FLIP, Object.XPos, Object.YPos)
	end if
	
	switch Object.State
	case SASURI2_CRAWLLEFT
	case SASURI2_CRAWLRIGHT
	case SASURI2_SPOTPLAYER
	case SASURI2_RETRACTCANNON
		// Draw the pulled back cannon frame
		DrawSpriteFX(10, FX_FLIP, Object.XPos, Object.YPos)
		break
		
	case SASURI2_SHOOT
		// The Sasuri's shooting, so draw the extended cannon frame
		DrawSpriteFX(11, FX_FLIP, Object.XPos, Object.YPos)
		break
	
	end switch
end sub


sub ObjectStartup
	LoadSpriteSheet("R5/Objects.gif")
	
	// Sasuri Frames
	
	// Good Main Sasuri Frame
	SpriteFrame(-24, -16, 48, 32, 174, 141)
	
	// Bad Main Sasuri Frame
	SpriteFrame(-24, -16, 48, 32, 174, 174)
	
	// Good Sasuri Paw Frames
	SpriteFrame(-32, -4, 32, 16, 100, 110)
	SpriteFrame(-21, -3, 32, 16, 100, 110)
	SpriteFrame(-28, -4, 32, 16, 100, 110)
	SpriteFrame(-25, -3, 32, 16, 100, 110)
	
	// Bad Sasuri Paw Frames
	SpriteFrame(-32, -4, 32, 16, 100, 127)
	SpriteFrame(-21, -3, 32, 16, 100, 127)
	SpriteFrame(-28, -4, 32, 16, 100, 127)
	SpriteFrame(-25, -3, 32, 16, 100, 127)
	
	// Good Sasuri Cannon Frames
	SpriteFrame(-9, -24, 24, 16, 100, 93)
	SpriteFrame(-5, -29, 24, 16, 100, 93)
	
	// Bad Sasuri Cannon Frames
	SpriteFrame(-1, -24, 16, 16, 133, 110)
	SpriteFrame(3, -29, 16, 16, 133, 110)
	
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(2)
	DrawSprite(0)
	DrawSprite(3)
	DrawSprite(6)
end sub


sub RSDKLoad
	LoadSpriteSheet("R5/Objects.gif")
	
	SpriteFrame(-24, -16, 48, 32, 174, 141)
	SpriteFrame(-24, -16, 48, 32, 174, 174)
	
	SpriteFrame(-32, -4, 32, 16, 100, 110)
	SpriteFrame(-21, -3, 32, 16, 100, 110)
	
	SpriteFrame(-32, -4, 32, 16, 100, 127)
	SpriteFrame(-21, -3, 32, 16, 100, 127)
	
	SpriteFrame(-9, -24, 24, 16, 100, 93)
	SpriteFrame(-1, -24, 16, 16, 133, 110)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub