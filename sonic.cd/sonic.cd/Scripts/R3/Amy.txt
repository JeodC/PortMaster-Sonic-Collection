//-----------------Sonic CD Amy Rose Script-------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0		:	Object.FrameTimer
#alias Object.Value1		:	Object.XVelocity
#alias Object.Value2		:	Object.YVelocity
#alias Object.Value3		:	Object.LeftLimit
#alias Object.Value4		:	Object.RightLimit
#alias Object.Value5		:	Object.Gravity
#alias Object.Value7		:	Object.HeartTimer

// Metal Sonic Aliases
#alias Object[+1].Value0	:	Metal.Timer
#alias Object[+1].State		:	Metal.State
#alias Object[+1].Priority	:	Metal.Priority

// States
#alias 0	:	AMY_IDLE
#alias 1	:	AMY_CHASE
#alias 2	:	AMY_AWAIT_RIGHT
#alias 3	:	AMY_AWAIT_LEFT
#alias 4	:	AMY_KIDNAP		// Set by MetalSonic

// Metal Sonic States
#alias 0	:	METALSONIC_WAIT
#alias 1	:	METALSONIC_APPROACH

// Gravity
#alias 0	:	GRAVITY_GROUND
#alias 1	:	GRAVITY_AIR

// Players
#alias 0	:	PLAYER_SONIC_A

// Collision Sides
#alias 0	:	CSIDE_FLOOR

// Ink
#alias 2	:	INK_ALPHA

// Game Mode Aliases
#alias 2	:	MODE_TIMEATTACK

// Priority
#alias 1	:	PRIORITY_ACTIVE


sub ObjectMain
	Object.Priority = PRIORITY_ACTIVE
	if Player.XPos < Object.XPos
		Object.Direction = FACING_LEFT
	else
		Object.Direction = FACING_RIGHT
	end if

	switch Object.State
	case AMY_IDLE
		TempValue0   = Object.FrameTimer
		TempValue0  /= 10
		TempValue0  += 11
		Object.Frame = TempValue0

		Object.FrameTimer++
		if Object.FrameTimer > 139
			Object.FrameTimer = 0
		end if

		PlayerObjectCollision(C_TOUCH, -96, -360, 96, 360)
		if CheckResult == true
			Object.XVelocity = 0
			Object.State = AMY_CHASE
			Object.Frame = 4
			Object.FrameTimer = 0
		end if
		break

	case AMY_CHASE
		Object.FrameTimer++
		switch Object.Frame
		case 4
		case 6
			if Object.FrameTimer == 4
				Object.FrameTimer = 0
				Object.Frame++
			end if
			break

		case 5
		case 7
			if Object.FrameTimer == 3
				Object.FrameTimer = 0
				Object.Frame++
			end if
			break

		end switch

		if Object.Frame > 7
			Object.Frame = 4
		end if

		if Object.Direction == FACING_RIGHT
			if Object.XVelocity < 0x20000
				Object.XVelocity += 0x2000
			end if

			if Object.XPos < Object.RightLimit
				Object.XPos += Object.XVelocity
			else
				Object.XVelocity = 0
				Object.XPos  = Object.RightLimit
				Object.State = AMY_AWAIT_RIGHT
				Object.Frame = 0
				Object.FrameTimer = 0
			end if
		else
			if Object.XVelocity > -0x20000
				Object.XVelocity -= 0x2000
			end if

			if Object.XPos > Object.LeftLimit
				Object.XPos += Object.XVelocity
			else
				Object.XVelocity = 0
				Object.XPos  = Object.LeftLimit
				Object.State = AMY_AWAIT_LEFT
				Object.Frame = 0
				Object.FrameTimer = 0
			end if
		end if
		break

	case AMY_AWAIT_RIGHT
		Object.FrameTimer++
		if Object.FrameTimer == 12
			Object.FrameTimer = 0
			Object.Frame++
		end if

		if Object.Frame > 3
			Object.Frame = 0
		end if

		if Metal.State == METALSONIC_WAIT
			if Metal.Timer < 60
				Metal.Timer++
			else
				Metal.Timer = 0
				Metal.State = METALSONIC_APPROACH
				Metal.Priority = PRIORITY_ACTIVE
			end if
		end if
		break

	case AMY_AWAIT_LEFT
		Object.FrameTimer++
		if Object.FrameTimer == 12
			Object.FrameTimer = 0
			Object.Frame++
		end if

		if Object.Frame > 3
			Object.Frame = 0
		end if

		if Object.Direction == FACING_RIGHT
			Object.State = AMY_CHASE
			Object.Frame = 4
			Object.FrameTimer = 0
		end if
		break

	case AMY_KIDNAP
		Object.FrameTimer++
		if Object.FrameTimer > 13
			Object.FrameTimer = 0
			Object.Frame++
			if Object.Frame > 9
				Object.Frame = 8
			end if
		end if
		break

	end switch

	Object.HeartTimer++
	if Object.State < 4
		if Object.HeartTimer > 41
			CreateTempObject(TypeName[Love Heart], 0, Object.XPos, Object.YPos)
			Object[TempObjectPos].iYPos -= 6
			Object[TempObjectPos].InkEffect = INK_ALPHA
			Object[TempObjectPos].Alpha = 255
			Object.HeartTimer = 0
		end if

	end if

	if Object.Gravity == GRAVITY_GROUND
		ObjectTileGrip(CSIDE_FLOOR, 0, 19, 0)
	else
		Object.YVelocity += 0x4000
		Object.YPos += Object.YVelocity
		ObjectTileCollision(CSIDE_FLOOR, 0, 19, 0)
		if CheckResult == true
			Object.YVelocity = 0
			Object.Gravity = GRAVITY_GROUND
		end if

	end if
end sub


sub ObjectDraw
	DrawSpriteFX(Object.Frame, FX_FLIP, Object.XPos, Object.YPos)
end sub


sub ObjectStartup
	LoadSpriteSheet("R3/Objects3.gif")

	// Idle
	SpriteFrame(-11, -20, 22, 40, 34, 208)		// #0  - Amy Idle Frame 0
	SpriteFrame(-11, -20, 22, 40, 57, 208)		// #1  - Amy Idle Frame 1
	SpriteFrame(-11, -20, 22, 40, 34, 208)		// #2  - Amy Idle Frame 2
	SpriteFrame(-11, -20, 22, 40, 126, 167)		// #3  - Amy Idle Frame 3

	// Walking
	SpriteFrame(-15, -20, 30, 40, 68, 167)		// #4  - Amy Walking Frame 0
	SpriteFrame(-10, -20, 21, 40, 206, 208)		// #5  - Amy Walking Frame 1
	SpriteFrame(-13, -20, 26, 40, 99, 167)		// #6  - Amy Walking Frame 2
	SpriteFrame(-10, -20, 21, 40, 206, 208)		// #7  - Amy Walking Frame 3

	// Kidnapped
	SpriteFrame(0, -16, 24, 40, 1, 170)			// #8  - Amy Kidnap Frame 0
	SpriteFrame(0, -16, 24, 40, 1, 211)			// #9  - Amy Kidnap Frame 1
	
	// Jumping
	SpriteFrame(-12, -20, 24, 40, 107, 208)		// #10 - Amy Jumping Frame 1

	// Looking for Sonic
	// Amy Looking Frame "0"
	SpriteFrame(-12, -20, 22, 40, 158, 208)		// #11
	SpriteFrame(-12, -20, 22, 40, 158, 208)		// #12
	SpriteFrame(-12, -20, 22, 40, 158, 208)		// #13
	SpriteFrame(-12, -20, 22, 40, 158, 208)		// #14
	SpriteFrame(-12, -20, 22, 40, 158, 208)		// #15

	// Amy Looking Frame "1"
	SpriteFrame(-14, -20, 25, 40, 132, 208)		// #16
	SpriteFrame(-14, -20, 25, 40, 132, 208)		// #17
	SpriteFrame(-14, -20, 25, 40, 132, 208)		// #18

	// Amy Looking Frame "2"
	SpriteFrame(-12, -20, 22, 40, 158, 208)		// #19
	SpriteFrame(-12, -20, 22, 40, 158, 208)		// #20
	SpriteFrame(-12, -20, 22, 40, 158, 208)		// #21

	// Amy Looking Frame "3"
	SpriteFrame(-14, -20, 25, 40, 132, 208)		// #22
	SpriteFrame(-14, -20, 25, 40, 132, 208)		// #23
	SpriteFrame(-14, -20, 25, 40, 132, 208)		// #24

	// Used to be below LoadSpriteSheet, moved here for consistency
	ArrayPos0 = 32
	while ArrayPos0 < 1056

		if Object[ArrayPos0].Type == TypeName[Amy Rose]

			if Stage.PlayerListPos == PLAYER_SONIC_A // PLAYER_SONIC on origins

				if Options.GameMode == MODE_TIMEATTACK
					Object[ArrayPos0].Type = TypeName[Blank Object]
					ArrayPos0++
					Object[ArrayPos0].Type = TypeName[Blank Object]
					ArrayPos0++
					Object[ArrayPos0].Type = TypeName[Blank Object]
					ArrayPos0++
					Object[ArrayPos0].Type = TypeName[Blank Object]
					ArrayPos0++
					Object[ArrayPos0].Type = TypeName[Blank Object]
				else
					Object[ArrayPos0].State = AMY_AWAIT_LEFT
					Object[ArrayPos0].LeftLimit  = 0x780000
					Object[ArrayPos0].RightLimit = 0x3BC0000
					Object[ArrayPos0].DrawOrder = 4
				end if

			else
				// Remove Amy
				Object[ArrayPos0].Type = TypeName[Blank Object]
				ArrayPos0++
				// Remove Metal
				Object[ArrayPos0].Type = TypeName[Blank Object]
				ArrayPos0++
				// Remove Spikes
				Object[ArrayPos0].Type = TypeName[Blank Object]
				ArrayPos0++
				Object[ArrayPos0].Type = TypeName[Blank Object]
				ArrayPos0++
				Object[ArrayPos0].Type = TypeName[Blank Object]
			end if

		end if
		ArrayPos0++
	loop
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	Object.Direction = FACING_LEFT
	DrawSpriteFX(0, FX_FLIP, Object.XPos, Object.YPos)
end sub


sub RSDKLoad
	LoadSpriteSheet("R3/Objects3.gif")

	// Idle
	SpriteFrame(-11, -20, 22, 40, 34, 208)		// #0  - Amy Idle Frame 0

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
