//----------------Sonic CD Tube Switch Script-----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.OnObject
#alias Object.Value1	:	Object.LedgeStop

// Player Aliases
#alias Player.Value6	:	Player.MinRollSpeed

// Gravity
#alias 0	:	GRAVITY_GROUND
#alias 1	:	GRAVITY_AIR

// Collision Modes
#alias 0	:	CMODE_FLOOR
#alias 1	:	CMODE_LWALL
#alias 2	:	CMODE_ROOF
#alias 3	:	CMODE_RWALL

// Property Values
#alias 0	:	TUBESWITCH_H_L
#alias 1	:	TUBESWITCH_H_R
#alias 2	:	TUBESWITCH_V_T
#alias 3	:	TUBESWITCH_V_B
#alias 4	:	TUBESWITCH_ROLL_ALIGN
#alias 5	:	TUBESWITCH_V_GROUNDED
#alias 6	:	TUBESWITCH_V_FORCE_DW
#alias 7	:	TUBESWITCH_V_FORCE_L_DW
#alias 8	:	TUBESWITCH_FORCE_ROLL
#alias 9	:	TUBESWITCH_V_GROUNDED_DW
#alias 10	:	TUBESWITCH_GROUNDED_CANCEL
#alias 11	:	TUBESWITCH_EDGE_BOUNCE
#alias 12	:	TUBESWITCH_ROLL_MASK

// Player Aliases
#alias Player.Value2 : Player.RollAnimationSpeed
#alias Player.Value6 : Player.MinRollSpeed


sub ObjectPlayerInteraction
	PlayerObjectCollision(C_TOUCH, -16, -16, 16, 16)
#platform: Use_Origins
	if CheckResult == true
		if Stage.PlayerListPos == PLAYER_KNUCKLES
			if Player.State == Player_State_LedgePullUp
				CheckResult   = false
				Object.LedgeStop = true
			end if
		end if
	end if
#endplatform
	if CheckResult == true
		switch Object.PropertyValue
		case TUBESWITCH_H_L
#platform: Use_Origins
			if Object.LedgeStop == true
				Player.Speed	 = 1
				Object.LedgeStop = false
			end if
#endplatform
			if Player.Speed > 0
				Player.Direction = FACING_RIGHT
				Player.State = Player_State_TubeRoll
				Player.Animation = ANI_JUMPING
				Player.MinRollSpeed = 0xA0000

				if Player.Speed < 0xA0000
					Player.Speed = 0xA0000
				end if

				if Player.Gravity == GRAVITY_AIR
					Player.Gravity = GRAVITY_GROUND
					Player.YPos = Object.YPos
				end if
			else
				if Player.Gravity == GRAVITY_GROUND
					if Player.Speed > -0x20000
						Player.Speed = -0x20000
					end if
				end if

				Player.State = Player_State_Roll
				Player.Animation = ANI_JUMPING
			end if
			break

		case TUBESWITCH_H_R
			if Player.Speed < 0
				Player.State = Player_State_TubeRoll
				Player.Animation = ANI_JUMPING
				Player.MinRollSpeed = 0xA0000

				if Player.Speed > -0xA0000
					Player.Speed = -0xA0000
				end if
			else
				if Player.Gravity == GRAVITY_GROUND
					if Player.Speed < 0x20000
						Player.Speed = 0x20000
					end if
				end if

				Player.State = Player_State_Roll
				Player.Animation = ANI_JUMPING
			end if
			break

		case TUBESWITCH_V_T
			if Object.OnObject == false

				if Player.Gravity == GRAVITY_AIR
					Player.Gravity = GRAVITY_GROUND
					Player.State = Player_State_TubeRoll
					Player.Animation = ANI_JUMPING
					Player.CollisionMode = CMODE_RWALL
					Player.Angle = 64
					Player.Speed = 0x100000
					Player.MinRollSpeed = 0x100000
				else
					if Player.Speed > 0
						Player.Gravity = GRAVITY_GROUND
						Player.State = Player_State_TubeRoll
						Player.Animation = ANI_JUMPING
						Player.CollisionMode = CMODE_RWALL
						Player.Angle = 64
						Player.Speed = 0x100000
						Player.MinRollSpeed = 0x100000
					else
						Player.Gravity = GRAVITY_AIR
						Player.YVelocity = Player.Speed
						Player.Speed = 0
						Player.XVelocity = 0
					end if

				end if
				
			end if
			break

		case TUBESWITCH_V_B
			if Object.OnObject == false
				if Player.Gravity == GRAVITY_AIR
					Player.Gravity	 = GRAVITY_GROUND
					Player.State	 = Player_State_TubeRoll
					Player.Animation = ANI_JUMPING
					Player.CollisionMode = CMODE_RWALL
					Player.Angle = 64
					Player.Speed = -0xA0000
					Player.MinRollSpeed = 0xA0000
				end if
				if Player.CollisionMode == CMODE_RWALL
					if Player.Speed < 0
						Player.Gravity	 = GRAVITY_GROUND
						Player.State	 = Player_State_TubeRoll
						Player.Animation = ANI_JUMPING
						Player.CollisionMode = CMODE_RWALL
						Player.Angle = 64
						Player.Speed = -0xA0000
						Player.MinRollSpeed = 0xA0000
					else
						Player.Gravity = GRAVITY_AIR
						Player.YVelocity = Player.Speed
						Player.Speed = 0
						Player.XVelocity = 0
					end if
				end if
			end if
			break

		case TUBESWITCH_ROLL_ALIGN
			if Player.Gravity == GRAVITY_GROUND
				Player.State = Player_State_Roll
			else
#platform: Use_Origins
				if Player.State != Player_State_Climb
					Player.XPos = Object.XPos
				end if
#endplatform

#platform: Use_Standalone
				Player.XPos = Object.XPos
#endplatform

			end if
			if Player.Animation != ANI_JUMPING
				Player.Animation = ANI_JUMPING
				Player.RollAnimationSpeed = 240
			end if
			break

		case TUBESWITCH_V_GROUNDED
			if Object.OnObject == false
				if Player.YVelocity > 0
					Player.State = Player_State_TubeRoll
					
					Player.Animation = ANI_JUMPING

					Player.Gravity = GRAVITY_GROUND
					Player.CollisionMode = CMODE_RWALL

					Player.Angle = 64
					Player.Speed = 0x100000
					Player.MinRollSpeed = 0x100000
					
				end if
			else

#platform: Use_Origins
				CheckEqual(Player.State, Player_State_Air_NoDropDash)
				TempValue0  = CheckResult
				CheckEqual(Player.State, Player_State_Air)
				TempValue0 |= CheckResult
				if TempValue0 != false
					Player.Animation = ANI_JUMPING
				end if
#endplatform

#platform: Use_Standalone
				if Player.State == Player_State_Air
					Player.Animation = ANI_JUMPING
				end if
#endplatform

			end if
			break

		case TUBESWITCH_V_FORCE_DW
			if Object.OnObject == false

				if Player.Gravity == GRAVITY_AIR
					Player.Gravity	 = GRAVITY_GROUND
					Player.State	 = Player_State_TubeRoll
					Player.Animation = ANI_JUMPING
					Player.CollisionMode = CMODE_RWALL
					Player.Angle = 64
					Player.Speed = 0x100000
					Player.MinRollSpeed = 0x100000
				else
					Player.Gravity = GRAVITY_AIR
					Player.YVelocity = Player.Speed
					FlipSign(Player.YVelocity)
					Player.Speed = 0
					Player.XVelocity = 0
				end if

			end if
			break

		case TUBESWITCH_V_FORCE_L_DW
			if Object.OnObject == false
				if Player.YVelocity > 0
					Player.Gravity	 = GRAVITY_GROUND
					Player.State	 = Player_State_TubeRoll
					Player.Animation = ANI_JUMPING
					Player.CollisionMode = CMODE_LWALL
					Player.Angle = 192
					Player.Speed = -0x100000
					Player.MinRollSpeed = 0x100000
				end if
			else
				Player.Animation = ANI_JUMPING
			end if
			break

		case TUBESWITCH_FORCE_ROLL
			if Player.State != Player_State_Fly
				if Player.Gravity == GRAVITY_GROUND
					Player.State = Player_State_Roll
				end if
				Player.Animation = ANI_JUMPING
			end if
			break

		case TUBESWITCH_V_GROUNDED_DW
			if Object.OnObject == false
				if Player.YVelocity > 0
					Player.Gravity	 = GRAVITY_GROUND
					Player.State	 = Player_State_TubeRoll
					Player.Animation = ANI_JUMPING
					Player.CollisionMode = CMODE_RWALL
					Player.Angle = 64
					Player.Speed = 0x80000
					Player.MinRollSpeed = 0x80000
				end if
			else
				Player.Animation = ANI_JUMPING
			end if
			break

		case TUBESWITCH_GROUNDED_CANCEL
			if Object.OnObject == false
				if Player.Gravity == GRAVITY_GROUND
					Player.Gravity	 = GRAVITY_AIR
					Player.YVelocity = Player.Speed
					Player.Speed	 = 0
					Player.XVelocity = 0
				end if
			end if
			break

		case TUBESWITCH_EDGE_BOUNCE
			if Player.YVelocity > 0
				if Player.XPos > Object.XPos
					TempValue0  = Player.XPos
					TempValue0 -= Object.XPos
					if TempValue0 < 0x60000
						Player.Gravity = GRAVITY_AIR
						Player.YVelocity = -0x30000
					end if
				else
					TempValue0  = Object.XPos
					TempValue0 -= Player.XPos
					if TempValue0 < 0x60000
						Player.Gravity = GRAVITY_AIR
						Player.YVelocity = -0x30000
					end if
				end if
			end if
			break

		case TUBESWITCH_ROLL_MASK
			if Player.State != Player_State_Fly
				if Player.XVelocity > 0x40000
					if Player.Gravity == GRAVITY_GROUND
						Player.State = Player_State_Roll
					end if
					Player.Animation = ANI_JUMPING
				end if
			end if
			break

		end switch
		Object.OnObject = true
	else
		Object.OnObject = false
	end if
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	TempValue0  = Object.XPos
	TempValue0 -= 0x80000
	TempValue1  = Object.YPos
	TempValue1 -= 0x80000

	DrawSpriteXY(0, TempValue0, TempValue1)

	TempValue0 += 0x100000
	DrawSpriteXY(0, TempValue0, TempValue1)

	TempValue1 += 0x100000
	DrawSpriteXY(0, TempValue0, TempValue1)

	TempValue0 -= 0x100000
	DrawSpriteXY(0, TempValue0, TempValue1)
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-8, -8, 16, 16, 173, 67)		// #0 - "T" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
