//---------------------Sonic CD Start Script------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.Fade
#alias Object.Value1	:	Object.StageSelectCheatPos
#alias Object.Value2	:	Object.SoundTestCheatPos
#alias Object.Value3 	:	Object.DemoTimer

// States
#alias 0	:	START_SETUP
#alias 1	:	START_GOTO_MENU

// These following three states go unused
#alias 2	:	START_GOTO_STAGESELECT
#alias 3	:	START_GOTO_SOUNDTEST
#alias 4	:	START_GOTO_DEMO

// Cheat Input Aliases
#alias 0	:	CHEATINPUT_NONE
#alias 1	:	CHEATINPUT_UP
#alias 2	:	CHEATINPUT_DOWN
#alias 3	:	CHEATINPUT_LEFT
#alias 4	:	CHEATINPUT_RIGHT
#alias 5	:	CHEATINPUT_BUTTONA
#alias 6	:	CHEATINPUT_BUTTONB
#alias 7	:	CHEATINPUT_BUTTONC

// DemoNumber Aliases
#alias 0	:	DEMO_R11A
#alias 1	:	DEMO_SS1
#alias 2	:	DEMO_R43C
#alias 3	:	DEMO_SS6
#alias 4	:	DEMO_R82A
#alias 5	:	DEMO_MAX

// Select Aliases
#alias 0	:	SELECT_INIT

// Global SFX
#alias 1	:	SFX_G_RING
#alias 27	:	SFX_G_SELECT

// Callbacks & Engine States
#alias 1	:	CALLBACK_PRESS_START
#alias 7	:	CALLBACK_EXIT_SELECTED
#alias 16	:	CALLBACK_MOREGAMES

// Game Mode Aliases
#alias 0	:	MODE_NOSAVE

// Language Aliases
#alias 0	:	LANG_ENGLISH
#alias 1	:	LANG_FRENCH
#alias 2	:	LANG_ITALIAN
#alias 3	:	LANG_DEUTSCH
#alias 4	:	LANG_SPANISH
#alias 5	:	LANG_JAPANESE

// Presentation Stages
#alias 1	:	STAGE_P_MENU
#alias 3	:	STAGE_P_STAGESELECT
#alias 4	:	STAGE_P_SOUNDTEST

// Regular Stages
#alias 0	:	STAGE_R_R11A
#alias 28 	:	STAGE_R_R43C
#alias 64	:	STAGE_R_R82A

// Special Stages
#alias 0	:	STAGE_S_SS1
#alias 5	:	STAGE_S_SS6

// Device Type
#alias 0	:	RETRO_STANDARD

// Entirely functional, yet unused
// Perhaps it was removed because it would devalue unlocking things in Time Attack?
// No way to say for sure
#function TouchStart_HandleCheatCodes


function TouchStart_HandleCheatCodes
	
	TempValue0 = CHEATINPUT_NONE
	
	// Funnily enough, they updated this in Origins to use the new input syntax, but it stayed unused all the same...
	if KeyPress[0].Up == true
		TempValue0 = CHEATINPUT_UP
	end if
	
	if KeyPress[0].Down == true
		TempValue0 = CHEATINPUT_DOWN
	end if
	
	if KeyPress[0].Left == true
		TempValue0 = CHEATINPUT_LEFT
	end if
	
	if KeyPress[0].Right == true
		TempValue0 = CHEATINPUT_RIGHT
	end if
	
	if KeyPress[0].ButtonA == true
		TempValue0 = CHEATINPUT_BUTTONA
	end if
	
	if KeyPress[0].ButtonB == true
		TempValue0 = CHEATINPUT_BUTTONB
	end if
	
	if KeyPress[0].ButtonC == true
		TempValue0 = CHEATINPUT_BUTTONC
	end if
	
	// Up, Down, Down, Left, Right, B
	switch Object.StageSelectCheatPos
	case 0
		if TempValue0 == CHEATINPUT_UP
			Object.StageSelectCheatPos++
		else
			if TempValue0 != CHEATINPUT_NONE
				Object.StageSelectCheatPos = 0
			end if
		end if
		break
		
	case 1
		if TempValue0 == CHEATINPUT_DOWN
			Object.StageSelectCheatPos++
		else
			if TempValue0 != CHEATINPUT_NONE
				Object.StageSelectCheatPos = 0
			end if
		end if
		break
		
	case 2
		if TempValue0 == CHEATINPUT_DOWN
			Object.StageSelectCheatPos++
		else
			if TempValue0 != CHEATINPUT_NONE
				Object.StageSelectCheatPos = 0
			end if
		end if
		break
		
	case 3
		if TempValue0 == CHEATINPUT_LEFT
			Object.StageSelectCheatPos++
		else
			if TempValue0 != CHEATINPUT_NONE
				Object.StageSelectCheatPos = 0
			end if
		end if
		break
		
	case 4
		if TempValue0 == CHEATINPUT_RIGHT
			Object.StageSelectCheatPos++
		else
			if TempValue0 != CHEATINPUT_NONE
				Object.StageSelectCheatPos = 0
			end if
		end if
		break
		
	case 5
		if TempValue0 == CHEATINPUT_BUTTONB
			Object.StageSelectCheatPos = 0
			
			// Go to the Stage Select
			Object.State = START_GOTO_STAGESELECT
			StopMusic()
			
			// Just a cool lil' jingle :)
			PlaySfx(SFX_G_RING, false)
		else
			if TempValue0 != CHEATINPUT_NONE
				Object.StageSelectCheatPos = 0
			end if
		end if
		break
		
	end switch
	
	// Down, Down, Down, Left, Right, A
	switch Object.SoundTestCheatPos
	case 0
		if TempValue0 == CHEATINPUT_DOWN
			Object.SoundTestCheatPos++
		else
			if TempValue0 != CHEATINPUT_NONE
				Object.SoundTestCheatPos = 0
			end if
		end if
		break
		
	case 1
		if TempValue0 == CHEATINPUT_DOWN
			Object.SoundTestCheatPos++
		else
			if TempValue0 != CHEATINPUT_NONE
				Object.SoundTestCheatPos = 0
			end if
		end if
		break
		
	case 2
		if TempValue0 == CHEATINPUT_DOWN
			Object.SoundTestCheatPos++
		else
			if TempValue0 != CHEATINPUT_NONE
				Object.SoundTestCheatPos = 0
			end if
		end if
		break
		
	case 3
		if TempValue0 == CHEATINPUT_LEFT
			Object.SoundTestCheatPos++
		else
			if TempValue0 != CHEATINPUT_NONE
				Object.SoundTestCheatPos = 0
			end if
		end if
		break
		
	case 4
		if TempValue0 == CHEATINPUT_RIGHT
			Object.SoundTestCheatPos++
		else
			if TempValue0 != CHEATINPUT_NONE
				Object.SoundTestCheatPos = 0
			end if
		end if
		break
		
	case 5
		if TempValue0 == CHEATINPUT_BUTTONA
			Object.SoundTestCheatPos = 0
			
			// Go to the Sound Test
			Object.State = START_GOTO_SOUNDTEST
			StopMusic()
			
			PlaySfx(SFX_G_RING, false)
		else
			if TempValue0 != CHEATINPUT_NONE
				Object.SoundTestCheatPos = 0
			end if
		end if
		break
		
	end switch
	
end function


sub ObjectMain
	switch Object.State
	case START_SETUP
		CheckTouchRect(0, 0, Screen.XSize, 240)
		
		if KeyPress[0].Start == true
			CheckResult = false
		end if

		if CheckResult > -1	// Check if you used the touch input, or if what's above is true
			Object.State = START_GOTO_MENU	// Select Menu
			
			StopMusic()
			PlaySfx(SFX_G_SELECT, false)
			
			HapticEffect(31, 0, 0, 0)
		else
			Object.DemoTimer++
			
			if Object.DemoTimer == 1550
				Object.State = START_GOTO_DEMO
			end if
		end if
		
		// I mean, if you want to I guess?
		if Engine.PlatformID == RETRO_WP7
			if KeyPress[0].ButtonB == true
				EngineCallback(CALLBACK_EXIT_SELECTED)
			end if
		end if
		break
		
	case START_GOTO_MENU
		Object.Fade += 4
		if Object.Fade == 384
			Stage.ListPos = STAGE_P_MENU
			LoadStage()
		end if
		SetScreenFade(0, 0, 0, Object.Fade)
		break
		
	case START_GOTO_STAGESELECT
		// This state is unused, but it's meant to fade out the game and go to the Level Select
		
		Object.Fade += 4
		
		if Object.Fade == 384
			Options.GameMode = MODE_NOSAVE
			Stage.ListPos = STAGE_P_STAGESELECT
			LoadStage()
		end if
		
		SetScreenFade(0, 0, 0, Object.Fade)
		break
		
	case START_GOTO_SOUNDTEST
		// This state is also unused, similarly meant to fade the game out to the Sound Test
		
		Object.Fade += 4
		
		if Object.Fade == 384
			Options.GameMode = MODE_NOSAVE
			Stage.ListPos = STAGE_P_SOUNDTEST
			LoadStage()
		end if
		
		SetScreenFade(0, 0, 0, Object.Fade)
		break
		
	case START_GOTO_DEMO
		// This state's unused as well...
		// The game's supposed to fade out and watch a demo, but Origins broke it so
		
		Object.Fade += 4
		
		if Object.Fade == 384
			
			Engine.HapticsEnabled = false
			
			// The game is to fade in from a black screen
			Fade_Colour = 0x000000
			
			// Reset loads of stuff
			Player.Score	  		= 0
			Player.Lives	  		= 3
			Player.ScoreBonus		= 50000
			Transporter_Destroyed 	= false
			MetalSonic_Destroyed 	= false
			Good_Future 			= false
			Options.GameMode		= MODE_NOSAVE
			Options.AttractMode 	= true

			if Engine.PlatformID == RETRO_ANDROID
				if Engine.TrialMode == true
					Options.DemoNumber = 0
				end if
			end if

			switch Options.DemoNumber
			case DEMO_R11A
				// R11A - Palmtree Panic Act 1, Present
				Stage.ActiveList = REGULAR_STAGE
				Stage.ListPos	 = STAGE_R_R11A
				break
				
			case DEMO_SS1
				// SS1 - Special Stage 1
				Stage.ActiveList = SPECIAL_STAGE
#platform: Use_Origins
				Stage.ActiveList = 3
#endplatform
				Stage.ListPos	 = STAGE_S_SS1
				break
				
			case DEMO_R43C
				// R43C - Tidal Tempest 3, Good Future
				Stage.ActiveList = REGULAR_STAGE
				Stage.ListPos	 = STAGE_R_R43C
				break
				
			case DEMO_SS6
				// SS6 - Special Stage 6
				Stage.ActiveList = SPECIAL_STAGE
#platform: Use_Origins
				Stage.ActiveList = 3
#endplatform
				Stage.ListPos	 = STAGE_S_SS6
				break
				
			case DEMO_R82A
				// R82A - Metallic Madness 2 Present
				Stage.ActiveList = REGULAR_STAGE
				Stage.ListPos	 = STAGE_R_R82A
				break
				
			end switch
			
			// Cycle through the same 6 demos
			Options.DemoNumber++
			Options.DemoNumber %= 5
			
			LoadStage()
		end if
		
		SetScreenFade(0, 0, 0, Object.Fade)
		break
		
	end switch
	
end sub


sub ObjectDraw
	Object.AnimationTimer++
	if Object.AnimationTimer > 59
		Object.AnimationTimer = 0
	end if

	if Object.AnimationTimer > 29
#platform: Standard
		DrawSprite(0)
#endplatform
#platform: Mobile
		DrawSprite(1)
#endplatform
	end if
end sub


sub ObjectStartup
	

	// Some callbacks, to let the engine know what's happening
#platform: Use_Origins
	game.callbackParam0 = 0
	EngineCallback(NOTIFY_LEVEL_SELECT_MENU)
#endplatform
	LoadSpriteSheet("Title/Title.gif")

	// The following Sprites differ between languages
	
	// They follow the order of 
	//   0 - Press to Start
	//   1 - Touch to Start!
	
	// But of course in the language they correspond to
	
	// Do note, sprite 0 - the Press to Start one - is unused
	

	switch Engine.Language
	case LANG_ENGLISH
	case LANG_JAPANESE
		
		// Press Start
		SpriteFrame(-43, 0, 87, 11, 106, 103)
		
		// Touch to Start!
		SpriteFrame(-56, 0, 117, 11, 106, 91)
		break
		
	case LANG_FRENCH
		
		// Appui sur Start
		SpriteFrame(-60, 0, 120, 11, 391, 387)
		
		// Touche pour Commencer !
		SpriteFrame(-94, 0, 189, 11, 322, 399)
		break
		
	case LANG_ITALIAN
		
		// Premi Start
		SpriteFrame(-44, 0, 88, 11, 270, 411)
		
		// Tocca Per Iniziare!
		SpriteFrame(-76, 0, 152, 11, 359, 411)
		break
		
	case LANG_DEUTSCH
		
		// Start Drücken
		SpriteFrame(-52, -2, 106, 13, 405, 373)
		
		// Zum Starten Antippen!
		SpriteFrame(-87, 0, 175, 11, 336, 423)
		break
		
	case LANG_SPANISH
		
		// Pulsa Start
		SpriteFrame(-43, 0, 87, 11, 274, 435)
		
		// ¡Toca para Empezar!
		SpriteFrame(-74, 0, 149, 11, 362, 435)
		break
		
	end switch
	// Press Button (Always LANG_ENGLISH)
	SpriteFrame(-48, 0, 97, 11, 414, 361)
	
	// Sega Copyright text
	SpriteFrame(-88, -8, 88, 8, 423, 311)
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Title/Title.gif")
	SpriteFrame(-43, 0, 87, 11, 106, 103) // LANG_ENGLISH "Press Start"

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
