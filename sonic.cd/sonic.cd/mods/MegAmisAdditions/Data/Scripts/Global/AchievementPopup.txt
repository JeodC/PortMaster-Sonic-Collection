//---Sonic CD Achievement Popup Script---//
//----------Scripted by MegAmi-----------//

// Aliases
#alias Object.PropertyValue	:	Object.Achievement
#alias Object.Value0		:	Object.Timer

// States
#alias 0	:	ACHIEVEMENT_SLIDEUP
#alias 1	:	ACHIEVEMENT_WAIT
#alias 2	:	ACHIEVEMENT_SLIDEDOWN

// Priority
#alias 1	:	PRIORITY_ACTIVE

// Global SFX
#alias 21	:	SFX_G_ACHIEVEMENT


#function Achievement_CheckAchievement


function Achievement_CheckAchievement
	if Achievement.Trigger > 0
		TempValue0 = Achievement.Trigger
		Achievement.Trigger = 0
		ArrayPos1 = 256
		ArrayPos1 += TempValue0
		ReadSaveRAM()
		if SaveRAM[ArrayPos1] != true
			SaveRAM[ArrayPos1] = true
			WriteSaveRAM()
			PlaySfx(SFX_G_ACHIEVEMENT, false)
			CreateTempObject(TypeName[Achievement Popup], TempValue0, 0, Screen.YSize)
			Object[TempObjectPos].Priority = PRIORITY_ACTIVE
			Object[TempObjectPos].DrawOrder = 6
			Object[TempObjectPos].State = ACHIEVEMENT_SLIDEUP
			Object[TempObjectPos].Timer = 0
		end if
	end if
end function


sub ObjectMain
	switch Object.State
	case ACHIEVEMENT_SLIDEUP
		Object.YPos -= 2
		TempValue0 = Screen.YSize
		TempValue0 -= 52
		if Object.YPos <= TempValue0
			Object.YPos = TempValue0
			Object.State++
		end if
		break
		
	case ACHIEVEMENT_WAIT
		Object.Timer++
		if Object.Timer >= 180
			Object.State++
		end if
		break
		
	case ACHIEVEMENT_SLIDEDOWN
		Object.YPos += 2
		TempValue0 = Screen.YSize
		TempValue0 += 30
		if Object.YPos >= TempValue0
			Object.Type = TypeName[Blank Object]
			CallFunction(AchievementFunction)
		end if
		break
	end switch
end sub


sub ObjectDraw
	DrawSpriteScreenXY(0, Screen.XSize, Object.YPos)
	TempValue0 = Screen.XSize
	TempValue0 -= 57
	TempValue1 = Object.YPos
	TempValue1 += 38
	DrawSpriteScreenXY(Object.Achievement, TempValue0, TempValue1)
end sub


sub ObjectStartup
	// Can't check the stage folder in v3 so uh
	CheckEqual(Stage.ActiveList, PRESENTATION_STAGE)
	TempValue0 = CheckResult
	CheckEqual(Stage.ListPos, StageName[P - TIME ATTACK])
	TempValue0 &= CheckResult
	if TempValue0 == true
		LoadSpriteSheet("TAttack/AchievementPopup.gif")
	else
		LoadSpriteSheet("Global/AchievementPopup.gif")
	end if
	SpriteFrame(-116,   0, 116, 52,  1,   1) // ACHIEVEMENT! - 0
	SpriteFrame( -31, -10,  63, 20,  1,  54) // 88 Miles Per Hour - 1
	SpriteFrame( -48, -10,  95, 20, 65,  54) // Just One Hug Is Enough - 2
	SpriteFrame( -31, -10,  63, 20,  1,  75) // Paradise Found - 3
	SpriteFrame( -35, -10,  71, 20, 65,  75) // Take The High Road - 4
	SpriteFrame( -35, -10,  71, 20,  1,  96) // King Of The Rings - 5
	SpriteFrame( -27, -10,  55, 20, 73,  96) // Statue Saviour - 6
	SpriteFrame( -19, -10,  39, 20,  1, 117) // Heavy Metal - 7
	SpriteFrame( -39, -10,  79, 20, 41, 117) // All Stages Clear! - 8
	SpriteFrame( -31, -10,  63, 20,  1, 138) // Treasure Hunter - 9
	SpriteFrame( -40, -10,  80, 20, 65, 138) // Dr. Eggman Got Served - 10
	SpriteFrame( -28, -10,  56, 20,  1, 159) // Just In Time! - 11
	SpriteFrame( -40, -10,  79, 20, 58, 159) // Saviour Of The Planet - 12
	
	AchievementFunction = Achievement_CheckAchievement
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/AchievementPopup.gif")
	SpriteFrame(-58, -26, 116, 52, 1, 1)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub