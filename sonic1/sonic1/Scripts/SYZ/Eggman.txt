// ----------------------------------
// RSDK Project: Sonic 1
// Script Description: Eggman Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// Using SYZEggman as a prefix here because there's like 12 "Eggman" objects in the game so its less confusing this way

// ========================
// Aliases
// ========================

private alias object.value0  : object.timer
private alias object.value1  : object.health
private alias object.value2  : object.invincibilityTimer
private alias object.value3  : object.flameAnim
private alias object.value4  : object.flameAnimTimer
private alias object.value5  : object.boundsL
private alias object.value6  : object.boundsR
private alias object.value7  : object.boundsB
private alias object.value8  : object.originPos.y
private alias object.value9  : object.stingerPos
private alias object.value10 : object.grabbedBlockSlot
private alias object.value11 : object.playerDetected

// Eggman States
private alias 0  : SYZEGGMAN_AWAITPLAYER
private alias 1  : SYZEGGMAN_MOVELEFT
private alias 2  : SYZEGGMAN_MOVERIGHT
private alias 3  : SYZEGGMAN_LOWERDOWN
private alias 4  : SYZEGGMAN_GRABBLOCK
private alias 5  : SYZEGGMAN_GRABBEDBLOCK
private alias 6  : SYZEGGMAN_SHAKEBLOCK
private alias 7  : SYZEGGMAN_RISEUP
private alias 8  : SYZEGGMAN_DESTROYBLOCK
private alias 9  : SYZEGGMAN_RETRACTSTINGER
private alias 10 : SYZEGGMAN_MISSEDBLOCK
private alias 11 : SYZEGGMAN_EXPLODE
private alias 12 : SYZEGGMAN_DEFEATED
private alias 13 : SYZEGGMAN_FLEE
private alias 14 : SYZEGGMAN_ESCAPE

// Eggman Animations
private alias 0 : SYZEGGANI_INVISIBLE
private alias 1 : SYZEGGANI_IDLE
private alias 2 : SYZEGGANI_LAUGH
private alias 3 : SYZEGGANI_LAUGH_LOOP
private alias 4 : SYZEGGANI_HIT
private alias 5 : SYZEGGANI_DEFEATED
private alias 6 : SYZEGGANI_TOASTED
private alias 7 : SYZEGGANI_PANIC

// Flame Animations
private alias 0 : FLAME_INACTIVE
private alias 1 : FLAME_ACTIVE
private alias 2 : FLAME_EXPLODE

// Marker Aliases
private alias object.xpos  : bossMarker.xpos
private alias object.ypos  : bossMarker.ypos
private alias object.ixpos : bossMarker.ixpos
private alias object.iypos : bossMarker.iypos

// Boss Barrier Aliases
private alias object.state : bossBarrier.state

private alias 1 : BOSSBARRIER_RISE
private alias 2 : BOSSBARRIER_LOWER

// Boss Block Aliases
private alias 1 : BOSSBLOCK_NONSOLID

// Player Aliases
private alias object.xpos : player.xpos
private alias object.xvel : player.xvel
private alias object.speed : player.speed
private alias object.animation : player.animation
private alias object.collisionRight : player.collisionRight

private alias object.value40 : player.hitboxLeft
private alias object.value38 : player.hitboxTop
private alias object.value41 : player.hitboxRight
private alias object.value39 : player.hitboxBottom

// Music Events
private alias 26 : SLOT_MUSICEVENT_BOSS

private alias 0 : MUSICEVENT_FADETOBOSS
private alias 1 : MUSICEVENT_FADETOSTAGE
private alias 2 : MUSICEVENT_TRANSITION


// ========================
// Function Declarations
// ========================

reserve function SYZEggman_Float
reserve function SYZEggman_CheckAbovePlayer
reserve function SYZEggman_HandleBlockMovement


// ========================
// Function Definitions
// ========================

private function SYZEggman_Float
	Sin256(object.ypos, object.angle)
	object.ypos <<= 10
	object.ypos += object.originPos.y
	object.angle += 2
	object.angle &= 0xFF
end function


private function SYZEggman_CheckAbovePlayer
	if object.playerDetected == false
		temp0 = player[0].xpos
		temp0 >>= 21
		temp1 = object.xpos
		temp1 >>= 21
		if temp0 == temp1
			temp0 = object.xpos
			temp0 >>= 16
			temp0 &= 31
			if temp0 == 16
				object.xpos &= 0xFFFF0000
				object.yvel = 0x18000
				object.flameAnim = FLAME_INACTIVE
				object.state = SYZEGGMAN_LOWERDOWN
				object.playerDetected = true
			end if
		end if
	end if
end function


private function SYZEggman_HandleBlockMovement
	arrayPos0 = object.grabbedBlockSlot
	object[arrayPos0].ypos = object.ypos
	object[arrayPos0].ypos += 0x280000
end function


// ========================
// Events
// ========================

event ObjectUpdate
	if camera[0].ypos < stage.newYBoundary1
		camera[0].ypos += 2
	end if

	switch object.state
	case SYZEGGMAN_AWAITPLAYER
		temp0 = bossMarker[+1].ixpos
		temp0 += screen.xcenter
		stage.newXBoundary2 = temp0

		temp0 -= screen.xsize
		stage.newXBoundary1 = temp0

		temp0 = bossMarker[+1].iypos
		temp0 -= screen.ysize
		stage.newYBoundary1 = temp0
		stage.newYBoundary2 = bossMarker[+1].iypos

		if player[0].xpos > bossMarker[+1].xpos
			object.boundsB = bossMarker[+1].ypos
			object.boundsB -= 0xC80000
			object.originPos.y = bossMarker[+1].ypos
			object.originPos.y -= 0xC80000
			object.boundsL = bossMarker[+1].xpos
			object.boundsL -= 0x960000
			object.boundsR = bossMarker[+1].xpos
			object.boundsR += 0x960000
			if screen.xsize > 320
				bossBarrier[+2].state = BOSSBARRIER_RISE
				bossBarrier[+3].state = BOSSBARRIER_RISE
			end if

			object.xvel = -0x10000
			object.flameAnim = FLAME_ACTIVE
			ResetObjectEntity(SLOT_MUSICEVENT_BOSS, TypeName[Music Event], MUSICEVENT_FADETOBOSS, 0, 0)
			object[SLOT_MUSICEVENT_BOSS].priority = PRIORITY_ACTIVE
			object.grabbedBlockSlot = -1
			object.state++
		end if
		break

	case SYZEGGMAN_MOVELEFT
		object.xpos += object.xvel
		if object.xpos <= object.boundsL
			FlipSign(object.xvel)
			object.direction = FACING_LEFT
			object.playerDetected = false
			object.state = SYZEGGMAN_MOVERIGHT
		end if
		CallFunction(SYZEggman_Float)
		CallFunction(SYZEggman_CheckAbovePlayer)
		break

	case SYZEGGMAN_MOVERIGHT
		object.xpos += object.xvel
		if object.xpos >= object.boundsR
			FlipSign(object.xvel)
			object.direction = FACING_RIGHT
			object.playerDetected = false
			object.state = SYZEGGMAN_MOVELEFT
		end if
		CallFunction(SYZEggman_Float)
		CallFunction(SYZEggman_CheckAbovePlayer)
		break

	case SYZEGGMAN_LOWERDOWN
		object.ypos += object.yvel
		temp0 = bossMarker[+1].ypos
		temp0 -= 0x4C0000

		if object.ypos >= temp0
			object.ypos = temp0
			object.yvel = 0
			object.state++
		end if

		if object.stingerPos < 0x200000
			object.stingerPos += 0x20000
		end if
		break

	case SYZEGGMAN_GRABBLOCK
		object.grabbedBlockSlot = -1
		foreach (TypeName[Boss Block], arrayPos0, ACTIVE_ENTITIES)
			BoxCollisionTest(C_TOUCH, object.entityPos, -8, -8, 8, 32, arrayPos0, -16, -16, 16, 16)
			if checkResult == true
				object[arrayPos0].state = BOSSBLOCK_NONSOLID
				object.grabbedBlockSlot = arrayPos0
			end if
		next

		if object.grabbedBlockSlot > -1
			object.state = SYZEGGMAN_GRABBEDBLOCK
			if object.animation == SYZEGGANI_IDLE
				object.animation = SYZEGGANI_PANIC
				object.animationTimer = 0
			end if
		else
			object.yvel = -0x40000
			object.state = SYZEGGMAN_MISSEDBLOCK
		end if
		break

	case SYZEGGMAN_GRABBEDBLOCK
		object.timer++
		if object.timer == 20
			object.timer = 0
			object.originPos.y = object.ypos
			object.state++
		end if
		break

	case SYZEGGMAN_SHAKEBLOCK
		temp0 = object.timer
		temp0 >>= 1
		temp0 &= 1
		if temp0 == 0
			object.ypos = -0x20000
		else
			object.ypos = 0x20000
		end if

		object.ypos += object.originPos.y
		object.timer++
		if object.timer == 32
			object.timer = 0
			object.yvel = -0x80000
			if object.animation != SYZEGGANI_PANIC
				object.animation = SYZEGGANI_PANIC
				object.animationTimer = 0
			end if
			object.state++
		end if

		CallFunction(SYZEggman_HandleBlockMovement)
		break

	case SYZEGGMAN_RISEUP
		object.ypos += object.yvel
		temp0 = bossMarker[+1].ypos
		temp0 -= 0xF00000
		if object.ypos <= temp0
			object.ypos = temp0
			object.originPos.y = object.ypos
			object.yvel = 0x18000
			object.state++
		end if
		CallFunction(SYZEggman_HandleBlockMovement)
		break

	case SYZEGGMAN_DESTROYBLOCK
		object.timer++
		if object.timer == 48
			object.ypos = object.originPos.y
			CallFunction(SYZEggman_HandleBlockMovement)
			object.timer = 0
			object.animation = SYZEGGANI_IDLE
			object.animationTimer = 0
			object[arrayPos0].type = TypeName[Blank Object]

			CreateTempObject(TypeName[Boss Block], 1, object[arrayPos0].xpos, object[arrayPos0].ypos)
			object[tempObjectPos].ixpos -= 8
			object[tempObjectPos].iypos -= 8
			object[tempObjectPos].xvel = -0x18000
			object[tempObjectPos].yvel = -0x20000

			CreateTempObject(TypeName[Boss Block], 2, object[arrayPos0].xpos, object[arrayPos0].ypos)
			object[tempObjectPos].ixpos += 8
			object[tempObjectPos].iypos -= 8
			object[tempObjectPos].xvel = 0x18000
			object[tempObjectPos].yvel = -0x20000

			CreateTempObject(TypeName[Boss Block], 3, object[arrayPos0].xpos, object[arrayPos0].ypos)
			object[tempObjectPos].ixpos -= 8
			object[tempObjectPos].iypos += 8
			object[tempObjectPos].xvel = -0x10000
			object[tempObjectPos].yvel = -0x10000

			CreateTempObject(TypeName[Boss Block], 4, object[arrayPos0].xpos, object[arrayPos0].ypos)
			object[tempObjectPos].ixpos += 8
			object[tempObjectPos].iypos += 8
			object[tempObjectPos].xvel = 0x10000
			object[tempObjectPos].yvel = -0x10000

			PlaySfx(SfxName[Block Break], false)
			object.grabbedBlockSlot = -1
			object.state++
		else
			object.originPos.y += object.yvel
			if object.originPos.y >= object.boundsB
				object.originPos.y = object.boundsB
				object.yvel = 0
			end if
			temp0 = object.timer
			temp0 >>= 1
			temp0 &= 1
			if temp0 == 0
				object.ypos = -0x10000
			else
				object.ypos = 0x10000
			end if
			object.ypos += object.originPos.y
			CallFunction(SYZEggman_HandleBlockMovement)
		end if
		break

	case SYZEGGMAN_RETRACTSTINGER
		if object.stingerPos > 0
			object.stingerPos -= 0x20000
		else
			object.angle = 0
			object.flameAnim = FLAME_ACTIVE
			if object.xvel > 0
				object.state = SYZEGGMAN_MOVERIGHT
			else
				object.state = SYZEGGMAN_MOVELEFT
			end if
		end if
		break

	case SYZEGGMAN_MISSEDBLOCK
		object.ypos += object.yvel
		object.yvel += 0x800
		if object.ypos <= object.boundsB
			object.ypos = object.boundsB
			object.originPos.y = object.ypos
			object.state = SYZEGGMAN_RETRACTSTINGER
		end if
		break

	case SYZEGGMAN_EXPLODE
		if object.stingerPos > 0
			object.stingerPos -= 0x20000
		end if

		object.timer++
		temp0 = object.timer
		temp0 &= 7
		if temp0 == 4
			Rand(temp0, 64)
			temp0 -= 32
			temp0 <<= 16
			temp0 += object.xpos
			Rand(temp1, 48)
			temp1 -= 24
			temp1 <<= 16
			temp1 += object.ypos
			CreateTempObject(TypeName[Explosion], 0, temp0, temp1)
			PlaySfx(SfxName[Explosion], false)
		end if

		if object.timer == 180
			object.animation = SYZEGGANI_TOASTED
			object.animationTimer = 0
			object.timer = 0
			object.direction = FACING_LEFT
			ResetObjectEntity(SLOT_MUSICEVENT_BOSS, TypeName[Music Event], MUSICEVENT_FADETOSTAGE, 0, 0)
			object[SLOT_MUSICEVENT_BOSS].priority = PRIORITY_ACTIVE
			object.state++
		end if
		break

	case SYZEGGMAN_DEFEATED
		if object.timer < 40
			object.ypos += object.yvel
			object.yvel -= 0x800
			object.timer++
		else
			object.timer = 0
			object.yvel = 0
			object.angle = 0
			object.originPos.y = object.ypos
			object.state++
		end if
		break

	case SYZEGGMAN_FLEE
		CallFunction(SYZEggman_Float)
		if object.timer < 8
			object.timer++
		else
			object.timer = 0
			object.animation = SYZEGGANI_PANIC
			object.animationTimer = 0
			object.flameAnim = FLAME_EXPLODE
			object.flameAnimTimer = 0
			object.priority = PRIORITY_ACTIVE
			temp0 = tileLayer[0].xsize
			temp0 <<= 7
			stage.newXBoundary2 = temp0
			object.state++
		end if
		break

	case SYZEGGMAN_ESCAPE
		CallFunction(SYZEggman_Float)
		object.xpos += 0x40000
		object.originPos.y -= 0x4000
		if object.outOfBounds == true
			object.type = TypeName[Blank Object]
			object.priority = PRIORITY_BOUNDS
		end if
		break
		
	end switch

	if object.health != 0
		if object.invincibilityTimer > 0
			object.invincibilityTimer--
		end if

		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			if object.invincibilityTimer == 0
				CheckEqual(player[currentPlayer].animation, ANI_HURT)
				temp0 = checkResult
				CheckEqual(player[currentPlayer].animation, ANI_DYING)
				temp0 |= checkResult
				CheckEqual(player[currentPlayer].animation, ANI_DROWNING)
				temp0 |= checkResult
				if temp0 != 0
					if object.animation != SYZEGGANI_LAUGH
						object.animation = SYZEGGANI_LAUGH
						object.animationTimer = 0
					end if
				end if

				if object.stingerPos > 0x100000
					BoxCollisionTest(C_TOUCH, object.entityPos, -6, 0, 6, 40, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)
					if checkResult == true
						CallFunction(Player_Hit)
					end if
				end if

#platform: USE_STANDALONE
				BoxCollisionTest(C_TOUCH, object.entityPos, -20, -16, 20, 16, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)
#endplatform
#platform: USE_ORIGINS
				BoxCollisionTest(C_TOUCH, object.entityPos, -20, -16, 20, 16, currentPlayer, player[currentPlayer].hitboxLeft, player[currentPlayer].hitboxTop, player[currentPlayer].hitboxRight, player[currentPlayer].hitboxBottom)
#endplatform
				if checkResult == true
					CallFunction(Player_CheckHit)
					if checkResult == true
						object.health--
						if object.health == 0
#platform: USE_ORIGINS
							CallNativeFunction2(NotifyCallback, NOTIFY_KILL_BOSS, 0)
							CallNativeFunction2(NotifyCallback, NOTIFY_BOSS_END, true)
#endplatform
							player.score += 1000
							object.animation = SYZEGGANI_DEFEATED
							object.animationTimer = 0
							object.flameAnim = FLAME_INACTIVE
							object.timer = 0
							object.yvel = 0
							object.state = SYZEGGMAN_EXPLODE
							if screen.xsize > 320
								bossBarrier[+2].state = BOSSBARRIER_LOWER
								bossBarrier[+3].state = BOSSBARRIER_LOWER
							end if

							if object.grabbedBlockSlot > -1
								arrayPos0 = object.grabbedBlockSlot
								object[arrayPos0].type = TypeName[Blank Object]

								CreateTempObject(TypeName[Boss Block], 1, object[arrayPos0].xpos, object[arrayPos0].ypos)
								object[tempObjectPos].ixpos -= 8
								object[tempObjectPos].iypos -= 8
								object[tempObjectPos].xvel = -0x18000
								object[tempObjectPos].yvel = -0x20000

								CreateTempObject(TypeName[Boss Block], 2, object[arrayPos0].xpos, object[arrayPos0].ypos)
								object[tempObjectPos].ixpos += 8
								object[tempObjectPos].iypos -= 8
								object[tempObjectPos].xvel = 0x18000
								object[tempObjectPos].yvel = -0x20000

								CreateTempObject(TypeName[Boss Block], 3, object[arrayPos0].xpos, object[arrayPos0].ypos)
								object[tempObjectPos].ixpos -= 8
								object[tempObjectPos].iypos += 8
								object[tempObjectPos].xvel = -0x10000
								object[tempObjectPos].yvel = -0x10000

								CreateTempObject(TypeName[Boss Block], 4, object[arrayPos0].xpos, object[arrayPos0].ypos)
								object[tempObjectPos].ixpos += 8
								object[tempObjectPos].iypos += 8
								object[tempObjectPos].xvel = 0x10000
								object[tempObjectPos].yvel = -0x10000
							end if
						else
							object.animation = SYZEGGANI_HIT
							object.animationTimer = 0
							object.invincibilityTimer = 32
							PlaySfx(SfxName[Boss Hit], false)
						end if
					end if
				end if
			end if
		next
	end if

	// Screen R Boundary Lock
	if object.state < SYZEGGMAN_FLEE
		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			temp0 = player[currentPlayer].collisionRight
			temp0 <<= 16
			temp0 += player[currentPlayer].xpos
			temp1 = stage.curXBoundary2
			temp1 <<= 16
			if temp0 > temp1
				player[currentPlayer].xvel = 0
				player[currentPlayer].speed = 0
				player[currentPlayer].xpos = temp1
				temp0 = player[currentPlayer].collisionRight
				temp0 <<= 16
				player[currentPlayer].xpos -= temp0
			end if
		next
	end if
end event


event ObjectDraw
	temp0 = object.ypos
	temp0 += object.stingerPos
	DrawSpriteFX(19, FX_FLIP, object.xpos, temp0)

	switch object.animation
	default
	case SYZEGGANI_INVISIBLE
		break

	case SYZEGGANI_IDLE
		temp0 = object.animationTimer
		temp0 /= 6
		object.animationTimer++
		object.animationTimer %= 12
		DrawSpriteFX(temp0, FX_FLIP, object.xpos, object.ypos)
		break

	case SYZEGGANI_LAUGH
		temp0 = object.animationTimer
		temp0 %= 10
		temp0 /= 5
		temp0 += 9
		object.animationTimer++
		if object.animationTimer == 50
			object.animationTimer = 0
			if player[0].animation != ANI_DYING
				object.animation = SYZEGGANI_IDLE
			end if
		end if
		DrawSpriteFX(temp0, FX_FLIP, object.xpos, object.ypos)
		break

	case SYZEGGANI_LAUGH_LOOP
		temp0 = object.animationTimer
		temp0 /= 5
		temp0 += 9
		object.animationTimer++
		object.animationTimer %= 10
		DrawSpriteFX(temp0, FX_FLIP, object.xpos, object.ypos)
		break

	case SYZEGGANI_HIT
		temp0 = object.animationTimer
		temp0 &= 1
		temp0 += 2
		object.animationTimer++
		if object.animationTimer == 30
			object.animationTimer = 0
			object.animation = SYZEGGANI_IDLE
		end if
		DrawSpriteFX(temp0, FX_FLIP, object.xpos, object.ypos)
		break

	case SYZEGGANI_DEFEATED
		temp0 = object.animationTimer
		temp0 >>= 5
		temp0 += 4
		object.animationTimer++
		object.animationTimer &= 63
		DrawSpriteFX(temp0, FX_FLIP, object.xpos, object.ypos)
		break
		
	case SYZEGGANI_TOASTED
		DrawSpriteFX(6, FX_FLIP, object.xpos, object.ypos)
		break

	case SYZEGGANI_PANIC
		temp0 = object.animationTimer
		temp0 >>= 2
		temp0 += 7
		object.animationTimer++
		object.animationTimer %= 7
		DrawSpriteFX(temp0, FX_FLIP, object.xpos, object.ypos)
		break
		
	end switch

	switch object.flameAnim
	default
	case FLAME_INACTIVE
		break

	case FLAME_ACTIVE
		temp0 = object.flameAnimTimer
		temp0 >>= 2
		temp0 += 11
		object.flameAnimTimer++
		object.flameAnimTimer &= 7
		DrawSpriteFX(temp0, FX_FLIP, object.xpos, object.ypos)
		break

	case FLAME_EXPLODE
		temp0 = object.flameAnimTimer
		temp0 /= 3
		temp0 += 11
		object.flameAnimTimer++
		if object.flameAnimTimer > 21
			object.flameAnimTimer = 18
		end if
		DrawSpriteFX(temp0, FX_FLIP, object.xpos, object.ypos)
		break
		
	end switch
end event


event ObjectStartup
	LoadSpriteSheet("Global/Eggman.gif")
	SpriteFrame(-28, -32, 64, 56, 1, 1)
	SpriteFrame(-28, -32, 64, 56, 66, 1)
	SpriteFrame(-28, -32, 64, 56, 131, 1)
	SpriteFrame(-28, -32, 64, 56, 1, 58)
	SpriteFrame(-28, -32, 64, 56, 1, 1)
	SpriteFrame(-28, -32, 64, 56, 1, 58)
	SpriteFrame(-28, -32, 64, 56, 66, 58)
	SpriteFrame(-28, -32, 64, 56, 131, 58)
	SpriteFrame(-28, -32, 64, 56, 66, 1)
	SpriteFrame(-28, -32, 64, 56, 1, 115)
	SpriteFrame(-28, -32, 64, 56, 66, 115)
	SpriteFrame(34, 1, 10, 9, 196, 2)
	SpriteFrame(33, 0, 15, 11, 196, 12)
	SpriteFrame(34, -1, 22, 14, 213, 1)
	SpriteFrame(34, -8, 32, 26, 213, 16)
	SpriteFrame(34, -1, 22, 14, 213, 1)
	SpriteFrame(34, -8, 32, 26, 213, 16)
	SpriteFrame(34, 1, 10, 9, 196, 2)
	SpriteFrame(33, 0, 15, 11, 196, 12)
	SpriteFrame(-8, -16, 16, 32, 238, 60)

	foreach (TypeName[Eggman], arrayPos0, ALL_ENTITIES)
		object[arrayPos0].animation = SYZEGGANI_IDLE
#platform: USE_ORIGINS
		if game.bossOneLife == false
			object[arrayPos0].health = 8
		else
			object[arrayPos0].health = 1
		end if
#endplatform
#platform: USE_STANDALONE
		object[arrayPos0].health = 8
#endplatform
	next
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	temp0 = object.ypos
	temp0 += 0x200000
	DrawSpriteFX(19, FX_FLIP, object.xpos, temp0)
	
	DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
	
	if editor.showGizmos == true
		editor.drawingOverlay = true

		DrawArrow(object.xpos, object.ypos, object[+1].xpos, object[+1].ypos, 255, 255, 0) // arena marker
		DrawArrow(object.xpos, object.ypos, object[+2].xpos, object[+2].ypos, 255, 255, 0) // boundsL
		DrawArrow(object.xpos, object.ypos, object[+3].xpos, object[+3].ypos, 255, 255, 0) // boundsR

		editor.drawingOverlay = false
	end if
end event


event RSDKLoad
	LoadSpriteSheet("Global/Eggman.gif")
	SpriteFrame(-28, -32, 64, 56, 1, 1)
	SpriteFrame(-28, -32, 64, 56, 66, 1)
	SpriteFrame(-28, -32, 64, 56, 131, 1)
	SpriteFrame(-28, -32, 64, 56, 1, 58)
	SpriteFrame(-28, -32, 64, 56, 1, 1)
	SpriteFrame(-28, -32, 64, 56, 1, 58)
	SpriteFrame(-28, -32, 64, 56, 66, 58)
	SpriteFrame(-28, -32, 64, 56, 131, 58)
	SpriteFrame(-28, -32, 64, 56, 66, 1)
	SpriteFrame(-28, -32, 64, 56, 1, 115)
	SpriteFrame(-28, -32, 64, 56, 66, 115)
	SpriteFrame(34, 1, 10, 9, 196, 2)
	SpriteFrame(33, 0, 15, 11, 196, 12)
	SpriteFrame(34, -1, 22, 14, 213, 1)
	SpriteFrame(34, -8, 32, 26, 213, 16)
	SpriteFrame(34, -1, 22, 14, 213, 1)
	SpriteFrame(34, -8, 32, 26, 213, 16)
	SpriteFrame(34, 1, 10, 9, 196, 2)
	SpriteFrame(33, 0, 15, 11, 196, 12)
	SpriteFrame(-8, -16, 16, 32, 238, 60)
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
