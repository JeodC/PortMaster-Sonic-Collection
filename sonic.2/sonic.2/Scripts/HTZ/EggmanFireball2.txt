// ----------------------------------
// RSDK Project: Sonic 2
// Script Description: Eggman Fireball2 Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================

// GroundFlame Aliases
private alias object.value0 : groundFlame.timer

// Path ID Aliases
private alias 0 : PATH_A


// ========================
// Events
// ========================

event ObjectUpdate
	object.xpos += object.xvel
	object.ypos += object.yvel
	object.yvel += 0x3800

	// Check if the object's touched the ground
	ObjectTileCollision(CSIDE_FLOOR, 0, 8, PATH_A)
	if checkResult == true
		// Create ground flames along the floor
		PlaySfx(SfxName[Flame Thrower], false)
		CreateTempObject(TypeName[Ground Flame], 0, object.xpos, object.ypos)
		groundFlame[tempObjectPos].timer = 3
		if object.xvel < 0
			object[tempObjectPos].direction = FLIP_NONE
			object[tempObjectPos].xpos -= 0x20000
		else
			object[tempObjectPos].direction = FLIP_X
			object[tempObjectPos].xpos += 0x20000
		end if
		object.type = TypeName[Blank Object]
	end if

	// Animate the object
	object.frame = object.animationTimer
	object.frame >>= 2
	object.animationTimer++
	object.animationTimer &= 7

	if object.outOfBounds == true
		object.type = TypeName[Blank Object]
	end if

	foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
		BoxCollisionTest(C_TOUCH, object.entityPos, -8, -8, 8, 8, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)
		if checkResult == true
			CallFunction(Player_FireHit)
		end if
	next
end event


event ObjectDraw
	DrawSprite(object.frame)
end event


event ObjectStartup
	CheckCurrentStageFolder("Zone05")
	if checkResult == true
		LoadSpriteSheet("HTZ/Objects.gif")
		SpriteFrame(-8, -8, 16, 16, 191, 94)
		SpriteFrame(-8, -8, 16, 16, 191, 110)
	else
		LoadSpriteSheet("MBZ/Objects.gif")
		SpriteFrame(-8, -8, 16, 16, 1008, 1)
		SpriteFrame(-8, -8, 16, 16, 1008, 17)
	end if
end event


event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	CheckCurrentStageFolder("Zone05")
	if checkResult == true
		LoadSpriteSheet("HTZ/Objects.gif")
		SpriteFrame(-8, -8, 16, 16, 191, 94)
	else
		LoadSpriteSheet("MBZ/Objects.gif")
		SpriteFrame(-8, -8, 16, 16, 1008, 1)
	end if

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
