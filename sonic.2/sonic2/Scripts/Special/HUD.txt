// ----------------------------------
// RSDK Project: Sonic 2
// Script Description: HUD Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================

private alias object.value1 : object.totalRings
private alias object.value2 : object.drawPos.x
private alias object.value3 : object.character

// Player Aliases
private alias object.value0  : player.rings
private alias object.value19 : player.vsCheckpointID


// ========================
// Function Declarations
// ========================

reserve function HUD_GetCharacterFrame
reserve function HUD_DrawRingCount


// ========================
// Function Definitions
// ========================

private function HUD_GetCharacterFrame
#platform: USE_ORIGINS
	// Introduced in Origins Plus
	if object.character != PLAYER_AMY
		object.frame = object.character
		object.frame += 12
	else
		object.frame = 15
	end if
#endplatform
end function


private function HUD_DrawRingCount
	DrawSpriteScreenXY(object.frame, object.drawPos.x, 20)
	object.drawPos.x += 44

	if player[currentPlayer].rings > 99
		// Draw 3 digits
		temp1 = player[currentPlayer].rings
		temp1 /= 100
		DrawSpriteScreenXY(temp1, object.drawPos.x, 29)

		object.drawPos.x += 8
		temp1 = player[currentPlayer].rings
		temp1 %= 100
		temp1 /= 10
		DrawSpriteScreenXY(temp1, object.drawPos.x, 29)

		object.drawPos.x += 8
		temp1 = player[currentPlayer].rings
		temp1 %= 10
		DrawSpriteScreenXY(temp1, object.drawPos.x, 29)
	else
		if player[currentPlayer].rings > 9
			// Draw 2 digits
			temp1 = player[currentPlayer].rings
			temp1 /= 10
			DrawSpriteScreenXY(temp1, object.drawPos.x, 29)

			object.drawPos.x += 8
			temp1 = player[currentPlayer].rings
			temp1 %= 10
			DrawSpriteScreenXY(temp1, object.drawPos.x, 29)
		else
			// Only draw a single digit
			DrawSpriteScreenXY(player[currentPlayer].rings, object.drawPos.x, 29)
		end if
	end if
end function


// ========================
// Events
// ========================

event ObjectDraw
	if options.vsMode == false
#platform: USE_ORIGINS
		currentPlayer = 2
		object.drawPos.x = screen.xcenter
		object.character = stage.playerListPos
		CallFunction(HUD_GetCharacterFrame)
#endplatform
		
		if stage.player2Enabled == false
#platform: USE_STANDALONE
			currentPlayer = 2
			object.drawPos.x = screen.xcenter
			object.drawPos.x -= 24
			object.frame = stage.playerListPos
			object.frame += 10
			CallFunction(HUD_DrawRingCount)
#endplatform
#platform: USE_ORIGINS
			object.drawPos.x -= 24
			CallFunction(HUD_DrawRingCount)
#endplatform
		else
#platform: USE_STANDALONE
			currentPlayer = 2
			object.drawPos.x = screen.xcenter
			object.drawPos.x -= 104
			object.frame = stage.playerListPos
			object.frame += 10
			CallFunction(HUD_DrawRingCount)
#endplatform
#platform: USE_ORIGINS
			object.drawPos.x -= 104
			CallFunction(HUD_DrawRingCount)
#endplatform

			currentPlayer = 3
			object.drawPos.x = screen.xcenter
			object.drawPos.x += 48
#platform: USE_STANDALONE
			object.frame = 11
#endplatform
#platform: USE_ORIGINS
			object.frame = 13
#endplatform
			CallFunction(HUD_DrawRingCount)

			object.totalRings = player[2].rings
			object.totalRings += player[3].rings
#platform: USE_STANDALONE
			DrawSpriteScreenXY(13, screen.xcenter, 21)
#endplatform
#platform: USE_ORIGINS
			DrawSpriteScreenXY(10, screen.xcenter, 21)
#endplatform

			object.drawPos.x = screen.xcenter
			if object.totalRings > 99
				object.drawPos.x -= 12
				temp1 = object.totalRings
				temp1 /= 100
				DrawSpriteScreenXY(temp1, object.drawPos.x, 29)

				object.drawPos.x += 8
				temp1 = object.totalRings
				temp1 %= 100
				temp1 /= 10
				DrawSpriteScreenXY(temp1, object.drawPos.x, 29)

				object.drawPos.x += 8
				temp1 = object.totalRings
				temp1 %= 10
				DrawSpriteScreenXY(temp1, object.drawPos.x, 29)
			else
				if object.totalRings > 9
					object.drawPos.x -= 8
					temp1 = object.totalRings
					temp1 /= 10
					DrawSpriteScreenXY(temp1, object.drawPos.x, 29)

					object.drawPos.x += 8
					temp1 = object.totalRings
					temp1 %= 10
					DrawSpriteScreenXY(temp1, object.drawPos.x, 29)
				else
					object.drawPos.x -= 4
					DrawSpriteScreenXY(object.totalRings, object.drawPos.x, 29)
				end if
			end if
		end if
	else
		currentPlayer = 2
		object.drawPos.x = screen.xcenter
		object.drawPos.x -= 104
#platform: USE_STANDALONE
		object.frame = stage.playerListPos
		object.frame += 10
#endplatform
#platform: USE_ORIGINS
		object.character = stage.playerListPos
		CallFunction(HUD_GetCharacterFrame)
#endplatform
		CallFunction(HUD_DrawRingCount)

		temp0 = 0
		object.drawPos.x = screen.xcenter
		object.drawPos.x -= 96
		while temp0 < player[2].vsCheckpointID
#platform: USE_STANDALONE
			DrawSpriteScreenXY(14, object.drawPos.x, 45)
#endplatform
#platform: USE_ORIGINS
			DrawSpriteScreenXY(11, object.drawPos.x, 45)
#endplatform
			object.drawPos.x += 16
			temp0++
		loop

		currentPlayer = 3
		object.drawPos.x = screen.xcenter
		object.drawPos.x += 48
#platform: USE_STANDALONE
		object.frame = vs.player2Type
		object.frame += 10
#endplatform
#platform: USE_ORIGINS
		object.character = vs.player2Type
		CallFunction(HUD_GetCharacterFrame)
#endplatform
		CallFunction(HUD_DrawRingCount)

		temp0 = 0
		object.drawPos.x = screen.xcenter
		object.drawPos.x += 56
		while temp0 < player[3].vsCheckpointID
#platform: USE_STANDALONE
			DrawSpriteScreenXY(14, object.drawPos.x, 45)
#endplatform
#platform: USE_ORIGINS
			DrawSpriteScreenXY(11, object.drawPos.x, 45)
#endplatform
			object.drawPos.x += 16
			temp0++
		loop
	end if
end event


event ObjectStartup
	LoadSpriteSheet("Special/Objects.gif")
	SpriteFrame(0, 0, 8, 15, 291, 42)		// Number 0 - #0/0
	SpriteFrame(0, 0, 8, 15, 300, 42)		// Number 1 - #1/1
	SpriteFrame(0, 0, 8, 15, 309, 42)		// Number 2 - #2/2
	SpriteFrame(0, 0, 8, 15, 318, 42)		// Number 3 - #3/3
	SpriteFrame(0, 0, 8, 15, 327, 42)		// Number 4 - #4/4
	SpriteFrame(0, 0, 8, 15, 336, 42)		// Number 5 - #5/5
	SpriteFrame(0, 0, 8, 15, 345, 42)		// Number 6 - #6/6
	SpriteFrame(0, 0, 8, 15, 354, 42)		// Number 7 - #7/7
	SpriteFrame(0, 0, 8, 15, 363, 42)		// Number 8 - #8/8
	SpriteFrame(0, 0, 8, 15, 372, 42)		// Number 9 - #9/9
#platform: USE_STANDALONE
	SpriteFrame(0, 0, 48, 24, 250, 1) 		// Sonic Rings - #10/X
	if options.region == 0
		SpriteFrame(0, 0, 48, 24, 299, 1) 	// Tails Rings - #11/X
	else
		SpriteFrame(0, 0, 48, 24, 348, 1) 	// Miles Rings - #11/X
	end if
	SpriteFrame(0, 0, 48, 24, 397, 1) 		// KTE Rings - #12/X
#endplatform
	SpriteFrame(-20, 0, 40, 28, 250, 26) 	// Total box - #13/10
	SpriteFrame(-8, 0, 16, 8, 442, 42) 		// Vs box - #14/11
#platform: USE_ORIGINS
	SpriteFrame(0, 0, 48, 24, 250, 1) 		// Sonic Rings - #X/12
	if options.region == 0
		SpriteFrame(0, 0, 48, 24, 299, 1) 	// Tails Rings - #X/13
	else
		SpriteFrame(0, 0, 48, 24, 348, 1) 	// Miles Rings - #X/13
	end if
	SpriteFrame(0, 0, 48, 24, 397, 1) 		// KTE Rings - #X/14
	SpriteFrame(0, 0, 48, 24, 446, 1)		// Amy Rings - #X/15
#endplatform
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
