// ----------------------------------
// RSDK Project: Sonic 2
// Script Description: Break Rock Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================

private alias 0 : BREAKROCK_ROCK
private alias 1 : BREAKROCK_FRAGMENT

// Player Aliases
private alias object.yvel : player.yvel
private alias object.gravity : player.gravity
private alias object.animation : player.animation
private alias object.value16 : player.isSidekick
private alias object.value19 : player.badnikBonus
private alias object.value40 : player.hitboxLeft
private alias object.value38 : player.hitboxTop
private alias object.value41 : player.hitboxRight
private alias object.value39 : player.hitboxBottom

// Global Variable ID Aliases
private alias 22 : GLOBAL_PLAYERSCORE


// ========================
// Function Declarations
// ========================

reserve function BreakRock_DebugDraw
reserve function BreakRock_DebugSpawn


// ========================
// Function Definitions
// ========================

private function BreakRock_DebugDraw
	DrawSprite(0)
end function


private function BreakRock_DebugSpawn
	CreateTempObject(TypeName[Break Rock], 0, object.xpos, object.ypos)
end function


// ========================
// Events
// ========================

event ObjectUpdate
	switch object.state
	case BREAKROCK_ROCK
		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
#platform: USE_STANDALONE
			if player[currentPlayer].animation == ANI_JUMPING
				BoxCollisionTest(C_SOLID, object.entityPos, -16, -16, 16, 16, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)
#endplatform
#platform: USE_ORIGINS
			checkResult = false
			
			if player[currentPlayer].animation == ANI_JUMPING
				checkResult = true
			end if
			
			if player[currentPlayer].animation == ANI_HAMMER_JUMP
				checkResult = 2
			end if
			
			if checkResult != false
				switch checkResult
				case 1
					BoxCollisionTest(C_SOLID, object.entityPos, -16, -16, 16, 16, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)
					break
					
				case 2
					BoxCollisionTest(C_SOLID, object.entityPos, -16, -16, 16, 16, currentPlayer, player[currentPlayer].hitboxLeft, player[currentPlayer].hitboxTop, player[currentPlayer].hitboxRight, player[currentPlayer].hitboxBottom)
					break
				end switch
#endplatform
				if checkResult == COL_TOP
					player[currentPlayer].yvel = -0x30000
					player[currentPlayer].gravity = GRAVITY_AIR

					// Create the rock fragments

					CreateTempObject(TypeName[Break Rock], 1, object.xpos, object.ypos)
					object[tempObjectPos].ixpos -= 16
					object[tempObjectPos].iypos -= 8
					object[tempObjectPos].xvel = -0x20000
					object[tempObjectPos].yvel = -0x20000
					object[tempObjectPos].state = BREAKROCK_FRAGMENT
					object[tempObjectPos].drawOrder = 5

					CreateTempObject(TypeName[Break Rock], 2, object.xpos, object.ypos)
					object[tempObjectPos].ixpos += 0
					object[tempObjectPos].iypos -= 8
					object[tempObjectPos].xvel = 0
					object[tempObjectPos].yvel = -0x28000
					object[tempObjectPos].state = BREAKROCK_FRAGMENT
					object[tempObjectPos].drawOrder = 5

					CreateTempObject(TypeName[Break Rock], 3, object.xpos, object.ypos)
					object[tempObjectPos].ixpos += 16
					object[tempObjectPos].iypos -= 8
					object[tempObjectPos].xvel = 0x20000
					object[tempObjectPos].yvel = -0x20000
					object[tempObjectPos].state = BREAKROCK_FRAGMENT
					object[tempObjectPos].drawOrder = 5

					CreateTempObject(TypeName[Break Rock], 4, object.xpos, object.ypos)
					object[tempObjectPos].ixpos -= 16
					object[tempObjectPos].iypos += 8
					object[tempObjectPos].xvel = -0x1C000
					object[tempObjectPos].yvel = -0x1C000
					object[tempObjectPos].state = BREAKROCK_FRAGMENT
					object[tempObjectPos].drawOrder = 5

					CreateTempObject(TypeName[Break Rock], 5, object.xpos, object.ypos)
					object[tempObjectPos].ixpos += 0
					object[tempObjectPos].iypos += 8
					object[tempObjectPos].xvel = 0
					object[tempObjectPos].yvel = -0x20000
					object[tempObjectPos].state = BREAKROCK_FRAGMENT
					object[tempObjectPos].drawOrder = 5

					CreateTempObject(TypeName[Break Rock], 6, object.xpos, object.ypos)
					object[tempObjectPos].ixpos += 16
					object[tempObjectPos].iypos += 8
					object[tempObjectPos].xvel = 0x1C000
					object[tempObjectPos].yvel = -0x1C000
					object[tempObjectPos].state = BREAKROCK_FRAGMENT
					object[tempObjectPos].drawOrder = 5

					// Make this object disappear
					object.type = TypeName[Blank Object]

					// global variable "array" (yes, this is actually how its done)
					arrayPos0 = GLOBAL_PLAYERSCORE
#platform: USE_DECOMP
					arrayPos0 = VarName[player.score]
#endplatform
					if player[currentPlayer].isSidekick == 0
						arrayPos0 += currentPlayer
					end if

					switch player[currentPlayer].badnikBonus
					case 0
						global[arrayPos0] += 100
						break

					case 1
						global[arrayPos0] += 200
						break

					case 2
						global[arrayPos0] += 500
						break

					case 3
					case 4
					case 5
					case 6
					case 7
					case 8
					case 9
					case 10
					case 11
					case 12
					case 13
					case 14
						global[arrayPos0] += 1000
						break
						
					case 15
						global[arrayPos0] += 10000
						break
					end switch

					CreateTempObject(TypeName[Object Score], player[currentPlayer].badnikBonus, object.xpos, object.ypos)
					object[tempObjectPos].drawOrder = 5
					if player[currentPlayer].gravity == 1
						if player[currentPlayer].badnikBonus < 15
							player[currentPlayer].badnikBonus++
						end if
					end if
					PlaySfx(SfxName[Block Break], false)
				end if
			else
				BoxCollisionTest(C_SOLID, object.entityPos, -24, -16, 24, 16, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)
			end if
		next
		break

	case BREAKROCK_FRAGMENT
		object.xpos += object.xvel
		object.ypos += object.yvel
		object.yvel += 0x1800
		
		if object.outOfBounds == true
			object.type = TypeName[Blank Object]
		end if
		break

	end switch
end event


event ObjectDraw
	DrawSprite(object.propertyValue)
end event


event ObjectStartup
	LoadSpriteSheet("HTZ/Objects.gif")
	SpriteFrame(-24, -16, 48, 32, 207, 94)
	SpriteFrame(-8, -8, 16, 16, 207, 94)
	SpriteFrame(-8, -8, 16, 16, 223, 94)
	SpriteFrame(-8, -8, 16, 16, 239, 94)
	SpriteFrame(-8, -8, 16, 16, 207, 110)
	SpriteFrame(-8, -8, 16, 16, 223, 110)
	SpriteFrame(-8, -8, 16, 16, 239, 110)

	SetTableValue(TypeName[Break Rock], DebugMode_ObjCount, DebugMode_TypesTable)
	SetTableValue(BreakRock_DebugDraw, DebugMode_ObjCount, DebugMode_DrawTable)
	SetTableValue(BreakRock_DebugSpawn, DebugMode_ObjCount, DebugMode_SpawnTable)
	DebugMode_ObjCount++
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("HTZ/Objects.gif")
	SpriteFrame(-24, -16, 48, 32, 207, 94)
	
	// Although used by the object, it shouldn't be set by the editor
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
