// ----------------------------------
// RSDK Project: Sonic 2
// Script Description: V Platform Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================

private alias object.value0 : object.centerPos.y
private alias object.value1 : object.hitboxL
private alias object.value2 : object.hitboxR
private alias object.value3 : object.stoodPlayers

// Player Aliases
private alias object.ypos : player.ypos

private alias object.value17 : debugMode.currentSelection


// ========================
// Function Declarations
// ========================

reserve function VPlatform_DebugDraw
reserve function VPlatform_DebugSpawn


// ========================
// Static Values
// ========================

private value VPlatform_debugStartID = 0


// ========================
// Function Definitions
// ========================

private function VPlatform_DebugDraw
	temp0 = debugMode[0].currentSelection
	temp0 -= VPlatform_debugStartID
	DrawSprite(temp0)
end function


private function VPlatform_DebugSpawn
	temp0 = debugMode[0].currentSelection
	temp0 -= VPlatform_debugStartID
	CreateTempObject(TypeName[V Platform], temp0, object.xpos, object.ypos)

	object[tempObjectPos].centerPos.y = object.ypos
	object[tempObjectPos].direction = object.direction
	if object[tempObjectPos].propertyValue == 0
		object[tempObjectPos].hitboxL = -32
		object[tempObjectPos].hitboxR = 32
	else
		object[tempObjectPos].hitboxL = -24
		object[tempObjectPos].hitboxR = 24
	end if
end function


// ========================
// Events
// ========================

event ObjectUpdate
	temp6 = object.ypos
	GetTableValue(temp0, 14, StageSetup_oscillationTable)
	if object.direction == FLIP_X
		FlipSign(temp0)
		temp0 += 0x8000
	end if
	temp0 <<= 8

	object.ypos = object.centerPos.y
	object.ypos -= temp0
	temp6 &= 0xFFFF0000
	temp7 = object.ypos
	temp7 &= 0xFFFF0000
	temp7 -= temp6

	temp2 = 0
	foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
		GetBit(temp0, object.stoodPlayers, temp2)
		if temp0 == true
			player[currentPlayer].ypos += temp7
		end if

		SetBit(object.stoodPlayers, temp2, false)
		BoxCollisionTest(C_PLATFORM, object.entityPos, object.hitboxL, -16, object.hitboxR, 0, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)
		if checkResult == true
			SetBit(object.stoodPlayers, temp2, true)
		end if
		temp2++
	next
end event


event ObjectDraw
	DrawSprite(object.propertyValue)
end event


event ObjectStartup
	LoadSpriteSheet("CPZ/Objects.gif")
	SpriteFrame(-32, -16, 64, 27, 136, 155) // Large Platform
	SpriteFrame(-24, -16, 48, 26, 136, 183) // Small Platform

	foreach (TypeName[V Platform], arrayPos0, ALL_ENTITIES)
		object[arrayPos0].centerPos.y = object[arrayPos0].ypos
		if object[arrayPos0].propertyValue == 0
			// Normal-sized platform
			object[arrayPos0].hitboxL = -32
			object[arrayPos0].hitboxR = 32
		else
			// Small-sized platform
			object[arrayPos0].hitboxL = -24
			object[arrayPos0].hitboxR = 24
		end if
	next

	temp0 = 0
	VPlatform_debugStartID = DebugMode_ObjCount
	while temp0 < 2
		SetTableValue(TypeName[V Platform], DebugMode_ObjCount, DebugMode_TypesTable)
		SetTableValue(VPlatform_DebugDraw, DebugMode_ObjCount, DebugMode_DrawTable)
		SetTableValue(VPlatform_DebugSpawn, DebugMode_ObjCount, DebugMode_SpawnTable)
		DebugMode_ObjCount++
		temp0++
	loop
end event


// ========================
// Editor Events
// ========================

event RSDKEdit
	if editor.returnVariable == true
		switch editor.variableID
		case EDIT_VAR_PROPVAL // property value
			checkResult = object.propertyValue
			break
			
		case 0 // type
			checkResult = object.propertyValue
			break
			
		end switch
	else
		switch editor.variableID
		case EDIT_VAR_PROPVAL // property value
			object.propertyValue = editor.variableValue
			break
			
		case 0 // type
			object.propertyValue = editor.variableValue
			break
			
		end switch
	end if
end event


event RSDKDraw
	DrawSprite(object.propertyValue)

	if editor.showGizmos == true
		// Draw arrows to indicate that this is a V platform

		editor.drawingOverlay = true

		temp0 = object.ypos
		temp0 -= 0x320000
		DrawArrow(object.xpos, object.ypos, object.xpos, temp0, 0, 224, 0)
		temp0 = object.ypos
		temp0 += 0x320000
		DrawArrow(object.xpos, object.ypos, object.xpos, temp0, 0, 224, 0)

		editor.drawingOverlay = false
	end if
end event


event RSDKLoad
	LoadSpriteSheet("CPZ/Objects.gif")
	SpriteFrame(-32, -16, 64, 27, 136, 155)
	SpriteFrame(-24, -16, 48, 26, 136, 183)

	AddEditorVariable("type")
	SetActiveVariable("type")
	AddEnumVariable("Large", 0)
	AddEnumVariable("Small", 1)
end event
